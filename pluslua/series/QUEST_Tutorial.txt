Content
	name <Starter Quest>
	desc <Basically a test of the scripting system. Edit it and see!>
	requires <*:Q_Starter>

	%
	% Quests are complex, static events added to the game world. Unlike plots they
	% are generated at the start of the campaign, and unlike scene content they can
	% affect an entire city. Examples of quests include dungeons, arenas, and
	% the leadership series of tasks.
	%
	% Just like every other type of content, the first thing you need to do is
	% requisition some elements for use in this quest. Elements may be created
	% from scratch or selected from among existing gears. In this case the PC's
	% home and Hogye Village already exist, but everything else has to be defined
	% here.
	%

	%% E1 is the home where the PC wakes up
	%% E2 is Hyolee
	%% E3 is Hogye
	%% E4 is the Dark Cave
	%% E5 is the Dark Cave Entrance
	%% E6 is Calmegie Research Station
	Element1 <Scene PCHOME>
	Element2 <Prefab>
	Place2 <6 (Citizens)>
	Element3 <.>
	Element4 <QuestScene>
	Place4 <3>
	Element5 <Prefab>
	Place5 <3>
	Element6 <QuestScene>
	Place6 <4>

	%
	% The Content gear itself can have attached scripts. In this case, at the
	% first START trigger it will print a message and start the conversation
	% with Hyolee.
	%

	lua
		function P.START( self )
			-- At startup, force a chat with Hyolee.
			if not self.v.HaveAlreadyStarted then
				gh_Alert( "You awake from a long slumber. It feels as though you'd been lying there for years." );
				gh_StartChat( %2% )
				self.v.HaveAlreadyStarted = true;
			end
		end
	endlua

	%
	% The gears contained in "sub" define special effects associated with the
	% elements requested above. As long as this quest is active, the behavior of
	% those elements will be different from normal.
	%

	sub
		Persona 2
		%
		% A Persona gear defines a conversation, in this case for Element2 (Hyolee).
		%
		say "I guess you've done everything there is to do, now. Get out there and add some content!"
		condition <return( self.v.HaveEarnedReward )>
		say "Congratulations, you made it to the research center! I'm afraid that for the time being this is the closest thing we have to a victory condition... like I said, this world really needs to be filled out more."
		condition <return( self.v.HaveDoneIntro and chatnpc:IsInPlay() )>
		effect <self.v.HaveEarnedReward = true; gh_GiveXP( 500 )>
		say "Nice to hear from you."
		condition <return( self.v.HaveDoneIntro )>
		say "About time you woke up, I was beginning to think we'd have to call the Snake Lake coma unit."
		effect <self.v.HaveDoneIntro = true;>
			reply "What happened? Where am I?"
				say "You're in Hogye Village, recovering from an injury to the head. The doctor said you're free to go as soon as you feel up to it."
					reply "Go? Go where?"
						say "That's really up to you. It's a big world out there... a bit empty for the time being, but with a bit of help I'm sure we can fill it up."
						label <GoGoWhere>
							reply "I see..."
								say "Have fun exploring. Feel free to drop by Calmegie Research Station when you're feeling better; I better get back to my experiments."
			reply "Sleeping in never hurt anyone."
				say "Maybe not, but getting bonked on the head has been known to cause a bit of damage. The doctor said you might have a touch of amnesia... but as long as there's nothing else wrong, you're free to go."
					reply "Go? Go where?"
						goto "GoGoWhere"
			reply "Yeah, I know."
				say "Now that you're awake I can get back to Calmegie Research Station. Feel free to drop by when you're all better."

		MetaScene 4
		%
		% A MetaScene is a blueprint for a scene. In a quest, a MetaScene may add features
		% to an existing scene or define an entirely new scene (if declared as a QuestScene
		% in the element request).
		%
		% Element4 is a QuestScene, meaning that when this quest is initialized it will be
		% added to the campaign world as a permanent scene. The Dark Cave has been given
		% the type "DUNGEON", so this blueprint will be used to construct a multi-level maze.
		%
		name <Dark Cave>
		DifficultyLevel 40
		special <NOEXIT>
		type <DUNGEON>
		terrain <GROUND>
		habitat <EARTH.MNTNS>
		dentrance <*GoUp>
		content1 <Some 1 40 Sub *DUNGEON_THREAT>
		content2 <Some 1 45 Sub *DUNGEON_DECOR>
		content3 <Some 1 30 Sub *DUNGEON_REWARD>
		mapwidth 30
		mapheight 30
		CaveMap
		Ceiling
		RockyTiles
		NeededCells 3

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monster>
			type <Cave Exotic Mutant>
			SetEnemy 1
			Stat 2 1

			room
			width 3
			height 3
			sub
				StairsDown
				Destination -1
			end
		end

		MetaScene 6
		%
		% Element6 is another QuestScene, in this case the research station in the heart of the
		% volcano. Note that by default elements placed in the Dark Cave will be put in the
		% deepest dungeon level. So, the entrance to this scene will be there as well.
		%
		name <Calmegie Research Station>
		entrance <*GoUp>
		special <ADDEXIT>
		MallMap
		MapWidth 10
		MapHeight 10
		sub
			team 1

			team 2
			name <Citizens>
			SetAlly 1
			Passive

			team 3
			name <Guards>
			SetAlly 2

			room
			name <Hyolee's Office>
			special <SHAREDPALETTE>
			minimap <....................#---#>
		end
	end

	%
	% The gears contained in "inv" are the prefabricated elements requested
	% above. They must be listed in the same order by which they were requested.
	%

	inv
		NPC Scientist
		name <Hyolee>
		chardesc Female Heroic Cheerful
		sdl_portrait <por_f_hyoleenewver(YC-).png>
		sdl_colors <70 121 119 142 62 39 234 180 88>

		Building
		name <Calmegie Research Station>
		Scale 3
		Altitude 3
		BM_Volcano
		roguechar <^>
		XPos 5
		YPos 5
		desig <ENTRANCE %4%>
	end



Content
	name <Cayley Mine>
	requires <*:Q_L_HOGYE_CayleyMine>
	%
	% Also going to include Cayley Mine here.
	%

	% Element1 is Hogye itself
	% Element2 is the mine entrance- we could do this automatically just by
	%   giving the dungeon metascene an ENTRANCE attribute, but by doing it
	%   manually I can set the exact X,Y position of the mine.
	% Element3 is the dungeon proper.
	Element1 <.>
	Element2 <Prefab>
	Place2 <1>
	Element3 <QuestScene>
	Place3 <1>

	sub
		STC QS_Dungeon_EarthMine
		SetID 3
		sub
			rect
			%
			% Normally, the map features in a dungeon metascene will be
			% reproduced on each level of the dungeon. This behavior can be
			% modified using the FORGOALLEVEL and FORENTRYLEVEL tags.
			width 5
			height 5
			ForGoalLevel
			sub
				Elevator
				name <Mecha Bay>
				lua
					P.USE = function( self )
						if not self.v.HaveTakenMecha then
							if gh_ChooseAOverB( "You find an old cargo elevator, and an abandoned mining robot inside... Hey! Someone left the keys in the ignition!" , "Take it." , "Leave it." ) then
								gh_CreateAndGivePart( "HMC-6 Wolfram" );
								self.v.HaveTakenMecha = true;
								gh_GotoScene( %1% );
								gh_SeekGate( %3%:GetXit() );
							end;
						else
							if gh_ChooseAOverB( "The cargo elevator is now empty, but you can ride it to the surface." , "Take it." , "Leave it." ) then
								gh_GotoScene( %1% );
								gh_SeekGate( %3%:GetXit() );
							end;
						end;
					end;
				endlua
			end
		end

	end
	inv
		STC BUILDING
		name <Hogye Mine>
		desig <ENTRANCE %3%>
		XPos 23
		YPos 11
	end



Content
	name <Ipshil Factory>
	requires <*:Q_L_HOGYE_IpshilFactory>
	%
	% Based on the Cayley Mine above
	%

	% Element1 is Hogye itself
	% Element2 is the mine entrance- we could do this automatically just by
	%   giving the dungeon metascene an ENTRANCE attribute, but by doing it
	%   manually I can set the exact X,Y position of the mine.
	% Element3 is the dungeon proper.
	Element1 <.>
	Element2 <Prefab>
	Place2 <1>
	Element3 <QuestScene>
	Place3 <1>

	content1 <Some 1 40 Sub *DUNGEON_THREAT>
	content2 <Some 1 45 Sub *DUNGEON_DECOR>
	content3 <Some 1 30 Sub *DUNGEON_REWARD>
	content4 <Some 1 40 Sub *DUNGEON_DECOR>
	content5 <Some 1 45 Sub *DUNGEON_DECOR>
	content6 <Some 1 30 Sub *DUNGEON_DECOR>

	sub
		STC QS_Dungeon_Factory
		SetID 3
	end
	inv
		STC BUILDING
		name <Ipshil Factory>
		desig <ENTRANCE %3%>
		XPos 21
		YPos 13
	end


