	%
	% Random Scene Content, abbreviated as RanCon, is used by the random map generator
	% to customize scenes. Unlike plots and quests, RanCom generally only affects
	% the scene in which it is generated, and does so permanently.
	%
	%
	% Just like every other type of content, the first thing you need to do is
	% requisition some elements for use in this quest. Elements may be created
	% from scratch or selected from among existing gears.
	%

Content
	name <Quentin's Shop>
	requires <*LOCAL_HOGYE_QuentinShop>
	%
	% The elements generated by RanCon are automatically placed in their
	% appropriate place- in this case, a cell of a shopping center.
	%
	% The only element needed by this content is Quentin himself.
	%
	element1 <Prefab>
	team1 <Citizens>
	teamdata1 <ally pass>

	%
	% One subplot is requested- the props for the shop cell.
	%
	subplot1 <*DECOR_FOR_GENERAL_SHOP>

	sub
		Persona 1
		say "Welcome to my shop. It's not particularly impressive, I know, but I think you'll be pleased with our selection."
			reply "Let me see what you've got."
				say "Take a look around, let me know if you see anything you like."
				label <GoOpenShop>
				effect <gh_OpenShop( "WEAPON CLOTHING FOOD TOOL COMMUNICATIONS MECHA ESSENTIAL" )>
				NoChatOps
			reply "Hey, I see you've spruced up the place."
			condition <return( not self.v.HaveMentionedSpruce )>
				say "Yeah, with the arena bringing in tons of cavaliers, I've been able to expand a bit. Now do you want anything or are you just admiring the decor?"
				effect <self.v.HaveMentionedSpruce = true;>
					reply "I'd like to buy something..."
						goto "GoOpenShop"
					reply "I'm just looking."
						pfrag "*GoodBye"
	end
	inv
		NPC Shopkeeper
		name <Quentin>
		chardesc Male Easygoing Sociable Cheerful
		Age 32
	end


Content
	name <Chae's Cafe>
	requires <*LOCAL_HOGYE_ChaeShop>
	%
	% Chae's shop is a little bit more complicated than her father's, but only
	% just. There are some props and a minimap to add to the scene.
	%
	% Element1 is Chae, Elements 2 and 3 are props.
	%
	element1 <Prefab>
	team1 <Citizens>
	teamdata1 <ally pass>
	element2 <Prefab>
	element3 <Prefab>

	%
	% The minimap attribute allows you to place objects and terrain exactly
	% within this 5x5 cell. The exact terrain drawn will depend upon the map
	% in question. Use "." for floor, "#" for a wall, "&" for a window,
	% '-' for a threshold and '+' for a door. Elements will be placed by
	% number.
	%
	minimap <2...3.......1.......&---&>

	sub
		Persona 1

	end
	inv
		NPC Shopkeeper
		name <Chae>
		chardesc Female
		Age 2

		STC VENDING-1
	end

