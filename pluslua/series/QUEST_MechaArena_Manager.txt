

Content
	name <Mecha Sporch>
	requires <*:Q_MECHA_ARENA_BASE HOGYE>

	% E1 is the town itself
	% E2 is the arena building
	% E3 is the arena
	% E4 is the manager
	Element1 <.>
	Element2 <QuestScene>
	Place2 <1>
	Element3 <QuestScene>
	Place3 <2>
	Element4 <Prefab>
	Place4 <2 (Citizens) pass ally>
	Element5 <Prefab>
	Place5 <2 (Citizens) pass ally>

%	SubPlot1 <*:Q_MECHA_ARENA_CHALLENGE #30 1 2>
%	SubPlot2 <*:Q_MECHA_ARENA_CHALLENGE #42 1 2>
%	SubPlot3 <*:Q_MECHA_ARENA_CHALLENGE #54 1 2>
%	SubPlot4 <*:Q_MECHA_ARENA_CHALLENGE #66 1 2>

	sub
		Persona 4
		say "Very well done. That was your tenth fight, making you our new champion."
		condition <return( ( %3%:GetArenaState() == NAV_AS_WIN ) and ( %3%:GetArenaWins() == 10 ) )>
		effect <%3%:ResetArena(); gh_GiveCash( gh_CalculateReward( %3%:GetArenaThreat() , 1000 ) ); gh_GiveXP( 500 ) >

		say "You won! Congratulations. Come back later for another match."
		condition <return( ( %3%:GetArenaState() == NAV_AS_WIN ) )>
		effect <%3%:ResetArena(); gh_GiveCash( gh_CalculateReward( %3%:GetArenaThreat() , 200 ) ) >

		say "You lost... better luck next time."
		condition <return( ( %3%:GetArenaState() == NAV_AS_LOSS ) )>
		effect <%3%:ResetArena()>

		say "What are you doing talking to me? Shouldn't you be fighting now?"
		condition <return( %3%:GetArenaState() ~= NAV_AS_VACANT )>

		say "The arena's closed. Come back tomorrow for another fight."
		condition <return( %3%:GetArenaRecharge() > gh_GetTime() )>

		say "Ready to battle? There's a match about to start."
		condition <return( self.v.HaveStartedArena )>
			reply "Yes, I am!"
				say "You're going to need a landworthy mecha to enter the arena. Go ask around and see if you can find one."
				condition <return( not gh_PCMekCanEnterScene( %3% ) )>
				label <GoStartBattle>

				say "Take the elevator to the arena level. Then, prepare to fight! If you win, come back and I'll give you a reward."
				effect <%3%:PrepArena( 100 )>

			reply "No, not right now..."
				say "You know what they say: nothing ventured, nothing gained."

		pfrag "*NiceToMeetYou"
		say "Welcome to Mecha Sporch, where armored titans compete for prizes and glory! Are you interested in watching the games, or did you think you might take part?"
			reply "Tell me more about taking part."
				say "Great! We're always on the lookout for fresh talent. First of all the most important question: do you already own a mecha?"
				effect <self.v.HaveStartedArena = true; %3%:SetArenaThreat( 5 )>
					reply "Yes, I do."
						say "Great! In that case, are you ready to battle?"
							reply "Sure!"
								goto "GoStartBattle"
							reply "Uh, not just yet..."
								say "Well come back when you are. We've got battle starting all the time."
					reply "Nope. Got any loaners?"
						say "As a matter of fact, we do. Try this CNA-15 Century! Now, are you ready to battle?"
						effect <gh_CreateAndGivePart( "CNA-15 Century" );>
							reply "Sure!"
								goto "GoStartBattle"
							reply "Uh, not just yet..."
								say "Well come back when you are. We've got battle starting all the time."

			reply "I'm just here to watch."
				say "Mecha Sporch can be seen on TV five nights a week. Check your local listings for time and channel."

		MetaScene 2
		name <Mecha Sporch>
		entrance <*ARENA>
		special <ADDEXIT>
		MallMap
		MapWidth 10
		MapHeight 10
		sub
			team 1

			team 2
			name <Citizens>
			SetAlly 1
			Passive

			team 3
			name <Guards>
			SetAlly 2

			room
			name <Foyer>
			desig <EntranceGrid>
			Content <Some 5 50 Here *URBAN_X>
			Content2 <Some 1 100 Here *ANTI_ATTACK_SECURITY 2>


			room
			name <Manager's Office>
			special <SHAREDPALETTE>
			desig <HOME>
			minimap <.............1......&---&>

			room
			name <Glory Road>
			special <SHAREDPALETTE>
			minimap <######&2&##...##...##...#>
			inv
				Elevator
				name <To Arena>
				desig <ENTRANCE %3%>
				MiniMapComponent 2
				lua
					function P.USE( self )
						-- Not gonna use the stairs...
						if gh_PCMekCanEnterScene( %3% ) then
							gh_Print( "You board the elevator." )
							gh_GotoScene( gh_GetStat( self.ptr , STAT_DESTINATION ) )
						else
							gh_Print( "You need a landworthy mecha to enter the arena." );
						end
					end
				endlua
			end
		end

		STC QS_MechaArena
		SetID 3
		name <Mecha Sporch Arena>
		mapwidth 30
		mapheight 30

		Persona 5
		pfrag "*NiceToMeetYou"
		say "I'm in charge of repair services for the Mecha Sporch arena. I've also got some mecha and other goods for sale."
			reply "I could use some of that."
				say "Let me know what you need, and I'll do my best."
				effect <gh_OpenShop( "MECHA MEXTRA" )>
				NoChatOps
			reply "Thanks, but I don't need anything."
				pfrag "*Goodbye"
	end
	inv
		NPC Arena Pilot
		faction <PDASS>
		NonCombatant

		NPC Mechanic
		faction <PDASS>
	end


