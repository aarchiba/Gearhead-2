%%	COMP_Pen_Enemy Revealed.txt
%%	+Pen	Nature of the enemy is discovered



Plot
	name <Fight Arch-Enemy>
	size 8
	desc <You fight your arch-enemy.>
	requires <#A +Tin ~+Ffi E:++ +Pen ~!Lo ~!Ne -E:ALLY>
	changes <+T-->

	update <if= V1 0 V= 1 1   MoveNPC E2 E1 SetNPCTeam E2 2 NPCLevel E2 StoryDL>

	element1 <Grab 6>
	element2 <Grab 1>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		% L3 = Script Immunity Counter
		% L4 = Identity of enemy NPC
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1   ForceChat E2   L= 4 E2   Compose FAINT E2 .E2Faint>
		.E2Faint <if= L1 0 Goto GoDie>
		GoDie <L= 1 2  L= 3 1   FreezeNPC E2   StoryNote 4   WinComp 0 .next>
		nu1 <if= T1 0  Return   if= L1 0 L= 1 1   StoryNote 2   LoseComp 0 .lose   ForceChat L4>
		nu2 <if= T2 0           if= L1 0 L= 1 2   StoryNote 3   AddRenown 1   XPV 100   Salvage   ifNPCOK L4 else GoDie   WinComp  0 .next   ForceChat L4>
		End <if# L3 0 Print 1>

		.lose <+T-->
		.next <+T-->

		Msg1 <\PERSONA L4 's cockpit is empty. Could \SPR E2 have escaped?>
		Msg2 <You fought your enemy \ELEMENT 2 , and lost.>
		Msg3 <You fought your enemy \ELEMENT 2 , and won.>
		Msg4 <You appeared to kill \ELEMENT 2 in battle, but \PPR E2 body wasn't found...>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction N2   WMecha 2 Threat StoryDL 75>
			ParaX 25
			ParaY 25
		end

		Persona 2
		special <UNLISTED>
		greeting <PCFEnemy N2  if= L1 2 else GoCheckLoss Goto GoWinSpeech>
		*GoWinSpeech <*GetYouNextTime>
		GoCheckLoss <if= L1 1 else GoThreat Goto GoLoseSpeech>
		*GoLoseSpeech <*YouLose>
		*GoThreat <*BattleChallenge GoFight na>
		*GoFight <*THEME_EXPO&ENEMY na>

	end

Plot
	name <Fight Henchmen>
	size 8
	desc <You fight the henchmen of your arch-enemy.>
	requires <#A +Tin ~+Ffi E:++ +Pen !Ne  -E:ALLY>
	changes <+T-->

	update <if= V1 0 V= 1 1   NPCLevel E2 StoryDL>

	element1 <Grab 6>
	element2 <Grab 1>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1   ForceChat E2>
		nu1 <if= T1 0  Return   if= L1 0 L= 1 1   StoryNote 2   ForceChat E2>
		nu2 <if= T2 0           if= L1 0 L= 1 2   StoryNote 3   AddRenown 1   XPV 100   Salvage   ForceChat E2>

		Msg2 <You fought \ELEMENT 2 's henchmen , and lost.>
		Msg3 <You fought \ELEMENT 2 's henchmen , and won.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction N2   WMecha 2 Threat StoryDL 75>
			ParaX 25
			ParaY 25
		end

		Persona 2
		special <UNLISTED>
		greeting <PCFEnemy N2   if= L1 2 else GoCheckLoss WinComp 0 .next Goto GoWinSpeech>
		GoWinSpeech <EndChat Say 2>
		GoCheckLoss <if= L1 1 else GoThreat LoseComp 0 .next Goto GoLoseSpeech>
		.next <+T-- +F-->
		*GoLoseSpeech <*YouLose>
		*GoThreat <*BattleChallenge GoFight na>
		GoFight <EndChat Say 1>
		Msg1 <I can see that I'm not needed here. My henchmen should be more than capable of taking you out.>
		Msg1_1 <As much as I'd love to stay and fight you myself, I'm afraid that there's business elsewhere I really must attend to. If you survive then we'll meet again.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <My men can finish you on their own. I have more important things to attend to.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <I'm afraid I have to go now, but my assistants should be skilled enough to keep you busy for a while.>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <It would be a waste of my talents to kill you myself. Men, destroy \PC ... I must be off.>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <You're very lucky, \PC ... I'm too busy to fight you myself, today. Of course I do have some assistants who will fight you in my place, so you're not that lucky.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <I cannot allow you to delay our mission. I must be off, but my subordinates will make sure you never leave this place. Goodbye \PC .>
		CMsg1_6 <ifNPCMelancholy Accept>

		Msg2 <You're stronger than I thought. Next time, I will face you myself.>
		Msg2_1 <Well done, \PC , I see now that you're destined to become a great pilot. The next time we meet I hope I have a chance to test your skills myself.>
		CMsg2_1 <ifNPCSociable Accept>
		Msg2_2 <So, you survived... We'll meet again.>
		CMsg2_2 <ifNPCShy Accept>
		Msg2_3 <They say good help is hard to find... I guess next time I'll have to face you myself. Until then, goodbye...>
		CMsg2_3 <ifNPCEasygoing Accept>
		Msg2_4 <So, you aren't as weak as you first appeared... I look forward to our next meeting.>
		CMsg2_4 <ifNPCPassionate Accept>
		Msg2_5 <How interesting. Next time I'll be sure to fight you myself.>
		CMsg2_5 <ifNPCCheerful Accept>
		Msg2_6 <How disappointing, I expected you to be dead by now... but in any case you were too slow to stop me.>
		CMsg2_6 <ifNPCMelancholy Accept>

	end





