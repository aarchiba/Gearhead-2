Plot 0

name <Search and Destroy Mission>

context <=MIL_DEFAULT_DEFENSE>

element1 <Character !Ally -1 !Near -3>
element2 <Scene Environs !Near -3>

%% Elements 3 through 5 are the encounters.
element3 <Prefab>
place3 <2>
element4 <Prefab>
place4 <2>
element5 <Prefab>
place5 <2>

% P1 = 	Timer/Initialization Counter
% P2 =	Mission has been given indicator
%	If P2=2, the story has ended and the mission is over.
% P3 =	E3 Activation Counter
% P4 =	E4 Activation Counter
% P5 =	E5 Activation Counter
%	For these activation counters, if Px=1 the encounter is active.
%	If Px=2, the encounter has been completed.
% P6 =	Victory counter
% P7 =	Difficulcy Rating
% P8 =	Reward Percentage
% P9 =	Number of missions attempted
% P10 = Email Sent Indicator

start <ifNPCOK E1 else GoDelete ifG V1 0 Goto GoCheckTime>
update <ifNPCOK E1 else GoDelete if= V1 0 else GoCheckTime  V= 1 ComTime  V+ 1 86400   P= 7 PCRenown  P= 8 100   P+ 7 d4   P+ 7 -d4   ifG 2 P7 P= 7 d4>
GoDelete <AdvancePlot 0>
GoCheckTime <ifG ComTime V1  if= P2 0 else GoSetEnd  AdvancePlot 0>
GoSetEnd <P= 2 2 email 2>
CleanUp <if= P2 0 else GoSetEnd  AdvancePlot 0>

% Insert email here
5min <if= P10 0  if= PCFac N1  if= P2 0  if= d20 1  ifG PCCharm d40  Email 1 P= 10 1>

Msg1_1 <\ELEMENT 1 @ \SCENE EScene 1 :// I've got a mission for you here in \SCENE N3 . Report to \SCENE EScene 1 as quickly as possible.>
Msg1_2 <\ELEMENT 1 @ \SCENE EScene 1 :// Hey \RANK , got a combat mission for you here in \NARRATIVE 3 .>
Msg1_3 <\ELEMENT 1 @ \SCENE EScene 1 :// Important message for \RANK \PC . \NARRATIVE 3 under attack by \NARRATIVE 2 ; come quick; bring guns.>
Msg1_4 <\ELEMENT 1 @ \SCENE EScene 1 :// Report to \NARRATIVE 3 for combat mission against \NARRATIVE 2 as soon as possible.>
CMsg1_4 <ifG 0 NPCRep E1 3 ACCEPT>
Msg1_5 <\ELEMENT 1 @ \SCENE EScene 1 :// There's trouble in \NARRATIVE 3 ; \NARRATIVE 2 has some troops in the area.>
Msg1_6 <\ELEMENT 1 @ \SCENE EScene 1 :// Get yer skinny butt to \NARRATIVE 3 and help us kick out those \NARRATIVE 2 losers!>
CMsg1_6 <ifG 0 NPCRep E1 4 ACCEPT>
Msg1_7 <\ELEMENT 1 @ \SCENE EScene 1 :// \RANK , \NARRATIVE 2 has some units in \NARRATIVE 3 , and it's up to \NARRATIVE 1 to stop them.>
MSG1_8 <\ELEMENT 1 @ \SCENE EScene 1 :// There's a party brewing in \NARRATIVE 3 with \NARRATIVE 2 asking for trouble!>
CMsg1_8 <ifG NPCRep E1 5 0 ACCEPT>
MSG1_9 <\ELEMENT 1 @ \SCENE EScene 1 :// \NARRATIVE 3 is under siege from \NARRATIVE 2 ; if we do not act quickly, all will be lost.>
CMsg1_9 <ifG 0 NPCRep E1 5 ACCEPT>

Msg2_1 <\ELEMENT 1 @ \SCENE EScene 1 :// The mission is over; report back here for debriefing.>
Msg2_2 <\ELEMENT 1 @ \SCENE EScene 1 :// Report back here for debriefing at once.>
Msg2_3 <\ELEMENT 1 @ \SCENE EScene 1 :// The enemy units have left the area; report back for debriefing.>

sub
	Persona 1
	% V1 = Reward per patrol
	rumor <!1 needs a mecha pilot for a city defense mission.>

	greeting <ifG P6 0 else GoCheckTime   NewChat  Say 8  CashPrize * V1 P6  AdvancePlot 0>
	GoCheckTime <if= P2 2 else GoNoWins   NewChat  Say 7  AdvancePlot 0>
	GoNoWins <ifG P9 0 else GoRemind      NewChat  Say 6  AdvancePlot 0>
	GoRemind <if= P2 1 else GoNoMission   NewChat  Say 5>

	*GoNoMission <*ENEMY_CHECK GoCheckEmail N1 GoEnd>
	GoCheckEmail <if= P10 1 else GoCheckMember Goto GoGotEmail>
	*GoGotEmail <*DidYouGetEmail GoMissionBriefing>
	GoCheckMember <if= PCFac N1 else GoCheckAuto  Goto GoMissionBriefing>
	*GoCheckAuto <*AutoMissionTest&Mecha GoMissionBriefing result2 GoCheckCivil N1 N2> 
	*GoCheckCivil <*CivilDefenseTest  GoMissionBriefing GoEnd GoVolunteer N1 N2>

	GoEnd <AdvancePlot 0>
	GoVolunteer <P= 8 65   Goto GoMissionBriefing>
	GoMissionBriefing <NewChat   V= 1 Reward Threat P7 100 P8  Say 1 AddChat 1 AddChat 2>

	*result1 <*GoodLuckOnMission GoR1Details N1 N2>
	% There will be 1-3 encounters activated.
	GoR1Details <Memo 4   P= 2 1 P= 3 1 if# d5 1 P= 4 1  if= d2 1 p= 5 1>
	*result2 <*RejectMission GoR2End>
	GoR2End <AdvancePlot 0>

	Msg1 <Some patrols from \FACTION N2 are operating in \ELEMENT 2 . For every group eliminated, you'll be paid $ \VAL V1 plus salvage.>
	Msg1_1 <Here are the mission details: We've got some scout patrols from \FACTION N2 operating in the \ELEMENT 2 area. I want you to seek these out and engage them; for each group defeated, you'll earn $ \VAl V1 plus salvage.>
	CMsg1_1 <ifNPCSociable Accept>
	Msg1_2 <Seek mecha patrols from \FACTION N2 in \ELEMENT 2 and engage them. You'll earn $ \VAL V1 plus salvage for each one defeated.>
	CMsg1_2 <ifNPCShy Accept>
	Msg1_3 <There are some mecha from \NARRATIVE 2 in \ELEMENT 2 . For every patrol you locate and disable, you'll earn $ \VAL V1 plus salvage. This shouldn't be a problem for an ace like you.>
	CMsg1_3 <ifNPCCheerful Accept>
	Msg1_4 <\NARRATIVE 3 is under attack, but we have one chance to end things before anyone gets killed. Mecha from \NARRATIVE 2 are operating in \ELEMENT 2 ; for every patrol you destroy, you'll earn $ \VAL V1 plus salvage.>
	CMsg1_4 <ifNPCMelancholy Accept>
	Msg1_5 <Here's the deal: there are a few patrols from \NARRATIVE 2 operating in \ELEMENT 2 . For each one you eliminate, you'll earn $ \VAL V1 plus salvage.>
	CMsg1_5 <ifNPCEasygoing Accept>
	Msg1_6 <Some mecha from \FACTION N2 have evaded our long range patrols. They must be destroyed... Seek combat groups in \ELEMENT 2 . You'll earn $ \VAL V1 plus salvage for each one eliminated.>
	CMsg1_6 <ifNPCPassionate Accept>
	Msg4 <\ELEMENT 1 in \SCENE EScene 1 hired you to search \ELEMENT 2 for \NARRATIVE 2 patrols.>
	Msg5 <Have you located any patrols yet?>
	Msg5_1 <Any progress to report on the mission I gave you? No? Then what have you been doing?>
	Msg5_2 <Get to work. There are still \NARRATIVE 2 patrols in \ELEMENT 2 .>
	Msg6 <You didn't do very well out there. This mission has been a failure.>
	Msg6_1 <You didn't manage to destroy a single patrol. The mission is a failure.>
	Msg7 <The city is safe once more; you don't have to keep searching.>
	Msg7_1 <All enemy troops have left the area. You can stop searching now.>

	Msg8 <Good job; you destroyed \VAL P6 enemy patrols. Here's your reward.>
	Msg8_1 <You did really well out there... \VAL P6 enemy patrols were encountered and destroyed. Here's the money you were promised.>
	CMsg8_1 <ifNPCSociable Accept>
	Msg8_2 <You did well. Here's your pay.>
	CMsg8_2 <ifNPCShy Accept>
	Msg8_3 <Great work, \PC . You took out \VAL P6 patrols. Here's your pay.>
	CMsg8_3 <ifNPCCheerful Accept>
	Msg8_4 <You destroyed \VAL P6 enemy patrols... I wonder how many more there are. In any case, here's your pay.>
	CMsg8_4 <ifNPCMelancholy Accept>
	Msg8_5 <Back already? Well, I see that you've eliminated \VAL P6 patrols... That's pretty good. Here's your reward.>
	CMsg8_5 <ifNPCEasygoing Accept>
	Msg8_6 <You destroyed \VAL P6 enemy patrols. Good job... Here's your pay.>
	CMsg8_6 <ifNPCPassionate Accept>

	Prompt1 <I'll do it.>
	Prompt1_1 <Alright, I'll do it.>
	Prompt1_2 <I accept the mission.>

	Prompt2 <That's not enough.>
	Prompt2_1 <Not good enough.>
	Prompt2_2 <It's tempting, but I can't.>





	MetaScene 3 2
	MapWidth 30
	MapHeight 30
	content <Some 1 5 Sub *BATTLE_X ->

	% L1 = Initialization
	% L2 = Victory Counter

	start <if= L1 0 L= 1 1 PCFEnemy N2   P= 3 2  P+ 9 1>

	nu1 <if= T1 0 Return if= L2 0 L= 2 1  AddRenown -8                    Goto GoLoss>
	nu2 <if= T2 0        if= L2 0 L= 2 1  XPV 100  AddRenown 1            Goto GoVictory>

	% The PC has won this encounter...
	GoVictory <Salvage  S+ 101 1   FacXP+ N1 1   P+ 6 1>

	% The PC has lost this encounter...
	GoLoss <S+ 101 -1>

	sub
		Team 1
		SetAlly 3
		SetEnemy 2

		Team 2
		SetEnemy 1 3
		Deploy <SetSelfFaction N2   WMecha 2 Threat P7 100>
	end





	MetaScene 4 2
	MapWidth 30
	MapHeight 30
	content <Some 1 5 Sub *BATTLE_X ->

	% L1 = Initialization
	% L2 = Victory Counter

	start <if= L1 0 L= 1 1 PCFEnemy N2   P= 4 2  P+ 9 1>

	nu1 <if= T1 0 Return if= L2 0 L= 2 1  AddRenown -8           Goto GoLoss>
	nu2 <if= T2 0        if= L2 0 L= 2 1  XPV 100  AddRenown 1   Goto GoVictory>

	% The PC has won this encounter...
	GoVictory <Salvage  S+ 101 1   FacXP+ N1 1   P+ 6 1>

	% The PC has lost this encounter...
	GoLoss <S+ 101 -1>

	sub
		Team 1
		SetAlly 3
		SetEnemy 2

		Team 2
		SetEnemy 1 3
		Deploy <SetSelfFaction N2   WMecha 2 Threat P7 100>
	end





	MetaScene 5 2
	MapWidth 30
	MapHeight 30
	content <Some 1 5 Sub *BATTLE_X ->

	% L1 = Initialization
	% L2 = Victory Counter

	start <if= L1 0 L= 1 1 PCFEnemy N2   P= 5 2  P+ 9 1>

	nu1 <if= T1 0 Return if= L2 0 L= 2 1  AddRenown -8           Goto GoLoss>
	nu2 <if= T2 0        if= L2 0 L= 2 1  XPV 100  AddRenown 1   Goto GoVictory>

	% The PC has won this encounter...
	GoVictory <Salvage  S+ 101 1   FacXP+ N1 1   P+ 6 1>

	% The PC has lost this encounter...
	GoLoss <S+ 101 -1>

	sub
		Team 1
		SetAlly 3
		SetEnemy 2

		Team 2
		SetEnemy 1 3
		Deploy <SetSelfFaction N2   WMecha 2 Threat P7 100>
	end

end
inv
	STC ENCOUNTER-WANDER
	name <Enemy Mecha>
	update <if= P3 1 else GoHide  if= P2 1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	ENCOUNTER_Hostile

	STC ENCOUNTER-WANDER
	name <Enemy Mecha>
	update <if= P4 1 else GoHide  if= P2 1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	ENCOUNTER_Hostile

	STC ENCOUNTER-WANDER
	name <Enemy Mecha>
	update <if= P5 1 else GoHide  if= P2 1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	ENCOUNTER_Hostile

end



