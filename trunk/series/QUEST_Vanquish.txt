%%
%% *:Vanquish Content
%%
%% The PC has just defeated someone or something. Because of this, an
%% event of some kind will happen. Often the PC will be offered a job
%% by someone else as a result of his heroism.
%% The calling quest will set this subquest's ID when the time comes.
%%
%% Param1: The thing vanquished; will be referenced in name only.
%%
%% Subtypes:
%% .Criminal		The PC vanquished a criminal
%% .Monster		The PC vanquished a monster

Content
	name <Tiny Monsters>
	desc <After the PC defeats a real monster, get job exterminating rats.>
	requires <*:Vanquish .Monster BRANCHES>

	% E1 is the monster that was vanquished
	% E2 is a mechanic with rats in the basement
	% E3 is the basement full of rats
	Element2 <Prefab>
	Scene2 <Building Public Garage>
	Team2 <Citizens>
	TeamData2 <Pass>

	Scene3 <New New *ExterminationBasement>

	% SubQuest1 is the victory event
	Quest1 <*SUCCESS1.Extermination 2>

	sub
		Persona 2
		% V1 = Have given rat quest
		rumor%id% <%name2% needs a monster-slayer like you to clear out some vermin.>
		greeting <if= QuestStatus %qid% %id% else GoGreet if= SceneVar %s3% 1 1 else GoCheckQuest EndChat Say 8 AddChat 8>
		GoCheckQuest <if= V1 1 else GoQuestIntro NewChat Say 6 AddChat 6 AddChat 7>
		*GoQuestIntro <*DoYouWantAJob&Chara GoQuestDetails>
		GoQuestDetails <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoGreet <*NiceToMeetYou GoShop>
		*GoShop <*SHOP_GENERAL GoBye>
		*GoBye <*GOODBYE>
		result1 <NewChat Say 2 QMemo %qid% 3 SceneVar= %s3% 11 1 UpdateProps V= 1 1>
		result2 <NewChat Say 4 AddCheerful -d8 SetQuestStatus %qid% -2 AddReact -d10>
		result3 <NewChat Say 5 AddChat 4 AddChat 5 AddCheerful d8>
		result4 <Goto result2>
		result5 <Goto result1>
		result6 <Goto GoShop>
		result7 <NewChat Say 7>
		result8 <Goto .%id1%_GoInit>
		Msg1 <I've got some rats in the storeroom beneath my shop. Since you're the one who defeated %name1%, I figure this should be easy for you.>
		Msg2 <Great! You can find the way down in the back room over there. Come see me when you're finished and I'll give you a reward then.>
		Msg3 <%name2% at \SCENE %s2% asked you to exterminate the rats from \PPR %2% storeroom.>
		Msg4 <So, after fighting %name1% you think you're too good for a regular job like this, eh? Fine, be like that.>
		Msg5 <Not at all. Unlike slaughtering legendary monsters on your own, this job actually pays money. How about it?>
		Msg6 <The rats are still down there. Please hurry; I'm counting on you.>
		Msg7 <I hope you finish before the rats drive away all my customers.>
		msg8 <Finally, the rats are gone. I'd like to thank you for your help.>
		Prompt1 <Alright, I'll help you.>
		Prompt2 <Find someone else to clean your basement.>
		Prompt3 <Isn't pest control a big step down for me?>
		Prompt4 <Sorry, I don't want to.>
		Prompt5 <Okay, I'll do it.>
		Prompt6 <I need to get my stuff repaired.>
		Prompt7 <I'm working on it.>
		Prompt8 <[Continue]>

		MetaScene 2
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 SetQuestStatus %qid1% -2 Goto %pop%>
		sub
			room
			minimap <2#...+#.......1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Door
				update <if= V1 0 else GoCheckQuest V= 1 1 SetStat STAT_Lock %sktar_hard%>
				GoCheckQuest <if= SceneVar %s3% 11 1 SetStat STAT_Lock 0>
			end
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s3%>
				use <if= SceneVar %s3% 11 1 else GoNotYet Print 1 Exit Destination>
				GoNotYet <Print 11>
				Msg11 <You don't want to go down there. It smells really bad.>
			end
		end
	end
	inv
		NPC Mechanic
	end

Content
	name <Work for Shopkeeper>
	desc <A shopkeeper will offer a new mission.>
	requires <*:Vanquish BRANCHES>

	% E1 is the entity that was vanquished.
	% E2 is a shopkeeper.
	Element2 <Prefab>
	Scene2 <Building Public Mall>
	Team2 <Guards>
	TeamData2 <SD Ally>

	% SubQuest1 is the mission.
	% SubQuest2 is the victory, after the mission is completed.
	Quest1 <*:GiveMission 2>
	Quest2 <*Success1.FinishMission 2>

	sub
		Persona 2
		rumor%id% <%name2% really wants to speak with you.>
		greeting <if= QuestStatus %qid% %id% else GoChat if= QUestStatus %qid1% -1 else GoCheckLoss EndChat Say 1 AddChat 1>
		GoCheckLoss <if= QuestStatus %qid1% -2 else GoCheckFirst SetQuestStatus %qid% -2 Goto GoEndInLoss>
		*GoEndInLoss <*MissionWasFailure na ChatNPCFac na>
		GoCheckFirst <NewChat Say 2 AddChat 2 AddChat 3>
		*GoChat <*NiceToMeetYou GoShop>
		*GoShop <*SHOP_GENERAL GoBye>
		*GoBye <*GOODBYE>
		result1 <Goto .%id2%_GoInit>
		result2 <Goto .%id1%_GoInit>
		result3 <NewChat Say 3 SetQuestStatus %qid% -2 AddReact -d20>
		Msg1 <I knew you were the right person to get the job done.>
		Msg2 <Ah, \PC , I was hoping I'd get the chance to speak with you. I have something you might be able to help with.>
		Msg3 <Oh, I see... Well, don't let me keep you, then.>
		Prompt1 <[Continue]>
		Prompt2 <I'd be happy to help.>
		Prompt3 <Sorry, I'm too busy for that now.>

		MetaScene 2
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 SetQuestStatus %qid1% -2 Goto %pop%>
		sub
			room
			minimap <..............1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end
	end
	inv
		NPC Shopkeeper
	end

Content
	name <L5Law Likes You>
	desc <A police officer will offer a new mission.>
	requires <*:Vanquish .Criminal ("FCOMS"|"RISHI"|"MAQUI") BRANCHES>

	% E1 is the criminal that was vanquished.
	% E2 is a L5Law officer.
	Element2 <Prefab>
	Scene2 <Building Public>
	Team2 <Guards>
	TeamData2 <SD Ally>

	% SubQuest1 is the mission.
	% SubQuest2 is the victory, after the mission is completed.
	Quest1 <*:GiveMission 2>
	Quest2 <*Success1.FinishMission 2>

	sub
		Persona 2
		rumor%id% <%name2% is impressed with how you dealt with %name1%.>
		greeting <if= QuestStatus %qid% %id% else GoChat if= QUestStatus %qid1% -1 else GoCheckLoss EndChat Say 1 AddChat 1>
		GoCheckLoss <if= QuestStatus %qid1% -2 else GoCheckFirst SetQuestStatus %qid% -2 Goto GoEndInLoss>
		*GoEndInLoss <*MissionWasFailure na ChatNPCFac na>
		% If the PC is an enemy of this NPC, no mission will be offered but at least
		% he'll get a reward.
		GoCheckFirst <ifFactionEnemy ChatNPCFac else GoOfferQuest NewChat Say 2 CashPrize Reward %threat% 100 SetQuestStatus %qid% -1 AddLawful 1>
		GoOfferQuest <NewChat Say 3 AddChat 2 AddChat 3>
		GoChat <NewChat SayAnything>
		result1 <Goto .%id2%_GoInit>
		result2 <Goto .%id1%_GoInit>
		result3 <NewChat Say 4 SetQuestStatus %qid% -2>
		Msg1 <I hear that your mission was a success.>
		Msg2 <You did a good thing defeating %name1%. Here, you deserve this reward.>
		Msg3 <Hello \PC . Word is that you're the one who defeated %name1% . L5Law could use your help with another case, if you're willing.>
		Msg4 <That's a shame.>
		Prompt1 <[Continue]>
		Prompt2 <Give me the details.>
		Prompt3 <No, I'm tired of catching crooks.>

		MetaScene 2
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 SetQuestStatus %qid1% -2 Goto %pop%>
	end
	inv
		NPC Police Officer
		SetFaction 9
	end



