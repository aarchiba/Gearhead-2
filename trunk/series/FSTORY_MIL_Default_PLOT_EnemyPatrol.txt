Plot 0

name <Enemy Patrol>

context <=MIL_DEFAULT_OFFENSE>

element1 <Character HasMecha !Comrade -2>
element2 <Scene Environs !Near -3>
element3 <Prefab>
place3 <2>

% We need to store the two factions involved for later...
Element4 <Grab 1>
Element5 <Grab 2>

% P1 = Timer
% P2 = The faction the PC will be fighting; either N1 or N2
% P3 = Combat has started; after combat has started, don't delete this plot from cleanup or timeout.
%	This var also functions as the initialization counter for the metascene.
% P4 = Victory condition achieved
% P5 = Difficulcy Rating
% P6 = Difficulcy Percentile
% P7 = Bomb chance calculator


start <ifNPCOK E1 else CleanUp ifG V1 0 Goto GoCheckTime>
update <ifNPCOK E1 else CleanUp if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 86400 MoveNPC E1 E3  SetNPCTeam E1 2  SetEncounterName E3 .name  P= 2 N2  P= 6 100  P= 5 PCRenown P+ 5 d5 P+ 5 -d5 ifG 2 P5 P= 5 d4>
.name <\ELEMENT 1 's Patrol>
GoCheckTime <ifG ComTime V1  if= P3 0   S+ 101 -1   EndPlot>
CleanUp <if= P3 0  EndPlot>

5Min <if= P3 0  if# P1 0   if= RootSceneID N3   if= d12 1   ifScene .scene   P= 7 NPCRep E2 1  P+ 7 * 10 S101   ifG -d70 P7 Goto GoBomb>
.scene <SCALE0>
GoBomb <Alert 1   AirRaidSiren   Bomb Bomb   EndPlot>

Msg1 <Without warning, the city is attacked by \NARRATIVE 2 ! Explosions go off all around you.>
Msg1_1 <Suddenly, the city is attacked by \NARRATIVE 2 ! Explosions go off all around you.>

sub
	Persona 1
	% V1 = Reward offered
	special <UNLISTED NOESCAPE>
	greeting <if= P4 1 else GoNoWin CashPrize V1  EndPlot  Goto GoWeWon>
	*GoWeWon <*WeWon&Reward E5 E4>

	GoNoWin <if= P3 1 else GoCheckEnemy   EndChat SayAnything>

	GoCheckEnemy <Print 1   ifFactionAlly N2 else GoNoMember   P= 2 N1  SetNPCTeam E1 -1  P= 6 250   Goto GoMemberJoin>
	*GoMemberJoin <*JoinOurFacPatrol GoJoinPatrol GoExit N2 N1>
	*GoJoinPatrol <*LetsStartPatrol GoStartCombat>
	GoStartCombat <Exit E3>
	GoExit <EndPlot>

	*GoNoMember <*BattleChallenge&Avoid GoThemeInfo GoExit>
	*GoThemeInfo <*THEME_EXPO&Enemy GoStartCombat>



	MetaScene 3 2
	MapWidth 30
	MapHeight 30
	content <Some 1 15 Sub *BATTLE_X ->

	% P3 = Initialization
	% L2 = Victory Counter

	start <if= P3 0 P= 3 1 PCFEnemy P2>

	nu1 <if= T1 0 Return if= L2 0 L= 2 1  AddRenown -8                    Goto GoLoss>
	nu2 <if= T2 0        if= L2 0 L= 2 1  XPV 100  AddRenown 1   P= 4 1   Goto GoVictory>

	% The PC has won this encounter... what happens next depends on who the PC was fighting.
	GoVictory <if= P2 E5 else GoVictoryForInvaders  S+ 101 1  FacXP+ E4 1   SALVAGE  EndPlot>
	GoVictoryForInvaders <S+ 101 -1   FacXP+ E5 2    ifNPCOK E1 else GoE1Died ForceChat E1>
	GoE1Died <EndPlot>

	% The PC has lost this encounter... what happens next depends on who the PC was fighting.
	GoLoss <if= P2 E5 else GoLossForInvaders  S+ 101 -1  EndPlot>
	GoLossForInvaders <S+ 101 1   EndPlot>

	sub
		Team 1
		SetAlly 3
		SetEnemy 2
		ParaX 5
		ParaY 5

		Team 2
		SetEnemy 1 3
		Deploy <SetSelfFaction P2   WMecha 2 Threat P5 P6>
		ParaX 25
		ParaY 25

		Team 3
		SetAlly 1
		SetEnemy 2

	end

end
inv
	STC ENCOUNTER-WANDER
	use <ifG StatVal STAT_MetaVisibility -1      ifG PCScale 0   Print 5 ForceChat E1>
	ATTACK <ifG StatVal STAT_MetaVisibility -1    ifG PCScale 0  ifUStealth 15 else GoAutoAttack ifYesNo 1 2 3 else GoAvoidAttack Goto Print 6 ForceChat E1>
	GoAutoAttack <Print 5 ForceChat E1>
	GoAvoidAttack <Print 4 AddSociable -1>
	Msg1 <You spot an attack patrol led by \ELEMENT 1 of \FACTION NPCFac E1 .>
	Msg2 <Contact \ELEMENT 1 .>
	Msg3 <Avoid \ELEMENT 1 .>
	Msg4 <You slip away unnoticed.>
	Msg5 <You are contacted by \ELEMENT 1 .>
	Msg6 <You contact \ELEMENT 1 .>
	ENCOUNTER_Hostile

end


