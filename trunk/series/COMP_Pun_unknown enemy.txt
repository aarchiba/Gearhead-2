%% COMP_Pun
%%	+Pun	An unknown enemy attacks

Plot
	name <Friend On Patrol>
	desc <Your friend is on patrol.>
	requires <#A +Pun +Tgt (+Fin|+F--|+Fch) (T:FRIEND|T:ALLY|T:LOVER|T:FAMILY) (T:MILIT|T:ADVEN|T:LABOR|T:POLIC) S:-->
	size 4

	% E1 is a local environs scene
	% E2 is a metascene to place there
	% E3 is the friend
	% E4 is a scene to meet at later
	Element1 <Scene Environs !Near -7>
	Element2 <Prefab>
	Place2 <1>
	Element3 <Grab 5>
	Place3 <2 sd ally (Friend)>
	Element4 <Scene (Meeting|Mall) Public !Near -7>

	update <if= PlotStatus 0   SetPlotStatus %id%   NPCLevel %3% StoryDL>

	% SubPlot1 is what happens if the PC aids the NPC.
	SubPlot1 <*CS_ThanksForAssist 3>

	sub
		MetaScene 2 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		% L1 = Match Over counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1 ForceChat %3% StoryNote 1>
		% If the NPC dies, this scenario ends in a loss.
		Faint%3% <L= 1 1  StoryNote 2  LoseComp 0 .loss>
		.loss <+T-- +F-->

		% If the PC defeats all of T2 before the NPC dies, it's a win.
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1               StoryNote 3   LoseComp 0 .loss>

		nu2 <if= T2 0   if= V1 0 V= 1 1   XPV 100  AddRenown 1   Alert 4 MoveNPC %3% %4% SMemo 5 History 6 SetPlotStatus %id1%>

		Msg1 <You found %name3% being attacked by mecha.>
		Msg2 <%name3% was killed in battle, and you were powerless to stop it.>
		Msg3 <You failed to help %name3% win the battle.>
		Msg4 <With the enemy mecha defeated, %name3% races off for %name4%.>
		Msg5 <You helped %name3% defeat some hostile mecha. You can speak with \OPR %3% at %name4% now.>
		Msg6 <You helped %name3% to win the battle.>

		sub
			team 1
			SetAlly 3
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			SetEnemy 1 3
			Deploy <SetSelfFaction &EnemyFac   WMecha 2 StoryDL 120>
			ParaX 25
			ParaY 25

			team 3
			name <Friend>
			SetAlly 1
			SetEnemy 2
			ParaX 10
			ParaY 5
		end

		Persona 3
		special <Unlisted>
		% V%id%01 = Introduction counter
		rumor%id% <%name3%'s mecha hasn't returned from %name1%.>
		greeting <if= PlotStatus %id% else GoChat if= V%id%01 1 else GoFirst Goto GoChat>
		GoChat <NewChat SayAnything>
		GoFirst <V= %id%01 1 EndChat Say %id%01 AddChat %id%01>
		*result%id%01 <*LetsDefeatThem &EnemyFac>
		Msg%id%01 <\PC , I could use a little help over here. I was out on patrol and got attacked by these thugs.>
		Prompt%id%01 <Don't worry, I've got your back.>
	end
	inv
		STC ENCOUNTER-WANDER
		name <%name3%'s Mecha>
		EncounterMove 75
	end

Plot
	name <Horton Hears a Zaku-II>
	desc <The PC can meet someone who has seen mecha outside.>
	requires <#A +Pun +T-->
	Size 1

	% E1 is a local, public scene
	% E2 is the trucker who saw the whotsit
	% E3 is an environs scene
	% E4 is the encounter to be placed there
	element1 <Scene !Near -7 public>
	element2 <Character LABOR !Near -7 NeverFail>
	NeverFail2 <Trucker>
	place2 <1 pass>
	element3 <Scene Environs !Near -7>
	element4 <Prefab>
	Place4 <3>

	start <ifNPCDead %2% LoseComp 0 .next>
	.next <+T-- +F-->

	sub
		Persona 2
		rumor <%name2% saw something odd.>
		*greeting <*IHaveInformation GoExplain>
		GoExplain <NewChat Say %id%01 StoryNote %id%02 AddChat %id%01 AddChat %id%02 NTBWinComp %4% .next>
		.next <+Ten +F-->
		result%id%01 <NewChat Say %id%03>
		result%id%02 <NewChat Say %id%04 AlterContext .worse>
		.worse <+Ffi>
		Msg%id%01 <While driving around, I saw something weird. It was a group of mecha, but they didn't look like locals... they looked like they were up to no good.>
		Msg%id%02 <%name2% saw some scary looking mecha in %name3%.>
		Msg%id%03 <Be careful. They didn't look like they were the type to take prisoners.>
		Msg%id%04 <I'm telling you exactly what I saw! Don't blame me when those meks come and kick your ass...>
		Prompt%id%01 <Thanks for telling me.>
		Prompt%id%01_1 <I'll have to go see about that.>
		Prompt%id%01_2 <Hmm... this could be important.>
		Prompt%id%02 <Are you sure you weren't imagining things?>
		Prompt%id%02_1 <You haven't been drinking, have you?>
		Prompt%id%02_2 <That doesn't sound too likely.>
	end
	inv
		STC ENCOUNTER-WANDER
		name <%name2%'s Tip>
		EncounterMove 25
	end

Plot
	name <So That's Who You Work For>
	desc <You learn who the enemy NPC has been working for.>
	requires <#A +Pun +Ten E:++ F:-->
	Size 7

	% Element1 is the metascene for the battle
	% Element2 is the enemy character
	% Element3 is the faction he's working for
	Element1 <Grab 6>
	Element2 <Grab 1>
	Place2 <1 (Enemies) SD>
	Element3 <Faction ArchEnemy !OKay 2>

	% P1 = Initialization Counter
	update <if= P1 0 P= 1 1 NPCLevel %2% StoryDL SetNPCFaction %2% %3%>

	sub
		Persona 2
		special <Unlisted NoEscape>
		greeting <EndChat Say %id%01 AddChat %id%01>
		result%id%01 <EndChat Say %id%02>
		Msg%id%01 <Oh, it's you. I should have expected that we would meet again... it serves me right for not being more thorough the last time. My masters at %name3% will be upset.>
		Msg%id%02 <Don't be so sure of yourself. You might be able to face me, but you don't stand a chance against our combined might.>
		Prompt%id%01 <They'll be even more upset when I defeat you again.>

		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		% L1 = Match Over counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1 ForceChat %2% &SetEnemyFac %3% StoryNote 1>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   Goto GoLoseMission>
		nu2 <if= T2 0            if= V1 0 V= 1 1   Goto GoWinMission>

		GoWinMission <XPV 100  AddRenown 1   Alert 2 WinComp 0 .next>
		GoLoseMission <AddRenown -5 LoseComp 0 .next>
		.next <+T-- +F-->

		Msg1 <You learned that %name2% was working for %name3%.>
		Msg2 <You have defeated %name2%, but the real threat is now %name3%.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			name <Enemies>
			Deploy <SetSelfFaction %3%   WMecha 2 StoryDL 30>
			SetEnemy 1
			ParaX 25
			ParaY 25
		end
	end

Plot
	name <Pirate War>
	desc <The thief you're looking for knows about the pirate war.>
	requires <#A +Tht (+Far|+Ffi|+F--) +Pun T:++ F:-- -T:ENEMY -P:CRIHN (T:THIEF|T:CRIHN) (L:FCOMS|L:MAQUI|L:RISHI|L:CRIHN|L:BOHEM) ~C:THEIF ~+Gmo>
	size 6

	% E1 is an environs scene where the pirates are hanging out
	% E2 is the metascene for the encounter
	% E3 is the pirate faction
	% E4 is a public scene
	% E5 is a pirate captain who the PC can fight, and learn about the war
	% E6 is the target NPC
	element1 <Scene Environs !Near -7>
	element2 <Prefab>
	Place2 <1>
	element3 <Faction CRIHN>
	element4 <Scene Building Public !Okay 3 !Near -7>
	element5 <Character HasMecha !Comrade 3 !Near 4 NeverFail>
	NeverFail5 <Pirate>
	element6 <Grab 5>

	% P%id%01 = Initialization Counter
	update <if= p%id%01 0 p= %id%01 1 NPCLevel %5% StoryDL MoveNPC %5% %2% SetNPCTeam %5% 2>

	sub
		MetaScene 2 2
		rumor <there have been a lot of pirates fighting in %name1%.>
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1   StoryNote 1   ForceChat %5%    &SetEnemyFac %3%>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1                                   LoseComp 0 .next>
		nu2 <if= T2 0            if= V1 0 V= 1 1   XPV 100  AddRenown 1   SALVAGE   WinComp 0 .next ifNPCOK %5% PCEnemy %5%>
		.next <+Tht +Fin>
		Msg1 <You learned from %name5% that %name6% has a secret that the pirates are fighting to get.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction E2   WMecha 2 StoryDL 30>
			ParaX 25
			ParaY 25
		end

		Persona 5
		special <UNLISTED NOESCAPE>
		rumor <all the pirates in town have been looking for %name6%.>
		greeting <EndChat Say 1 AddChat 1 AddChat 2>
		result1 <EndChat Say 2 AddChat 3>
		result2 <EndChat Say 3 AddChat 3>
		result3 <EndChat Say 4>
		Msg1 <You there... The word on the street is that you can tell me where %name6% is.>
		Msg2 <Don't play games. I know that you've been looking for %name6%, just like we all have. Maybe you even found out \PPR %6% secret already. If you want to live you'd better share that knowledge with me.>
		Msg3 <Ah, a clever ploy, but it's not going to work. I know that you've been looking for %name6%, just like we all have. Maybe you even found out \PPR %6% secret already. If you want to live you'd better share that knowledge with me.>
		Msg4 <Five captains of the Blades have declared war to find out what %name6% knows. Maybe you really don't have any part in what's going on... but that's not a chance I'm willing to take.>
		prompt1 <Me? I know nothing.>
		prompt2 <And here I thought you were the one who could tell me.>
		Prompt3 <Look, I don't understand what you're talking about.>
	end
	inv
		Encounter
		name <Pirates>
		EncounterMove 50
	end

Plot
	name <City Under Attack>
	desc <The leader of the city puts out a call for heroes.>
	requires <#A +T-- +Pun F:-- -L:ENEMY ~L:ALLY ~P:-- ~+Gpe>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the meme, for extra coverage
	element1 <Scene Building Public !Ally -7 !Near -7>
	element2 <Character POLIT !Near -7 !Comrade -7 NeverFail>
	NeverFail2 <Politician>
	Place2 <1 (Citizens) pass>
	Element3 <Prefab>

	% All this plot does is move the NPC, post some news,
	% and activate the mission subplot
	update <if= PlotStatus 0  News 1  ActivateMeme %3% %1%  SetPlotStatus %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	Msg1_1  <%name2% of \NARRATIVE 7 seeks cavaliers to help defend the town. Interested parties should apply at %name1%.>
	Msg1_2  <\NARRATIVE 7 is under attack! %name2% will be interviewing cavaliers for defense missions at %name1%.>
	Msg1_3  <Unknown mecha are attacking \SCENE &EpisodeScene . %name2% has put out a call for volunteers; applicants are to meet at %name1%.>
	Msg1_4  <Is \SCENE &EpisodeScene under attack? %name2% has been meeting with mecha pilots at %name1%.>
	Msg1_5  <%name2% has called on adventurers to defend \NARRATIVE 7 from a mysterious enemy. Defense efforts are being coordinated from %name1%.>

	inv
		Meme
		MaxMemeViews 1
		MemeTimeLimit
		Msg <%name2% is looking for someone to fight the mecha that have been attacking town.>
		Msg_1 <You might be interested to know that %name2% is looking for some mecha pilots at %name1%. I think it has something to do with the mysterious attacks we've had.>
		CMsg_1 <ifNPCSociable Accept>
		Msg_2 <There was something on the news about those mecha attacks.>
		CMsg_2 <ifNPCShy Accept>
		Msg_3 <If you're interested in the recent mecha attacks, I hear that %name2% at %name1% is looking for someone to fight them.>
		CMsg_3 <ifNPCEasygoing Accept>
		Msg_4 <>
		CMsg_4 <ifNPCPassionate Accept>
		Msg_5 <%name2% will find out what's going on with those mecha attacks. I'm sure of it.>
		CMsg_5 <ifNPCCheerful Accept>
		Msg_6 <Is there going to be a war? I hope %name2% can find out who sent those mecha to attack us.>
		CMsg_6 <ifNPCMelancholy Accept>
	end

Plot
	name <Shrine Destroyed>
	desc <A shrine is destroyed; the caretaker demands action!>
	requires <#A +T-- +Pun C:FAITH I:-->

	% E1 is a metascene
	% E2 is a public scene; the "natural home" of the monk
	% E3 is the monk who will tell the PC about the situation
	% E4 is the artifact the attackers may have been looking for
	% E5 is a location for a combat mission
	% E6 is the other researcher
	element1 <Metascene  !Near -7 *Building NeverFail>
	element2 <Scene Public Legit !Near -7>
	element3 <Character "FAITH" !Near 2 NeverFail>
	neverfail3 <Monk>
	Place3 <1 (Citizens) Pass>
	element4 <Artifact PreZero>
	element5 <Scene Environs !Near -7>
	element6 <Prefab>

	% P1 = Initialization Counter

	start <if= PlotStatus 0 ifNPCOK E3 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1    News 1   FreezeItem E4>

	Msg1 <The Ecumenical Temple of \NARRATIVE 7 was heavily damaged today when a force of unidentified mecha attacked %name1%.>

	% SubPlot1 is the combat encounter, should the PC want to go after the people
	%  who did this.
	SubPlot1 <*CS_MechaRetribution 3 5>

	sub
		MetaScene 1
		% The Ecumenical Temple
		ClubMap
		MapWidth 20
		MapHeight 25
		type <TEMPLE LEGIT BUILDING PUBLIC>
		special <AddExit>
		nu1 <if= T1 0 Return>
		inv
			STC SHRINE-1
		end
		sub
			Team 1

			Team 2
			name <Civilian>
			setally 1
			Passive

			room
			name <Foyer>
			desig <EntranceGrid>
			Content <Some 4 45 Here *URBAN_X>
		end

		Persona 3
		% The persona of the monk to give the mission...
		greeting <if# PlotStatus 0 else GoStartPlot NewChat SayAnything>
		*GoStartPlot <*DestructionGreeting GoTellStory &EnemyFac>
		GoTellStory <NewChat ifNPCPassionate else GoEasyStory Say 1 AddChat 1 AddChat 2>
		GoEasyStory <Say 2 AddChat 3 AddChat 4>
		result1 <NewChat Say 3 History 10 SMemo 4 History 11  AddEasygoing -10 Goto GoPassionateWin>
		GoPassionateWin <&SetTargetItem %4% SetPlotStatus %id1%>
		result2 <NewChat Say 5 AddChat 1 AddChat 3>
		result3 <NewChat Say 6 AddChat 4 AddChat 5>
		result4 <NewChat Say 7 AddChat 7 AddChat 6>
		result5 <NewChat Say 8 History 10 StoryNote 9  AddEasygoing 10  Goto GoEasyWin>
		GoEasyWin <FreezeNPC %6% &SetTargetNPC %6% &SetTargetItem %4%  NegateComp 0 .easygoing>
		.easygoing <+Pla +Tgt +Fin>
		result6 <Goto result5>
		result7 <Goto result1>
		Msg1 <The animals who destroyed this temple must be punished.>
		Msg2 <They must have attacked this place because they were looking for the %name4% ... It's not here any longer, but I fear what dark purpose they have planned for it.>
		Msg3 <They can't have gotten far... You should be able to find them somewhere in %name5%.>
		Msg4 <%name3% asked you to find the mecha in %name5% who attacked \PPR E3 temple.>
		Msg5 <They must have been searching for the %name4% . It was here once, a long time ago, but was moved years ago.>
		Msg6 <It's an artifact from PreZero times; a relic from the past... and perhaps something more. Professor %name6% was researching it but I don't know where it is now.>
		Msg7 <There will be time for that later. Right now, I'm concerned about %name6% ... \SPR %6% has been researching the %name4% and the same thugs who attacked this temple may go after \OPR %6% .>
		Msg8 <Go find Professor %name6% , then, before the people who attacked the temple find \OPR %6% .>
		Msg9 <%name3% asked you to find %name6% , who knows about the %name4%.>
		Msg10 <Unknown mecha destroyed the Ecumenical Temple in \NARRATIVE 7 .>
		Msg11 <%name3% asked you to find the mecha who attacked \PPR E3 temple.>
		Prompt1 <I could bring them to justice.>
		Prompt1_1 <I could bring make them pay for it.>
		Prompt2 <Why did they attack this temple?>
		Prompt2_1 <Why was this place attacked?>
		Prompt3 <What is the %name4%?>
		Prompt4 <The people responsible for this must be punished.>
		Prompt5 <I could find it for you.>
		Prompt6 <I'll find %name6% and protect \OPR %6% .>
		Prompt7 <It'd be better if I found those mecha.>
	end
	inv
		NPC Professor
		chardesc Old Heroic Spiritual
	end

