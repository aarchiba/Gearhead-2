% Component List

% STATE OF STORY DESCRIPTION
%  The state of the story is determined by both the "context" attribute held in
%  the story and by certain variables which are determined when it's time to
%  generate a new component. These variables could include:
%  - The existence and nature of the palette entries
%  - The alleigance of the current location with either the PC's faction
%    or the palette enemy faction

Plot
	name <Bad Party>
	desc <We're going to a bad party. Actually, meet a bored arena pilot and go duel.>
	requires <#A +Tgs +Fpa ~+Gpo>
	Size 1

	% Element1 is the scene where this will take place
	% Element2 is the bored arena pilot
	% Element3 is an environs scene for the duel
	Element1 <Grab 6>
	Element2 <Character HasMecha PDAss !Near -7 Neverfail>
	NeverFail2 <Arena Pilot>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% V1 = Initialization Counter
	% V2 = Partner Observation
	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% 0 ifNPCDead %2% else GoCheckScene LoseComp 0 .changes>
	GoCheckScene <if= SceneID %1% ifNPCOK &PartnerNPC if= V2 0  Monologue &PartnerNPC 3 V= 2 1>
	.changes <+T-- +F-->

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% Branch activators
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %plotid% %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg3 <Look over there... %name2% doesn't look like \SPR %2% 's enjoying the party.>
	Msg3_1 <Say, isn't that %name2% the famous arena pilot? I wonder if we can get an autograph... or a duel.>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% 0 else GoChat Goto GoFirstTime>
		*GoChat <*MISC_CHATTER>
		GoFirstTime <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <StoryNote 2 SetPlotStatus %plotid% %id1% Goto GoAcceptChallenge>
		*GoAcceptChallenge <*IAcceptYourChallenge %3%>
		result2 <NewChat Say 3 StoryNote 4 LoseComp 0 .next>
		.next <+T-- +F-->
		Msg1 <Are you as bored with this party as I am? Why don't we leave this place and go find some real action... I mean a duel, of course.>
		Msg2 <At the party, %name2% challenged you to a duel in %name3%.>
		Msg3 <Is that so? I'm afraid to say that you've just blown the one chance you had for something interesting.>
		Msg4 <At the party, %name2% challenged you to a duel but you refused.>
		Prompt1 <Let's do it.>
		Prompt2 <Sorry, I don't want to.>

		STC MS_CAFE
		SetID 1
	end


Plot
	name <What's the point?>
	desc <Your partner will ask what happens next.>
	requires <#A +T-- R:++ +G-->
	Size 1

	% Element1 is the partner
	% Element2 is a local metascene- useful for many things
	% Element3 is a character to ask for a job (+Gmo)
	% Element4 is a character to challenge to a duel (+Gpo)
	Element1 <Grab 4>
	Element2 <MetaScene !Near -7 *Building NeverFail>
	Element3 <Character HasMecha !Okay 1 !Okay -3 !Xclude -2 !Near -7 -ArchEnemy>
	Element4 <Character HasMecha !Okay 1 !Near -7>

	% P1 = Have unlocked conversation by visiting destination scene
	start <ifNPCOK %1% else GoEndPlot ifScene .scenetype if= RootSceneID &EpisodeScene if= P1 0     P= 1 1  ForceChat %1% SMemo 1>
	.scenetype <outdoors>
	GoEndPlot <StoryNote 2 AddRenown -10   LoseComp 0 .loss>
	.loss <+Bad>

	Msg1 <%name1% wants to speak with you about something.>
	Msg2 <%name1% died.>

	sub
		Persona 1
		% V1 = First Reputation Counter
		% V2 = Skill to improve
		greeting <if= P1 0 else GoReveal NewChat SayAnything>
		*GoReveal <*LancemateWantsToTalk GoNow GoLater>
		GoLater <EndChat Say %id%01>
		GoNow <NewChat Say %id%02 AddChat %id%01 AddChat %id%02 AddChat %id%03 AddChat %id%04>
		GoNoMeeting <NewChat Say %id%03 StoryNote %id%04 WinComp %2% .party>
		.party <+Tgs +Fpa>
		result%id%01 <AlterContext .altermoney  ifNPCOK %3% else GoNoMeeting NewChat Say %id%05 StoryNote %id%06 &SetTargetNPC %3% WinComp %2% .mission>
		.altermoney <+Gmo>
		.mission <+Tgt +Fmi>
		result%id%02 <AlterContext .alterpower  ifNPCOK %4% else GoNoMeeting NewChat Say %id%07 StoryNote %id%08 &SetTargetNPC %4% WinComp %2% .duel>
		.alterpower <+Gpo>
		.duel <+Tgt +Fch>
		result%id%03 <AlterContext .alterlove  NewChat Say %id%03 StoryNote %id%04 WinComp %2% .party>
		.alterlove <+Glo>
		result%id%04 <AlterContext .alterpeace  NewChat Say %id%03 StoryNote %id%04 WinComp %2% .party>
		.alterpeace <+Gpe>
		Msg%id%01 <That's fine. It wasn't too important... ask me again later.>
		Msg%id%02 <\PC , why are we doing this? What is it that made you want to be a cavalier?>
		Msg%id%03 <So that's it. You see, I heard that there's going to be a party at %name2% soon and a lot of cavaliers are going to be there. I wanted to get into the proper mindset before we crash the joint.>
		Msg%id%04 <%name1% suggested that you crash a party at %name2%.>
		Msg%id%05 <Money is very important. Hey, I heard that %name3% is looking to hire someone over at %name2%. Let's go over there and see if we can get the job.>
		Msg%id%06 <%name1% suggested that you go ask %name3% for a mission at %name2%.>
		Msg%id%07 <Neat. Well, the way to become the best is by challenging other pilots... I've heard that %name4% has been spoiling for a fight recently. Let's go to %name2% and ask \OPE %4% for a duel.>
		Msg%id%08 <%name1% suggested that you challenge %name4% at %name2%.>
		Prompt%id%01 <I'm in it for the money.>
		Prompt%id%02 <I just want to be the best pilot ever.>
		Prompt%id%03 <Cavaliers are sexy. I like that.>
		Prompt%id%04 <I want to make a real difference in the world.>
	end
	

Plot
	name <Generic Battle Encounter>
	desc <You go out looking for a fight, and you find a fight.>
	requires <#A +Ten (+Ffi|+F--) E:++>
	Size 8

	% Element1 is the encounter
	% Element2 is the enemy NPC
	Element1 <Grab 6>
	Element2 <Grab 1>
	Place2 <1 sd enemy (Enemies)>

	update <if= v%id%01 0 v= %id%01 1 NPCLevel %2% StoryDL>

	sub
		Metascene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		% L1 = Match Over counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1 ForceChat %2%>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1               StoryNote 1   LoseComp 0 .changes>
		nu2 <if= T2 0   if= V1 0 V= 1 1   XPV 100  AddRenown 1   Alert 2  Salvage StoryNote 3  WinComp 0 .changes>
		.changes <+T-- +F-->

		Msg1 <You encountered %name2%, but lost the battle.>
		Msg2 <You have defeated %name2%'s lance.>
		Msg3 <You encountered %name2% and defeated \OPR %2% in battle.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			name <Enemies>
			SetEnemy 1
			Deploy <SetSelfFaction &EnemyFac   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25
		end

		Persona 2
		special <UNLISTED>
		greeting <ifChatNPCKnown else GoFirstMeeting Goto GoAlreadyMet>
		*GoFirstMeeting <*BattleIntroduction GoThemeExpo>
		*GoAlreadyMet <*BattleChallenge GoThemeExpo GoThemeExpo>
		*GoThemeExpo <*Theme_Expo&Enemy>
	end

Plot
	name <They're Out There!>
	desc <Research will reveal an encounter to investigate.>
	requires <#A +Tgs (+F--|+Fin|+Fmi) S:++>
	Size 1

	% Element1 is the scene being investigated
	% Element2 is the pilot who saw the thing
	% Element3 is an outdoors scene
	% Element4 is an encounter to place there
	Element1 <Grab 6>
	Element2 <Character HasMecha !Near -7 -ENEMY NeverFail>
	NeverFail2 <Mecha Pilot>
	Place2 <1 pass ally (Citizens)>
	Element3 <Scene Outdoors !Near -7>
	Element4 <Prefab>
	Place4 <3>

	start <ifNPCDead %2% LoseComp 0 .loss>
	.loss <+T-- +F-->

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% got beaten up in %name3%.>
		*greeting <*IHaveInformation GoReveal na>
		GoReveal <NewChat Say 1 StoryNote 2 WinComp %4% .next>
		.next <+Ten +Ffi>
		Msg1 <While traveling through %name3% I was attacked by mecha. I don't know who they are, but I was lucky to escape with my life.>
		Msg2 <%name2% told you that he had been attacked in %name3%.>

		STC MS_CAFE
		SetID 1
	end
	inv
		STC ENCOUNTER-WANDER
		name <%name2%'s Tip>
		EncounterMove 25
	end

Plot
	name <Partner Arrange Duel>
	desc <Your partner has arranged a duel between you and someone else.>
	requires <#A +T-- R:PDASS>

	% Element1 is the partner
	% Element2 is the person to ask for a duel
	% Element3 is a scene for the meeting
	Element1 <Grab 4>
	Element2 <Character HasMecha !Okay 1 !Near -7>
	Element3 <Scene Building Public !Near -7 !Okay 2>

	% P1 = Have unlocked conversation by visiting destination scene
	start <ifNPCOK %1% else GoEndPlot ifScene .scenetype if= RootSceneID &EpisodeScene if= P1 0     P= 1 1  ForceChat %1% SMemo 1>
	.scenetype <outdoors>
	GoEndPlot <StoryNote 2 AddRenown -10   LoseComp 0 .loss>
	.loss <+Bad>

	Msg1 <%name1% wants to speak with you about something.>
	Msg2 <%name1% died.>

	sub
		Persona 1
		% V1 = First Reputation Counter
		% V2 = Skill to improve
		greeting <if= P1 0 else GoReveal NewChat SayAnything>
		*GoReveal <*LancemateWantsToTalk GoNow GoLater>
		GoNow <ifNPCOK %2% else GoE2Died NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		GoE2Died <NewChat Say %id%02 StoryNote %id%03 WinComp %3% .dead>
		.dead <+Tgs +Fin>
		GoLater <EndChat Say %id%04>
		result%id%01 <NewChat Say %id%05 SMemo %id%06 History %id%07 &SetTargetNPC %2% WinComp %3% .duel>
		.duel <+Tgt +Fch>
		result%id%02 <NewChat Say %id%08 StoryNote %id%03 WinComp %3% .dead>
		result%id%03 <NewChat Say %id%09 AddChat %id%04 AddChat %id%05>
		result%id%04 <goto result%id%01>
		result%id%05 <goto result%id%02>
		msg%id%01 <Since we're not doing anything right now, I was wondering if you'd be interested in a duel with %name2%. I'm pretty sure that \SPR %2% 'd be willing.>
		Msg%id%02 <I heard a rumor that there's something going on at %name3%. I think we should go check it out.>
		Msg%id%03 <%name1% suggested that you look for information at %name3%.>
		Msg%id%04 <Don't forget to ask me about it later, then.>
		Msg%id%05 <Great! We can meet \OPR %2% at %name3%. Let's get over there right away.>
		Msg%id%06 <%name1% suggested that you challenge %name2% to a duel. You can meet \OPR %2% at %name3%.>
		Msg%id%07 <%name1% suggested that you challenge %name2% to a duel.>
		Msg%id%08 <Whatever you say, \PC . Look, when you get tired of doing whatever important work it is that you're doing, I think we should head over to %name3%. I've heard rumors that something's going on there.>
		Msg%id%09 <Well, it might be fun. You'll get some experience out of it. Does a mecha pilot really need a reason?>
		Prompt%id%01 <Alright, that sounds like fun.>
		Prompt%id%02 <No way. I've got better things to do.>
		Prompt%id%03 <Why would I want to do that?>
		Prompt%id%04 <Fine. Let's go do it.>
		Prompt%id%05 <That's not a convincing argument.>
	end

Plot
	name <Partner Job Lead>
	desc <Your partner has a message for you.>
	requires <#A +T-- R:++ -R:NOFAC -R:PDASS>

	% Element1 is the partner
	% Element2 is the episode town
	% Element3 is the person to ask for a mission
	% Element4 is a scene for the meeting
	Element1 <Grab 4>
	Element2 <Grab 7>
	Element3 <Character !Ally 1 !Okay -3 !Near 2>
	Element4 <Scene Building Public !Near 2 !Okay 3>

	% P1 = Have unlocked conversation by visiting destination scene
	start <ifNPCOK %1% else GoEndPlot if= SceneID %2% if= P1 0     P= 1 1  ForceChat %1% SMemo 1>
	GoEndPlot <StoryNote 2 AddRenown -10   LoseComp 0 .loss>
	.loss <+Cfa +Bad>

	Msg1 <%name1% wants to speak with you about something.>
	Msg2 <%name1% died.>

	sub
		Persona 1
		% V1 = First Reputation Counter
		% V2 = Skill to improve
		greeting <if= P1 0 else GoReveal NewChat SayAnything>
		*GoReveal <*LancemateWantsToTalk GoNow GoLater>
		GoNow <NewChat Say %id%01 SMemo %id%03 AddChat %id%01 ifNPCOK %3% else GoE3Died &SetTargetNPC %3% WinComp %4% .next>
		.next <+Tgt +Fmi>
		GoE3Died <WinComp %4% .dead>
		.dead <+Tgs +Fin>
		GoLater <EndChat Say %id%02>
		result%id%01 <NewChat Say %id%04 SMemo %id%05>
		Msg%id%01 <I got a call from %name3%. We've been asked to go to %name4% for a mission.>
		Msg%id%02 <Fine. Ask me about it later, then.>
		Msg%id%03 <You should meet %name3% at %name4% for a mission.>
		Msg%id%04 <Really? Well, maybe we should head over there and see if we can find any information, anyhow.>
		Msg%id%05 <You should search %name4% for information.>
		Prompt%id%01 <Didn't you hear? %name3% is dead.>
		CPrompt%id%01 <ifNPCDead %3% Accept>
	end

Plot
	name <Protect, then Inform>
	desc <The enemy character has sent some mecha to trail this NPC; the PC can get info if he destroys those mecha.>
	requires <#A +Tpt +Fin F:++ S:++>

	% E1 is the scene where this will take place
	% E2 is the NPC to be protected
	% E3 is the outdoors scene where the encounter is
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass ally>
	element3 <Scene Outdoors !Near -7>

	% SubPlot1 is the combat encounter
	% SubPlot2 is the revelation of this NPC
	SubPlot1 <*CS_InvisibleEncounter 3>
	SubPlot2 <*CS_Revelation&Friendly 2>

	start <if= PlotStatus %plotid% %id% else GoCheckFirst ifNPCDead %2% LoseComp 0 .loss>
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoJustEnd StoryNote 1 Alert 2 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <DeleteNPC %2%  Goto GoJustEnd>
	GoJustEnd <LoseComp 0 .loss>
	.loss <+T-- +F-->

	Msg1 <You defeated the mecha which were threatening %name2%.>
	Msg2 <You have defeated the mecha which were threatening %name2%.>

	sub
		Persona 2
		greeting <if# PlotStatus %plotid% 0 else GoGreet if# PlotStatus %plotid% %id1% else GoRemind NewChat SayAnything>
		*GoGreet <*ProtectCheckIn GoMissionInfo>
		*GoRemind <*MechaMissionReminder %3%>
		GoMissionInfo <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		result%id%01 <NewChat Say %id%02 SMemo %id%03 History %id%04 SetPlotStatus %plotid% %id1%>
		result%id%02 <EndChat Say %id%05 AddChat %id%03>
		result%id%03 <AddHeroic -1 AddReact -10 Goto .%id2%_GoInit>
		Msg%id%01 <You're not here to protect me; you're just trying to find out what I know. You're no better than \FACTION &EnemyFac .>
		Msg%id%02 <You think you can do that? They've sent a mecha that follows me wherever I go. Get rid of it, then we can talk... and if you fail, at least I'll have one less person bothering me.>
		Msg%id%03 <%name2% is being harassed by \FACTION &EnemyFac . If you can destroy the mecha they sent to follow \OPR %2% , \SPR %2% will give you some information.>
		Msg%id%04 <You agreed to defeat the mecha harassing %name2%.>
		Msg%id%05 <Er... You make a persuasive argument.>
		Prompt%id%01 <If I make them go away, will you help me?>
		Prompt%id%02 <Tell me what you know, or you'll find out I'm worse.>
		CPrompt%id%02 <ifIntimidation HardSkillTar StoryDL Accept>
		Prompt%id%03 <[Continue]>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <Drive Off Attacker>
	desc <You walk in just as NPC is about to be attacked.>
	requires <#A +Tpt -Fin T:++ S:++ ~C:POLIC>

	% E1 is the scene where this will take place
	% E2 is the NPC to be rescued
	% E3 is the attacker
	% E4 is the exterior scene where the battle will take place
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass ally>
	element3 <Prefab>
	Place3 <1 (Citizens) pass ally>
	element4 <Scene Outdoors !Near -7>

	% SubPlot1 is the combat with E3
	% SubPlot2 is the victory
	SubPlot1 <*CS_NPCAmbushPC 4 3>
	SubPlot2 <*CS_ThanksForRescue 2>

	start <if= PlotStatus %plotid% %id% else GoCheckFirst ifNPCDead %2% LoseComp 0 .loss>
	GoCheckFirst <if= PlotStatus %plotid% 0 if= SceneID %1% Monologue %3% 1 Monologue %2% 2 Monologue %3% 3 StoryNote 4 Print 5 FreezeNPC %3% SetPlotStatus %plotid% %id% SetNPCFaction %3% &EnemyFac>
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoJustEnd StoryNote 6 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <DeleteNPC %2%  Goto GoJustEnd>
	GoJustEnd <LoseComp 0 .loss>
	.loss <+T-- +F-->

	Msg1 <Hey %name2%, it's time for you to get what's coming to you.>
	Msg2 <Not so fast- it looks like my reinforcements have just arrived.>
	Msg3 <Hmph. You win this time, %name2%, but I'll be back later...>
	Msg4 <You scared off %name3% before \SPR %3% could attack %name2%.>
	Msg5 <%name3% leaves in a hurry.>
	Msg6 <You were ambushed by %name3% , but defeated \OPR %3% .>

	sub
		Persona 2
		special <UNLISTED>
		rumor%id2% <%name2% wants to thank you for defeating %name3%.>
		greeting <if= PlotStatus %plotid% %id% else GoChat NewChat Say 1 AddChat 1 AddChat 2 EraseSpecial>
		GoChat <NewChat SayAnything>
		result1 <NewChat Say 2 SMemo 3 AddReact 10 &SetTargetNPC %3% NegateComp 0 .FindE3>
		.FindE3 <+Tht +Far>
		result2 <NewChat Say 4 SMemo 5 PCEnemy %3% SetPlotStatus %plotid% %id1%>
		Msg1 <\PC , thanks for driving off %name3%. I was pretty worried there for a minute... The problem is, I know that \SPR %3% 's just going to be back later.>
		Msg2 <You plan to go after \OPR %3% ? Good luck.>
		Msg3 <You told %name2% that you'd find %name3% before \SPR %3% can attack \OPR %2% again.>
		Msg4 <You showed up at the right time; that's all you had to do, today. I'm sure %name3% isn't going to forget this, though.>
		Msg5 <After driving off %name3%, %name2% warned you that \SPR %2% might come looking for revenge.>
		Prompt1 <Not if I find \OPR %3% first.>
		Prompt2 <I didn't even really do anything.>

		STC MS_CAFE
		SetID 1
	end
	inv
		NPC Soldier
		job <Hired Thug>
		job_desig <ADVEN>
		chardesc villainous criminal
	end

Plot
	name <Level That House>
	size 1
	desc <The meeting place is attacked by mecha!>
	requires <#A +Tpt T:++ S:++ S:meta>

	% P1 = Initialization Counter

	% E1 is the scene where this will take place
	% E2 is the NPC the PC wants to talk to
	% E3 is the scene outside the meeting area
	% E4 is a public building, if the player chooses to sneak
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass ally>
	element3 <Scene Urban !Near -7>
	element4 <Scene Building Public !Near -7 !Okay 2>

	% SubPlot1 contains the ambush battle
	% SubPlot2 contains the victory, if the PC defeats the enemies
	SubPlot1 <*CS_AmbushEncounter 3>
	SubPlot2 <*CS_ThanksForRescue 2>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .loss>
	.loss <+T-- +F-->

	% Needed Scripts
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoJustEnd Alert 4 StoryNote 2 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <ifNPCOK %2% else GoJustEnd Alert 3 StoryNote 1 DeleteNPC %2% Goto GoJustEnd>
	GoJustEnd <LoseComp 0 .loss>
	.loss <+T-- +Pun>
	Msg1 <You couldn't protect %name2% from a mecha attack.>
	Msg2 <You rescued %name2% from a mecha attack against %name1%.>
	Msg3 <%name1% is blasted by the mecha, killing those trapped inside.>
	Msg4 <You have defended %name2% from the mecha attack.>

	sub
		MetaScene 1
		% L1 = Bombing started
		% L2 = Bomb counter

		BoxMap
		MapWidth 15
		MapHeight 15

		special <AddExit>

		sub
			Team 1

			Team 2
			name <Citizens>
			setally 1
			Passive

			room
			name <Dance Floor>
			desig <EntranceGrid>
		end

		Persona 2
		special <UNLISTED>
		greeting <if= PlotStatus %plotid% %id1% else GoCheckFirstMeeting EndChat Say 1>
		GoCheckFirstMeeting <if= PlotStatus %plotid% 0 else GoChat Goto GoGreet>
		GoSetAttack <StoryNote 2 SetPlotStatus %plotid% %id1%>
		GoChat <NewChat SayAnything>
		*GoGreet <*ProtectCheckIn GoMiisionInfo>
		GoMiisionInfo <NewChat Say 3 AddChat 1 AddChat 2 Goto GoSetAttack>
		result1 <NewChat Say 4>
		result2 <NewChat Say 5 AddChat 3 AddChat 4>
		result3 <EndChat Say 6 AddReact d10 Print 7 XPV 100 Exit %4% SeekGate %3% MoveNPC %2% %4% StoryNote 8 SetPlotStatus %plotid% %id2%>
		result4 <Goto result1>
		Msg1 <We can't leave here while those mecha are outside...>
		Msg2 <You found %name2% trapped in %name1% by hostile mecha.>
		Msg3_10 <I can't leave this building. There are people outside, with mecha... I'm certain that \PERSONA &EnemyNPC sent them.>
		CMsg3_10 <ifNPCOK &EnemyNPC Accept>
		Msg3_20 <I can't leave this building. There are people outside, with mecha... The second I show my face in public I'll be dead.>
		CMsg3_20 <ifNPCDead &EnemyNPC Accept>
		Msg4 <Good luck.>
		Msg5 <Are you sure? That sounds awfully dangerous...>
		Msg6 <I'll believe in you. Let's go.>
		Msg7 <You sneak %name2% out of the building, and the two of you proceed to %name4%.>
		Msg8 <You sneaked %name2% away to safety.>
		Prompt1 <I can handle the mecha.>
		Prompt2 <Why don't we just sneak past them?>
		CPrompt2 <IfStealth HardSkillTar StoryDL Accept>
		Prompt3 <Trust me. I know what I'm doing.>
		Prompt4 <You could be right. I'll just fight the mecha.>
	end

Plot
	name <Convention Press Conference>
	desc <This convention is about to feature a press conference.>
	requires <#A +Tgs (+Fcc|+Fin) S:META>

	% E1 is the scene where the conference will take place
	% E2 is a local scene; natural home to E4
	% E3 is the character doing the talking
	Element1 <Grab 6>
	Element2 <Scene !Near -7 !Okay -3 Public>
	Element3 <NewNPC 2 2>
	Place3 <2>

	% V1 = Initialization Counter
	update <if= V1 0 MoveNPC %3% %1% V= 1 1>
	start <if= PlotStatus %plotid% 0 if= SceneID %1% Alert 1 SMemo 2 SetPlotStatus %plotid% %id1%>

	Msg1 <At the convention hall, %name3% appears to be just finishing up a speech.>
	Msg2 <At %name1%, %name3% just gave a speech about something.>

	%% SubPlot1 is the press conference
	SubPlot1 <*CS_PressConference 1 3>

	sub
		STC MS_PRESENTATION
		SetID 1
	end

Plot
	name <Rescue Reward>
	desc <The PC has to speak to a person he just rescued.>
	requires <#A +Tgt +Frt S:++>
	Size 1

	% E1 is the scene where the meeting will take place.
	% E2 is the person who was rescued.
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All the update does is start the rescue reward going...
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	% The subplot contains the reaction/reward.
	SubPlot1 <*CS_ThanksForRescue 2>

	sub
		STC MS_CAFE
		SetID 1
	end

Plot
	name <Locate Target NPC: Perm>
	desc <The PC needs to find an NPC for some reason, but no scene exists.>
	requires <#A +Tpt S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is the character who knows where E1 is.
	% E3 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Character !Near -7 !Okay 1>
	Element3 <Scene !Near -7 Building Public !Okay 1>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% NegateComp 0 na>

	sub
		Persona 2
		rumor <%name2% has seen %name1%.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %3% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <As far as I know, %name1% is now at %name3%. I hope \SPR %1% isn't in any trouble.>
		Msg2 <%name2% told you that %name1% was at %name3%.>
	end

Plot
	name <Locate Target NPC: Meta>
	desc <The PC needs to find an NPC for some reason, but no scene exists.>
	requires <#A +Tpt S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is the character who knows where E1 is.
	% E3 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Character !Near -7 !Okay 1>
	Element3 <MetaScene !Near -7 *Building NeverFail>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% NegateComp 0 na>

	sub
		Persona 2
		rumor <%name2% has seen %name1%.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %3% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <As far as I know, %name1% is now at %name3%. I hope \SPR %1% isn't in any trouble.>
		Msg2 <%name2% told you that %name1% was at %name3%.>
	end

Plot
	name <Parking Ticket Blues>
	desc <The PC's friend needs to pay off some fines.>
	requires <#A +Tgt S:-- T:THIEF T:FRIEND>
	Size 3

	% E1 is the character being sought.
	% E2 is the police officer who's looking for him.
	% E3 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Character !Near -7 POLIC>
	Element3 <Scene !Near -7 Building Public !Okay 1>

	start <if= PlotStatus %plotid% 0 ifNPCDead %3% NegateComp 0 na>

	sub
		Persona 2
		rumor <%name2% has been looking for %name1%.>
		% V1 = Fine Counter
		greeting <if= V1 0 else GoTell Goto GoCheckFirst>
		GoCheckFirst <*LookingForNPC&NoFailure %1% GoTell GoTell>
		GoTell <V= 1 Threat StoryDL 5 NewChat Say 1 StoryNote 2 AddChat 1 AddChat 2 AddChat 3 AddChat 4>
		GoReveal <StoryNote 10 WinComp %3% na>
		result1 <NewChat Say 3 AddChat 5 AddChat 4>
		result2 <ifConversation SkillTar &StoryDL else GoR2Fail NewChat Say 4 Goto GoReveal>
		GoR2Fail <NewChat Say 5 AddChat 1 AddChat 3 AddChat 4>
		result3 <AddHeroic -1 NewChat Say 6 StoryNote 7 &SetTargetNPC 0 NegateComp %3% .negate>
		.negate <+Tgs +Fmi>
		result4 <NewChat Say 8>
		result5 <NewChat Say 9 Cash+ -V1 Goto GoReveal>
		Msg1 <%name1% is in hiding. I know this because I'm the one looking for \OPR %1% ; \SPR %1% 's got $ \VAL v1 worth of unpaid fines, and if I don't get that money then \SPR %1% 's going straight to the lockup.>
		Msg2 <You learned from %name2% that %name1% has many unpaid fines.>
		Msg3 <It would be kind of unorthodox, but as long as the fines get paid it's not my business to judge. That's be the judges.>
		Msg4 <Well, if you say it's important, I'll give %name1% some more time to raise the money... as long as \SPR %1% doesn't do anything illegal to raise it. Now that the heat is off you'll probably be able to find \OPR %1% at %name3% again.>
		Msg5 <Nothing is more important than upholding the law. I'm afraid I can't let up one bit.>
		Msg6 <Nope. I can't say that I do... though you might want to check at %name3%. I hear there's a lot going on over there.>
		Msg7 <After abandoning %name1%, %name2% suggested you find a job at %name3%.>
		Msg8 <Maybe while you're gone, %name1% will come back and pay the fines... I doubt it though.>
		Msg9 <There we go. Now that the fines are paid I expect you'll be able to find %name1% at %name3% again.>
		Msg10 <%name2% told you that %name1% could likely be found at %name3%.>
		Prompt1 <What about if I paid the fines?>
		CPrompt1 <ifG PC$ V1 Accept>
		Prompt2 <Look, could you cut \OPR %1% a little slack? It's important.>
		CPrompt2 <ifChatNPCAlly Accept>
		Prompt3 <Forget about %name1%, then. Do you know any job leads?>
		Prompt4 <I'll be back later.>
		Prompt5 <Here you go. [Give Money]>
	end

Plot
	name <Degenerate Lookfer: Perm>
	desc <The PC needs to find an NPC for some reason. After speaking with an NPC, the search changes to +Tgt.>
	requires <#A +Tgt S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is a public scene in the city, needed for NeverFail
	% E3 is the character who knows where E1 is.
	% E4 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Scene Public !Near -7 !Okay 1>
	Element3 <Character !Near -7 !Okay 1>
	Element4 <Scene !Near -7 Building Public !Okay 1>

	start <if= PlotStatus %plotid% 0 ifNPCDead %3% NegateComp 0 na>

	sub
		Persona 3
		rumor <%name3% knows where %name1% is.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %4% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <The last time I saw \OPR %1% , %name1% was going to %name4%. That's where you should probably check.>
		Msg2 <%name3% told you that %name1% was at %name4%.>
	end

Plot
	name <Degenerate Lookfer: Meta>
	desc <The PC needs to find an NPC for some reason. After speaking with an NPC, the search changes to +Tgt.>
	requires <#A +Tgt S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is a public scene in the city, needed for NeverFail
	% E3 is the character who knows where E1 is.
	% E4 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Scene Public !Near -7 !Okay 1>
	Element3 <Character !Near 2 !Okay 1 NeverFail>
	NeverFail3 <Citizen>
	Element4 <MetaScene !Near -7 *Building NeverFail>

	start <if= PlotStatus %plotid% 0 ifNPCDead %3% NegateComp 0 na>

	sub
		Persona 3
		rumor <%name3% knows where %name1% is.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %4% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <The last time I saw \OPR %1% , %name1% was going to %name4%. That's where you should probably check.>
		Msg2 <%name3% told you that %name1% was at %name4%.>
	end

Content
	name <I Won't Be An Easy Fight>
	desc <Your target, basically a noncom character, claims he won't be an easy fight.>
	requires <#A +Tht (+Ffi|+Fin) S:META (T:ACADE|T:CRAFT|T:MEDIA|T:MEDIC|T:POLIT|T:TRADE) F:++ -T:ENEMY>
	Size 5

	% E1 is the target
	% E2 is the scene
	% E3 is an outdoors scene for the enemy base
	% E4 is an encounter for the enemy base
	Element1 <Grab 5>
	Place1 <2 (Enemies) SD>
	Element2 <Grab 6>
	Element3 <Scene Environs !Near -7>
	Element4 <Prefab>
	Place4 <3>

	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id% NPCLevel %1% StoryDL>

	% SubPlot1 is the enemy base
	SubPlot1 <*CS_RaidEnemyBase&Research 4>

	sub
		Persona 1
		special <Unlisted>
		greeting <ifChatNPCSurrendered else .%id%_GoAsk Goto .%id%_GoSurrender>
		*.%id%_GoAsk <*WhatDoYouWantWithMe&Information .%id%_GoRefuse>
		.%id%_GoRefuse <EndChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoSurrender <EndChat Say %id%03 SMemo %id%04 History %id%05 ShowEntrance %4% SetPlotStatus %plotid% %id1% FreezeNPC %1% XPV 100>
		result%id%01 <EndChat Say %id%02>
		result%id%02 <ifIntimidation HardSkillTar StoryDL else .%id%_GoR2Fail Goto .%id%_GoSurrender>
		.%id%_GoR2Fail <AddRenown -1 EndChat Say %id%06>
		Msg%id%01 <Don't expect me to roll over and surrender just because I don't look like a fighter. I can hold my own against you, no problem.>
		Msg%id%02 <You'll see what I'm capable of!>
		Msg%id%03 <Alright, I surrender! I was hired by \FACTION &EnemyFac to work at their base in %name3%. You can take your problems to them; I want no further part in this...>
		Msg%id%04 <%name1% revealed that \FACTION &EnemyFac has a secret base in %name3%.>
		Msg%id%05 <%name1% revealed that \FACTION &EnemyFac had a secret base.>
		Msg%id%06 <I wish I could say that the feeling was mutual!>
		Prompt%id%01 <Good enough. Let's do it.>
		Prompt%id%02 <Don't be stupid. I don't want to kill you.>

		MetaScene 2
		BoxMap
		MapWidth 13
		MapHeight 13
		special <AddExit>
		% V1 = Initialization Counter
		% V3 = Kill E1 Counter
		start <Print 1 if= V1 0 V= 1 1 ForceChat %1%>
		FAINT%1% <if= V3 0 V= 3 1 History 2>
		nu1 <if= T1 0 Return if= PlotStatus %plotid% %id% StoryNote 1 LoseComp 0 .next>
		.next <+T-- +F-->

		Msg1 <You didn't learn anything from %name1%'s hideout.>
		Msg2 <You killed %name1% in personal combat.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Enemies>
			setEnemy 1
		end
		inv
			STC Computer-1
			% If the PC kills the NPC, he can get the info from this computer.
			MiniMapComponent 2
			use <if= PlotStatus %plotid% %id% else GoNoNeed ifNPCDead %1% else GoNPCAlive Print 1>
			clue37 <if= PlotStatus %plotid% %id% else GoNoNeed ifNPCDead %1% else GoNPCAlive ifInvestigation SkillTar StoryDL else GoFail Alert 2 Goto GoReveal>
			clue32 <if= PlotStatus %plotid% %id% else GoNoNeed ifNPCDead %1% else GoNPCAlive ifCodeBreaking SkillTar StoryDL else GoFail Alert 3 Goto GoReveal>
			GoNPCAlive <Print 4>
			GoNoNeed <Print 5>
			GoFail <Print 6>
			GoReveal <SMemo 7 History 8 XPV 100 ShowEntrance %4% SetPlotStatus %plotid% %id1%>
			Msg1 <You could probably learn some things from this computer, if only you knew how.>
			Msg2 <In the front drawer of the computer desk you find some handwritten notes indicating that \FACTION &EnemyFac has a base in %name3%.>
			Msg3 <You hack into this computer and learn that \FACTION &EnemyFac has a base in %name3%.>
			Msg4 <You really don't have time to be playing with this computer while %name1% is still around.>
			Msg5 <There's nothing else you can learn from this computer.>
			Msg6 <You fail to learn anything from this computer.>
			Msg7 <You learned from %name1%'s computer that \FACTION &EnemyFac has a base in %name3%.>
			Msg8 <You learned from %name1%'s computer that \FACTION &EnemyFac had a base in %name3%.>
		end
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <the Secret Base>
	end


Content
	name <Robot Monster>
	desc <That target you've been chasing left a robot monster for you. How thoughtful.>
	requires <#A +Tht S:META E:-- (T:ACADE|T:CORPO|T:CRAFT)>
	size 5

	% E1 is the target
	% E2 is the scene
	% E3 is the robot monster
	Element1 <Grab 5>
	Place1 </>
	Element2 <Grab 6>
	Element3 <Prefab>
	Place3 <2 (Monster) sd>

	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id% ItemLevel %3% StoryDL>

	sub
		MetaScene 2
		special <NOEXIT>
		BoxMap
		MapWidth 21
		MapHeight 16

		% L1 = Initialization Counter
		% L2 = Victory Counter
		start <Print 1 if= L1 0 L= 1 1 Monologue %1% 2 Monologue %1% 4 &SetEnemyNPC %1% StoryNote 3>
		nu1 <if= T1 0 Return if= L2 0 L= 2 1 LoseComp 0 .next>
		nu2 <if= T2 0 if= L2 0 L= 2 1 Monologue %1% 5 Print 6 XPV 100 WinComp 0 .next>
		.next <+T-- +F-->

		Msg1 <You enter %name1%'s hideout.>
		Msg2 <You've done well in finding my hideout, \PC . I hope you won't be disappointed that I'm no longer there.>
		Msg3 <You found %name1%'s hideout, but \SPR %1% left a robot monster for you.>
		Msg4 <I've left a special present behind for you. I haven't given him a name yet, but maybe you can think up one in the final minutes you have left to live.>
		Msg5 <Fascinating. I'll have to use you to test all of my new weapons from now on.>
		Msg6 <You hear a click sound from the trapdoor.>

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monster>
			SetEnemy 1

			room
			special <STARTHERE>
			minimap <............2............>
			sub
				TrapDoor
				Destination -1
				MiniMapComponent 2
				use <if# L2 0 else GoLocked Print 1 Return>
				GoLocked <Print 101>
				msg101 <This trapdoor is barred from the other side. There's no way to get out.>
			end
		end
	end
	inv
		Monster Androbot
		name <Robot Monster>
	end

Content
	name <Target Has Hostage>
	desc <That target you've been chasing has a hostage.>
	requires <#A +Tht S:META (+Far|+Ffi) (T:MEDIC|T:THIEF|T:ADVEN)>
	Size 6

	% E3 is the hostage
	Element1 <Grab 5>
	Place1 <2 (Citizens) pass>
	Element2 <Grab 6>
	Element3 <NewNPC -3 -7>
	Place3 <2 (Citizens) pass>

	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id% NPCLevel %1% StoryDL>

	sub
		Persona 1
		% The person being sought... may be forced to surrender.
		Special <UNLISTED>
		greeting <if= PlotStatus %plotid% %id% else .%id%_GoChat ifChatNPCSurrendered else .%id%_GoFirstTime Goto .%id%_GoWillSurrender>
		.%id%_GoChat <NewChat SayAnything>
		.%id%_GoFirstTime <if= L2 0 else .%id%_GoGreet SoloAttack %1% L= 2 1 Goto .%id%_GoGreet>
		.%id%_GoGreet <EndChat Say %id%01 AddChat %id%01 AddChat %id%02>
		*.%id%_GoWillSurrender <*ISurrender&Arrest .%id%_GoSurrender>
		*.%id%_GoWillResist <*IDisagreeYouDie na>
		.%id%_GoSurrender <L= 2 2 Print %id%02 FreezeNPC %1% PCEnemy %1% ifNPCOK %3% else .%id%_GoE3Died StoryNote %id%03 Print %id%04 FreezeNPC %3% &SetTargetNPC %3% WinComp 0 .%id%_win>
		.%id%_GoE3Died <StoryNote %id%05 WinComp 0 .%id%_loss>
		.%id%_win <+Tgt +Frt>
		.%id%_loss <+T-- +F-->
		result%id%01 <ifConversation HardSkillTar StoryDL else .%id%_GoWillResist FreezeNPC %3% EndChat Say %id%06 XPV 100>
		result%id%02 <ifIntimidation HardSkillTar StoryDL else .%id%_GoWillResist EndChat Say %id%07 AddChat %id%03>
		result%id%03 <Goto .%id%_GoWillSurrender>
		Msg%id%01_10 <Why do you have to make this difficult, \PC ? I didn't want to hurt anyone today but it seems that you're tying my hands...>
		CMsg%id%01_10 <ifChatNPCKnown Accept>
		Msg%id%01_20 <You're getting involved in something you don't understand. %name3% and I have unfinished business.>
		CMsg%id%01_20 <ifChatNPCUnknown Accept>
		Msg%id%02 <%name1% is captured.>
		Msg%id%03 <You rescued %name3% from %name1%.>
		Msg%id%04 <%name3% slips away and disappears...>
		Msg%id%05 <You captured %name1%, but not soon enough to save %name3%.>
		Msg%id%06 <I'll let %name3% go, all right, but I've got no interest in talking...>
		Msg%id%07 <Wait, I've got a hostage, you can't attack me now!>
		Prompt%id%01 <Let %name3% go and we can talk.>
		Prompt%id%01_1 <You don't need a hostage; let %name3% go.>
		Prompt%id%01_2 <I want you to release %name3%.>
		Prompt%id%02 <Give up now before I'm forced to kill you.>
		Prompt%id%02_1 <You have one chance to surrender, then you die.>
		Prompt%id%02_2 <It's over, %name1%. Give up.>
		Prompt%id%03 <Do I look like I care?>

		Metascene 2
		% Contains scripts for the scene.
		% L1 = Initialization Counter
		% L2 = Have started fight (1), have ended fight (2)
		start <if= L1 0 L= 1 1 Alert 1 Monologue %3% 2 ForceChat %1%>
		end <if= L2 1 FreezeNPC %1% PCEnemy %1% DeleteNPC %3% StoryNote 3 LoseComp 0 .loss>
		.loss <+T-- +F-->
		Surrender%1% <ForceChat %1%>
		Faint%1% <L= 2 2 ifNPCOK %3% else GoE3Died StoryNote 4 Print 5 FreezeNPC %3% &SetTargetNPC %3% WinComp 0 .win>
		GoE3Died <StoryNote 6 WinComp 0 .loss>
		.win <+Tgt +Frt>
		Msg1 <As you enter %name1%'s hideout, you see that \SPR %1% has a hostage.>
		Msg2 <Please, help me get away from here!>
		Msg3 <You failed to save %name3% from %name1%.>
		Msg4 <You rescued %name3% from %name1%.>
		Msg5 <In the chaos of battle, %name3% slips away and disappears...>
		Msg6 <You defeated %name1%, but not soon enough to save %name3%.>

		sub
			Team 1
			SetAlly 3
			SetEnemy -1

			Team 3
			name <Citizens>
			SetEnemy -1
			SetAlly 1
			Passive
		end
	end

Content
	name <Target becomes Enemy>
	desc <That target you've been chasing? Surprise! He's your new enemy!>
	requires <#A +Tht S:++ (+Far|+Ffi) E:-- ~+P-->
	Size 1

	% E1 is the target
	% E2 is the scene
	% E3 is the note left for the PC
	Element1 <Grab 5>
	Place1 </>
	Element2 <Grab 6>
	Element3 <Prefab>
	Place3 <2>

	start <if= SceneID %2% Alert 1 History 2 &SetEnemyNPC %1% &SetTargetNPC 0 WinComp 0 .%id%_Next>
	.%id%_Next <+T-- +F-->

	Msg1 <There's no sign of %name1% here; \SPR %1% must have known you were coming.>
	Msg2 <%name1% evaded your attempt to capture \OPR %1% .>

	sub
		STC MS_EmptyBuilding
		SetID 2
	end
	inv
		Treasure 1
		name <Note>
		desc <Don't bother looking for me. From now on, I'll be looking for you instead. - %name1%>
	end


Plot
	name <Tell Me About The Base>
	desc <The target will tell you about an enemy base nearby.>
	requires <#A +Tht S:++ (+Fin|+F--) ~1:MILIT>
	Size 1

	% E1 is the NPC being sought
	% E2 is his scene
	% E3 is an outdoors scene for the enemy base
	% E4 is an encounter for the enemy base
	Element1 <Grab 5>
	Place1 <2 (Citizens) Pass>
	Element2 <Grab 6>
	Element3 <Scene Outdoors !Near -7>
	Element4 <Prefab>
	Place4 <3>

	start <ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	% SubPlot1 is the enemy base
	SubPlot1 <*CS_RaidEnemyBase 4>

	sub
		Persona 1
		special <UNLISTED>
		*greeting <*WhatDoYouWantWithMe&Information .%id%_GoTell>
		.%id%_GoTell <NewChat Say %id%01 AddChat %id%01 AddChat %id%02 SMemo %id%02 History %id%03 ShowEntrance %4% SetPlotStatus %plotid% %id1%>
		result%id%01 <NewChat Say %id%04 AddCheerful d10>
		result%id%02 <EndChat Say %id%05 FreezeNPC %1% PCEnemy %1% SkillXP 28 100 AddCheerful -d10>
		Msg%id%01 <I don't know much of anything... There's a base nearby and they hired me to do a few jobs, but I didn't hear anything.>
		Msg%id%01_1 <I don't know anything, really! I came here to work at a secret base and I did a couple of missions but I don't even know what the purpose was!>
		Msg%id%02 <%name1% told you that there is a secret base in %name3%.>
		Msg%id%03 <%name1% told you that there was a secret base nearby.>
		Msg%id%04 <Just make sure no-one finds out we were talking... that could be very bad for my health.>
		Msg%id%05 <Next time we meet, hopefully it'll be on more equal terms. Goodbye, \PC ...>
		Prompt%id%01 <You've been most helpful.>
		Prompt%id%01_1 <Thanks. That's all I need to know.>
		Prompt%id%02 <That's it? Get out of here before I lose my temper.>
		Prompt%id%02_1 <If I thought you were lying now, I'd kill you.>

		STC MS_EmptyBuilding
		SetID 2
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <the Secret Base>
	end


Plot
	name <Basic Hideout>
	requires <#A +Tht S:-->
	desc <A NPC will tell the PC where to find the target's hideout>
	Size 2

	% E1 is the character being sought
	% E2 is an urban scene, for placing the encounter
	% E3 is the encounter/metascene for the hideout
	% E4 is a character who can tell the PC where the hideout is.
	Element1 <Grab 5>
	Place1 </>
	Element2 <Scene Outdoors !Near -7>
	Element3 <Prefab>
	Place3 <2>
	Element4 <Character !Near -7 !Okay 1>

	% If the person with the knowledge dies, do we give up? Of course not! Keep ahead in
	% the same course as before.
	Start <if= PlotStatus %plotid% 0  ifNPCDead %4% LoseComp 0 na>
sub
	Persona 4
	rumor <%name4% knows where %name1% is.>
	*greeting <*LookingForNPC %1% GoSuccess GoFailure>
	GoSuccess <NewChat Say 1 SMemo 2 WinComp %3% na>
	*GoFailure <*BrushOff&Questioning>
	Msg1 <I'll tell you- %name1% used to have a hideout in %name2%. If you're looking for \OPR %1% , that's the first place I'd check.>
	Msg2 <%name4% told you that %name1%'s hideout can be found in %name2%.>
end
inv
	STC ENCOUNTER-DUEL
	name <%name1%'s Hideout>
	ENCOUNTER_Hostile
end

Plot
	name <Basic Challenge>
	desc <You're going to challenge some guy.>
	requires <#A +Tgt +Fch T:++ S:++ Common>

	% E1 is the scene where the meeting will take place.
	% E2 is the friend being met.
	% E3 is a location for the duel.
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+T-- +F-->

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% Branch activators
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %plotid% %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% 0 else GoChat Goto GoFirstTime>
		*GoChat <*MISC_CHATTER>
		*GoFirstTime <*PCChallengeNPC GoMakeChallenge GoWussOut>
		GoMakeChallenge <StoryNote 1 SetPlotStatus %plotid% %id1% Goto GoAcceptChallenge>
		*GoAcceptChallenge <*IAcceptYourChallenge %3%>
		*GoWussOut <*GodImBored>
		Msg1 <You challenged %name2% to a duel in %name3%.>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <Lead: Politics>
	desc <A politician will call about something.>
	requires <#A +T-- C:POLIT -L:ENEMY>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Near -7>
	element2 <Character !Near 1 POLIT NeverFail -ArchEnemy>
	NeverFail2 <Bureaucrat>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC, send an email,
	% and activate the mission subplot
	5min <if= PlotStatus %plotid% 0  if= RootSceneID &EpisodeScene V+ 1 1 ifG v1 d6 EMail 1  SetPlotStatus %plotid% %id1%>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .next>
	.next <+T-- +F-->

	%% SubPlot1 is the conversation to be had
	SubPlot1 <*CS_Conversation 2>

	Msg1_1  <%name2%@ \NARRATIVE 7 :// There are matters I want to discuss. Meet me at %name1%.>
	Msg1_2  <%name2%@ \NARRATIVE 7 :// I have some information for you. Meet me at %name1%.>
	Msg1_3  <%name2%@ \NARRATIVE 7 :// I could use a consultation at %name1%. Drop by when you have time.>
	Msg1_4  <%name2%@ \NARRATIVE 7 :// Stop by for a consultation. I'll be at %name1%.>
	Msg1_5  <%name2%@ \NARRATIVE 7 :// Come by %name1% when you get the chance; there are issues we need to discuss.>

Plot
	name <Lead: Media>
	desc <A media personality will call about something.>
	requires <#A +T-- C:MEDIA>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Near -7>
	element2 <Character !Near 1 MEDIA NeverFail>
	NeverFail2 <Journalist>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC, send an email,
	% and activate the mission subplot
	5min <if= PlotStatus %plotid% 0  if= RootSceneID &EpisodeScene V+ 1 1 ifG v1 d6 EMail 1  SetPlotStatus %plotid% %id1%>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .next>
	.next <+T-- +F-->

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_Conversation 2>

	Msg1_1  <%name2%@ \NARRATIVE 7 :// I have a lead for you, \PC . Come meet me at %name1%.>
	Msg1_2  <%name2%@ \NARRATIVE 7 :// Looking for a story? Come see me at %name1%.>
	Msg1_3  <%name2%@ \NARRATIVE 7 :// Hey, let's do lunch at %name1%.>
	Msg1_4  <%name2%@ \NARRATIVE 7 :// I've got an angle to pitch to you. Come see me at %name1%.>
	Msg1_5  <%name2%@ \NARRATIVE 7 :// Have you tried %name1% yet? Meet me there, it's fabulous.>

Plot
	name <Basic Job Offer>
	desc <The PC has to see someone about a job; here it is!>
	requires <#A +Tgt +Fmi S:++ Common>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the mission subplot
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	sub
		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Basic Conversation>
	desc <The PC has to talk with someone; here it is!>
	requires <#A +Tgt (+F--|+Fin) S:++ Common>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the conversation subplot
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the conversation to be had
	SubPlot1 <*CS_Conversation 2>

	sub
		STC MS_CAFE
		SetID 1
	end

Plot
	name <Basic Friend Contact>
	desc <The PC's friend will call about something.>
	requires <#A +T-->

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Near -7>
	element2 <Character !Near -7 !Okay 1 Friend>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC, send an email,
	% and activate the mission subplot
	5min <if= PlotStatus %plotid% 0  if= RootSceneID &EpisodeScene V+ 1 1 ifG v1 d6 EMail 1  SetPlotStatus %plotid% %id1%>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .next>
	.next <+T-- +F-->

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_Conversation 2>

	Msg1_1  <%name2%@ \NARRATIVE 7 :// Hey \PC , when you have the time come see me at %name1%>
	Msg1_2  <%name2%@ \NARRATIVE 7 :// I'll be at %name1% for a while. Drop by and we can have a chat.>
	Msg1_3  <%name2%@ \NARRATIVE 7 :// If you're not busy, I'd like to meet with you at %name1%.>
	Msg1_4  <%name2%@ \NARRATIVE 7 :// There are things we need to discuss. Meet me at %name1%.>
	Msg1_5  <%name2%@ \NARRATIVE 7 :// \PC , come meet me at %name1%. I'll be waiting there.>

Plot
	name <Basic Ally Mission>
	desc <The PC will be offered a job by an ally.>
	requires <#A +T-->

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Okay -3 !Near -7>
	element2 <Character !Near -7 !Okay -3 !XClude -2 ArchAlly>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC, send an email,
	% and activate the mission subplot
	5min <if= PlotStatus %plotid% 0  if= RootSceneID &EpisodeScene V+ 1 1 ifG v1 d6 EMail 1  SetPlotStatus %plotid% %id1%>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .next>
	.next <+T-- +F-->

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	Msg1_1  <%name2%@ \NARRATIVE 7 :// Hey, I've got a very special assignment for you. Meet me in %name1%.>
	Msg1_2  <%name2%@ \NARRATIVE 7 :// I have something for you to do at %name1%. This is sort of important.>
	Msg1_3  <%name2%@ \NARRATIVE 7 :// I have a mission specifically for you. Come see me at %name1%.>
	Msg1_4  <%name2%@ \NARRATIVE 7 :// When you're finished with the email spam come see me at %name1%. I've got a mission for you.>
	Msg1_5  <%name2%@ \NARRATIVE 7 :// Hey \PC , I have something here that may be of personal interest to you. Come meet me at %name1%.>

Plot
	name <Basic Friendly Mission>
	desc <The PC will be offered a job by a faction member.>
	requires <#A +T-- P:++ -P:ENEMY>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Okay -3 !Near -7>
	element2 <Character !Near -7 !Comrade -3>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC, send an email,
	% and activate the mission subplot
	5min <if= PlotStatus %plotid% 0  if= RootSceneID &EpisodeScene V+ 1 1 ifG v1 d6 EMail 1  SetPlotStatus %plotid% %id1%>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .next>
	.next <+T-- +F-->

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	Msg1_1  <%name2%@ \NARRATIVE 7 :// I've got a job for you. Report to %name1% as soon as possible.>
	Msg1_2  <%name2%@ \NARRATIVE 7 :// You have some work from \NARRATIVE 3 . I'll be waiting for you here at %name1%.>
	Msg1_3  <%name2%@ \NARRATIVE 7 :// I have a mission for you from \NARRATIVE 3 . Call me, or come to %name1%.>
	Msg1_4  <%name2%@ \NARRATIVE 7 :// \PC - I've got some work for you to do. Come to %name1% for the briefing.>
	Msg1_5  <%name2%@ \NARRATIVE 7 :// Your presence is required in %name1%.>


