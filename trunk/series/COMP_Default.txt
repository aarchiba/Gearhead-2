% Component List

% STATE OF STORY DESCRIPTION
%  The state of the story is determined by both the "context" attribute held in
%  the story and by certain variables which are determined when it's time to
%  generate a new component. These variables could include:
%  - The existence and nature of the palette entries
%  - The alleigance of the current location with either the PC's faction
%    or the palette enemy faction

%%
%% #C Conclusion
%%
%%  All conclusions start the same: with a stub. This plot doesn't do anything by
%%  itself, but sets the mood and invokes the following subplots:
%%      *CS_END_Intro
%%      *:CS_END_RallyDefenses
%%
%%  It must have the following macros defined:
%%      &RallyDefenses
%%      &ifDefensesRallied
%%

Plot
	name <Conclusion Stub>
	desc <This stub just holds all the conclusion subplots and sets the mood.>
	requires <#C F:++>

	% Element1 is the mood for the conclusion
	Element1 <Prefab>

	% P%id%01 = Defense Counter

	% Everything starts when the PC enters the episode scene.
	start <if= PlotStatus %plotid% 0 if= RootSceneID &EpisodeScene  Alert %id%01 History %id%02  SetPlotStatus %plotid1% %id1% SetPlotStatus %plotid2% %id2% SetPlotStatus %plotid3% %id3% SetPlotStatus %plotid4% %id4%  SetMood %1% &EpisodeScene>

	&RallyDefenses <P= %id%01 1>
	&ifDefensesRallied <if= P%id%01 1>

	Msg%id%01 <As you enter \SCENE &EpisodeScene , you experience a distinct feeling of dread. Something big is about to happen here.>
	Msg%id%02 <You found \SCENE &EpisodeScene under the control of \FACTION &EnemyFac .>

	% SubPlot1 is the confrontation intro
	%     This will handle the arch-enemy and load the other subplots
	% SubPlot2 is the rally defenses subplot-
	%     This will be determined by the +T task.
	SubPlot1 <*CS_END_Intro 1>
	SubPlot2 <*:CS_END_RallyDefenses>

	inv
		Mood 5
		name <Conclusion Mood>
		plot_type <*FactionRampage>
		Element1 <Grab -2>
		Update <if= v1 0 V= 1 1   News 1>
		Msg1_1 <%city% under siege as %name3%'s personal forces take control.>
		Msg1_2 <Mecha from \FACTION &EnemyFac take control of %city% under the orders of %name3%.>
		Msg1_3 <%name3% has begun a purge of \PPR %3% enemies from %city%; authorities powerless to stop the destruction.>
		% Meme Messages
		Msg_1 <When will the trouble with %me_name1% end?>
		CMsg_1 <if# ChatNPCFac %me_1% Accept>
		Msg_2 <Someone really ought to do something about %me_name1%... They must be stopped!>
		CMsg_2 <if# ChatNPCFac %me_1% Accept>
		Msg_3 <What did %city% do to deserve this? Those jerks from %me_name1% are acting like they own the place.>
		CMsg_3 <if# ChatNPCFac %me_1% Accept>
	end

%%
%% #A Plots
%%

Plot
	name <Right Place For a Mission>
	desc <The city government is hiring.>
	requires <#A +Tgs (+Fmi|+Fin|+F--) S:PERM -L:ENEMY ~C:ADVEN>

	% E1 is the scene where this component will take place
	% E2 is the government faction, or a faction allied with it
	% E3 is the NPC that the PC will speak with
	element1 <Grab 6>
	element2 <Faction !Ally -7 !Okay 1 -ENEMY>
	element3 <Character !Near 1 !Comrade 2 NeverFail>
	NeverFail3 <Bureaucrat>
	Place3 <1 (Citizens) pass>

	% All this plot does is move the NPC, post some news,
	% and activate the mission subplot
	update <if= PlotStatus %plotid% 0   SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 3>

	inv
		Persona 3
		rumor%id1% <%name3% is at %name1% looking for a cavalier. It seems pretty important.>
		Greeting <NewChat SayAnything>
	end

Plot
	name <I have information for trade>
	Size 1
	desc <An NPC has information for you, but you have to do something first...>
	requires <#A +Tgs (+Fin|+F--) S:++ ~+Gkn>

	% E1 is the scene where the meeting will take place.
	% E2 is a public scene
	% E3 is the pilot being met.
	Element1 <Grab 6>
	Element2 <Scene Public (Building|Meeting) !Near -7>
	Element3 <Character ADVEN -ENEMY !Near 2 NeverFail>
	Place3 <1 sd ally (Guards)>

	% P%id%01 = Initialization Counter

	%% FAIL CONDITIONS:
	%% - E3 dies before the Gather Information started

	update <if= P%id%01 0 P= %id%01 1 SetPlotStatus %plotid% %id%>
	end <if= PlotStatus %plotid2% 0 ifNPCDead %3% LoseComp 0 .next>

	.%id2%_%plotid2%_GoWin <WinComp 0 .next>
	.%id2%_%plotid2%_GoLoss <LoseComp 0 .next>
	.next <+T-- +F-->

	% SubPlot1 is the Minor Mission
	% SubPlot2 is the Gather Information
	SubPlot1 <*:CS_MinorMission 3>
	SubPlot2 <*:CS_GatherInformation 3>

	sub
		Persona 3
		rumor%id% <%name3% at %name1% knows something. I just know it.>
		greeting <if= PlotStatus %plotid% %id% else GoChat ifSubPlotWon %plotid1% else GoCheckLoss Goto GoReveal>
		*GoReveal <*RevealSecret GoStartGI>
		GoStartGI <History 3 WinSubPlot %plotid% PMemo %plotid1% 0 SetPlotStatus %plotid2% %id2% Goto .%id2%_GoInform>
		GoCheckLoss <ifSubPlotLost %plotid1% else GoCheckProgress History 4 SMemo 0 NTBLoseComp 0 .next Goto GoLoss>
		*GoLoss <*MissionWasFailure na na na>
		GoChat <NewChat SayAnything>
		GoCheckProgress <if# PlotStatus %plotid1% 0 else GoFirstTime Goto GoRemind>
		*GoRemind <*GenericMissionReminder>
		*GoFirstTime <*IHaveInformation GoMakeOffer na>
		GoMakeOffer <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <History 2 Goto .%id1%_GoInit>
		*result2 <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <SMemo 0 NTBLoseComp 0 .next>
		.next <+T-- +F-->
		Msg1 <Before I let you in on the secret, I need you to do a little something for me.>
		Msg2 <%name3% offered to tell you a secret, in exchange for a favor.>
		Msg3 <You completed %name3%'s task and learned the secret.>
		Msg4 <You failed at %name3%'s task.>
		Prompt1 <No problem. What do you need?>
		Prompt2 <Sorry, but I don't think it's worth it.>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <They Say You're Tough>
	Size 1
	desc <You will be challenged to a duel.>
	requires <#A +Tgt (+F--|+Fch) (T:ADVEN|T:PDASS) (C:ADVEN|P:PDASS) -1:FRIEND -1:LOVER -1:FAMILY ~+Gpo ~+Gfa>

	% E1 is the scene where the meeting will take place.
	% E2 is the pilot being met.
	% E3 is a location for the duel.
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+T-- +F-->

	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %plotid% %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% 0 else GoChat Goto GoGreet>
		GoChat <NewChat SayAnything>
		*GoGreet <*TheySayYoureTough GoChallenge>
		GoChallenge <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <EndCHat Say 2 History 3 SMemo 4 SetPlotStatus %plotid1% %id1%>
		result2 <NewChat Say 5 AddRenown -10  History 6 SMemo 0 LoseComp 0 .loss>
		.loss <+T-- +F-->
		Msg1 <I'd like to find out for myself. I challenge you to a duel- what do you say, me and you in %name3% right now?>
		Msg1_1 <>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <Cool. Meet me in %name3% whenever you're ready.>
		Msg3 <%name2% challenged you to a mecha duel.>
		Msg4 <%name2% challenged you to a mecha duel. You should go meet \OPR %2% in %name3% when you're ready.>
		Msg5 <You're chickening out? That's not cool at all.>
		Msg6 <%name2% challenged you to a mecha duel, but you refused.>
		Prompt1 <You're on. Let's do it.>
		Prompt2 <Sorry, but I've got more important things to do.>

		STC MS_CAFE
		SetID 1
	end


Plot
	name <Conference Corporate Mission: L5 Region>
	desc <Go to a conference, find someone who's hiring.>
	requires <#A +Tgs (+Fcc|+Fmi|+F--) (P:--|P:CORPO) (L:MAQUI|L:FCOMS|L:RISHI)>
	Size 1

	% E1 is the scene where this component will take place
	% E2 is the corporate faction that's hiring
	% E3 is a local public scene for generating a new NPC
	% E4 is the NPC who will give the mission
	element1 <Grab 6>
	Element2 <Faction (COMET|HOELL|MUGLE) !Okay -3 !XClude -2 -Enemy>
	element3 <Scene Building Public !Near -7 !Okay 2>
	element4 <Character !Comrade 2 !Near 3 NeverFail>
	NeverFail4 <Corporate Executive>
	Place4 <1 (Citizens) pass>

	% This plot updates when the PC enters the location
	start <if= PlotStatus %plotid% 0 ifNPCOK %4% else GoNPCDied if= SceneID %1%  SetPlotStatus %plotid% %id1%>
	GoNPCDied <LoseComp 0 .next>
	.next <>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 4>

	sub
		Persona 4
		rumor%id1% <%name4% is looking to hire someone for %name2%.>

		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Protect me from Enemy!>
	desc <The target will tell you that the Enemy is doing something.>
	requires <#A +Tgt +Fpr T:++ E:++ S:++>

	% E1 is the NPC being sought
	% E2 is his scene
	% E3 is the enemy
	Element1 <Grab 5>
	Place1 <2 (Citizens) Pass>
	Element2 <Grab 6>
	Element3 <Grab 1>

	start <if= PlotStatus %plotid% 0  ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	.%id1%_%plotid1%_GoWin <SetPlotStatus %plotid2% %id2%     SMemo %id%01>
	.%id1%_%plotid1%_GoLoss <SMemo 0   History %id%02   DeleteNPC %1%   LoseComp 0 .%id%_loss>

	Msg%id%01 <You stopped %name3%'s plan. You should go tell %name1% at %name2%.>
	Msg%id%02 <%name1% was killed by %name3%.>

	% SubPlot1 is the enemy mission
	% SubPlot2 is the win task
	SubPlot1 <*:CS_StopNPCMission&IsEnemyNPC 3>
	SubPlot2 <*CS_WinTask&Protection 1>

	sub
		Persona 1
		special <UNLISTED>
		greeting <if# PlotStatus %plotid1% 0 else .%id%_GoGreet NewCHat Say %id%01>
		*.%id%_GoGreet <*ProtectCheckIn .%id%_GoTell>
		.%id%_GoTell <NewChat Say %id%02 SMemo %id%03 History %id%04 SetPlotStatus %plotid% %id% SetPlotStatus %plotid1% %id1% AddChat %id%01>
		result%id%01 <NewChat Say %id%05>
		Msg%id%01 <%name3% is still out there, somewhere...>
		Msg%id%02 <Thank goodness! %name3% is in town on some kind of mission; if \SPR %3% finds out where I am, I'm as good as dead.>
		Msg%id%03 <You learned from %name1% that %name3% is in town on some kind of mission.>
		Msg%id%04 <You learned from %name1% that %name3% was in town.>
		Msg%id%05 <I believe you can. Come back here and let me know if you succeed; it'd be really nice to walk outside again.>
		Prompt%id%01 <Don't worry. I plan to reach %name3% first.>

		STC MS_EmptyBuilding
		SetID 2
	end


Plot
	name <Protect Me For Info>
	desc <The person you sought seeks protection from your enemy.>
	requires <#A +Tgt +Fin -T:ENEMY -T:FRIEND -T:ALLY -T:LOVER -T:FAMILY F:++ T:NOFAC T:A.--- S:++ S:META R:-->
	Size 2

	% Element1 is the hideout
	% Element2 is the NPC
	% Element3 is the enemy faction
	% Element4 is a public scene, in case the PC rejects E2's offer
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Grab 2>
	Element4 <Scene Public !Near -7>

	%% P1 = Initialization Counter

	% If the NPC dies, this plot counts as a loss.
	start <ifNPCDead %2% History 1 LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if= P1 0 P= 1 1 NPCLevel %2% StoryDL  SetXXRAttitude %2% XXR_A_HasSecret>

	Msg1 <%name2% died before you could learn anything.>

	sub
		Persona 2
		special <UNLISTED>
		*greeting <*WhatDoYouWantWithMe&Information GoCheckAlignment>
		% Please forgive the messy numbering here. As a root plot, the messages and prompts don't
		% strictly need the %id% in front, but I have a cold and am confused in the head so the
		% messages use 1-99 and the prompts use %id%01-%id%99.
		GoCheckAlignment <if= v1 0 else GoMakeOffer NewChat Say 1 AddChat %id%01 AddChat %id%02 AddChat %id%03 AddChat %id%04>
		GoMakeOffer <V= 1 1 NewChat Say 2 AddChat %id%05 AddCHat %id%06>
		GoJoinLance <&SetPartnerNPC %2% &AddPartnerToLance &SetTargetNPC 0 WinComp %4% .next>
		.next <+Tgs +Fin>
		result%id%01 <AddSociable   d6   Goto GoMakeOffer>
		result%id%02 <Goto GoMakeOffer>
		result%id%03 <AddLawful 1        Goto GoMakeOffer>
		result%id%04 <AddSociable -d10   Goto GoMakeOffer>
		result%id%05 <NewCHat Say 3 SMemo 4 History 5 Goto GoJoinLance>
		result%id%06 <NewChat Say 6 AddChat %id%07 AddChat %id%08>
		result%id%07 <NewChat Say 7 SMemo 4 History 8 Goto GoJoinLance>
		result%id%08 <NewChat Say 9 StoryNote 4 WinComp %4% .next>
		Msg1 <So you're looking for information, huh? How do I know you're not with %name3%?>
		Msg2 <Fair enough. Look, I'm sure there's some reason why %name3% is after me, but I don't know anything. At least I don't think I do.>
		Msg3 <You know, I'm not entirely defenseless by myself... but I suppose that I could use some help. Alright then, let's go together! I hear that there's something interesting going on at %name4%; maybe we could learn something.>
		Msg4 <%name2% suggested that you might learn something at %name4%.>
		Msg5 <%name2% joined your lance for protection from %name3%.>
		Msg6 <What's that supposed to mean!? I've held my own against %name3%, haven't I?>
		Msg7 <I will! That'll show you! I can even prove that I know something you don't- some members of %name3% have been seen at %name4% recently. If we go there, maybe we can learn something.>
		Msg8 <%name2% joined your lance to help fight %name3%.>
		Msg9 <I know that some of them have recently been seen at %name4%. Maybe if you head over there right away you can get rid of some of that ignorance!>
		Prompt%id%01 <If I was with %name3% you'd already be dead.>
		CPrompt%id%01 <if# PCFac %3% Accept>
		Prompt%id%02 <Relax, I'm fighting against %name3%.>
		Prompt%id%03 <Actually, I am with %name3%, but I'm a good one.>
		CPrompt%id%03 <if= PCFac %3% Accept>
		Prompt%id%04 <You don't know that.>
		Prompt%id%05 <I could protect you from them.>
		Prompt%id%06 <You're useless.>
		Prompt%id%07 <Want to prove it? Join me, let's fight them together.>
		Prompt%id%08 <You don't know anything about %name3%.>

		STC MS_EmptyBuilding
		SetID 1
	end

Plot
	name <Party Contact>
	desc <At the party, the PC can get a job from someone.>
	requires <#A +Tgs (+Fpa|+Fcc) (P:--|P:PDASS|P:CORPO) ~+Gmo>
	Size 1

	% Element1 is the scene where this will take place
	% Element2 is a local public scene
	% Element3 is the NPC who will offer the mission
	% Element4 is the bodyguard
	% Element5 is an environs scene for the duel
	Element1 <Grab 6>
	Element2 <Scene Public Meeting !Near -7>
	Element3 <Prefab>
	Place3 <1 pass ally>
	Element4 <Prefab>
	Place4 <1 SD Ally>
	Element5 <Scene Environs !Near -7>

	% P%id%01 = Duel victory counter; 1=Win,-1=Loss
	% P%id%02 = Loss Timer
	start <if# P%id%02 0 ifG ComTime P%id%02 SMemo 0 LoseComp 0 .loss>
	end <ifNPCDead %3% LoseComp 0 .loss>
	.loss <+T-- +F-->

	.%id1%_%plotid1%_GoWin  <SMemo %id%01  p= %id%01  1  FreezeNPC %4%>
	.%id1%_%plotid1%_GoLose <SMemo %id%01  p= %id%01 -1  FreezeNPC %4%   P= %id%02 ComTime P+ %id%02 86400 P+ %id%02 d7200>

	Msg%id%01 <You should go tell %name3% at %name1% about your duel with %name4%.>

	% SubPlot1 is the duel with the bodyguard
	SubPlot1 <*CS_Duel&NoDerailment 4 1 5>

	sub
		Persona 3
		greeting <if= p%id%01 1 else GoCheckLoss NewChat Say %id%01 SMemo %id%02 History %id%03 Goto GoGetMission>
		GoGetMission <&SetTargetNPC %3% MoveNPC %4% %2% WinComp %2% .%id%_next>
		.%id%_next <+Tgt +Fmi>
		GoCheckLoss <if= p%id%01 -1 else GoCheckStatus NewChat Say %id%04 SMemo %id%02 History %id%03 AddChat %id%01 &SetTargetNPC %3% MoveNPC %4% %2% LoseComp %2% .%id%_next>
		GoCheckStatus <if# PlotStatus %plotid% 0 else GoIntro NewChat Say %id%05>
		*GoIntro <*NiceToMeetYou GoOffer>
		GoOffer <NewChat Say %id%06 AddChat %id%02 AddChat %id%03>

		result%id%01 <NewChat Say %id%07>
		result%id%02 <ifNPCOK %4% else .%id%_GoNoTest NewChat Say %id%08 AddChat %id%04 AddChat %id%05>
		.%id%_GoNoTest <NewChat Say %id%09 SMemo %id%02 History %id%03 &SetTargetNPC %3% NegateComp %2% .%id%_next>
		result%id%03 <NewChat Say %id%10 SMemo 0 NegateComp 0 .%id%_refuse>
		.%id%_refuse <+T-- +F-->
		result%id%04 <NewChat Say %id%11 SMemo %id%12 History %id%13 SetPlotStatus %plotid% %id1%>
		result%id%05 <Goto result%id%03>

		Msg%id%01 <I knew you looked like a winner. Come see me at %name2% later on and I'll have that mission for you.>
		Msg%id%02 <You should go meet %name3% at %name2% for a mission.>
		Msg%id%03 <After impressing \OPR %3% , %name3% offered you a job.>
		Msg%id%04 <Very good. I wasn't sure I was going to see you again. Come see me at %name2% later on and I'll try to find a mission for you.>
		Msg%id%05 <Come back to see me after you've fought %name4% in %name5%.>
		Msg%id%06 <You're not a cavalier are you? I've been looking for someone to run a couple of missions for me. Of course, I'd have to test your abilities before offering you the job...>
		Msg%id%07 <I know... but you came back to report after a humiliating defeat. That kind of diligence can be very useful. I'll see you at %name2%.>
		Msg%id%08 <See my bodyguard %name4% over there? I want you to go with \OPR %4% to %name5%; there, you'll be tested on your mecha combat skills. If I am impressed with your performance then the job is yours.>
		Msg%id%09 <I suppose a test isn't strictly necessary... Come see me in %name2% and I'll give you a mission.>
		Msg%id%10 <That's too bad.>
		Msg%id%11 <Wonderful. Come back here when you've finished the duel.>
		Msg%id%12 <%name3% sent you to duel %name4% in %name5%. If you win, \SPR %3% will give you a job.>
		Msg%id%13 <You agreed to duel %name4% to prove your abilities to %name3%.>

		Prompt%id%01 <But... I lost the duel!>
		Prompt%id%02 <What kind of test are you talking about?>
		Prompt%id%03 <Sorry, I'm not interested.>
		Prompt%id%04 <It's a deal.>
		Prompt%id%05 <No way. %name4% scares me.>

		Persona 4
		greeting <NewChat Say 1>
		msg1 <Go see %name3% if you've got any business.>

		STC MS_SOCIAL
		SetID 1
	end
	inv
		NPC Shopkeeper
		statline 10 9 12 13 14 16 15 16
		chardesc renowned
		job <Investor>

		NPC Soldier
		job <Bodyguard>
		job_desig <ADVEN>
		chardesc shy
	end

Plot
	name <Bad Party>
	desc <We're going to a bad party. Actually, meet a bored arena pilot and go duel.>
	requires <#A +Tgs +Fpa ~+Gpo>
	Size 1

	% Element1 is the scene where this will take place
	% Element2 is the bored arena pilot
	% Element3 is an environs scene for the duel
	Element1 <Grab 6>
	Element2 <Character HasMecha PDAss !Near -7 Neverfail>
	NeverFail2 <Arena Pilot>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% V1 = Initialization Counter
	% V2 = Partner Observation
	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% 0 ifNPCDead %2% else GoCheckScene LoseComp 0 .changes>
	GoCheckScene <if= SceneID %1% ifNPCOK &PartnerNPC if= V2 0  Monologue &PartnerNPC 3 V= 2 1>
	.changes <+T-- +F-->

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% Branch activators
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %plotid% %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg3 <Look over there... %name2% doesn't look like \SPR %2% 's enjoying the party.>
	Msg3_1 <Say, isn't that %name2% the famous arena pilot? I wonder if we can get an autograph... or a duel.>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% 0 else GoChat Goto GoFirstTime>
		*GoChat <*MISC_CHATTER>
		GoFirstTime <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <StoryNote 2 SetPlotStatus %plotid% %id1% Goto GoAcceptChallenge>
		*GoAcceptChallenge <*IAcceptYourChallenge %3%>
		result2 <NewChat Say 3 StoryNote 4 LoseComp 0 .next>
		.next <+T-- +F-->
		Msg1 <Are you as bored with this party as I am? Why don't we leave this place and go find some real action... I mean a duel, of course.>
		Msg2 <At the party, %name2% challenged you to a duel in %name3%.>
		Msg3 <Is that so? I'm afraid to say that you've just blown the one chance you had for something interesting.>
		Msg4 <At the party, %name2% challenged you to a duel but you refused.>
		Prompt1 <Let's do it.>
		Prompt2 <Sorry, I don't want to.>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <Convention Press Conference>
	desc <This convention is about to feature a press conference.>
	requires <#A +Tgs (+Fcc|+Fin) S:META>

	% E1 is the scene where the conference will take place
	% E2 is a local scene; natural home to E4
	% E3 is the character doing the talking
	Element1 <Grab 6>
	Element2 <Scene !Near -7 !Okay -3 Public>
	Element3 <NewNPC 2 2>
	Place3 <2>

	% V1 = Initialization Counter
	update <if= V1 0 MoveNPC %3% %1% V= 1 1>
	start <if= PlotStatus %plotid% 0 if= SceneID %1% Alert 1 SMemo 2 SetPlotStatus %plotid% %id1%>

	Msg1 <At the convention hall, %name3% appears to be just finishing up a speech.>
	Msg2 <At %name1%, %name3% just gave a speech about something.>

	%% SubPlot1 is the press conference
	SubPlot1 <*CS_PressConference 1 3>

	sub
		STC MS_PRESENTATION
		SetID 1
	end

Plot
	name <Rescue Reward>
	desc <The PC has to speak to a person he just rescued.>
	requires <#A +Tgt +Frt S:++>
	Size 1

	% E1 is the scene where the meeting will take place.
	% E2 is the person who was rescued.
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All the update does is start the rescue reward going...
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	% The subplot contains the reaction/reward.
	SubPlot1 <*CS_ThanksForRescue 2>

	sub
		STC MS_CAFE
		SetID 1
	end

Plot
	name <Locate Target NPC: Perm>
	desc <The PC needs to find an NPC for some reason, but no scene exists.>
	requires <#A +Tgt +Fpr S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is the character who knows where E1 is.
	% E3 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Character !Near -7 !Okay 1>
	Element3 <Scene !Near -7 Building Public !Okay 1>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% NegateComp 0 na>

	sub
		Persona 2
		rumor <%name2% has seen %name1%.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %3% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <As far as I know, %name1% is now at %name3%. I hope \SPR %1% isn't in any trouble.>
		Msg2 <%name2% told you that %name1% was at %name3%.>
	end

Plot
	name <Locate Target NPC: Meta>
	desc <The PC needs to find an NPC for some reason, but no scene exists.>
	requires <#A +Tgt +Fpr S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is the character who knows where E1 is.
	% E3 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Character !Near -7 !Okay 1>
	Element3 <MetaScene !Near -7 *Building NeverFail>

	start <if= PlotStatus %plotid% 0 ifNPCDead %2% NegateComp 0 na>

	sub
		Persona 2
		rumor <%name2% has seen %name1%.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %3% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <As far as I know, %name1% is now at %name3%. I hope \SPR %1% isn't in any trouble.>
		Msg2 <%name2% told you that %name1% was at %name3%.>
	end

Plot
	name <Parking Ticket Blues>
	desc <The PC's friend needs to pay off some fines.>
	requires <#A +Tgt S:-- T:THIEF T:FRIEND>
	Size 3

	% E1 is the character being sought.
	% E2 is the police officer who's looking for him.
	% E3 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Character !Near -7 POLIC>
	Element3 <Scene !Near -7 Building Public !Okay 1>

	start <if= PlotStatus %plotid% 0 ifNPCDead %3% NegateComp 0 na>

	sub
		Persona 2
		rumor <%name2% has been looking for %name1%.>
		% V1 = Fine Counter
		greeting <if= V1 0 else GoTell Goto GoCheckFirst>
		GoCheckFirst <*LookingForNPC&NoFailure %1% GoTell GoTell>
		GoTell <V= 1 Threat StoryDL 5 NewChat Say 1 StoryNote 2 AddChat 1 AddChat 2 AddChat 3 AddChat 4>
		GoReveal <StoryNote 10 WinComp %3% na>
		result1 <NewChat Say 3 AddChat 5 AddChat 4>
		result2 <ifConversation SkillTar StoryDL else GoR2Fail NewChat Say 4 Goto GoReveal>
		GoR2Fail <NewChat Say 5 AddChat 1 AddChat 3 AddChat 4>
		result3 <AddHeroic -1 NewChat Say 6 StoryNote 7 &SetTargetNPC 0 NegateComp %3% .negate>
		.negate <+Tgs +Fmi>
		result4 <NewChat Say 8>
		result5 <NewChat Say 9 Cash+ -V1 Goto GoReveal>
		Msg1 <%name1% is in hiding. I know this because I'm the one looking for \OPR %1% ; \SPR %1% 's got $ \VAL v1 worth of unpaid fines, and if I don't get that money then \SPR %1% 's going straight to the lockup.>
		Msg2 <You learned from %name2% that %name1% has many unpaid fines.>
		Msg3 <It would be kind of unorthodox, but as long as the fines get paid it's not my business to judge. That's be the judges.>
		Msg4 <Well, if you say it's important, I'll give %name1% some more time to raise the money... as long as \SPR %1% doesn't do anything illegal to raise it. Now that the heat is off you'll probably be able to find \OPR %1% at %name3% again.>
		Msg5 <Nothing is more important than upholding the law. I'm afraid I can't let up one bit.>
		Msg6 <Nope. I can't say that I do... though you might want to check at %name3%. I hear there's a lot going on over there.>
		Msg7 <After abandoning %name1%, %name2% suggested you find a job at %name3%.>
		Msg8 <Maybe while you're gone, %name1% will come back and pay the fines... I doubt it though.>
		Msg9 <There we go. Now that the fines are paid I expect you'll be able to find %name1% at %name3% again.>
		Msg10 <%name2% told you that %name1% could likely be found at %name3%.>
		Prompt1 <What about if I paid the fines?>
		CPrompt1 <ifG PC$ V1 Accept>
		Prompt2 <Look, could you cut \OPR %1% a little slack? It's important.>
		CPrompt2 <ifChatNPCAlly Accept>
		Prompt3 <Forget about %name1%, then. Do you know any job leads?>
		Prompt4 <I'll be back later.>
		Prompt5 <Here you go. [Give Money]>
	end

Plot
	name <Degenerate Lookfer: Perm>
	desc <The PC needs to find an NPC for some reason. After speaking with an NPC, the search changes to +Tgt.>
	requires <#A +Tgt S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is a public scene in the city, needed for NeverFail
	% E3 is the character who knows where E1 is.
	% E4 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Scene Public !Near -7 !Okay 1>
	Element3 <Character !Near -7 !Okay 1>
	Element4 <Scene !Near -7 Building Public !Okay 1>

	start <if= PlotStatus %plotid% 0 ifNPCDead %3% NegateComp 0 na>

	sub
		Persona 3
		rumor <%name3% knows where %name1% is.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %4% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <The last time I saw \OPR %1% , %name1% was going to %name4%. That's where you should probably check.>
		Msg2 <%name3% told you that %name1% was at %name4%.>
	end

Plot
	name <Degenerate Lookfer: Meta>
	desc <The PC needs to find an NPC for some reason. After speaking with an NPC, the search changes to +Tgt.>
	requires <#A +Tgt S:-->
	Size 2

	% E1 is the character being sought.
	% E2 is a public scene in the city, needed for NeverFail
	% E3 is the character who knows where E1 is.
	% E4 is a scene for the meeting
	Element1 <Grab 5>
	Place1 </>
	Element2 <Scene Public !Near -7 !Okay 1>
	Element3 <Character !Near 2 !Okay 1 NeverFail>
	NeverFail3 <Citizen>
	Element4 <MetaScene !Near -7 *Building NeverFail>

	start <if= PlotStatus %plotid% 0 ifNPCDead %3% NegateComp 0 na>

	sub
		Persona 3
		rumor <%name3% knows where %name1% is.>
		*greeting <*LookingForNPC %1% GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 SMemo 2 WinComp %4% .win>
		.win <>
		*GoFailure <*BrushOff&Questioning>
		Msg1 <The last time I saw \OPR %1% , %name1% was going to %name4%. That's where you should probably check.>
		Msg2 <%name3% told you that %name1% was at %name4%.>
	end

Plot
	name <Enemy is after me>
	desc <The target will tell you that the Enemy is after them.>
	requires <#A +Tgt +Fin T:++ S:META -T:FRIEND -T:FAMILY -T:ALLY -T:LOVER ~T:ENEMY E:++ (T:THIEF|T:ADVEN)>
	Size 1

	% E1 is the NPC being sought
	% E2 is his scene
	% E3 is the enemy
	Element1 <Grab 5>
	Place1 <2 (Citizens) Pass>
	Element2 <Grab 6>
	Element3 <Grab 1>

	start <if= PlotStatus %plotid% 0  ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	.%id1%_%plotid1%_GoWin <SetPlotStatus %plotid2% %id2%     SMemo %id%01>
	.%id1%_%plotid1%_GoLoss <SMemo 0   History %id%02   DeleteNPC %1%   LoseComp 0 .%id%_loss>

	Msg%id%01 <You stopped %name3%'s plan. You should go tell %name1% at %name2%.>
	Msg%id%02 <%name1% was killed by %name3%.>

	% SubPlot1 is the enemy mission
	% SubPlot2 is the win task
	SubPlot1 <*:CS_StopNPCMission&IsEnemyNPC 3>
	SubPlot2 <*CS_WinTask 1>

	sub
		Persona 1
		special <UNLISTED>
		greeting <if= PlotStatus %plotid% %id% else .%id%_GoGreet   NewChat Say %id%01>
		*.%id%_GoGreet <*IHaveInformation&AboutNPC .%id%_GoTell %3%>
		.%id%_GoTell <NewChat Say %id%01 SMemo %id%02 History %id%03 SetPlotStatus %plotid% %id% SetPlotStatus %plotid1% %id1%>
		Msg%id%01 <%name3% is in town on some kind of mission. I don't know exactly what it is, since I'm hiding from \OPR %3% until it's finished... If \SPR %3% finds out I'm here, I'm as good as dead.>
		Msg%id%02 <You learned from %name1% that %name3% is in town on some kind of mission.>
		Msg%id%03 <You learned from %name1% that %name3% was in town.>

		STC MS_EmptyBuilding
		SetID 2
	end

Plot
	name <Basic Challenge>
	desc <You're going to challenge some guy.>
	requires <#A +Tgt +Fch T:++ S:++ Common>

	% E1 is the scene where the meeting will take place.
	% E2 is the friend being met.
	% E3 is a location for the duel.
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+T-- +F-->

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% Branch activators
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %plotid% %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% 0 else GoChat Goto GoFirstTime>
		*GoChat <*MISC_CHATTER>
		*GoFirstTime <*PCChallengeNPC GoMakeChallenge GoWussOut>
		GoMakeChallenge <StoryNote 1 SetPlotStatus %plotid% %id1% Goto GoAcceptChallenge>
		*GoAcceptChallenge <*IAcceptYourChallenge %3%>
		*GoWussOut <*GodImBored>
		Msg1 <You challenged %name2% to a duel in %name3%.>

		STC MS_CAFE
		SetID 1
	end


Plot
	name <Phone Job Offer>
	desc <The PC has to see someone about a job; here it is!>
	requires <#A +Tgt +Fmi S:-- Common>

	% E1 is a scene for this job offer
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Near -7 !Okay -5>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the mission subplot
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

Plot
	name <Basic Job Offer>
	desc <The PC has to see someone about a job; here it is!>
	requires <#A +Tgt +Fmi S:++ Common>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the mission subplot
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	sub
		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Phone Conversation>
name <DEBUG>
	desc <The PC has to talk with someone; here it is!>
	requires <#A +Tgt (+F--|+Fin) S:-- Common>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Scene Building Public !Near -7 !Okay -5>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the conversation subplot
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the conversation to be had
	SubPlot1 <*CS_Conversation 2>

Plot
	name <Basic Conversation>
	desc <The PC has to talk with someone; here it is!>
	requires <#A +Tgt (+F--|+Fin) S:++ Common>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the conversation subplot
	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the conversation to be had
	SubPlot1 <*CS_Conversation 2>

	sub
		STC MS_CAFE
		SetID 1
	end



