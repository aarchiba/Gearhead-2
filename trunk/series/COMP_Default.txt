% Component List

% STATE OF STORY DESCRIPTION
%  The state of the story is determined by both the "context" attribute held in
%  the story and by certain variables which are determined when it's time to
%  generate a new component. These variables could include:
%  - The existence and nature of the palette entries
%  - The alleigance of the current location with either the PC's faction
%    or the palette enemy faction

% The palette entries are:
%	STAT_XRP_EnemyChar = 1;			{ E: }
%	STAT_XRP_EnemyFac = 2;			{ F: }
%	STAT_XRP_PCFac = 3;			{ P: }
%	STAT_XRP_PlotStateNPC = 4;		{ M: }
%	STAT_XRP_TargetNPC = 5;			{ T: }
%	STAT_XRP_ComponentScene = 6;		{ S: }
%	STAT_XRP_EpisodeScene = 7;		{ L: }
%	STAT_XRP_TargetItem = 8;		{ I: }

Plot
	name <Corporate Espionage>
	desc <The PC finds out that there's a spy in the company.>
	requires <#A +Tgs P:COR ~F:COR S:META>
	Size 4

	% Here's how this component works: A scene is created which includes in it a
	% CORE_CE_Spy component, and several CORE_CE_RedHerring components. When/if the
	% PC locates the spy, it will set a local variable.
	% The CORE_CE_Spy content is responsible for both the WinComp call and also the
	% storynote.
	% If the case can't be solved within 24 hours, the PC loses.

	% E1 = Scene to guard
	Element1 <Grab 6>

	% P1 = Initialization Counter
	% P2 = Time Limit
	start <if# P2 0 ifG ComTime P2 if# SceneID E1 LoseComp 0 .next>
	update <if= V1 0 V= 1 1  SetSceneFaction E1 &AllyFac>
	.next <+T-- +F-->

	sub
		MetaScene 1
		% L1 = Variable set when clue discovered/lost
		%	if L1 is positive, the PC solved the case.
		%	if L1 is negative, the PC either destroyed the clue or is taking a fall.
		ClubMap
		MapWidth 24
		MapHeight 24
		type <BUILDING LEGIT PUBLIC Office>
		special <AddExit>
		nu1 <if= T1 0 Return>
		Content1 <Some 1 100 sub *CORE_D_CE_Clue na>
		Content2 <Some 2  85 sub *CORE_D_CE_RedHerring na>

		%% The first time this scene is entered, start the 24-hour timer.
		start <if= P2 0 P= 2 ComTime P+ 2 86400>

		sub
			Team 1

			Team 2
			name <Civilian>
			setally 1
			Passive

			room
			name <Foyer>
			desig <EntranceGrid>
			Content1 <Some 1 100 Here *CORE_D_CE_Intro na>
			Content2 <Some 3 45 Here *URBAN_X na>
		end


	end

Plot
	name <Rescued an Ally>
	desc <The PC has just rescued an ally, who will suggest that he go speak with another member of the faction.>
	requires <#A +Ttt +Frt T:Ally>

	% E1 is the scene where this will take place
	% E2 is the character who's been rescued
	% E3 is a public scene in this city, to be used as home scene if a
	%    pre-existing E4 can't be found.
	% E4 is another character allied with E2
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene Public !Okay -3 Building !Near -7>
	element4 <Character !Comrade 2 !Near 3 Neverfail>
	NeverFail4 <Mecha Pilot>

	% P1 = Initialization Counter
	start <ifNPCOK E2 else GoLose ifNPCOK E4 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-->

	update <if= V1 0 V= 1 1 MoveNPC E2 E1>

	sub
		Persona 2
		rumor <%name2% wanted to speak with you at %name1%.>
		% V1 = Can only get reward once
		*Greeting <*ThanksForRescue GoReward>
		GoReward <if= V1 0 else GoBriefing V= 1 1 CashPrize Reward Threat StoryDL 100 200 NewChat Say 1 AddChat 1 AddChat 2>
		GoBriefing <NewChat Say 2 StoryNote 3 &SetTargetChar E4  WinComp EScene 4 .changes>
		.changes <+Tgt +Fmi>
		result1 <Goto GoBriefing>
		result2 <if= ChatNPCFac PCFac else GoR2Reject if# PCFac 0 else GoR2Reject NewChat Say 4 AddChat 3>
		*GoR2Reject <*RejectMission GoR2Exit>
		GoR2Exit <LoseComp 0 .changes2>
		.changes2 <+T-- +F-->
		result3 <Goto GoBriefing>
		Msg1 <Here's a reward for your quick thinking. This matter needs to be dealt with quickly, before it gets out of hand. We could use someone like you on our side.>
		Msg2 <Go speak with \ELEMENT 4 at \ELEMENT 3 . I think \SPR E4 'll have a mission you can help with.>
		Msg3 <\ELEMENT 2 sent you to speak with \ELEMENT 4 at \ELEMENT 3 .>
		Msg4 <This is now official \FACTION ChatNPCFac business. All your other missions can wait; this is top priority.>
		Prompt1 <Tell me what you need.>
		Prompt2 <Forget it, I'm finished here.>
		Prompt3 <Understood. Tell me what I have to do.>
		Prompt3_1 <Yeah, I'll be a good little quest-bitch...>
		CPrompt3_1 <ifPCPassionate ifPCMelancholy Accept>

		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Reward Scene (allied)>
	desc <This will prevent rewards from being loaded without scenes.>
	requires <+Tre T:++ S:-- Common>

	% E1 is the NPC who will be giving the reward.
	% E2 is a scene that's acceptable for the meeting.
	element1 <Grab 5>
	element2 <Scene !Near -7 !Ally 1 Building Public>

	update <Email 1 WinComp E2 .next>
	.next <>

	% An empty return, to store the email...
	return < >

	Msg1 <\ELEMENT 1 @ \ELEMENT 2 :// Meet me at \ELEMENT 2 for your reward.>

Plot
	name <Reward Scene>
	desc <This will prevent rewards from being loaded without scenes.>
	requires <+Tre T:++ S:-->

	% E1 is the NPC who will be giving the reward.
	% E2 is a scene that's acceptable for the meeting.
	element1 <Grab 5>
	element2 <Scene !Near -7 !Okay 1 Building Public>

	update <Email 1 WinComp E2 .next>
	.next <>

	% An empty return, to store the email...
	return < >

	Msg1 <\ELEMENT 1 @ \ELEMENT 2 :// Meet me at \ELEMENT 2 for your reward.>

Plot
	name <Catchall Reward>
	desc <This is a catch-all reward that will just give the PC money and send him on his way.>
	requires <(#A|#L) +Tre S:++ (+F--|+Fmo)>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	element1 <Grab 6>
	element2 <Grab 5>

	% P1 = Initialization Counter

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-->

	update <if= V1 0 V= 1 1 MoveNPC E2 E1 &SetTargetChar 0>

	sub
		Persona 2
		rumor <%name2% has a reward for you.>
		*greeting <*MissionWasSuccess&Reward GoExit &AllyFac &EnemyFac>
		GoExit <CashPrize Reward Threat StoryDL 150 100 WinComp 0 .next>
		.next <+T-- +F-->

		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Reward then Guard>
	desc <The PC will get a reward, then be asked to go guard a building.>
	requires <#A +Tre S:++ (+F--|+Fmo|+Fmi) (+P--|+Pme|+Pun|+Pla|+Pen) ~F:CRM ~P:POL ~P:GOV>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is a metascene for the PC to guard
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <MetaScene !Near -7 *Building NeverFail>

	% P1 = Initialization Counter
	% P2 = Initial victory count
	% P3 = Mission Accepted Indicator

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-->

	update <if= V1 0 V= 1 1 MoveNPC E2 E1  V= 2 CurrentPPScore>

	return <ifNPCOK E2 if# V3 0 &SetTargetChar E2  ifG CurrentPPScore V2 else GoNoWin AlterContext .loadreward>
	GoNoWin <AlterContext .loadnoreward>
	.loadreward <+Tre +F-->
	.loadnoreward <+T-- +F-->

	sub
		Persona 2
		rumor <%name2% needs an adventurer for some kind of mission.>
		% V1 = Reward Obtained Counter
		greeting <if= V1 1 else GoGetReward Goto GoBriefing>
		*GoGetReward <*MissionWasSuccess&Reward GoNext &AllyFac &EnemyFac>
		GoNext <V= 1 1 CashPrize Reward Threat StoryDL 100 100 AddChat 1>
		*GoBriefing <*CORE_GuardJob GoAccept GoDeny>
		*GoAccept <*GoodLuckOnMission GoR1Exit ChatNPCFac &EnemyFac>
		GoR1Exit <P= 3 1 WinComp E3 .changes>
		.changes <+Tgs +F-->
		*GoDeny <*RejectMission GoR2Exit>
		GoR2Exit <LoseComp 0 .changes2>
		.changes2 <+T-- +F-->
		result1 <Goto GoBriefing>
		Prompt1 <Thank you.>

		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Reward then Fight>
	desc <The PC will get a reward, then be asked to go fight some mecha.>
	requires <#A  +Tre S:++ (+F--|+Fmo|+Fmi) (+P--|+Pme|+Pun|+Pen) ~F:MIL ~P:MIL>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the outdoors location where the encounter will be placed
	% E4 is the encounter sought by the PC

	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene Environs !Near -7>
	element4 <Prefab>
	place4 <3>

	% P1 = Initialization Counter
	% P2 = Initial victory count
	% P3 = Mission Accepted Indicator

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-->

	update <if= V1 0 V= 1 1 MoveNPC E2 E1 V= 2 CurrentPPScore>

	return <if# P3 0 ifNPCOK E2 &SetTargetChar E2  ifG CurrentPPScore V2 else GoNoWin AlterContext .loadreward>
	GoNoWin <AlterContext .loadnoreward>
	.loadreward <+Tre +F-->
	.loadnoreward <+T-- +F-->

	sub
		Persona 2
		rumor <%name2% needs an adventurer for some kind of mission.>
		% V1 = Reward Obtained Counter
		greeting <if= V1 1 else GoGetReward Goto GoBriefing>
		*GoGetReward <*MissionWasSuccess&Reward GoNext &AllyFac &EnemyFac>
		GoNext <V= 1 1 CashPrize Reward Threat StoryDL 100 100 AddChat 1>
		*GoBriefing <*CORE_BasicJob GoAccept GoDeny>
		*GoAccept <*GoodLuckOnMission GoR1Exit ChatNPCFac &EnemyFac>
		GoR1Exit <P= 3 1 WinComp E4 .changes>
		.changes <+Tin +F-->
		*GoDeny <*RejectMission GoR2Exit>
		GoR2Exit <LoseComp 0 .changes2>
		.changes2 <+T-- +F-->
		result1 <Goto GoBriefing>
		Prompt1 <Thank you.>

		STC MS_OFFICE
		SetID 1
	end
	inv
		STC ENCOUNTER-WANDER
		name <Target Mecha>
		EncounterMove 10
	end

Plot
	name <Basic Guard Job>
	desc <The PC will be asked to go guard a building.>
	requires <#A +Ttt +Fmi (+P--|+Pme|+Pun|+Pla|+Pen) ~F:CRM ~P:POL ~P:GOV>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is a metascene for the PC to guard
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <MetaScene !Near -7 *Building NeverFail>

	% P1 = Initialization Counter
	% P2 = Initial victory count
	% P3 = Mission Accepted Indicator

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-->

	update <if= V1 0 V= 1 1 MoveNPC E2 E1  V= 2 CurrentPPScore>

	return <ifNPCOK E2 if# V3 0 &SetTargetChar E2  ifG CurrentPPScore V2 else GoNoWin AlterContext .loadreward>
	GoNoWin <AlterContext .loadnoreward>
	.loadreward <+Tre +F-->
	.loadnoreward <+T-- +F-->

	sub
		Persona 2
		rumor <%name2% needs an adventurer for some kind of mission.>
		greeting <ifChatNPCUnknown else GoKnownGreeting ifChatNPCAlly else GoUnknownGreeting Goto GoKnownGreeting>
		*GoKnownGreeting <*IHaveAJobForYou GoBriefing>
		*GoUnknownGreeting <*AreYouHereAboutJob GoBriefing>
		*GoBriefing <*CORE_GuardJob GoAccept GoDeny>
		*GoAccept <*GoodLuckOnMission GoR1Exit ChatNPCFac &EnemyFac>
		GoR1Exit <P= 3 1 WinComp E3 .changes>
		.changes <+Tgs +F-->
		*GoDeny <*RejectMission GoR2Exit>
		GoR2Exit <LoseComp 0 .changes2>
		.changes2 <+T-- +F-->

		STC MS_OFFICE
		SetID 1
	end

Plot
	name <Basic Fighting Job>
	desc <The PC will be asked to go fight some mecha.>
	requires <#A +Ttt +Fmi (+P--|+Pme|+Pun|+Pen) ~F:MIL ~P:MIL>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the outdoors location where the encounter will be placed
	% E4 is the encounter sought by the PC

	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene Environs !Near -7>
	element4 <Prefab>
	place4 <3>

	% P1 = Initialization Counter
	% P2 = Initial victory count
	% P3 = Mission Accepted Indicator

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-->

	update <if= V1 0 V= 1 1 MoveNPC E2 E1 V= 2 CurrentPPScore>

	return <if# P3 0 ifNPCOK E2 &SetTargetChar E2  ifG CurrentPPScore V2 else GoNoWin AlterContext .loadreward>
	GoNoWin <AlterContext .loadnoreward>
	.loadreward <+Tre +F-->
	.loadnoreward <+T-- +F-->

	sub
		Persona 2
		rumor <%name2% needs a cavalier for some kind of mission.>
		greeting <ifChatNPCUnknown else GoKnownGreeting ifChatNPCAlly else GoUnknownGreeting Goto GoKnownGreeting>
		*GoKnownGreeting <*IHaveAJobForYou GoBriefing>
		*GoUnknownGreeting <*AreYouHereAboutJob&Mecha GoBriefing>
		*GoBriefing <*CORE_BasicJob GoAccept GoDeny>
		*GoAccept <*GoodLuckOnMission GoR1Exit ChatNPCFac &EnemyFac>
		GoR1Exit <P= 3 1 WinComp E4 .changes>
		.changes <+Tin +F-->
		*GoDeny <*RejectMission GoR2Exit>
		GoR2Exit <LoseComp 0 .changes2>
		.changes2 <+T-- +F-->

		STC MS_OFFICE
		SetID 1
	end
	inv
		STC ENCOUNTER-WANDER
		name <Target Mecha>
		EncounterMove 10
	end


Plot
	name <Basic Date>
	desc <This is a generic date. If the PC answers some questions well, the relationship status gets upgraded.>
	Size 2
	requires <#A +Tda S:++>

	% E1 = Scene for meeting
	% E2 = NPC to meet
	element1 <Grab 6>
	element2 <Grab 5>

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  &SetTargetChar 0  MoveNPC E2 E1>

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% is waiting for you in %name1%.>
		*greeting <*DateGreeting GoQuestion>
		*GoQuestion <*DateQuestion GoActivity>
		*GoActivity <*DateActivity GoResult>
		GoResult <if= NumDatePoints 0 else GoCheckLow NewChat Say 1 StoryNote 2 LoseComp 0 .next>
		GoCheckLow <if= NumDatePoints 1 else GoSuccess EndChat Time 3600 Alert 3 Say 4 StoryNote 5   LoseComp 0 .next>
		GoSuccess <EndChat Time 3600 Alert 3 Say 6 StoryNote 7 PCLover E2  WinComp 0 .next>
		.next <+T-- +F-->
		Msg1 <Um, I just remembered, I have to go. There's something important I have to do. Maybe I'll see you around later.>
		Msg2 <\ELEMENT 2 backed out on your date.>
		Msg3 <Afterwards...>
		Msg4 <Thanks for taking me out. I'll call you sometime. Really.>
		Msg5 <Your date with \ELEMENT 2 didn't go so well.>
		Msg6 <That was a lot of fun. Let's do it again some time.>
		Msg7 <Your date with \ELEMENT 2 went very well.>

		STC MS_CLUB
		special <SOLO>
		SetID 1
	end

Plot
	name <We can have that date now>
	desc <The PC has succeeded in the task, and so may date T.>
	requires <#A +Tre S:++ +Fda>

	% E1 = Scene for meeting
	% E2 = NPC to meet
	% E3 = Scene for date
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <MetaScene !Near -7 *Building NeverFail>

	% P1 = Initialization Counter

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  &SetTargetChar 0  MoveNPC E2 E1>

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% wants to speak with you in %name1%.>
		*Greeting <*MissionWasSuccess GoContinue na na>
		GoContinue <AddChat 1>
		result1 <NewChat Say 1 StoryNote 2 &SetTargetChar E2   WinComp E3 .date>
		.date <+Tda +F-->
		Msg1 <We can go on that date now. Meet me in \ELEMENT 3 , and don't be late.>
		Msg2 <\ELEMENT 2 invited you on a date to \ELEMENT 3 .>
		Prompt1 <Continue>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <An Admirer>
	desc <The PC is asked on a date by T.>
	requires <#A +Ttt +Fda>

	% E1 = Scene for meeting
	% E2 = NPC to meet
	% E3 = Scene for date
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene Building Public !Near -7>

	% P1 = Initialization Counter

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  &SetTargetChar 0  MoveNPC E2 E1>

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% wants to speak with you in %name1%.>
		Greeting <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <NewChat Say 2 StoryNote 3 &SetTargetChar E2   WinComp E3 .date>
		.date <+Tda +F-->
		result2 <NewChat Say 4 StoryNote 5 AddRenown -1  LoseComp 0 .loss>
		.loss <+T-- +F-->
		Msg1 <I was wondering... How would you like to go out with me sometime?>
		Msg2 <Great! Let's meet at \ELEMENT 3 a bit later.>
		Msg3 <You agreed to go on a date with \ELEMENT 2 at \ELEMENT 3 .>
		Msg4 <That's too bad...>
		Msg5 <You rejected \ELEMENT 2 's offer to go on a date.>
		Prompt1 <I'd like that very much.>
		Prompt2 <Sorry, I don't think so.>

		STC MS_CAFE
		SetID 1
	end


Plot
	name <Let's Date>
	desc <The PC asks for a date, the NPC says yes.>
	requires <#A +Ttt +Fad Common>

	% E1 = Scene for meeting
	% E2 = NPC to meet
	% E3 = Scene for date
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene Building Public !Near -7>

	% P1 = Initialization Counter

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  &SetTargetChar 0  MoveNPC E2 E1>

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% is waiting for you in %name1%.>
		*Greeting <*HowAreYou GoShimli>
		*GoShimli <*SHIMLI_CHATTER GoAsk>
		*GoAsk <*PCInviteNPC GoAccept GoLose>
		GoAccept <NewChat Say 1 StoryNote 2 &SetTargetChar E2   WinComp E3 .date AddChat 1>
		.date <+Tda +F-->
		GoLose <NewChat Say 3 StoryNote 4 AddRenown -1  LoseComp 0 .loss>
		.loss <+T-- +F-->
		*result1 <*GoodBye>
		Msg1 <Sounds good! How about we meet in \ELEMENT 3 later on today?>
		Msg2 <\ELEMENT 2 agreed to meet you in \ELEMENT 3 .>
		Msg3 <Maybe I'll see you around later. Take care.>
		Msg4 <You didn't ask \ELEMENT 2 for a date.>
		Prompt1 <I'll see you there.>

		STC MS_CAFE
		SetID 1
	end


Plot
	name <Let's Combat Date>
	desc <The PC is asking for a date from an adventurous sort... T won't agree if PC can't win duel.>
	requires <#A +Ttt +Fad (T:ADVEN|T:MILIT|T:ENEMY) ~C:ADVEN ~C:MILIT ~P:PDASS ~+P-- ~+Gpo>

	% E1 = Scene for meeting
	% E2 = NPC to meet
	% E3 = Outdoors scene
	% E4 = Encounter
	% E5 = Scene for immediate date if PC can convince NPC of that
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene Outdoors !Near -7>
	element4 <Prefab>
	place4 <3>
	element5 <Scene Building Public Meeting !Near -7>

	% P1 = Initialization Counter
	% P2 = Victory score at start
	% P3 = In zero, can maybe get date

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  &SetTargetChar 0  MoveNPC E2 E1   V= 2 CurrentPPScore   GrabEntrance E4 GSetSAtt name Msg1>

	% Upon return, if the PC won the duel, the NPC will agree to a date.
	return <if= P3 0  ifNPCOK E2 ifG CurrentPPScore V2 &SetTargetChar E2 AlterContext .next>
	.next <+Tre +Fda>

	Msg1 <\ELEMENT 2 's Mecha>

	sub
		Persona 2
		special <UNLISTED>
		% V1 = Have been challenged
		rumor <%name2% is waiting for you in %name1%.>
		Greeting <if= V1 1 else GoHowAreYou   Goto GoChallenge>
		*GoHowAreYou <*HowAreYou GoShimli>
		*GoShimli <*SHIMLI_CHATTER GoAsk>
		*GoAsk <*PCInviteNPC GoChallenge GoLose>
		GoChallenge <NewChat Say 1 V= 1 1  AddChat 1 AddChat 2 AddChat 3>
		GoLose <NewChat Say 3 StoryNote 4 P= 3 1 AddRenown -1  LoseComp 0 .loss>
		.loss <+T-- +F-->
		result1 <NewChat Say 5 StoryNote 2 &SetTargetChar E2  WinComp E4 .next>
		.next <+Tdu>
		result2 <NewChat Say 6 AddChat 4 AddChat 5 AddChat 3>
		result3 <NewChat Say 7 StoryNote 8 P= 3 1 LoseComp 0 .loss>
		result4 <Goto result1>
		result5 <ifFlirtation ChatNPCEgo else GoR5Fail AddRenown 1 NewChat Say 9 StoryNote 10   P= 3 1   &SetTargetChar E2  WinComp E5 .date>
		.date <+Tda +F-->
		GoR5Fail <NewChat Say 11  StoryNote 8 P= 3 1 LoseComp 0 .loss>
		Msg1 <I'm not entirely sure you're ready for something like that. Here's an idea- let's fight a match in our meks, and if you win I'll go out with you.>
		Msg2 <\ELEMENT 2 challenged you to a duel in \ELEMENT 3 , to prove your worthiness.>
		Msg3 <Well, I have a lot of things to do today, so I can't really stick around.>
		Msg4 <You didn't ask \ELEMENT 2 for a date.>
		Msg5 <Great! Get your mecha ready then meet me in \ELEMENT 3 . I can't wait to see how well you perform...>
		Msg6 <What's life without a bit of danger? I don't want to get involved with someone who isn't at least my equal. Now are you going to prove yourself or not?>
		Msg7 <In that case I'd say you have more important things to worry about than going on dates. I can tell you've got a lot of issues... Maybe you need some time for yourself before starting a relationship.>
		Msg8 <\ELEMENT 2 rejected your romantic advances.>
		Msg9 <I'll bet you can... Okay, I'll go out with you. Meet me at \ELEMENT 5 later today. Don't be late or we may end up fighting after all.>
		Msg10 <\ELEMENT 2 agreed to go on a date at \ELEMENT 5 .>
		Msg11 <I sincerely doubt it. Don't waste my time.>
		Prompt1 <It's a deal!>
		CPrompt1 <ifG PCMeks 0 Accept>
		Prompt2 <That's too dangerous...>
		Prompt2_1 <I don't want to fight you...>
		Prompt3 <I don't have a mecha!>
		CPrompt3 <if= PCMeks 0 Accept>
		Prompt4 <Alright, I'll do it.>
		CPrompt4 <ifG PCMeks 0 Accept>
		Prompt5 <There are other ways I can prove myself.>

		STC MS_CAFE
		SetID 1
	end
	inv
		STC ENCOUNTER-DUEL
	end

Plot
	name <Basic Duel>
	desc <You and the target character duke it out in mecha combat.>
	Size 6
	requires <#A +Tdu T:++>

	% E1 = Encounter for fight
	% E2 = NPC to fight
	element1 <Grab 6>
	element2 <Grab 5>

	% V1 = Initialization Counter
	% V2 = Opponent Skill Value
	update <if= V1 0 V= 1 1   MoveNPC E2 E1 SetNPCTeam E2 2  V= 2 StoryDL  V+ 2 d5  NPCLevel E2 V2>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		% L4 = Identity of enemy NPC
		MapWidth 30
		MapHeight 30

		special <ARENA>

		Start <if= L2 0 L= 2 1   ForceChat E2   L= 4 E2   Compose FAINT E2 .E2Faint>
		.E2Faint <if= L1 0  Goto GoDie>  
		GoDie <L= 1 2  StoryNote 1  WinComp 0 .next>
		nu1 <if= T1 0  Return   if= L1 0 L= 1 1   StoryNote 2   ForceChat L4>
		nu2 <if= T2 0           if= L1 0 L= 1 2   StoryNote 3   AddRenown 1   ifNPCOK L4 else GoDie  XPV 100   ForceChat L4>

		.next <+T-- +F-->

		Msg1 <You killed \ELEMENT 2 in a duel.>
		Msg2 <You faced \ELEMENT 2 in a duel, and lost.>
		Msg3 <You faced \ELEMENT 2 in a duel, and won.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			ParaX 25
			ParaY 25
		end

		Persona 2
		special <UNLISTED>
		greeting <if= L1 2 else GoCheckLoss WinComp  0 .next Goto GoWinSpeech>
		*GoWinSpeech <*YouWonFair>
		GoCheckLoss <if= L1 1 else GoThreat LoseComp  0 .next Goto GoLoseSpeech>
		.next <+T-- +F-->
		*GoLoseSpeech <*YouLose>
		*GoThreat <*ArenaChallenge GoExpo>
		*GoExpo <*THEME_EXPO&Enemy na>
	end

Plot
	name <Lead: POLICE>
	desc <The police might need help with some criminals.>
	%% The PC can't be an enemy of the city's faction, and the PC's enemy must criminal or
	%% the PC must be a police officer himself.
	requires <#A +T-- -L:ENEMY (F:crm|C:POLIC) ~P:pol>
	% E1 is the town leader.
	element1 <Character !Near -7 !Ally -7 "POLIT">
	update <News 1  &SetTargetChar E1  WinComp EScene 1 .next>
	.next <+Tgt +Fmi>
	return < >
	Msg1 <\ELEMENT 1 in \NARRATIVE 7 seeks mecha pilot to aid in criminal investigation. Apply at \SCENE EScene 1 .>


Plot
	name <Lead: LEADER>
	desc <The leader of town needs adventurers. How cliche.>
	%% The PC can't be an enemy of the city's faction, and the PC's enemy must either
	%% be unknown, military, or a government.
	requires <#A +T-- -L:ENEMY (F:--|F:mil|F:gov) ~P:gov>
	% E1 is the town leader.
	element1 <Character !Near -7 !Ally -7 "POLIT">
	update <News 1  &SetTargetChar E1  WinComp EScene 1 .next>
	.next <+Tgt +Fmi>
	return < >
	Msg1 <\ELEMENT 1 in \NARRATIVE 7 seeks mecha pilots for urgent mission. Apply at \SCENE EScene 1 .>

Plot
	name <Lead: FRIEND>
	desc <A friend may have information for you.>
	requires <#A +T-- ~+Glo>
	% E1 is a friend of the PC.
	element1 <Character !Near -7 "FRIEND">
	update <Email 1  &SetTargetChar E1  WinComp EScene 1 .next>
	.next <+Tgt +F-->
	return < >
	Msg1 <\ELEMENT 1 @ \SCENE EScene 1 :// How's it going? Come see me the next time you're in \NARRATIVE 7 .>

Plot
	name <Lead: PCFAC>
	desc <A colleague may have a job for you.>
	requires <#A +T-- P:ALLY>
	% E1 is a character who should be allied with the PC/TarFac.
	element1 <Character !Near -7 !Ally -3 "ArchAlly">
	update <Email 1  &SetTargetChar E1  WinComp EScene 1 .next>
	.next <+Tgt +Fmi>
	return < >
	Msg1 <\ELEMENT 1 @ \SCENE EScene 1 :// I might have a job for you. Come see me when you're in \NARRATIVE 7 .>

Plot
	name <Lead: TARFAC>
	desc <A semi-colleague may have a job for you.>
	requires <#A +T-- P:++>
	% E1 is a character who should be allied with the PC/TarFac.
	element1 <Character !Near -7 !Ally -3>
	update <Email 1  &SetTargetChar E1  WinComp EScene 1 .next>
	.next <+Tgt +Fmi>
	return < >
	Msg1 <\ELEMENT 1 @ \NARRATIVE 7 :// You're needed in \NARRATIVE 7 . Come meet me in \SCENE EScene 1 .>

Plot
	name <Lead: FAITH>
	desc <A colleague may have information for you.>
	requires <#A +T-- C:FAITH>
	% E1 is a character who might have something useful for the PC.
	element1 <Character !Near -7 "Faith">
	update <Email 1  &SetTargetChar E1  WinComp EScene 1 .next>
	.next <+Tgt +Fin>
	return < >
	Msg1 <\ELEMENT 1 @ \SCENE EScene 1 :// I have some information for you. Come see me when you're in \NARRATIVE 7 .>

Plot
	name <Search for Target>
	desc <You have to search for the target a bit.>
	Size 1
	requires <#A +Tlt T:++ S:-- Common>

	% E1 is the character who knows where T is
	element1 <Character !Near -7 Citizen>
	element2 <Scene Building Public !Near -7>
	element3 <Grab 5>

	% P1 = Initialization Counter

	start <ifNPCOK E1 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+Tlt>

	update <if=  P1 0  P= 1 1  FreezeNPC E3>

	sub
		Persona 1
		rumor <%name1% knows where %name3% is.>
		*greeting <*LookingForNPC E3 GoSuccess GoFailure>
		GoSuccess <NewChat Say 1 StoryNote 2 WinComp E2 .next>
		.next <+Tgt>
		GoFailure <NewChat Say 3 LoseComp E2 .loss>
		.loss <+Tlt>
		Msg1 <I think \SPR E3 's in \ELEMENT 2 now.>
		Msg2 <\ELEMENT 1 told you that \ELEMENT 3 is in \ELEMENT 2 .>
		Msg3 <I don't know anything about that.>
		Msg3_1 <Sorry, I really don't know where \ELEMENT 3 is.>
		CMsg3_1 <ifG React 30 Accept>
		Msg3_2 <I don't know anything. Just leave me alone.>
		CMsg3_2 <ifG 1 React Accept>
	end

Plot
	name <Switch: Conversation>
	desc <The PC finds Target at a MetaScene without incident.>
	requires <#A +Tlt T:++ S:-->
	update <WinComp E1 .changes>
	.changes <+Ttt>
	element1 <Metascene  !Near -7 *Building NeverFail>

Plot
	name <Switch: Conversation>
	desc <The PC finds Target at a regular scene without incident.>
	requires <#A +Tlt T:++ S:-->
	update <WinComp E1 .changes>
	.changes <+Ttt>
	element1 <Scene Building Public !Near -7>

Plot
	name <Switch: Conversation>
	desc <The PC finds Target without incident.>
	requires <#A +Tlt T:++ S:++ Common>
	update <WinComp N6 .changes>
	.changes <+Ttt>

Plot
	name <Switch: Conversation>
	desc <The PC finds Target without incident.>
	requires <#A +Tgt T:++ Common>
	update <WinComp N6 .changes>
	.changes <+Ttt>

Plot
	name <Switch: Talk about Mission>
	desc <The PC finds Target without incident.>
	requires <#A +Tgt +Fmi T:++ Common>
	update <WinComp N6 .changes>
	.changes <+Ttt>

Plot
	name <Switch: Talk about Information>
	desc <The PC finds Target without incident.>
	requires <#A +Tgt +Fin T:++ Common>
	update <WinComp N6 .changes>
	.changes <+Ttt>

Plot
	name <Mecha at the Gate>
	Size 1
	desc <Building PC asked to guard surrounded by mecha.>
	requires <#A +Tgs ~+Ffi S:++>

	% E1 is the scene where this will take place
	% E2 is the NPC to tell the PC about the security problem
	element1 <Grab 6>
	element2 <Prefab>
	element3 <Scene Urban !Near -7>
	element4 <Prefab>
	place4 <3 !Near 1>

	% P1 = Initialization Counter

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  MoveNPC E2 E1>

	sub
		Persona 2
		special <UNLISTED>
		*greeting <*DefenseCheckIn GoBriefing>
		GoBriefing <NewChat Say 1 StoryNote 2 WinComp E4 .changes>
		.changes <+Tin +Ffi>
		Msg1 <Some unauthorized mecha have been spotted just outside.>
		Msg2 <You learned \SCENE E1 was about to be attacked.>


		STC MS_OFFICE
		SetID 1
	end
	inv
		NPC Soldier
		job <Security Guard>

		STC ENCOUNTER-SEEKPC
		name <Hostile Mecha>
		EncounterMove 100
	end

Plot
	name <Thanks for the Help>
	desc <The PC has rescued someone, who now wants to join the lance.>
	requires <#A +Ttt +Frt T:++ -T:Ally -T:Enemy ~+Glo>

	%% E1 is the scene where this will take place
	%% E2 is the NPC that the PC rescued
	%% E3 is a nice scene for a date
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene !Near -7 Building Public Meeting>

	start <ifNPCOK E2 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1  &SetTargetChar 0  MoveNPC E2 E1>

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% has been recuperating in %name1%.>
		*greeting <*ThanksForRescue GoOffer>
		GoOffer <PCFriend E2 ifG React 30 else GoNoOffer ifG PCCharm 10 else GoNoOffer NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoNoOffer <ifChatNPCSexy else GoCashAward ifG React d25 else GoCashAward NewChat Say 8 AddChat 6 AddChat 7>
		GoCashAward <NewChat Say 7  StoryNote 5 CashPrize Reward Threat StoryDL 150 100 WinComp 0 .next>
		result1 <NewChat Say 2 StoryNote 3 PCAlly E2 WinComp 0 .next>
		.next <+T-- +F-->
		result2 <NewChat Say 4 StoryNote 5 CashPrize Reward Threat StoryDL 150 100 WinComp 0 .next>
		result3 <NewChat Say 6 AddChat 4 AddChat 5>
		result4 <Goto result1>
		result5 <Goto result2>
		result6 <NewChat Say 9 StoryNote 10 &SetTargetChar E2 WinComp E3 .date>
		.date <+Tda +F-->
		result7 <Goto result2>
		Msg1 <I thought that to repay you for saving my life, I could join your team and maybe help out. What do you think?>
		Msg2 <Thanks! I won't let you down!>
		Msg3 <\ELEMENT 2 joined your lance in thanks for saving \PPR E2 life.>
		Msg4 <I understand. Well, the least I can do is give you some money. Thanks for your help. I won't forget it.>
		Msg5 <\ELEMENT 2 rewarded you for saving \PPR E2 life.>
		Msg6 <I know, but... I have other talents that could be useful. And you could teach me to fight. Can't I come along with you, please?>
		Msg7 <In thanks for saving my life, I'd like you to have this money.>
		Msg8 <In thanks for saving my life, I'd like... honestly, I'd like to go out with you sometime.>
		Msg9 <Great! Meet me in \ELEMENT 3 later on.>
		Msg10 <After saving \PPR E2 life, \ELEMENT 2 invited you to \ELEMENT 3 for a date.>
		Prompt1 <I think it's a good idea.>
		Prompt1_1 <I'd be honored to have you join me.>
		Prompt2 <No thanks. I don't need any help.>
		Prompt2_1 <Sorry, I'm not looking for another lancemate.>
		Prompt3 <Thanks, but aren't you a bit useless?>
		CPrompt3 <if= NPCSkill E2 10 0 Accept>
		Prompt3_1 <Thanks, but you aren't much of a fighter.>
		Prompt4 <Alright. You can join me.>
		Prompt5 <I don't have time to look after you.>
		Prompt6 <I'd like that too.>
		Prompt7 <Sorry, I don't want to get involved right now.>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <A Pilot in Trouble>
	size 6
	desc <Find a pilot fighting other mecha; can help out.>
	requires <#A +Tin ~+Ffi>

	% E1 is the metascene where combat will take place
	% E2 is the place where the pilot will be found after the battle
	% E3 is the pilot to help
	element1 <Grab 6>
	element2 <Scene !Near -7 !XClude -2 building public>
	element3 <Prefab>
	place3 <2 ally pass>

	% V1 = Initialization Counter

	update <if= V1 0 V= 1 1   MoveNPC E3 E1 SetNPCTeam E3 3 NPCLevel E3 StoryDL>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		% L1 = Match Over counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1 ForceChat E3  Compose FAINT E3 .e3dies>
		% If the NPC dies, this scenario ends in a loss.
		.e3dies <L= 1 1 StoryNote 1 LoseComp 0 .loss>
		% If the PC defeats all of T2 before E3 dies, it's a win.
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1               StoryNote 1 LoseComp 0 .loss>
		nu2 <if= T2 0   if= V1 0 V= 1 1   XPV 100  AddRenown 1 &SetTargetChar E3  ifAwareness 15 else GoNoAware Alert 4 RunAway E3 StoryNote 2 WinComp E2 .win>
		GoNoAware <Alert 3 RunAway E3 StoryNote 2 WinComp E2 .win>
		Msg1 <You found \ELEMENT 3 being attacked by mecha, but failed to help.>
		Msg2 <You rescued \ELEMENT 3 from hostile mecha.>
		Msg2_1 <You rescued \ELEMENT 3 from \FACTION N2 .>
		CMsg2_1 <ifFactionExists N2 Accept>
		Msg3 <As the battle ends, \ELEMENT 3 races off for \NARRATIVE 7 .>
		Msg4 <As the battle ends, \ELEMENT 3 races off for \ELEMENT 2 .>
		.win <+Tlt +Frt>
		.loss <+T-- +F-->

		sub
			team 1
			SetAlly 3
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction N2   WMecha 2 Threat StoryDL 130>
			ParaX 25
			ParaY 25

			team 3
			SetAlly 1
			SetEnemy 2
			ParaX 10
			ParaY 5
		end

		Persona 3
		special <Unlisted>
		greeting <if# V1 0 else GoFirst EndChat Say 1>
		*GoFirst <*HelpMeVsMecha GoAccept GoDeny>
		GoAccept <V= 1 1 EndChat Say 2 AddHeroic 1>
		GoDeny <V= 1 1 EndChat Say 3 AddRenown -1>
		Msg1 <Shouldn't we concentrate on fighting right now?>
		Msg2 <Thanks! Between the two of us, they shouldn't be much trouble...>
		Msg3 <Well, seeing that you're here, it seems that those mecha are your problem as well right now...>

	end
	inv
		NPC Mecha Pilot
	end

Plot
	name <Surprise Attack: Social Meeting>
	size 1
	desc <The meeting place is attacked by mecha!>
	requires <#A +Tgt -+Fmi T:++ S:meta>

	% P1 = Initialization Counter

	% E1 is the scene where this will take place
	% E2 is the NPC the PC wants to talk to
	% E3 is the encounter that caused the trouble
	% E4 is the scene where the "debriefing" will take place
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Prefab>
	place3 <~1 !Near 1>
	element4 <Scene Building Public !Near -7 !Okay -5>

	update <if= P1 0   P= 1 1   MoveNPC E2 E1>
	return <ifNPCOK E2 &SetTargetChar E2 &SetCompScene E4 AlterContext .next>
	.next <+Ttt +Frt>

	sub
		MetaScene 1
		% V1 = Bombing has started

		ClubMap
		MapWidth 20
		MapHeight 25

		type <CLUB LEGIT PUBLIC>
		special <AddExit>
		Content <Some 5 25 Sub *CLUB_X>

		5Min <if= V1 0 else GoBomb if= d3 1 V= 1 1 AirRaidSiren Alert 1 StoryNote 2 Bomb DeleteNPC E2 LoseComp E3 .next>
		GoBomb <Print 3 Bomb>
		.next <+Tin +Ffi>
		nu1 <if= T1 0 Return>
		Msg1 <Unexpectantly, the building is attacked by mecha!>
		Msg2 <While meeting \PERSONA E2 , \SCENE E1 was attacked by mysterious mecha.>
		Msg3 <The battle outside rages!>
		sub
			Team 1

			Team 2
			name <Civilian>
			setally 1
			Passive

			room
			name <Dance Floor>
			desig <EntranceGrid>
			Content <Some 8 45 Here *URBAN_X>

			Persona 2
			special <UNLISTED>
			*greeting <*HowAreYou GoMessage>
			GoMessage <NewChat if= L1 1 else GoFirstTime Say 2>
			GoFirstTime <Say 1 L= 1 1 AirRaidSiren StoryNote 3 WinComp E3 .next>
			.next <+Tin +Ffi>
			Msg1 <We're being attacked!>
			Msg2 <Let's get out of here!>
			Msg3 <While meeting \PERSONA E2 , \SCENE E1 was attacked by mysterious mecha.>
		end
	end
	inv
		STC ENCOUNTER-SEEKPC
		name <Mysterious Mecha>
		EncounterMove 100
	end

Plot
	name <Surprise Attack: Business Meeting>
	size 1
	desc <The meeting place is attacked by mecha!>
	requires <#A +Tgt ~+Fmi T:++ S:meta>

	% P1 = Initialization Counter

	% E1 is the scene where this will take place
	% E2 is the NPC the PC wants to talk to
	% E3 is the encounter that caused the trouble
	% E4 is the scene where the "debriefing" will take place
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Prefab>
	place3 <~1 !Near 1>
	element4 <Scene Building Public !Near -7 !Okay -5>

	update <if= P1 0   P= 1 1   MoveNPC E2 E1>
	return <ifNPCOK E2 &SetTargetChar E2 &SetCompScene E4 AlterContext .next>
	.next <+Ttt +Frt>

	sub
		MetaScene 1
		% V1 = Bombing has started

		ClubMap
		MapWidth 20
		MapHeight 25

		type <BUSINESS LEGIT PUBLIC>
		special <AddExit>

		5Min <if= V1 0 else GoBomb if= d3 1 V= 1 1 AirRaidSiren Alert 1 StoryNote 2 Bomb DeleteNPC E2 LoseComp E3 .next>
		GoBomb <Print 3 Bomb>
		.next <+Tin +Ffi>
		nu1 <if= T1 0 Return>
		Msg1 <Unexpectantly, the building is attacked by mecha!>
		Msg2 <While meeting \PERSONA E2 , \SCENE E1 was attacked by mysterious mecha.>
		Msg3 <The battle outside rages!>
		sub
			Team 1

			Team 2
			name <Civilian>
			setally 1
			Passive

			room
			name <Foyer>
			desig <EntranceGrid>
			Content <Some 8 45 Here *URBAN_X>

			Persona 2
			special <UNLISTED>
			*greeting <*HowAreYou GoMessage>
			GoMessage <NewChat if= L1 1 else GoFirstTime Say 2>
			GoFirstTime <Say 1 L= 1 1 AirRaidSiren StoryNote 3 WinComp E3 .next>
			.next <+Tin +Ffi>
			Msg1 <We're being attacked!>
			Msg2 <Let's get out of here!>
			Msg3 <While meeting \PERSONA E2 , \SCENE E1 was attacked by mysterious mecha.>
		end
	end
	inv
		STC ENCOUNTER-SEEKPC
		name <Mysterious Mecha>
		EncounterMove 100
	end


