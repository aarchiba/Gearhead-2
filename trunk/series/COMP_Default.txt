% Component List

% STATE OF STORY DESCRIPTION
%  The state of the story is determined by both the "context" attribute held in
%  the story and by certain variables which are determined when it's time to
%  generate a new component. These variables could include:
%  - The existence and nature of the palette entries
%  - The alleigance of the current location with either the PC's faction
%    or the palette enemy faction

% The palette entries are:
%	STAT_XRP_EnemyChar = 1;			{ E: }
%	STAT_XRP_EnemyFac = 2;			{ F: }
%	STAT_XRP_PCFac = 3;			{ P: }
%	STAT_XRP_PlotStateNPC = 4;		{ M: }
%	STAT_XRP_TargetNPC = 5;			{ T: }
%	STAT_XRP_ComponentScene = 6;		{ S: }
%	STAT_XRP_EpisodeScene = 7;		{ L: }
%	STAT_XRP_TargetItem = 8;		{ I: }

Plot
	name <Basic Duel>
	desc <You're going to challenge some guy.>
	requires <#A +Tgt +Fch>

	% E1 is the scene where the meeting will take place.
	% E2 is the friend being met.
	% E3 is a location for the duel.
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+T-- +F-->

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% Branch activators
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SetPlotStatus %id2%>
	.%id1%_%plotid%_GoLose <SetPlotStatus %id3%>
	GoE2Died <WinComp 0 .changes>

	sub
		Persona 2
		greeting <if= PlotStatus 0 else GoChat Goto GoFirstTime>
		*GoChat <*MISC_CHATTER>
		*GoFirstTime <*PCChallengeNPC GoMakeChallenge GoWussOut>
		GoMakeChallenge <NewChat Say 2 SMemo 3 History 4 SetPlotStatus %id1%>
		GoWussOut <NewChat Say 1>

		Msg1 <I wish I had something to do.>
		Msg2 <You think you're good enough to face me? Meet me in %name3% and we'll find out.>
		Msg3 <You challenged %name2% to a duel in %name3%.>
		Msg4 <You challenged %name2% to a duel.>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <Basic Fighting Job>
	desc <The PC will be asked to go fight some mecha.>
	requires <#A +Tgt +Fmi (+P--|+Pme|+Pun) ~F:MIL ~P:MIL>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the outdoors location where the encounter will be placed

	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	element3 <Scene Environs !Near -7>

	start <if= PlotStatus 0 ifNPCDead E2 LoseComp 0 .loss>
	.loss <+T-->

	%% SubPlot1 is the combat encounter itself
	SubPlot1 <*CS_MechaEncounter 2 3>

	sub
		Persona 2
		rumor0 <%name2% needs a cavalier for some kind of mission.>
		greeting <if= PlotStatus 0 else GoChat ifChatNPCUnknown else GoKnownGreeting ifChatNPCAlly else GoUnknownGreeting Goto GoKnownGreeting>
		*GoKnownGreeting <*IHaveAJobForYou GoBriefing>
		*GoUnknownGreeting <*AreYouHereAboutJob&Mecha GoBriefing>
		*GoBriefing <*CORE_BasicJob GoAccept GoDeny>
		*GoAccept <*GoodLuckOnMission GoR1Exit ChatNPCFac &EnemyFac>
		*GoChat <*MISC_CHATTER>
		GoR1Exit <SetPlotStatus %id1%>
		*GoDeny <*RejectMission GoR2Exit>
		GoR2Exit <LoseComp 0 .changes2>
		.changes2 <+T-- +F-->

		STC MS_OFFICE
		SetID 1
	end

