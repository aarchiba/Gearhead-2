%%
%% *:ConstructionProblem Content
%%
%% There's a problem at a construction site. To start this subquest, the calling
%% quest should set its QuestId to its component ID. At the end the QuestID will
%% be set to -1 if the construction problem was solved, or -2 if the construction
%% problem has been rendered unsolvable.
%%
%% Param1: The boss NPC who sent the PC to find the problem
%% Param2: Construction Site Scene

Content
	name <Robot Rampage>
	requires <*:ConstructionProblem>
	desc <The construction robots have malfunctioned; the PC can repair them or destroy them.>

	% E1 is the boss
	% E2 is the construction site
	% E3 is the robotics specialist
	% E4 is the basement full of robots
	Element3 <Prefab>
	Scene3 <Grab 2>
	Team3 <Citizens>
	TeamData3 <Pass Ally>

	Scene4 <New *BLANK.Basement>

	sub
		Persona 3
		% This character just explains how to win the component; other than that he's
		% pretty useless. If the PC wins without destroying all the robots, then this
		% person will become a friend.
		rumor%id% <%name3% is in charge of the robots for %name1%'s project.>
		% V1 = Have given victory speech
		% V2 = Have given first introduction
		greeting <if= QuestStatus %qid% -1 else GoCheckQuest if= V1 0 else GoChat V= 1 1 if= SceneVar %s4% 1 0 else GoBadWin PCFriend %3% NewChat Say 7>
		GoBadWin <NewChat Say 8>
		GoCheckQuest <if= QuestStatus %qid% %id% else GoChat if= V2 1 else GoFirstTime NewChat Say 5 AddChat 3 AddChat 4>
		*GoFirstTime <*SomeoneSentPCToTalk %1% GoExplain>
		*GoChat <*MISC_CHATTER>
		GoExplain <V= 2 1 NewChat Say 1 QMemo %qid% 2 AddChat 1 AddChat 2>
		Result1 <NewChat Say 3>
		Result2 <NewChat Say 4>
		Result3 <Goto GoExplain>
		Result4 <NewChat Say 6>
		Msg1 <The construction robots on the lower level have malfunctioned. They haven't gone aggressive, exactly, it's just that they no longer seem to recognize the difference between human beings and building materials. It's pretty dangerous down there.>
		Msg2 <%name3% told you that the construction robots at %name1%'s project have malfunctioned, making life dangerous for the workers.>
		Msg3 <Not really. The control station here stopped working, so we can't even shut them down remotely. Personally I suspect industrial sabotage, though I shudder to think that one of our employees could be working for someone else.>
		Msg4 <Destroying the robots is probably the only way we're going to get construction back on track. Please try to leave enough wreckage so we can figure out what went wrong.>
		Msg5 <Do you have anything to report?>
		Msg6 <Please do your best.>
		Msg7 <The problem has been fixed, and we didn't even need to destroy all the robots. Thanks to you we'll be back on schedule in no time! You should go tell %name1% about what you've accomplished.>
		Msg8 <The problem has been solved, so I guess it's time for us to get to work... and without any robots to do the heavy bits, either. You should go tell %name1% that the project is back on track.>
		Prompt1 <Any idea what happened?>
		Prompt2 <Don't worry, I can deal with them.>
		Prompt3 <Could you remind me what's going on again?>
		Prompt4 <Not yet. I'll be back when I do.>

		MetaScene 3
		sub
			room
			desig <HOME>
			minimap <......1.2..#3#..###......>
			special <SHAREDPALETTE>
			inv
				STC COMPUTER-1
				% Using this computer may repair the robots.
				MiniMapComponent 2
				PDir 6
				use <if= QuestStatus %qid% %id% else GoQuestFinished Print 1>
				GoQuestFinished <Print 2>
				GoWin  <Print 3 SetQuestStatus %qid% -1 QMemo %prev_qid% 4 XPV 100>
				GoLose <Print 5>
				GoNothing <Print 2>
				% Mecha Repair, Code Breaking and Science allow difficult skill rolls to fix the robots
				Clue15 <if= QuestStatus %qid% %id% else GoNothing  ifRepair   SkillTar %threat% else GoLose Goto GoWin>
				Clue29 <if= QuestStatus %qid% %id% else GoNothing  ifScience       HardSkillTar %threat% else GoLose Goto GoWin>
				Clue32 <if= QuestStatus %qid% %id% else GoNothing  ifCodeBreaking  HardSkillTar %threat% else GoLose Goto GoWin>
				% General Repair allows a relatively easy roll to fix the robots
				Clue23 <if= QuestStatus %qid% %id% else GoNothing  ifRepair SkillTar %threat%     else GoLose Goto GoWin>
				Msg1 <This computer controls the robots for the construction site. The lights on its display are blinking wildly.>
				Msg2 <This computer controls the robots for the construction site.>
				Msg3 <You repair the computer and deactivate the malfunctioning robots.>
				Msg4 <You repaired the malfunctioning robots that were causing problems at %name1%'s construction site.>
				Msg5 <You can't repair this computer at your current skill level.>

				StairsDown
				desig <ENTRANCE %s4%>
				MiniMapComponent 3
			end
		end

		MetaScene 4
		% L1 = Robots destroyed counter
		% L2 = Monster generation counter
		Start <Print 1 if= L2 0 if= QuestStatus %qid% %id% L= 2 1 WMonster 2 %threat% 200>
		nu2 <if= T2 0 if= QuestStatus %qid% %id% Print 2 XPV 100 SetQuestStatus %qid% -1 QMemo %prev_qid% 3 L= 1 1>
		Msg1 <You enter the robot construction site.>
		Msg2 <That appears to be the last of the robots.>
		Msg3 <You deactivated the malfunctioning robots that were causing problems at %name1%'s construction site.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Enemies>
			SetEnemy 1
			type <Robot>
		end
	end
	inv
		NPC Construction Worker
		job <Robot Wrangler>
		Skill 38 5
	end


Content
	name <Rowdy Foreman>
	requires <*:ConstructionProblem>
	desc <The foreman is refusing to work; the PC has to put a stop to it.>

	% E1 is the boss
	% E2 is the construction site
	% E3 is the on-site manager
	% E4 is the rowdy foreman
	% E5 is the mecha scene where a fight will take place
	Element3 <Prefab>
	Scene3 <Grab 2>
	Team3 <Citizens>
	TeamData3 <Pass Ally>

	Element4 <Prefab>
	Scene4 <Grab 2>
	Team4 <Citizens>
	TeamData4 <Pass Ally>

	Scene5 <New *Arena_ConstructionSite>

	sub
		Persona 3
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst if= SceneVar %s2% 11 1 else GoExplain if= ArenaState %5% NAV_AS_Win else GoWaiting NewChat Say 2 QMemo %prev_id% 3 SetQuestStatus %qid% -1 AddChat 1>
		GoWaiting <NewChat Say 4>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat NewChat Say 1>
		GoChat <NewChat SayAnything>
		GoExplain <NewChat Say 5 AddChat 2>
		result1 <NewChat Say 6>
		result2 <ifNPCOK %4% else GoTrivialWin NewChat Say 7 QMemo %qid% 9 SceneVar= %s2% 11 1 AddChat 3 AddChat 4>
		GoTrivialWin <NewChat Say 8 QMemo %prev_id% 3 SetQuestStatus %qid% -1>
		result3 <NewChat Say 10>
		result4 <AddCheerful -d6 NewChat Say 11 AddChat 5>
		result5 <NewChat Say 12>
		Msg1 <If %name1% didn't send you, you've got no business being in here.>
		Msg2 <Now that %name4% has been dealt with, our work can continue.>
		Msg3 <You solved %name1%'s construction problem.>
		Msg4 <Let me know when you've convinced %name4% to go back to work.>
		Msg5 <Ah, you must have been sent here by %name1%. My name's %name3%; I'm the manager of this project.>
		Msg6 <Not much, no... We can't fire someone with senority, but as long as they're gone we can always promote someone else.>
		Msg7 <The trouble is %name4%; \SPR %4% got into a big argument with me about how this project should be done and now \SPR %4% 's stopped working. None of the other workers will do anything either because %name4% has senority.>
		Msg8 <We were having some personnel problems, but those are all cleared up now.>
		Msg9 <%name3% revealed that %name1%'s project is being delayed by the foreman, %name4%.>
		Msg10 <You can try, but I doubt it'll do any good... %name4% never listens to anyone.>
		Msg11 <We can't. As I said, %name4% has senority. According to company rules \SPR %4% can't be fired.>
		Msg12 <Look, just go and talk with \OPR %4% yourself. Maybe \SPR %4% 'll listen to you.>
		Prompt1 <Doesn't it matter that %name4% is dead?>
		CPrompt1 <ifNPCDead %4% Accept>
		Prompt2 <Could you tell me what the problem is?>
		Prompt3 <I'll have a talk with \OPR %4% .>
		Prompt4 <Why don't you just fire \OPR %4% ?>
		Prompt5 <Then why don't you quit?>

		Persona 4
		% V1 = Have been challenged to mecha duel
		Greeting <if= QuestStatus %qid% %id% else GoNoMatch if= SceneVar %s2% 11 1 else GoNotYet  if= ArenaState %5% NAV_AS_Battle else GoCheckStatus Goto GoTrashTalk>
		*GoTrashTalk <*ArenaChallenge GoThemeInfo>
		*GoThemeInfo <*THEME_EXPO&Enemy NA>
		GoCheckStatus <if= ArenaState %5% NAV_AS_Ready else GoCheckWin Goto GoWaiting>
		*GoWaiting <*WaitingForDuel %5%>
		GoCheckWin <if= ArenaState %5% NAV_AS_Win else GoCheckChallenge NewChat Say 2>
		GoCheckChallenge <ResetArena %5% ifG ComTime ArenaRecharge %5% else GoLater if= V1 1 else GoFirstTime NewChat Say 3 AddChat 1 AddChat 2>
		GoFirstTime <NewChat Say 5 AddChat 3 AddChat 4>
		*GoLater <*ChallengeMeLater>
		GoNotYet <NewChat Say 4>
		GoNoMatch <if= QuestStatus %qid% 0 else GoChat NewChat Say 1>
		GoChat <NewChat SayAnything>
		GoNoTalk <NewChat Say 6 AddChat 5>
		GoStartCombat <SetChallengerID %5% %4% SetChallengerHome %5% %s4% MoveNPC %4% %5% PrepArena %5% 0 SetNPCTeam %4% 2>
		*result1 <*MeetMeAtArena %5% GoStartCombat>
		*result2 <*RejectChallenge>
		result3 <ifConversation HardSkillTar %threat% else GoNoTalk NewChat Say 7 AddChat 6 AddChat 5>
		result4 <ifIntimidation HardSkillTar %threat% else GoNoTalk NewChat Say 8 AddChat 7 AddChat 5>
		result5 <NewChat Say 9 AddChat 8 AddChat 9>
		result6 <ifIntimidation SkillTar %threat% else GoNoTalk NewChat Say 10 XPV 100 SetArenaState %5% NAV_AS_Win>
		result7 <ifConversation SkillTar %threat% else GoNoTalk NewChat Say 10 XPV 100 SetArenaState %5% NAV_AS_Win>
		result8 <V= 1 1 QMemo %qid% 11 Goto result1>
		result9 <NewChat Say 12>
		Msg1 <We've got a lot of work to do today. Personally, I'm trying to avoid it.>
		Msg2 <You beat me... Go tell %name3% that everybody will get back to work.>
		Msg3 <So, you think you can take me?>
		Msg4 <If you've got something to talk about, you better do that with %name3% . I'm not gonna have any part in it.>
		Msg5 <%name3% sent you to talk, didn't \OPR %3% ?>
		Msg6 <There's no way I'm going to take orders from a pencil-necked paper-pusher like %name3%, who doesn't even know the first thing about construction.>
		Msg7 <%name3% comes down here from \PPR %3% fancy school and starts telling us to do all kinds of things that make no sense. I'm not going to do that, and I won't subject my men to it either.>
		Msg8 <Delaying a project isn't something I want to do, but I just don't believe in %name3%. It'd be a bigger waste of time to go along with \OPR %3% and then learn that everything's falling to bits.>
		Msg9 <If you're so interested in this, why don't you take me up on a little wager? There's a big pit downstairs called \SCENE %s5% . If you can beat me in mecha combat down there, I'll get back to work.>
		Msg10 <Argh... you're right. Go tell %name3% that me and the boys will get back to work.>
		Msg11 <You agreed to duel %name4% beneath the construction site as part of a wager to force \OPR %4% to go back to work.>
		Msg12 <Suit yourself. I can stay here all day.>
		Prompt1 <I know I can.>
		Prompt2 <Not right now, maybe later.>
		Prompt3 <Why aren't you working?>
		Prompt4 <You should stop wasting time.>
		Prompt5 <Is there anything I can do to change your mind?>
		Prompt6 <You better accept it or this project will fail.>
		Prompt7 <Sometimes you have to try new things.>
		Prompt8 <Sounds good. Let's do it.>
		Prompt9 <You've got to be joking.>

		MetaScene 3
		% L11 = Have been told of problem
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %3% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		Faint%4% <if= QuestStatus %qid% %id% if= L11 1 SetArenaState %5% NAV_AS_Win>
		sub
			Room
			minimap <......#.#...2...#.#......>
			sub
				Trapdoor
				name <To Quarry>
				desig <ENTRANCE %s5%>
				MiniMapComponent 2
			end
		end
	end
	inv
		NPC Construction Worker
		job <Manager>

		NPC Mecha Pilot
		job <Foreman>
		job_desig <LABOR>
		chardesc Passionate Melancholy
	end

Content
	name <Stupid Fungus>
	requires <*:ConstructionProblem (Spinner|Asteroid)>
	% The construction site has been infested with spaceworms, and the boss hasn't
	% provided the funds for extermination.

	% E1 is the boss
	% E2 is the construction site
	% E3 is the foreman
	% E4 is the extermination scene
	element3 <Prefab>
	scene3 <Grab 2>
	team3 <Citizens>
	teamdata3 <Pass Ally>

	scene4 <New *BLANK.Basement>

	sub
		% Both the boss and the foreman get speaking roles this time around- if the
		% PC returns to speak with the boss, he may be able to convince him to pay for
		% a sensible de-fung'ing of the construction site.
		Persona 1
		% V%id%01 = Have argued the point already
		% If the PC has spoken to character 3, then he can talk to E1 and maybe get money.
		greeting <if= QuestStatus %qid% %id% else %pop% if= NPCVar %3% 1 1 else %pop% Goto .%id%_GoArgue>
		.%id%_GoArgue <if= v%id%01 0 else .%id%_GoArgue2 NewChat Say %id%01 V= %id%01 1 AddChat %id%01>
		.%id%_GoArgue2 <NewChat Say %id%02 AddChat %id%02>
		result%id%01 <NewChat Say %id%03 AddChat %id%03>
		result%id%02 <NewChat Say %id%04 AddChat %id%04 AddChat %id%05 AddChat %id%06>
		result%id%03 <Goto result%id%02>
		result%id%04 <NewChat Say %id%05>
		result%id%05 <NewChat Say %id%06 NPCVar= %3% 1 2>
		result%id%06 <NewChat Say %id%07 NPCVar= %3% 1 2>
		Msg%id%01 <Have you found out from %name3% why construction has been delayed?>
		Msg%id%02 <I'm still waiting for %name3% to finish construction.>
		Msg%id%03 <That shouldn't be a big problem. I already provided funds for an exterminator.>
		Msg%id%04 <This project is over budget already. There must be a better way... You're a cavalier, couldn't you just go kill all the fungus?>
		Msg%id%05 <You do that.>
		Msg%id%06 <You're probably right... some added expense at this point will save us headaches down the road. Tell %name3% that I'll arrange for a more thorough extermination.>
		Msg%id%07 <Seriously? In that case, I guess there's nothing I can do... Tell %name3% that I'll provide extra funds for extermination.>
		Prompt%id%01 <There's a fungus infestation at the site.>
		Prompt%id%02 <%name3%'s still waiting for the exterminator.>
		Prompt%id%03 <Apparently, the funds weren't enough.>
		Prompt%id%04 <I'll see what I can do.>
		Prompt%id%05 <Really, I think we need a professional.>
		CPrompt%id%05 <ifConversation %sktar_hard% Accept>
		Prompt%id%06 <Without proper treatment they'll be back in a week.>
		CPrompt%id%06 <ifScience %sktar% Accept>

		Persona 3
		% NPCVar %3% 1 = Have told about problem (1), have secured funding (2)
		% V%id%01 = Have discussed problem
		% V%id%02 = PC is a biotech expert qualified to do the extermination
		rumor%id% <%name3% has been having problems with %name1%'s construction project.>
		greeting <if= QuestStatus %qid% %id% else GoChat  if= v%id%01 1 else GoFirstTime NewChat Say 10 AddChat 9 AddChat 10 AddChat 11>
		GoFirstTime <NewChat Say 1 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 V= %id%01 1 NPCVar= %3% 1 1 QMemo %qid% 6 AddChat 3 AddChat 4 AddChat 5>
		result2 <AddEasygoing -d8 Goto result1>
		result3 <NewChat Say 3 AddChat 6 AddChat 7 AddChat 8>
		result4 <NewChat Say 4>
		result5 <NewChat Say 5>
		result6 <NewChat Say 7>
		result7 <NewChat Say 8 V= %id%02 1>
		result8 <NewChat Say 9>
		result9 <NewChat Say 11>
		result10 <NewChat Say 12 XPV 100 SetQuestStatus %qid% -1>
		result11 <SetQuestStatus %qid% -1  if= v%id%02 1 else GoR11Fail NewChat Say 12 XPV 100>
		GoR11Fail <NewChat Say 13 XPV 50>
		Msg1 <Yes, were you sent here by %name1%? It's about time that something was done...>
		Msg2 <Construction has halted due to a fungus infestation. It's not even safe for the workbots to go there. %name1% hired an exterminator once, but now the fungi are back and stronger than ever.>
		Msg3 <I don't think that's a good idea. We already tried that, remember? After a while the fungus came back... What we really need is some kind of quasinatural organism expert to clear the place out.>
		Msg4 <I think that would be best. Let me know what happens.>
		Msg5 <Come back and let me know if you find some way to help.>
		Msg6 <%name3% revealed that %name1%'s construction site is infested with space fungus.>
		Msg7 <Fine. Do whatever you think is best... Come back and let me know when we can get back to work.>
		Msg8 <Really? In that case, you should be qualified to handle the extermination. Come back and let me know when you're satisfied that the construction zone is safe.>
		Msg9 <I know I am. If only %name1% were as reasonable as you...>
		Msg10 <We will be able to begin construction as soon as the fungi are cleared out. Do you have any progress to report?>
		Msg11 <Well, come see me when you do.>
		Msg12 <Fantastic. You can tell %name1% that construction will resume as soon as possible.>
		Msg13 <Thanks. Hopefully this time they'll stay gone, at least long enough for us to finish the work. You can go tell %name1% that construction will resume shortly.>
		Prompt1 <Hold on, what's the matter?>
		Prompt2 <I'm here. Everything will be fine now.>
		Prompt3 <Why don't I just kill the fungi for you?>
		Prompt4 <I'll talk to %name1% about the exterminator.>
		Prompt5 <Let me see what I can do.>
		Prompt6 <We don't need no stinking expert!>
		Prompt7 <Hey, I'm an expert on quasinatural organisms.>
		CPrompt7 <ifScience %sktar% Accept>
		Prompt8 <You're probably right.>
		Prompt9 <Nothing yet.>
		Prompt10 <%name1% has agreed to provide extra funds.>
		CPrompt10 <if= NPCVar %3% 1 2 Accept>
		Prompt11 <I've exterminated all the fungus myself.>
		CPrompt11 <if= SceneVar %s4% 1 1 Accept>


		MetaScene 3
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %3% else %pop% SetQuestSTatus %qid% -2 Goto %pop%>
		end   <if= QuestStatus %qid% %id% else %pop% ifNPCDead %3% else %pop% SetQuestSTatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <......###..#2#........1..>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Elevator
				name <Sub-Basement>
				desig <Entrance %s4%>
				MiniMapComponent 2
				use <if= V1 1 else GoLocked Print 1 Exit Destination>
				Update <if= QuestStatus %qid% %id% V= 1 1>
				GoLocked <Print 2>
				Msg2 <There's nothing interesting in here.>
			end
		end

		MetaScene 4
		% L1 = Victory Indicator
		% L2 = Initialization Counter
		% L3 = Monster Level; clearing this by brute force not reccomended
		content <Fill Sub *STORAGE_X na>
		Start <Print 1 if= L2 0 if= QuestStatus %qid% %id% L= 2 1 L= 3 %threat% L+ 3 20 WMonster 2 L3 200>
		nu2 <if= t2 0 if= L2 1 if= L1 0 Print 2 XPV 100 L= 1 1>
		Msg1 <You enter the basement.>
		Msg2 <That seems to be the last of the fungi.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monsters>
			type <Fungus>
			SetEnemy 1
		end

	end
	inv
		NPC Construction Worker
		job <Foreman>
	end


Content
	name <Supplies Went Missing>
	requires <*:ConstructionProblem BRANCHES>
	% The trouble with construction is that someone stole the materials!

	% E1 is the boss
	% E2 is the construction site
	% E3 is the foreman
	element3 <Prefab>
	scene3 <Grab 2>
	team3 <Citizens>
	teamdata3 <Pass Ally>

	Quest1 <*:StolenMaterials&ConstructionSite 3>

	sub
		Persona 3
		% V1 = Have revealed problem
		greeting <if= QuestStatus %qid% %id% else GoChat if= V1 1 else GoFirstTime NewChat Say 3 AddChat 2 AddChat 5>
		GoFirstTime <NewChat Say 1 AddChat 1>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat V= 1 1 Say 2 AddChat 3 AddChat 4 SetQuestStatus %qid1% %id1% QMemo %qid% 4>
		result2 <NewChat Say 5 SetQuestStatus %qid% -1 ifG React %threat% PCFriend %3%>
		result3 <NewChat Say 6>
		result4 <NewChat Say 7 AddCheerful -d10>
		result5 <NewChat Say 8 SetQuestStatus %qid% -2>
		Msg1 <Yes, may I help you? We're very busy here right now.>
		Msg2 <I wish I could tell you that things are proceeding well, but they aren't. The trouble is, we've been losing building materials.>
		Msg3 <Have you found anything about the missing building materials?>
		Msg4 <%name3% revealed that materials are being stolen from %name1%'s construction site.>
		Msg5 <Fantastic! We'll be able to get back to work soon... You should report to %name1% .>
		Msg6 <Ask around and see if you can uncover any clues. I'll help you in whatever way I can.>
		Msg7 <If you can't find the materials and bring them back, then it'll be both of us who have failed, won't it? Search around and see what you can find out. I'll keep working on the project.>
		Msg8 <The our project is lost... report back to %name1%.>
		Prompt1 <I was send to hurry up the construction.>
		Prompt1_1 <%name1% sent me to check your progress.>
		Prompt2 <I've solved your materials problem.>
		CPrompt2 <if= QuestStatus %qid1% -1 Accept>
		Prompt3 <I could find out what happened to them.>
		Prompt4 <%name1% won't be pleased at your failure.>
		Prompt5 <I think you better give up on those supplies...>
		CPrompt5 <if= QuestStatus %qid1% -2 Accept>
	end
	inv
		NPC Construction Worker
		job <Foreman>
	end

