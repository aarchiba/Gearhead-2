%% *:CS_GatherInformation Content
%%	&AboutEnemy	The information is about the enemy faction or NPC
%%
%% In this component, the PC will be able to gain some information via a difficult source.
%% This component will almost certainly change the story context.
%%
%% The Param1 NPC must have a persona which calls the following script:
%%   .%id%_GoInform
%% This script tells the PC about the situation, setting up the rest of this
%% subplot. The SubPlotID must be set separately.
%%
%% When this subplot concludes, it sets one of the following triggers:
%%  .%id%_%plotid%_GoWin
%%  .%id%_%plotid%_GoLoss
%%
%% This is a MAIN COURSE subplot. Favored by: POLIT, MEDIA, ACADE
%%
%%
%%  Param1: The NPC who will tell the PC about the situation.
%%          After introducing the component, this NPC will serve no further purpose
%%          in this narrative thread.
%%

%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************

%%  ****************************
%%  ***   +Pme  Meet Enemy   ***
%%  ****************************

%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************

Content
	name <Hotheaded Partner>
	desc <Your partner endangers a recon mission.>
	requires <*:CS_GatherInformation (+Pun|+Pla|+Pew) E:++ R:LANCE (R:M.rev|R:M.ggd|R:M.nih) (R:A.---|R:A.jr_|R:A.sr_)>
	changes <R:A>
	Size 6

	% E1 is the person who tells the PC about the mobile base
	% E2 is an outdoors scene for the mobile base
	% E3 is the mobile base itself
	% E4 is the enemy NPC, needed to find its base
	% E5 is the partner
	Element2 <Scene Outdoors !Near -7>
	Element3 <Prefab>
	Place3 <2>
	Element4 <Grab 1>
	Place4 </>
	Element5 <Grab 4>

	%% FAIL CONDITIONS:
	%% - The RevealEncounter task fails

	% P%id%01 = Initialization Counter
	% P%id%02 = Conversation Counter
	% P%id%03 = Have told E5 to calm down

	start <ifNPCOK %5% ifScene .%id%_scenetype if= RootSceneID &EpisodeScene if= P%id%02 0     P= %id%02 1  ForceChat %5%>
	.%id%_scenetype <outdoors>

	update <if= P%id%01 0 P= %id%01 1 SetPlotStatus %plotid1% %id1%>
	end <ifSubPlotLost %plotid1%  LoseSubPlot %plotid%  Trigger0 .%id%_%plotid%_GoLoss>

	% SubPlot1 is the Reveal Enemy Base task
	SubPlot1 <*:CS_RevealEncounter_NPCBase&EnemyNPC 2 3 4>

	sub
		Persona 1
		.%id%_GoInform <NewChat Say %id%01 SMemo %id%02 History %id%03>
		Msg%id%01 <%name4% is searching for something in \SCENE &EpisodeScene . In fact, \SPR %4% 's got a recon team operating in town right now.>
		Msg%id%02 <%name1% revealed that %name4% has a recon team operating in \SCENE &EpisodeScene .>
		Msg%id%03 <%name1% revealed that %name4% sent a recon team to \SCENE &EpisodeScene .>

		Persona 5
		% V1 = Have had discussion
		greeting <if= PlotStatus %plotid% %id% else GoChat if= V1 0 else GoChat Goto GoReveal>
		GoChat <NewChat SayAnything>
		*GoReveal <*LancemateWantsToTalk GoNow GoLater>
		GoLater <EndChat Say %id%01 V= 1 1>
		GoNow <NewChat Say %id%02 V= 1 1 AddChat %id%01 AddChat %id%02>
		result%id%01 <NewChat Say %id%03 AddChat %id%03 AddChat %id%04 AddEasygoing d10>
		result%id%02 <EndChat Say %id%04 AddEasygoing -d10 AddReact 10>
		result%id%03 <EndChat Say %id%05 P= %id%03 1 AddReact -d10>
		result%id%04 <EndChat Say %id%06>
		Msg%id%01 <Never mind... it wasn't all that important.>
		Msg%id%02 <I can't wait to find this recon team and destroy them... %name4% is going to pay for everything \SPR %4% 's done.>
		Msg%id%03 <So what are you saying? You know all the things %name4% is responsible for... \SPR %4% and \PPR %4% men deserve nothing but death.>
		Msg%id%04 <Yeah! Let's get started.>
		Msg%id%05 <You really think so? Okay, let's do things your way, this time... but if we find %name4% don't expect me to hold back.>
		Msg%id%06 <I know I'm right. After %name4% is dead you can search for all the information you want.>
		Prompt%id%01 <Calm down, our task is just to learn what \SPR %4% 's doing.>
		Prompt%id%02 <I agree. %name4% has earned a good arse-whupping.>
		Prompt%id%03 <If we want to defeat them, we need information.>
		Prompt%id%04 <Maybe you're right. I don't know.>

		MetaScene 3
		special <SHOWALL>
		boxmap
		MapWidth 17
		MapHeight 17
		special <AddExit>

		% L1 = Encounter Over Counter
		% L2 = Initialization Counter

		start <if= V2 0 V= 2 1 ifNPCOK %5% else GoSoloEntry if= P%id%03 0 else GoSubtleEntry Goto GoLoudEntry>

		GoSoloEntry <ifStealth SkillTar StoryDL else GoSoloLoud Alert 1 Retreat 2>
		GoSubtleEntry <Alert 1 Monologue %5% 2 Retreat 2>
		GoSoloLoud <Alert 3>
		GoLoudEntry <Alert 4 Monologue %5% 5 Alert 6>

		.%id%_%plotid%_LocalEvent <if= L1 0 L= 1 1 Alert 10 History 11 XPV 100  SkillXP 22 100  WinSubPlot %plotid% Trigger0 .%id%_%plotid%_GoWin ifNPCOK %5% Monologue %5% 12 SetXXRAttitude %5% XXR_A_IsEqual>

		end <if# L1 0 SetEncounterInactive %3%>

		nu3 <if= T3 0 if= L1 0 L= 1 1 Alert 7 History 9 LoseSubPlot %plotid% Trigger0 .%id%_%plotid%_GoLoss ifNPCOK %5% Monologue %5% 8 SetXXRAttitude %5% XXR_A_Antagonistic>

		Msg1 <You sneak into the recon base, and wait for an opportune moment before accessing the computer.>
		Msg2 <The coast is clear... looks like we can go now.>
		Msg3 <You sneak into the recon base, but find the computer station well guarded. Looks like you'll have to fight your way through.>
		Msg4 <You sneak into the recon base, and find a good place to hide until the guards turn their backs.>
		Msg5 <Waarrrgh! Death to all of %name4%'s lackeys!!!>
		Msg6 <Looks like you'll have to fight after all.>
		Msg7 <The base's computer is destroyed in the fighting, and with it goes any chance you had of learning %name4%'s plan.>
		Msg8 <That wasn't supposed to happen!>
		Msg9 <You failed to learn anything at the recon base.>
		Msg10 <The data core contains plans for a new mecha that %name4% is apparently helping to design. You save copies of as many files as you can.>
		Msg11 <You learned that %name4% was building a new mecha.>
		Msg12 <Pretty good, \PC . this information will help when it's time to fight %name4%, or when we're building some new mecha of our own...>

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Recon Experts, Maybe?>
			SetEnemy 1
			Deploy <if= L1 0   WMonster 2 StoryDL 100>
			type <Guard Robot Criminal>

			Team 3
			name <Computers>
		end
		inv
			STC COMPUTER-1
			SetTeam 3
			use <if= T2 0 else GoNotNow Alert 1 Trigger0 .%id%_%plotid%_LocalEvent>
			GoNotNow <Alert 2>
			Msg1 <You access the computer.>
			Msg2 <You can't use the computer while there are people trying to kill you!>
		end
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <Recon Base>
	end

Content
	name <They're searching for something.>
	desc <Your enemy is in town searching for something.>
	requires <*:CS_GatherInformation +Pun (E:++|F:++) I:-->
	changes <+P I:++ E:++>
	Size 5

	% E1 is the person who tells the PC about it
	% E2 is the item being sought
	% E3 is an urban scene where the enemies will search
	% E4 is the basement where you'll find your enemies
	% E5 is a replacement enemy, in case your current enemy is dead at the time of revelation
	Element2 <Artifact>
	Place2 </>
	Element3 <Scene Urban !Near -7>
	Element4 <Prefab>
	Place4 <3>
	Element5 <NewNPC -2 0>
	Place5 </>

	%% FAIL CONDITIONS:
	%% - The RevealEncounter task fails

	% P%id%01 = Initialization Counter

	update <if= P%id%01 0 P= %id%01 1 SetPlotStatus %plotid1% %id1%>
	end <ifSubPlotLost %plotid1%  LoseSubPlot %plotid%  Trigger0 .%id%_%plotid%_GoLoss>

	% SubPlot1 is the locate enemy target task
	SubPlot1 <*:CS_RevealEncounter_Raiders&ReconMission 3 4>

	sub
		Persona 1
		.%id%_GoInform <NewChat Say %id%01 StoryNote %id%02>
		Msg%id%01_1 <\PERSONA &EnemyNPC is in town... \SPR &EnemyNPC 's searching for something. I don't know what it is, but it must be pretty important.>
		CMsg%id%01_1 <ifNPCOK &EnemyNPC Accept>
		Msg%id%01_2 <There's a search team from \FACTION &ENemyFac in town looking for something. I don't know what it is, but it must be pretty important.>
		CMsg%id%01_2 <if# 0 &EnemyFac ifNPCDead &EnemyNPC Accept>
		Msg%id%01_3 <Your enemies are in town searching for something. I don't know what it is, but it must be pretty important.>
		CMsg%id%01_3 <if= 0 &EnemyFac ifNPCDead &EnemyNPC Accept>
		Msg%id%02 <You learned that your enemies were in town looking for something.>

		Metascene 4
		type <Dungeon Building Basement>
		mapwidth 50
		mapheight 50
		MonkeyMap
		LockedDoorChance 0
		SecretDoorChance 10

		NeededCells 3

		% L1 = Initialization Counter
		start <if= L1 0 else GoBeenBefore L= 1 1 Alert 1 Alert 2 ifNPCOK &EnemyNPC else GoReplaceEnemy DeleteNPC %5% Goto GoMonologue>
		GoReplaceEnemy <&SetEnemyNPC %5% SetXXRAttitude %5% XXR_A_NeverMet Goto GoMonologue>
		GoMonologue <Monologue &EnemyNPC 3 &SetTargetItem %2% AlterContext .next History 4 WinSubPlot %plotid% Trigger0 .%id%_%plotid%_GoWin>
		.next <+Pla>
		GoBeenBefore <Print 5>

		end <SetEncounterInactive %4%>

		Msg1 <You enter the abandoned building. All of a sudden, the floor gives way and you tumble into the basement!>
		Msg2 <As you lie there amid the wreckage and dust you hear faint voices from the upper level.>
		Msg3 <The %name2% isn't here. Tell the others to move out- we will continue our search elsewhere.>
		Msg4 <You learned that \PERSONA &EnemyNPC was looking for the %name2%.>
		Msg5 <You enter the abandoned basement.>

		content1 <Some 1 70 Sub *DUNGEON_THREAT>
		content2 <Some 1 40 Sub *DUNGEON_DECOR>
		content3 <Some 1 80 Sub *DUNGEON_REWARD>

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monster>
			type <Ruin Vermin City>
			Deploy <if= V1 0 V= 1 1 MonsterUp 2 StoryDL>
			SetEnemy 1

			room
			special <STARTHERE>
			MarbleType 22

			room
			minimap <............2............>
			sub
				StairsUp
				Destination -1
				MiniMapComponent 2
				use <Print 1 XPV 100 Return>
			end
		end
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <Abandoned Building>
	end

%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************

Content
	name <Do Not Want!>
	desc <The PC beats the NPC to the core story item, but the Enemy pulls a LOLcat.>
	requires <*:CS_GatherInformation +Pla I:++ E:++ F:++ -!Ne -I:TL_Md -I:TL_Hi -I:TL_Ex>
	changes <+P>
	Size 4

	% E1 is the person who tells the PC about it
	% E2 is the item
	% E3 is the enemy NPC
	Element2 <Grab 8>
	Element3 <Grab 1>
	Place3 </>

	% P%id%01 = Have gotten item/Have lost item
	% P%id%02 = Initialization Counter

	%% FAIL CONDITIONS:
	%% - ItemCozy fails

	%% If the PC has retrieved the item, entering an outdoors scene will trigger the ending
	%% conversation with the enemy.
	start <if# p%id%01 0 ifScene .%id%_scene Alert %id%01 ForceChat %3%>
	.%id%_scene <Outdoors>

	update <if= p%id%02 0 p= %id%02 1 SetPlotStatus %plotid1% %id1%>
	end <ifSubPlotLost %plotid1%  P= %id%01 -1>
	Get%2% <P= %id%01 1>

	Msg%id%01 <As you leave, you are approached by %name3% and \PPR %3% lance.>

	% SubPlot1 is the Item Cozy
	subplot1 <*:CS_ItemCozy&Abandoned 2>

	sub
		Persona 1
		.%id%_GoInform <NewChat Say %id%01 SMemo %id%02 History %id%03>
		Msg%id%01 <It's believed that the %name2% is near here. In fact, %name3% is searching for it at this very moment.>
		Msg%id%02 <%name1% revealed that %name3% is in town searching for the %name2%.>
		Msg%id%03 <%name1% revealed that the %name2% was nearby.>

		Persona 3
		special <UNLISTED NOESCAPE>
		greeting <EndChat Say 1 AddChat 1 AddChat 2>
		result1 <EndChat Say 2 StoryNote 3 Print 4 &SetTargetItem 0           WinSubPlot %plotid%   Trigger0 .%id%_%plotid%_GoWin   AlterContext .next  SavePos  Dynamic 2 StoryDL 100 .nu1 .nu2  DynaFaction &EnemyFac>
		result2 <EndChat Say 2 History 5 StoryNote 3 Print 4 &SetTargetItem 0 LoseSubPlot %plotid%  Trigger0 .%id%_%plotid%_GoLoss  AlterContext .next  SavePos  Dynamic 2 StoryDL 100 .nu1 .nu2  DynaFaction &EnemyFac>
		.next <+Pew>
		.nu1 <if= T1 0  Return   if= L2 0  L= 2 1  AddRenown -5>
		.nu2 <if= T2 0           if= L2 0  L= 2 1  XPV 100  AddRenown 1  Salvage   Return>
		Msg1 <\PC , I should have expected to find you here. Well, it hardly matters- you're too late to stop me.>
		Msg2 <It wasn't the %name2% that I was after- it was the computer in that place which was the real prize. This information will be a great help to our new weapons program. Unfortunately, you will not live to see the glorious rise of \FACTION &EnemyFac !>
		Msg3 <%name3% revealed that \FACTION &EnemyFac were working on a new weapons program.>
		Msg4 <You are attacked by %name3%'s men.>
		Msg5 <You failed to prevent %name3% from locating the %name2%.>
		Prompt1 <Too late? I beat you to the %name2%!>
		CPrompt1 <if= P%id%01 1 Accept>
		Prompt2 <I can still get the %name2% back from you!>
		CPrompt2 <if= P%id%01 -1 Accept>
	end

%%  ****************************************
%%  ***   +Pew  Enemy  Weapon  Program   ***
%%  ****************************************

%%  *******************
%%  ***   GENERAL   ***
%%  *******************

Content
	name <The Enemy's Decision>
	desc <Your enemy consults their mentor; the PC of course thinks it's a kidnapping>
	requires <*:CS_GatherInformation E:A.env (E:M.---|E:M.mer|E:M.com) ~&AboutEnemy>
	changes <E:M>
	Size 5

	% E1 is the person who tells the PC
	% E2 is an environs scene for the enemy base
	% E3 is the base itself
	% E4 is a local public scene for the mentor's home
	% E5 is the mentor
	% E6 is the enemy
	% E7 is a secret
	Element2 <Scene Environs !Near -7>
	Element3 <Prefab>
	Place3 <2>
	Element4 <Scene Building Public !Near -7 !Okay -3>
	Element5 <NewNPC 4 4 Old Old Old Heroic Renowned>
	Place5 <3 (Citizens) pass>
	Element6 <Grab 1>
	Place6 <3 (Guards) sd>
	Element7 <Prefab>

	%% FAIL CONDITIONS:
	%% - DiscoverNPCisMissing Fails
	%% - Reveal NPC Base Fails

	% P%id%01 = Initialization Counter
	% P%id%02 = Have learned secret

	update <if= p%id%01 0 p= %id%01 1 SetPlotStatus %plotid1% %id1% SetPlotStatus %plotid3% %id3%>
	start <ifSubPlotLost %plotid1% else .%id%_GoCheckSP2   Goto .%id%_GoLoseMission>
	.%id%_GoCheckSP2 <ifSubPlotLost %plotid2%   Goto .%id%_GoLoseMission>
	.%id%_GoLoseMission <CancelSubPlot %plotid3%  LoseSubPlot %plotid% Trigger0 .%id%_%plotid%_GoLoss>

	.%id1%_%plotid1%_GoDiscover <SetPlotStatus %plotid2% %id2%>
	.%id3%_%plotid3%_GoLearnSecret <P= %id%02 1>

	% SubPlot1 is the revelation that the NPC is missing
	% SubPlot2 is the Reveal NPC Base task
	% SubPlot3 is the secret revelation
	SubPlot1 <*:CS_DiscoverNPCKidnappedByCSE&NotReally 4 5>
	SubPlot2 <*:CS_RevealEncounter_NPCBase&EnemyNPC 2 3 6>
	SubPlot3 <*:CS_LearnSecretAboutNPC 7 5>

	sub
		Persona 1
		.%id%_GoInform <NewChat Say %id%01 SMemo %id%02 History %id%03>
		Msg%id%01 <%name6% has recently been in contact with the famous mecha trainer %name5%. I don't know what kind of history those two have, but you can ask \OPR %5% about it at %name4%.>
		Msg%id%02 <You learned that %name6% was recently in contact with %name5%; \SPR %5% can be found at %name4%.>
		Msg%id%03 <You learned that %name6% was in town to see %name5%.>

		STC MS_EmptyBuilding
		SetID 3
		start <if= L1 0 L= 1 1 ForceChat %6%>
		end <MoveNPC %5% %4%>

		Persona 5
		special <UNLISTED>
		greeting <if= V1 0 else GoBeenBefore NewChat Say 2 V= 1 1 AddCHat 1 AddChat 2>
		GoBeenBefore <NewChat Say 1>
		result1 <NewChat Say 3 AddChat 3>
		result2 <NewChat Say 4 AddChat 3>
		result3 <NewChat Say 5 AddChat 4>
		result4 <EndChat Say 6 History 7 Print 8 Time 3600   SkillXP 1 150   SkillXP 2 150   SkillXP 3 200>
		Msg1 <Don't worry about me. I can get home by myself.>
		Msg2 <Sorry for causing all this fuss... %name6% always did have a problem with subtlety. Thanks for trying to rescue me, anyhow, even if I didn't really need it.>
		Msg3 <Ever since \SPR %6% was my student \SPR %6% 's been like that. I think \SPR %6% isn't very happy with the direction \PPR %6% life has taken; \SPR %6% came to me for advice.>
		Msg4 <Oh, no, not at all. %name6% is a former student of mine; \SPR %6% just came to see me for some advice. I think \SPR %6% isn't very happy with the direction \PPR %6% life has taken.>
		Msg5 <I told \OPR %6% that \SPR %6% has to do what \SPR %6% thinks is right. I just hope that I managed to instill in \OPR %6% a good sense of right and wrong...>
		Msg6 <Well, I retired a while back, but I've still got a few tricks up my sleeve. Let me show you what I know about mecha combat.>
		Msg7 <%name5% taught you a few things about mecha combat.>
		Msg8 <You train with %name5% for a short time.>
		Prompt1 <What is %name6%'s problem, anyhow?>
		Prompt2 <Are you okay? Did %name6% hurt you?>
		Prompt3 <What kind of advice did you give \OPR %6% ?>
		Prompt4 <Do you think you could spare some time to teach me?>

		Persona 6
		special <UNLISTED NOESCAPE>
		greeting <EndChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoNormalEnding <History 5 History 6 Goto GoEnd>
		GoGoodEnding <PCFriend %6% History 5 History 11 Goto GoEnd>
		GoEnd <Print 4 FreezeNPC %6%   SetXXRMotivation %6% XXR_M_Seeker   ForceChat %5%   WinSubPlot %plotid% Trigger0 .%id%_%plotid%_GoWin>
		result1 <EndChat Say 2 Goto GoNormalEnding>
		result2 <EndChat Say 3 AddEasygoing -d10   Goto GoNormalEnding>
		result3 <EndChat Say 7 AddChat 4 AddChat 5 AddChat 6>
		result4 <ifTaunt SkillTar StoryDL else GoR4Fail EndChat Say 8 Goto GoGoodEnding>
		GoR4Fail <EndChat Say 9 Goto GoNormalEnding>
		result5 <EndChat Say 10 Goto GoGoodEnding>
		result6 <EndChat Say 10 Goto GoGoodEnding>
		Msg1 <\PC ... You are a persistent one, I'll give you that. Unfortunately I am in no mood to fight just now.>
		Msg1_1 <Ah, I heard that there was someone looking for me. I should have guessed that it would be you... I'm afraid that I really must be going, I'm in no mood to kill you right now.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <What do you want? I'm in no mood for a fight today.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Look at who just wandered in here. Couldn't you have waited a few minutes until after I'd gone? I'm in no mood to fight you today.>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <You!!! What are you doing here!? I would kill you for intruding on my business, but my work here is finished, and I'm in no mood to dirty my blade with your blood...>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <Well, look at this. If I were in any mood to fight right now this would be my lucky day.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <What now!? Oh, it's you... why must you continue to accost me, even when I'm having a day off? Get lost. I'm in no mood to fight.>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <You think that I've harmed %name5%? You really are ignorant. I have no more time to waste on you, \PC . Until we meet again...>
		Msg2_1 <What, can't you see \OPR %5% standing over there? Really, this is stupid, even for you. Goodbye \PC ... the next time we meet, it will be in battle.>
		Msg2_2 <Why don't you ask \OPR %5% yourself? I've had enough of your foolishness, \PC ... Next time we meet, it'll be the last time.>
		Msg3 <You seem to be laboring under the delusion that you have some say in the matter. Don't worry, though... the next time we meet, I promise to finish you.>
		Msg3_1 <The only one who is going is me. Goodbye, \PC .>
		Msg3_2 <Don't worry, I promise a great battle the next time we meet. Goodbye, \PC .>
		Msg4 <%name6% leaves the area.>
		Msg5 <You learned that %name6% didn't actually kidnap %name5%.>
		Msg6 <Instead, \SPR %6% sought \PPR %6% former mentor to ask for advice.>
		Msg7 <You could say that's the reason I'm here. What do you know about it?>
		Msg8 <Hah! Maybe, just maybe you're right about that... I'm still better than you, though. Until we meet again, \PC .>
		Msg9 <You know nothing. The next time we meet, \PC , I will end this... I promise.>
		Msg10 <You may be right about that, \PC . If things were different then perhaps we need not be enemies... but destiny is formed by the choices we make, and this is where we are. Until we meet again...>
		Msg11 <%name6% expressed regret that the two of you were enemies.>
		Prompt1 <Tell me what you did with %name5%.>
		Prompt1_1 <What did you do with %name5%?>
		Prompt2 <I never have that problem. Let's go!>
		Prompt2_1 <I don't care about your mood. Let's go!>
		Prompt3 <Are you in the mood to talk?>
		CPrompt3 <if= P%id%02 1 if= ChatNPCRelationship 0 ifG ChatNPCHeroism -1 Accept>
		Prompt4 <I know that you're better at talking than fighting.>
		Prompt5 <I know that it's never too late to change.>
		CPrompt5 <ifConversation SkillTar StoryDL Accept>
		Prompt6 <I know that you've made some bad decisions.>
		CPrompt6 <ifInsight SkillTar StoryDL Accept>
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <Secret Base>
		% Since this is the main combat of this episode, make it a harder than usual.
		GoStartCombat <Alert 1 SavePos Dynamic 2 StoryDL 150 .nu1 .nu2 ComposeD Msg1 .Msg1 DynaVar= 3 Destination  DynaFaction &EnemyFac>

		Secret
		Msg <%name5% used to be %name6%'s teacher. I think \SPR %5% was always disappointed in the path %name6% chose.>
	end

Content
	name <Ex-Lover vs Ex-Partner>
	desc <The enemy's ex-lover is on the run from the enemy's ex-partner.>
	requires <*:CS_GatherInformation E:A.sr_ (E:M.---|E:M.mer) ~&AboutEnemy>
	changes <E:M>
	Size 7

	%% The centerpiece of this component is an encounter in which the PC can help
	%% defend the ex-lover against the ex-partner. It should be possible, if the PC
	%% plays the cards right, for the encounter to be won without a shot being
	%% fired.

	% E1 is the person who tells the PC.
	% E2 is the ex-lover
	% E3 is the ex-partner
	% E4 is an outdoors scene for the duel
	% E5 is the encounter for the duel
	% E6 is E3's secret which may be revealed
	% E7 is the enemy, which must be grabbed because it's going to get modified
	Element2 <NewNPC  0 -7>
	Place2 </>
	Element3 <NewNPC -2 -7>
	Place3 </>
	Element4 <Scene Outdoors !Near -7>
	Element5 <Prefab>
	Place5 <4>
	Element6 <Prefab>
	Element7 <Grab 1>
	Place7 </>

	% FAIL CONDITIONS
	% - LocateDuel fails

	% P%id%01 = Initialization Counter
	% P%id%02 = Decimation Counter
	% P%id%03 = Learn Secret Counter
	% P%id%04 = Have won the encounter

	% At initialization, move and level both of the characters, then activate
	% the three subplots.
	update <if= p%id%01 0 p= %id%01 1 NPCLevel %2% StoryDL NPCLevel %3% StoryDL MoveNPC %2% %5% SetNPCTeam %2% 3 MoveNPC %3% %5% SetNPCTeam %3% 2 SetPlotStatus %plotid1% %id1% SetPlotStatus %plotid2% %id2% SetPlotStatus %plotid3% %id3%>
	start <ifSubPlotLost %plotid1% CancelSubPlot %plotid2% CancelSubPlot %plotid3% History %id%01 LoseSubPlot %plotid% Trigger0 .%id%_%plotid%_GoLoss>

	.%id2%_%plotid2%_GoDecimation <P= %id%02 1>
	.%id3%_%plotid3%_GoLearnSecret <P= %id%03 1>

	Msg%id%01 <You failed to protect %name2% from %name3%.>

	%% SubPlot1 is the LocateDuel task
	%% SubPlot2 is the GainAdvantageVsNPC one
	%% SubPlot3 is the ability to learn E3's secret
	SubPlot1 <*:CS_LocateDuel 4 5 2 3>
	SubPlot2 <*:CS_GainAdvantageVsNPC&Decimation 3>
	SubPlot3 <*:CS_LearnSecretAboutNPC 6 3>

	sub
		Persona 1
		.%id%_GoInform <NPCConGen %2% %7% NewChat Say %id%01 StoryNote %id%02>
		Msg%id%01 <%name7%'s ex-lover %name2% is in town; \SPR %2% 's on the run from %name7%'s partner %name3%. I guess their breakup must have been pretty rough.>
		Msg%id%02 <You learned that %name7%'s partner %name3% is trying to kill \PPR %7% ex-lover %name2%.>


		Persona 2
		% E2 will speak with the PC after the combat is won; before then, no special message.
		special <UNLISTED NOESCAPE>
		greeting <if= p%id%04 1 else GoNotNow  EndChat Say 2 AddCHat 1>
		GoNotNow <EndChat Say 1>
		GoWin <AddChat 2 XPV 100   WinSubPlot %plotid%   Trigger0 .%id%_%plotid%_GoWin>
		result1 <EndChat Say %id%01 StoryNote %id%02 SetXXRMotivation &EnemyNPC XXR_M_Professional Goto GoWin>
		result2 <EndChat Say 3 PCAlly %2% FreezeNPC %2%>
		Msg1 <We shouldn't be talking now; we're in the middle of combat!>
		Msg2 <Thank you! I couldn't have defeated %name3% without your help.>
		Msg3 <No, that's why \SPR %7% broke up with me. I appreciate your help, \PC . Maybe someday I'll be able to return the favor.>
		Msg%id%01 <You might think that \PERSONA &EnemyNPC is a simple mercenary, but you'd be wrong. Actually \SPR &EnemyNPC uses conflict as a tool for personal growth; \SPR &EnemyNPC seeks perfection in all things.>
		Msg%id%02 <You learned from \ChatNPC that \PERSONA &EnemyNPC seeks perfection through combat.>
		Prompt1 <No problem. I was hoping you could tell me about %name7%.>
		Prompt2 <Is that why you broke up with \OPR %7% ?>


		Persona 3
		% E3 will speak with the PC at the beginning of combat. If the PC has learned the secret, it's
		% possible through a series of skill rolls to cause E3 to flee without firing a shot.
		special <UNLISTED>
		% V1 = One time only counter
		greeting <if= v1 0 else GoThemeInfo V= 1 1 EndChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoThemeInfo <*THEME_EXPO&Enemy NA>
		result1 <Goto GoThemeInfo>
		result2 <Goto GoThemeInfo>
		result3 <EndChat Say 2 AddChat 4 AddChat 5 AddChat 6>
		result4 <EndChat Say 3 AddChat 7 AddChat 8>
		result5 <Goto result4>
		result6 <Goto GoThemeInfo>
		result7 <ifConversation SkillTar StoryDL else GoR7Fail EndCHat Say 4 Print 5 StoryNote 6 Retreat 2 FreezeNPC %3% PCEnemy %3%  L= 1 1   P= %id%04 1 ForceChat %2%>
		GoR7Fail <EndCHat Say 7>
		result8 <Goto GoThemeInfo>
		Msg1 <\PC , I should have expected to see you here. I suppose you plan on defending %name2%?>
		Msg2 <What do you mean by that!? I... I'm just protecting %name7%'s interests.>
		Msg3 <What do you know about love!? What do you know about anything!? I'm doing this because it's what I have to do...>
		Msg4 <You... you're right. I'll leave here today, but know this- I won't forgive you, and %name7% will be mine!>
		Msg5 <%name3% leaves the area.>
		Msg6 <You talked %name3% into leaving %name2% alone.>
		Msg7 <You're wrong! I'll prove that you're wrong! Neither you nor %name2% can stop our love!>
		Prompt1 <That's right. That's just the kind of pilot I am.>
		Prompt2 <I'm more interested in defeating you.>
		Prompt3 <Does %name7% know about your broken heart?>
		CPrompt3 <if# p%id%03 0 Accept>
		Prompt4 <Pathetic. You think loving %name7% gives you a license to kill?>
		CPrompt4 <ifIntimidation SkillTar StoryDL Accept>
		Prompt5 <%name7% doesn't even know about this, does \SPR %7% ?>
		CPrompt5 <ifInsight SkillTar StoryDL Accept>
		Prompt6 <Well then, I'm going to protect %name2%. Let's fight.>
		Prompt7 <Killing %name2% won't make \OPR %7% love you.>
		Prompt8 <I can see it's useless to even try talking with you.>


		MetaScene 5 2
		% This is where the PC will be able to try defending E2 from E3. Succeed, and
		% E3 will tell the PC some things about the enemy. Fail, and, well... you fail.
		MapWidth 50
		MapHeight 50

		% L1 = Encounter Over Counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1   L= 1 0   Alert 1 Monologue %2% 2 ForceChat %3% CancelSubPlot %plotid2% CancelSubPlot %plotid3%>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   Goto GoLose>
		nu2 <if= T2 0   if= V1 0 V= 1 1   ifG T3 0 else GoLose  P= %id%04 1 History 4 ForceChat %2%>
		GoLose <Alert 3 StoryNote 3 AddRenown -1  Trigger0 .%id%_%plotid%_GoLoss LoseSubPlot %plotid% ifNPCOK %3% PCEnemy %3%>
		end <SetEncounterInactive %5%>

		Msg1 <You find %name3% about to attack %name2%.>
		Msg2 <Help! I'm being attacked!>
		Msg3 <You failed to protect %name2% from %name3%.>
		Msg4 <You defeated %name3%.>

		sub
			team 1
			SetEnemy 2
			SetAlly 3
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1 3
			% Team 2 is the enemy side. If the PC earned the Decimation advantage, it will consist
			% only of E3; otherwise it's E3 and a bunch of mecha. Ouch.
			Deploy <if= PlotStatus %plotid% %id% if= p%id%02 0   SetSelfFaction &EnemyFac   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25

			team 3
			SetEnemy 2
			SetAlly 1
			ParaX 45
			ParaY 45
		end

	end
	inv
		stc CORE-ACTIVATABLE
		name <%name2% and %name3%>

		Secret
		Msg <%name3% hates %name2% because \SPR %3% was secretly in love with %name7%.>
	end


