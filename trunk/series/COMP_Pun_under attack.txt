%% COMP_Pun
%%	+Pun	An unknown enemy attacks

Plot
	name <Don't Know How To Party>
	desc <Holding a party while unknown enemies gather? Not a great idea.>
	requires <#A +Pun +Tgs +Fpa S:++ A:-- ~+Glo>
	changes <A:++>
	Size 1

	% E1 is the scene for the party
	% E2 is the urban scene where the attack will take place
	% E3 is a local public scene for character generation purposes
	% E4 is a potential love interest of the PC
	% E5 is the host of the party
	% E6 is an encounter for the combat to follow
	Element1 <Grab 6>
	Element2 <Scene Urban !Near -7>
	Element3 <Scene Public !Near -7>
	Element4 <NewNPC -3 3>
	Place4 <1 pass ally>
	Element5 <Character (MEDIA|POLIT|CORPO) Sociable Renowned !Near 3 NeverFail>
	Place5 <1 pass ally>
	Element6 <Prefab>
	Place6 <2>

	% P%id%01 = Initialization Counter
	% P%id%02 = First time message counter
	% P%id%03 = Freeze E4 Counter

	% The potential love interest will be of the opposite gender of the PC and charming.
	update <if= P%id%01 0 P= %id%01 1 ConGen %4% NPCStat= %4% 8 14 NPCStat+ %4% 8 d6>

	start <if= p%id%02 0 if= SceneID %1% p= %id%02 1 Alert %id%01 Monologue %5% %id%02 ifNPCInPlay &PartnerNPC Monologue &PartnerNPC %id%03>
	end <if# PlotStatus %plotid1% 0 if= P%id%03 0 P= %id%03 1 FreezeNPC %4%>

	% If E4 dies somehow, the attack starts automatically.
	Faint%4% <if= PlotStatus %plotid1% 0 Alert %id%04 StoryNote %id%05 AirRaidSiren Bomb SetPlotStatus %plotid1% %id1%>

	%% Win or lose the ambush encounter- this activates the main combat and freezes E4
	.%id1%_%plotid1%_GoWin <Alert %id%06 SMemo %id%07  History %id%09  SetPlotStatus %plotid2% %id2% SetPlotStatus %plotid3% %id3%  WinSubPlot %plotid%>
	.%id1%_%plotid1%_GoLose <Alert %id%08 SMemo %id%07 History %id%09  SetPlotStatus %plotid2% %id2% SetPlotStatus %plotid3% %id3%  LoseSubPlot %plotid%>

	%% Win or lose the main combat
	.%id2%_%plotid2%_GoWin <SetXXRAttitude %4% XXR_A_Thankful   WinComp 0 .next>
	.%id2%_%plotid2%_GoLose <SetXXRAttitude %4% XXR_A_IsSenior  LoseComp 0 .next>
	.next <+T-- +F-->

	Msg%id%01 <As you enter the party, you see the host %name5% greeting the guests.>
	Msg%id%02 <Welcome, everyone. I ask you to leave the troubles of the outside world behind, and tonight let us simply enjoy ourselves.>
	Msg%id%03_1 <Hey \PC , is that girl over there giving you the eye? You should go talk with her.>
	CMsg%id%03_1 <ifPCMale Accept>
	Msg%id%03_2 <Hey \PC , is that guy over there giving you the eye? You should go talk with him.>
	CMsg%id%03_2 <ifPCFemale Accept>
	Msg%id%04 <Suddenly, the building is rocked by explosions! A mecha battle has started just outside.>
	Msg%id%05 <The party you were attending was attacked by mecha.>
	Msg%id%06 <You have defeated these mecha, but it seems clear that they were only part of a larger force. The others must still be around here someplace.>
	Msg%id%07 <Some raiders remain in town to complete their mission.>
	Msg%id%08 <The remaining mecha escape, but you know that they won't be leaving \SCENE &EpisodeScene until they've completed their mission.>
	Msg%id%09 <You couldn't stop all of the raiders.>

	% SubPlot1 is the Ambush Encounter
	% SubPlot2 is the main combat
	% SubPlot3 is the reveal encounter for the main combat
	SubPlot1 <*CS_AmbushEncounter 2>
	SubPlot2 <*:CS_MechaEncounter&Raiders 2 6>
	SubPlot3 <*:CS_RevealEncounter_Raiders 2 6>

	sub
		Persona 4
		special <UNLISTED>
		greeting <if# PlotStatus %plotid1% 0 else GoGreet EndChat Say 1>
		*GoGreet <*NiceToMeetYou&NoTrivial GoAsk>
		GoAsk <NewChat Say 2 AddChat 1 AddChat 2 AddChat 3>
		GoStartAttack <Alert 3 StoryNote 4 EndChat Say 5 AirRaidSiren SetPlotStatus %plotid1% %id1% &SetAmoreNPC %4%>
		result1 <PCLover %4% Goto GoStartAttack>
		result2 <PCLover %4% Goto GoStartAttack>
		result3 <AddSociable -d10   Goto GoStartAttack>
		Msg1 <I'd love to talk more, but the building is under attack!>
		Msg2 <So, as long as we're here, how would you like to dance?>
		Msg3 <Suddenly, the building is rocked by explosions! A mecha battle has started just outside.>
		Msg4 <While talking with %name4%, a mecha battle started outside the party.>
		Msg5 <Look, we're under attack! We better get out of here.>
		Prompt1 <I'd love to.>
		Prompt2 <I don't like to dance, but I'll talk for a while.>
		Prompt3 <Nah. I'm just going to go home.>

		Persona 5
		special <UNLISTED>
		% V1 = Have asked question
		greeting <if= PlotStatus %plotid1% 0 else GoAttackStarted  Goto GoGreet>
		GoAttackStarted <NewChat Say 1 AddCHat 1>
		*GoGreet <*HowAreYou GoAsk>
		GoAsk <if= V1 0 else GoState NewChat Say 2 AddChat 2 AddChat 3>
		GoState <NewChat Say 4>
		result1 <NewChat Say 3>
		result2 <NewChat Say 4 V= 1 1 AddCheerful d10>
		result3 <NewChat Say 4 V= 1 1 AddCheerful -d8>
		Msg1 <What kind of barbarian would attack a party like this? Honestly, what is the world coming to?>
		Msg2 <So, what do you think of my party? Are you having a good time?>
		Msg3 <No, I'm sorry, I didn't... I'm sure \SPR %4% got home safely, though.>
		Msg4 <At trying times, such as those we now face, it's important to take a moment and appreciate life.>
		Prompt1 <You didn't see what happened to %name4%, did you?>
		CPrompt1 <ifG 0 PlotStatus %plotid% ifNPCOK %4% Accept>
		Prompt2 <It's very nice, thank you.>
		Prompt3 <This is hardly a good time for a party, with raiders at the gate.>

		STC MS_SOCIAL
		SetID 1
	end
	inv
		stc CORE-ACTIVATABLE
		name <Party Crashers>
	end

Plot
	name <Champion Tournament>
	desc <The technocracy is holding a tournament to locate a champion.>
	requires <#A +Pun +T-- F:-- (P:--|P:PDASS) (L:MAQUI|L:RISHI|L:FCOMS) -L:ENEMY>
	changes <P:++>

	% E1 is a local public scene
	% E2 is the politician to do the judging
	% E3 is Contestant 1
	% E4 is Contestant 2
	% E5 is Contestant 3
	% E6 is a metascene for the conclusion
	element1 <Scene Public !Okay -7 !Near -7 (Meeting|Garage)>
	element2 <Character POLIT !Near -7 !Comrade -7 NeverFail>
	NeverFail2 <Politician>
	Place2 <1 (Citizens) pass ally>
	element3 <NewNPC -7 -7>
	place3 <1 (Guards) sd ally>
	element4 <NewNPC  0 -7>
	place4 <1 (Guards) sd ally>
	element5 <NewNPC  0 -7>
	place5 <1 (Guards) sd ally>
	element6 <MetaScene !Near -7 *Building NeverFail>

	%% FAIL CONDITIONS:
	%%  - E2 Dies

	% P%id%01 = Win Counter
	% P%id%02 = Match Counter

	update <if= PlotStatus %plotid% 0 SetPlotStatus %plotid% %id% News %id%01>
	end <if= PlotStatus %plotid% %id% ifNPCDead %2% LoseComp 0 .%id%_next>
	.%id%_next <+T-- +F-->

	%% Each tournament win will increase the win counter; each completed match
	%% will increase the match counter.
	.%id1%_%plotid1%_GoWin  <P+ %id%02 1 P+ %id%01 1>
	.%id1%_%plotid1%_GoLose <P+ %id%02 1>
	.%id2%_%plotid2%_GoWin  <P+ %id%02 1 P+ %id%01 1>
	.%id2%_%plotid2%_GoLose <P+ %id%02 1>
	.%id3%_%plotid3%_GoWin  <P+ %id%02 1 P+ %id%01 1>
	.%id3%_%plotid3%_GoLose <P+ %id%02 1>

	Msg%id%01 <A mecha tournament is being held at %name1% in \Scene &EpisodeScene . Interested parties are asked to contact %name2%.>


	% SubPlot1 is E3's Duel
	% SubPlot2 is E4's Duel
	% SubPlot3 is E5's Duel
	SubPlot1 <*:CS_TournamentMatch 3 1>
	SubPlot2 <*:CS_TournamentMatch 4 1>
	SubPlot3 <*:CS_TournamentMatch 5 1>

	sub
		Persona 2
		rumor%id% <%name2% is running a mecha contest for the technocracy.>
		% V%id%01 = Have started the tournament
		greeting <if= PlotStatus %plotid% %id% else GoChat if= v%id%01 1 else GoFirstTime ifG P%id%02 2 else GoInProgress if= P%id%01 3 else GoCheckMinorWin  Goto GoMajorWin>
		GoEndWin <StoryNote 2 &SetTargetNPC %2% WinComp %6% .win>
		.win <+Tgt +Fmi>
		GoMajorWin <NewChat Say 1 History 3  CustomMecha .mek StoryDL  Goto GoEndWin>
		.mek <GENERAL \FACTION_DESIG ChatNPCFac>
		GoCheckMinorWin <ifG P%id%01 0 else GoLoseTournament  NewChat Say 4 Goto GoEndWin>
		GoLoseTournament <NewChat Say 5 SMemo 0 History 6 NTBLoseComp 0 .loss>
		.loss <+T-- +F-->
		GoChat <NewChat SayAnything>
		GoInProgress <NewChat Say 7>
		*GoFirstTime <*AreYouHereAboutJob&Recruit GoExplain>
		GoExplain <NewChat Say 8 AddChat 1 AddChat 2 AddChat 3 &SetAllyFac ChatNPCFac>
		result1 <NewChat Say 9 SMemo 10 History 11 V= %id%01 1  SetPlotStatus %plotid1% %id1%  SetPlotStatus %plotid2% %id2%  SetPlotStatus %plotid3% %id3%>
		result2 <AddSociable -d8 Goto result1>
		result3 <NewChat Say 12 AddChat 4 AddChat 5>
		result4 <AddPragmatic d8 Goto result1>
		*result5 <*RejectMission GoR5End>
		GoR5End <AddHeroic -2 AddSociable -d10   SMemo 0 History 13 NTBLoseComp 0 .loss>
		Msg1 <Congratulations, you are the winner of our tournament! In recognition of your prowress, I'm happy to present you with this custom mecha. Come see me at %name6% later on and I may have some work for you.>
		Msg2 <%name2% asked you to meet \OPR %2% at %name6% later on for a mission.>
		Msg3 <You placed first in the tournament, earning a custom mecha.>
		Msg4 <You didn't win the tournament, but you didn't do too badly either. Come see me at %name6% later on, if you're interested. I just might have a mission for you.>
		Msg5 <I'm afraid that you came in last.>
		Msg6 <You lost the tournament, coming in dead last.>
		Msg7 <The tournament is still in progress. Come back here after you've challenged %name3%, %name4%, and %name5%.>
		Msg8 <The technocracy is holding a mecha tournament in order to locate a champion for \SCENE &EpisodeScene . There are numerous prizes and other benefits on offer.>
		Msg9 <Welcome on board. You will be competing against three other pilots: %name3%, %name4%, and %name5%. Go challenge them, then come see me when you're finished. If you are the winner I will have a prize waiting.>
		Msg10 <You entered a mecha tournament sponsored by %name2% of \FACTION ChatNPCFac . Your opponets are %name3%, %name4%, and %name5%.>
		Msg11 <You entered a mecha tournament sponsored by %name2% of \FACTION ChatNPCFac .>
		Msg12 <That law may be suspended in special cases. Recently there has been a string of unexplained mecha attacks; holding a tournament like this is the most efficient way to attract pilots and identify those who are strong enough to help.>
		Msg13 <You refused to help \SCENE &EpisodeScene against the mysterious attackers.>
		Prompt1 <Sign me up!>
		Prompt2 <I'm no champion, but I like prizes!>
		Prompt3 <Wait, I thought dueling was illegal around here?>
		Prompt4 <Sounds reasonable. I'm in.>
		Prompt5 <I'm not interested in your tournament, or in helping.>
	end

Plot
	name <Friend On Patrol>
	desc <Your friend is on patrol.>
	requires <#A +Pun +Tgt (+Fin|+F--|+Fch) (T:FRIEND|T:ALLY|T:LOVER|T:FAMILY) (T:MILIT|T:ADVEN|T:LABOR|T:POLIC) S:-->
	size 4

	% E1 is a local environs scene
	% E2 is a metascene to place there
	% E3 is the friend
	% E4 is a scene to meet at later
	Element1 <Scene Environs !Near -7>
	Element2 <Prefab>
	Place2 <1>
	Element3 <Grab 5>
	Place3 <2 sd ally (Friend)>
	Element4 <Scene (Meeting|Mall) Public !Near -7>

	update <if= PlotStatus %plotid% 0   SetPlotStatus %plotid% %id%   NPCLevel %3% StoryDL>

	% SubPlot1 is what happens if the PC aids the NPC.
	SubPlot1 <*CS_ThanksForAssist 3>

	sub
		MetaScene 2 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		% L1 = Match Over counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1 ForceChat %3% StoryNote 1>
		% If the NPC dies, this scenario ends in a loss.
		Faint%3% <L= 1 1  StoryNote 2  LoseComp 0 .loss>
		.loss <+T-- +F-->

		% If the PC defeats all of T2 before the NPC dies, it's a win.
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1               StoryNote 3   LoseComp 0 .loss>

		nu2 <if= T2 0   if= V1 0 V= 1 1   XPV 100  AddRenown 1   Alert 4 MoveNPC %3% %4% SMemo 5 History 6 SetPlotStatus %plotid% %id1%>

		Msg1 <You found %name3% being attacked by mecha.>
		Msg2 <%name3% was killed in battle, and you were powerless to stop it.>
		Msg3 <You failed to help %name3% win the battle.>
		Msg4 <With the enemy mecha defeated, %name3% races off for %name4%.>
		Msg5 <You helped %name3% defeat some hostile mecha. You can speak with \OPR %3% at %name4% now.>
		Msg6 <You helped %name3% to win the battle.>

		sub
			team 1
			SetAlly 3
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			SetEnemy 1 3
			Deploy <SetSelfFaction &EnemyFac   WMecha 2 StoryDL 120>
			ParaX 25
			ParaY 25

			team 3
			name <Friend>
			SetAlly 1
			SetEnemy 2
			ParaX 10
			ParaY 5
		end

		Persona 3
		special <Unlisted>
		% V%id%01 = Introduction counter
		rumor%id% <%name3%'s mecha hasn't returned from %name1%.>
		greeting <if= PlotStatus %plotid% %id% else GoChat if= V%id%01 1 else GoFirst Goto GoChat>
		GoChat <NewChat SayAnything>
		GoFirst <V= %id%01 1 EndChat Say %id%01 AddChat %id%01>
		*result%id%01 <*LetsDefeatThem &EnemyFac>
		Msg%id%01 <\PC , I could use a little help over here. I was out on patrol and got attacked by these thugs.>
		Prompt%id%01 <Don't worry, I've got your back.>
	end
	inv
		STC ENCOUNTER-WANDER
		name <%name3%'s Mecha>
		EncounterMove 75
	end


Plot
	name <Pirate War>
	desc <The thief you're looking for knows about the pirate war.>
	requires <#A +Tgt (+Far|+Ffi|+F--) +Pun T:++ F:-- -T:ENEMY -P:CRIHN (T:THIEF|T:CRIHN|T:REDMA) (L:FCOMS|L:MAQUI|L:RISHI|L:CRIHN|L:BOHEM|L:REDMA) ~C:THEIF ~+Gmo>
	size 6

	% E1 is an environs scene where the pirates are hanging out
	% E2 is the metascene for the encounter
	% E3 is the pirate faction
	% E4 is a public scene
	% E5 is a pirate captain who the PC can fight, and learn about the war
	% E6 is the target NPC
	element1 <Scene Environs !Near -7>
	element2 <Prefab>
	Place2 <1>
	element3 <Faction CRIHN>
	element4 <Scene Building Public !Okay 3 !Near -7>
	element5 <Character HasMecha !Comrade 3 !Near 4 NeverFail>
	NeverFail5 <Pirate>
	element6 <Grab 5>

	% P%id%01 = Initialization Counter
	update <if= p%id%01 0 p= %id%01 1 NPCLevel %5% StoryDL MoveNPC %5% %2% SetNPCTeam %5% 2>

	sub
		MetaScene 2 2
		rumor <there have been a lot of pirates fighting in %name1%.>
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1   StoryNote 1   ForceChat %5%    &SetEnemyFac %3%>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1                                   LoseComp 0 .next>
		nu2 <if= T2 0            if= V1 0 V= 1 1   XPV 100  AddRenown 1   SALVAGE   WinComp 0 .next ifNPCOK %5% PCEnemy %5%>
		.next <+Tgt +Fin>
		Msg1 <You learned from %name5% that %name6% has a secret that the pirates are fighting to get.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction E2   WMecha 2 StoryDL 30>
			ParaX 25
			ParaY 25
		end

		Persona 5
		special <UNLISTED NOESCAPE>
		rumor <all the pirates in town have been looking for %name6%.>
		greeting <EndChat Say 1 AddChat 1 AddChat 2>
		result1 <EndChat Say 2 AddChat 3>
		result2 <EndChat Say 3 AddChat 3>
		result3 <EndChat Say 4>
		Msg1 <You there... The word on the street is that you can tell me where %name6% is.>
		Msg2 <Don't play games. I know that you've been looking for %name6%, just like we all have. Maybe you even found out \PPR %6% secret already. If you want to live you'd better share that knowledge with me.>
		Msg3 <Ah, a clever ploy, but it's not going to work. I know that you've been looking for %name6%, just like we all have. Maybe you even found out \PPR %6% secret already. If you want to live you'd better share that knowledge with me.>
		Msg4 <Five captains of the Blades have declared war to find out what %name6% knows. Maybe you really don't have any part in what's going on... but that's not a chance I'm willing to take.>
		prompt1 <Me? I know nothing.>
		prompt2 <And here I thought you were the one who could tell me.>
		Prompt3 <Look, I don't understand what you're talking about.>
	end
	inv
		Encounter
		name <Pirates>
		EncounterMove 50
	end

Plot
	name <City Under Attack>
	desc <The leader of the city puts out a call for heroes.>
	requires <#A +T-- +Pun F:-- -L:ENEMY ~L:ALLY ~P:-- ~+Gpe>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the meme, for extra coverage
	element1 <Scene Building Public !Ally -7 !Near -7>
	element2 <Character POLIT !Near 1 !Comrade -7 NeverFail>
	NeverFail2 <Politician>
	Place2 <1 (Citizens) pass>
	Element3 <Prefab>

	% All this plot does is move the NPC, post some news,
	% and activate the mission subplot
	update <if= PlotStatus %plotid% 0  News 1  ActivateMeme %3% %1%  SetPlotStatus %plotid% %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	Msg1_1  <%name2% of \NARRATIVE 7 seeks cavaliers to help defend the town. Interested parties should apply at %name1%.>
	Msg1_2  <\NARRATIVE 7 is under attack! %name2% will be interviewing cavaliers for defense missions at %name1%.>
	Msg1_3  <Unknown mecha are attacking \SCENE &EpisodeScene . %name2% has put out a call for volunteers; applicants are to meet at %name1%.>
	Msg1_4  <Is \SCENE &EpisodeScene under attack? %name2% has been meeting with mecha pilots at %name1%.>
	Msg1_5  <%name2% has called on adventurers to defend \NARRATIVE 7 from a mysterious enemy. Defense efforts are being coordinated from %name1%.>

	inv
		Meme
		MaxMemeViews 1
		MemeTimeLimit
		Msg <%name2% is looking for someone to fight the mecha that have been attacking town.>
		Msg_1 <You might be interested to know that %name2% is looking for some mecha pilots at %name1%. I think it has something to do with the mysterious attacks we've had.>
		CMsg_1 <ifNPCSociable Accept>
		Msg_2 <There was something on the news about those mecha attacks.>
		CMsg_2 <ifNPCShy Accept>
		Msg_3 <If you're interested in the recent mecha attacks, I hear that %name2% at %name1% is looking for someone to fight them.>
		CMsg_3 <ifNPCEasygoing Accept>
		Msg_4 <>
		CMsg_4 <ifNPCPassionate Accept>
		Msg_5 <%name2% will find out what's going on with those mecha attacks. I'm sure of it.>
		CMsg_5 <ifNPCCheerful Accept>
		Msg_6 <Is there going to be a war? I hope %name2% can find out who sent those mecha to attack us.>
		CMsg_6 <ifNPCMelancholy Accept>
	end

Plot
	name <Shrine Destroyed>
	desc <A shrine is destroyed; the caretaker demands action!>
	requires <#A +T-- +Pun C:FAITH I:-->

	% E1 is a metascene
	% E2 is a public scene; the "natural home" of the monk
	% E3 is the monk who will tell the PC about the situation
	% E4 is the artifact the attackers may have been looking for
	% E5 is a location for a combat mission
	% E6 is the other researcher
	element1 <Metascene  !Near -7 *Building NeverFail>
	element2 <Scene Public Legit !Near -7>
	element3 <Character "FAITH" !Near 2 NeverFail>
	neverfail3 <Monk>
	Place3 <1 (Citizens) Pass>
	element4 <Artifact PreZero>
	element5 <Scene Environs !Near -7>
	element6 <Prefab>

	% P1 = Initialization Counter

	start <if= PlotStatus %plotid% 0 ifNPCOK E3 else GoLose>
	GoLose <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if=  P1 0  P= 1 1    News 1   FreezeItem E4>

	Msg1 <The Ecumenical Temple of \NARRATIVE 7 was heavily damaged today when a force of unidentified mecha attacked %name1%.>

	% SubPlot1 is the combat encounter, should the PC want to go after the people
	%  who did this.
	SubPlot1 <*CS_MechaRetribution 3 5>

	sub
		MetaScene 1
		% The Ecumenical Temple
		ClubMap
		MapWidth 20
		MapHeight 25
		type <TEMPLE LEGIT BUILDING PUBLIC>
		special <AddExit>
		nu1 <if= T1 0 Return>
		inv
			STC SHRINE-1
		end
		sub
			Team 1

			Team 2
			name <Civilian>
			setally 1
			Passive

			room
			name <Foyer>
			desig <EntranceGrid>
			Content <Some 4 45 Here *URBAN_X>
		end

		Persona 3
		% The persona of the monk to give the mission...
		greeting <if# PlotStatus %plotid% 0 else GoStartPlot NewChat SayAnything>
		*GoStartPlot <*DestructionGreeting GoTellStory &EnemyFac>
		GoTellStory <NewChat ifNPCPassionate else GoEasyStory Say 1 AddChat 1 AddChat 2>
		GoEasyStory <Say 2 AddChat 3 AddChat 4>
		result1 <NewChat Say 3 History 10 SMemo 4 History 11  AddEasygoing -10 Goto GoPassionateWin>
		GoPassionateWin <&SetTargetItem %4% SetPlotStatus %plotid% %id1%>
		result2 <NewChat Say 5 AddChat 1 AddChat 3>
		result3 <NewChat Say 6 AddChat 4 AddChat 5>
		result4 <NewChat Say 7 AddChat 7 AddChat 6>
		result5 <NewChat Say 8 History 10 StoryNote 9  AddEasygoing 10  Goto GoEasyWin>
		GoEasyWin <FreezeNPC %6% &SetTargetNPC %6% &SetTargetItem %4%  NegateComp 0 .easygoing>
		.easygoing <+Pla +Tgt +Fin>
		result6 <Goto result5>
		result7 <Goto result1>
		Msg1 <The animals who destroyed this temple must be punished.>
		Msg2 <They must have attacked this place because they were looking for the %name4% ... It's not here any longer, but I fear what dark purpose they have planned for it.>
		Msg3 <They can't have gotten far... You should be able to find them somewhere in %name5%.>
		Msg4 <%name3% asked you to find the mecha in %name5% who attacked \PPR E3 temple.>
		Msg5 <They must have been searching for the %name4% . It was here once, a long time ago, but was moved years ago.>
		Msg6 <It's an artifact from PreZero times; a relic from the past... and perhaps something more. Professor %name6% was researching it but I don't know where it is now.>
		Msg7 <There will be time for that later. Right now, I'm concerned about %name6% ... \SPR %6% has been researching the %name4% and the same thugs who attacked this temple may go after \OPR %6% .>
		Msg8 <Go find Professor %name6% , then, before the people who attacked the temple find \OPR %6% .>
		Msg9 <%name3% asked you to find %name6% , who knows about the %name4%.>
		Msg10 <Unknown mecha destroyed the Ecumenical Temple in \NARRATIVE 7 .>
		Msg11 <%name3% asked you to find the mecha who attacked \PPR E3 temple.>
		Prompt1 <I could bring them to justice.>
		Prompt1_1 <I could bring make them pay for it.>
		Prompt2 <Why did they attack this temple?>
		Prompt2_1 <Why was this place attacked?>
		Prompt3 <What is the %name4%?>
		Prompt4 <The people responsible for this must be punished.>
		Prompt5 <I could find it for you.>
		Prompt6 <I'll find %name6% and protect \OPR %6% .>
		Prompt7 <It'd be better if I found those mecha.>
	end
	inv
		NPC Professor
		chardesc Old Heroic Spiritual
	end

