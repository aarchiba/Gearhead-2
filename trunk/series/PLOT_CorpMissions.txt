Plot
	name <Basic Corporate Patrol Job>
	% The basic patrol job is a fight against some generic enemy mecha.
	% This job gives salvage. This job will not result in the PC making
	% any enemies.
	requires <*GENERAL>
	PayRate 105

	% E1 is a character who will offer the mission
	% E2 is the corporate faction that E1 belongs to
	% E3 is a scene where the encounter will take place
	Element1 <Character Corporate>
	element2 <Faction Corporate !Comrade 1>
	Element3 <Scene Environs>

	% SubPlot1 is the combat encounter
	SubPlot1 <*MECHAMISSION_Basic 1 3>

	% P1 = Time Limit
	% P2 = Email Indicator
	start <if= PlotStatus 0 ifG ComTime P1 EndPlot>
	update <if= PlotStatus 0 if= P1 0 P= 1 ComTime P+ 1 86400>

	% Insert email here
	5min <if= PlotStatus 0 if= P2 0  if= PCFac %2%  if= d20 1  ifG PCCharm d40  Email 1 P= 2 1>
	Msg1 <\ELEMENT 1 @ \SCENE EScene 1 :// I have a mission for you from \ELEMENT 2 .>
	Msg1_1 <\ELEMENT 1 @ \SCENE EScene 1 :// Report to \SCENE EScene 1 for a mission at once.>
	Msg1_2 <\ELEMENT 1 @ \SCENE EScene 1 :// Your services are required by \ELEMENT 2 . Come see me for mission briefing.>
	Msg1_3 <\ELEMENT 1 @ \SCENE EScene 1 :// I've got a mission for you. Come here for the briefing.>
	Msg1_4 <\ELEMENT 1 @ \SCENE EScene 1 :// There's a matter that requires your attention. This is official \ELEMENT 2 business, so come as soon as possible.>

	sub
		Persona 1
		rumor0 <%name1% needs a mecha pilot to do a mission for %name2%.>
		greeting <if= PlotStatus %id1% else GoCheckOffer Goto GoRemind>
		*GoRemind <*MechaMissionReminder %3%>
		GoCheckOffer <if# PlotStatus 0 else GoCheckEnemy NewChat SayAnything>
		*GoCheckEnemy <*ENEMY_CHECK GoCheckEmail %2% GoEnd>
		GoCheckEmail <if= P2 1 else GoCheckMember Goto GoGotEmail>
		*GoGotEmail <*DidYouGetEmail GoMissionBriefing>
		GoCheckMember <if= PCFac %2% else GoCheckAuto  Goto GoIsMember>
		*GoIsMember <*IHaveAJobForYou GoMissionBriefing>
		*GoCheckAuto <*AutoMissionTest&Mecha GoMissionBriefing GoRejectMission GoCheckSkill %2% na>
		*GoCheckSkill <*GenericMissionTest&Mecha  GoMissionBriefing GoEnd GoRejectMission %2% na>
		*GoRejectMission <*RejectMission GoEnd>
		GoEnd <EndPlot>
		GoMissionBriefing <NewChat Say 1  AddChat 1 AddChat 2>
		*result1 <*GoodLuckOnMission&NoEnemyFac GoR1Final %2% na>
		GoR1Final <Memo 2 SetPlotStatus %id1%>
		result2 <Goto GoRejectMission>
		Msg1 <Recently, a number of our shipments have been attacked by bandits. Your job will be to locate those responsible and make sure they are incapable of attacking us again. For this you will earn $ \VAL V1 plus salvage.>
		Msg1_1 <You've probably heard that several of our convoys have been attacked recently. We've identified the bandit gang responsible; your job will be to eliminate them once and for all. Do this and you'll earn $ \VAL V1 plus salvage.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <Some bandits have been attacking our shipments. Defeat them and you'll earn $ \VAL V1 plus salvage rights.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <This should be a straightforward assignment. Some bandits have been attacking our shipments; you'll be paid $ \VAL V1 plus salvage rights for locating and eliminating them.>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <The enemies of \ELEMENT 2 never sleep. Some bandits have been attacking our property... Your job will be to engage them in combat and eliminate them. Do this and you'll earn $ \VAL V1 plus salvage.>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <I want you to locate a particular bandit gang and disable their mecha. If you can perform this task, \ELEMENT 2 will pay you $ \VAL V1 plus full salvage rights.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <A gang of bandits has been attacking our shipments; several employees of \ELEMENT 2 have been killed already. I want you to locate the bandits and eliminate them. Upon completion of this mission you'll earn $ \VAL V1 and salvage.>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <\ELEMENT 1 in \SCENE EScene 1 hired you to fight some bandits in \ELEMENT 3 for \ELEMENT 2 .>
		Prompt1 <Sounds good. I'll do it.>
		Prompt1_1 <Alright, I'll do it.>
		Prompt1_2 <I'll get to work immediately.>
		Prompt2 <Sorry, I don't want this job.>
		CPrompt2 <if# PCFac &AllyFac Accept>
		Prompt2_1 <On second thought, I don't want this job.>
		Prompt2_2 <Really, I don't want to do this.>
	end


