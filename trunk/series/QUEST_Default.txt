%%
%% STATIC ADVENTURE CONTENT
%%
%% This is used when a new campaign is started to fill each location with
%% randomly generated content. The content is hardcoded into the adventure
%% instead of being housed in a plot or story.
%%

%%
%% *:PersonalIntervention Content
%%
%% E1 is a character who's into a lot of bad things. E2 sent the PC to
%% locate him and hopefully reform/return him.
%%
%% Note that if this subquest derails the quest, it can set the previous
%% quests's status to -2.
%%
%% PARAM1: The intervention target
%% PARAM2: The mission-giving NPC

Content
	name <Pissed Off Loan Shark>
	desc <E1 hasn't paid his debt, and so has been thrown in the pit.>
	requires <*:PersonalIntervention ~"City" ~"Dangerous">

	% E1 is the prodigal target
	% E2 is the mission-giver
	% E3 is the NPC who can tell the PC where E1 went
	% E4 is the trapdoor leading to the pit
	% E5 is the pit itself
	% E6 is the space under the pit where E1 is hiding
	Element3 <NewNPC 0 0 Criminal>
	Scene3 <Grab 1>
	Team3 <Citizens>
	TeamData3 <Pass>

	Element4 <Prefab>
	Scene4 <Building Public Sleazy>

	Scene5 <New *Dungeon.Sewer>
	Scene6 <New *HideOut&BoxMap>

	sub
		Persona 1
		% V1 = Have asked for help before
		greeting <if= QuestStatus %qid% %id% else %pop% if= SceneVar %s6% 12 1 else .%id%_GoCheckProgress Goto .%id%_GoVictory>
		.%id%_GoVictory <EndChat Say %id%01 QMemo %prev_qid% %id%02 MoveNPC %1% %s2% SetQuestStatus %qid% -1>
		.%id%_GoCheckProgress <ifNPCLancemate else .%id%_GoAskHelp NewChat Say %id%03>
		.%id%_GoAskHelp <ifChatNPCInPlay else .%id%_GoBusy if= V1 1 else .%id%_GoFirstTime NewChat Say %id%03 Goto .%id%_GoJoinLance>
		*.%id%_GoBusy <*BrushOff>
		.%id%_GoJoinLance <V= 1 1 QMemo %qid% %id%07 ChatLMFollow AddTempLancemate %1%>
		.%id%_GoFirstTime <NewChat Say %id%04 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		result%id%01 <NewChat Say %id%05 Goto .%id%_GoJoinLance>
		result%id%02 <NewChat Say %id%06 Goto .%id%_GoJoinLance>
		result%id%03 <EndChat Say %id%08 SoloAttack %1% SetQuestStatus %qid% -2 AddHeroic -25 AddLawful -25>
		Msg%id%01 <Thanks for getting me out of there! I'll go back to \SCENE %s2% and see %name2% now.>
		Msg%id%02 <You rescued %name1% from the pit; \SPR %1% went to \SCENE %s2% to meet with %name2%.>
		Msg%id%03 <Just get me back to \SCENE %s4% and I'll be able to go home by myself.>
		Msg%id%04 <You've got to help me! I was tossed down into this monster infested place, without any tools or weapons!>
		Msg%id%05 <Thank you so much! Just get me back to \SCENE %s4% and I'll be able to find my way home from there.>
		Msg%id%06 <You have no worries about that. There's nothing like being nearly eaten by rats to make a person re-evaluate their life choices. If I get home, I'll never go back to \SCENE %s4% .>
		Msg%id%07 <You found %name1% at the bottom of the pit and now must lead \OPR %1% to safety.>
		Msg%id%08 <I've survived down here by myself for a while, so don't think you can defeat me easily!>
		Prompt%id%01 <%name2% sent me to rescue you.>
		Prompt%id%02 <I'll help if you promise to reform your life.>
		Prompt%id%03 <Actually, I think I'll kill you.>
		CPrompt%id%03 <ifPCVillainous Accept>

		Persona 3
		rumor%id% <%name3% knows where %name1% is.>
		% V1 = Have given information
		greeting <if# QuestStatus %qid% %id% else GoCheckInfo NewChat SayAnything>
		GoCheckInfo <if= V1 1 else GoGiveInfo NewChat SayAnything>
		*GoGiveInfo <*LookingForNPC %1% GoMoreInfo GoLessInfo>
		GoMoreInfo <NewChat Say 1 QMemo %qid% 2 AddChat 1 Goto GoActivateDungeon>
		GoLessInfo <NewChat Say 3 QMemo %qid% 4 Goto GoActivateDungeon>
		GoActivateDungeon <V= 1 1 ItemVar= %4% 1 1>
		result1 <NewChat Say 5>
		Msg1 <From what I heard, %name1% had some outstanding debts to a loan shark over at \SCENE %s4% . Because of that \SPR %1% was thrown into the pit.>
		Msg2 <%name3% told you that %name1% was thrown into a pit at \SCENE %s4% .>
		Msg3 <I don't know anything about %name1%, all I know is that you shouldn't piss off the loan sharks at \SCENE %s4% .>
		Msg4 <%name3% implied that %name1% had run afoul of some loan sharks at \SCENE %s4% .>
		Msg5 <Technically it's called \SCENE %s5% . It's a creature-infested length of service tunnels running underneath the building. The crime bosses use it to make people disappear.>
		Prompt1 <What is "The Pit"?>

		% SceneVar %s6% 11 = Initialization counter
		MetaScene 1
		% At the start of this scene, move the target NPC to the hideout.
		start <if= QuestStatus %qid% %id% else %pop% if= SceneVar %s6% 11 0 else %pop% SceneVar= %s6% 11 1 MoveNPC %1% %s6% Goto %pop%>
		% Just in case this subquest is activates from Scene 1, do the same thing at update.
		update <if= QuestStatus %qid% %id% else %pop% if= SceneVar %s6% 11 0 else %pop% SceneVar= %s6% 11 1 MoveNPC %1% %s6% Goto %pop%>

		% SceneVar %s6% 12 = Finalization counter
		MetaScene 4
		% At the start of this scene, if E1 is present with the PC, this results in victory.
		start <if= QuestStatus %qid% %id% else %pop% ifNPCInPlay %1% else %pop% if= SceneVar %s6% 12 0 else %pop% SceneVar= %s6% 12 1 RemoveTempLancemate %1% XPV 200 ForceChat %1% Goto %pop%>
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		MetaScene 5
		sub
			room
			minimap <............2............>
			special <SHAREDPALETTE>
			sub
				Trapdoor
				desig <ENTRANCE %s6%>
				MiniMapComponent 2
			end
		end
	end
	inv
		TrapDoor
		name <To The Pit>
		desig <Entrance %s5%>
		% V1 = if nonzero, reveal trapdoor
		UPDATE <if# v1 0 else GoHide SetStat STAT_MetaVisibility 0>
		GoHide <SetStat STAT_MetaVisibility -1>
		Hide
	end

Content
	name <Slave to Some Guy>
	desc <E1 is in debt to a smuggler and can't leave until it's paid off.>
	requires <*:PersonalIntervention>

	% E1 is the prodigal target
	% E2 is the mission-giver
	% E3 is the character that E1 is in debt to
	% E4 is E3's "office"
	% E5 is the town scene
	% E6 is a law enforcement faction loyal to E5, needed for "gotcha" victory
	Element3 <Prefab>
	Scene3 <Grab 1>
	Team3 <Citizens>
	TeamData3 <Pass>

	Scene4 <New *BLANK.BoxBasement>

	Element5 <.>
	Element6 <Faction Police !Ally 5>

	sub
		% Scene %s4% is going to hold all the main variables for this quest.

		Persona 1
		rumor%id% <%name1% has been paying off a debt to a smuggler.>
		greeting <if= QuestStatus %qid% %id% else %pop% ifG SceneVar %s4% 1 0 else .%id%_GoQuestIntro NewChat Say %id%08 AddChat %id%04 AddChat %id%05 AddChat %id%06>
		.%id%_GoQuestIntro <NewChat Say %id%01 AddChat %id%01>
		.%id%_GoWinQuest <QMemo %prev_qid% %id%05 MoveNPC %1% %s2% SetQuestStatus %qid% -1>
		Result%id%01 <ifNPCOK %3% else .%id%_GoTrivial NewChat Say %id%02 SceneVar= %s4% 1 1 QMemo %qid% %id%03 AddCHat %id%02 AddChat %id%03>
		.%id%_GoTrivial <NewChat Say %id%04 Goto .%id%_GoWinQuest>
		Result%id%02 <NewChat Say %id%06>
		Result%id%03 <NewChat Say %id%07>
		Result%id%04 <NewChat Say %id%09>
		Result%id%05 <NewChat Say %id%10 Goto .%id%_GoWinQuest>
		Result%id%06 <NewChat Say %id%11 Goto .%id%_GoWinQuest>
		Msg%id%01 <Yes, what is it? I'm really busy right now... I'll get in trouble if I don't finish this work.>
		Msg%id%02 <I'm sorry that I haven't contacted %name2%, but I can't just yet. I want to get out of this life, I'm really trying, but I still owe some debts to %name3%. I have to work for \OPR %3% until everything's paid off.>
		Msg%id%03 <%name1% owes a debt to %name3%, and cannot return to %name2% until this debt is repaid.>
		Msg%id%04 <Now that %name3% is dead, there's no reason for me to stay around here. I'll go back to %name2%.>
		Msg%id%05 <%name1% has been freed from %name3%, and went back to %name2% at \SCENE %s2% .>
		Msg%id%06 <I don't think it'll do any good, but you can always try... be sure you don't make \OPR %3% angry, though, or it'll be taken out on me.>
		Msg%id%07 <That's not possible, I'd have to spend the rest of my life in hiding. Just leave me here... it might not be much of a life, but at least I'm alive.>
		Msg%id%08 <Did you convince %name3% to let me go?>
		Msg%id%09 <I'm stuck here forever, I guess...>
		Msg%id%10 <Is that true? I don't know what you did, but thanks! As long as there's no reason for me to stay here, I'm going to \SCENE %s2% and not looking back!>
		Msg%id%11 <I wish I could say that I'm sorry, but after all I've been through I can't... I'll see you back at \SCENE %s2% . Thanks for saving me.>
		Prompt%id%01 <%name2% sent me to check up on you.>
		Prompt%id%02 <I'll speak with %name3% about that.>
		Prompt%id%03 <Why don't I just sneak you out of here?>
		Prompt%id%04 <No, not yet.>
		Prompt%id%05 <%name3% has agreed to release you.>
		CPrompt%id%05 <if= SceneVar %s4% 1 2 Accept>
		Prompt%id%06 <%name3% is dead.>
		CPrompt%id%06 <ifNPCDead %3% Accept>

		Persona 3
		% V1 = Debt Amount
		special <UNLISTED>
		greeting <if= QuestStatus %qid% %id% else GoChat if= SceneVar %s4% 1 1 else GoChat if= SceneVar %s4% 2 1 else GoPreFight ifChatNPCSurrendered else GoMaybeFight EndChat Say 10 UnSurrender %3% MoveAndPacifyNPC %3% %s1% SceneVar= %s4% 1 2 XPV 100>
		GoMaybeFight <ifNPCHeroic else GoStartFight NewChat Say 11 AddChat 7 SceneVar= %s4% 1 2>
		GoStartFight <EndChat Say 13 SoloAttack %3% L= 3 1>
		*GoPreFight <*IWantToTalkAboutNPC&YouBadNPC %1% GoExplain>
		GoChat <NewChat SayAnything>
		GoExplain <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		result1 <NewChat V= 1 Threat %threat% 8 Say 2 AddChat 4 AddChat 5 AddCHat 2>
		result2 <NewChat Say 3 QMemo %qid% 4 MoveNPC %3% %s4% SceneVar= %s4% 2 1>
		result3 <NewChat Say 5 AddChat 1 AddChat 2 AddChat 5 AddChat 6>
		result4 <NewChat Say 6 SceneVar= %s4% 1 2 Cash+ -V1>
		result5 <NewChat Say 7>
		result6 <EndChat Say 8 Print 9 SceneVar= %s4% 1 2 FacXP+ %6% 2 FreezeNPC %3% PCEnemy %3% History 14>
		result7 <NewChat Say 12>
		Msg1 <%name1% had a lot of expensive habits, and because of these habits became very deeply indebted to me. I can't let \OPR %1% go until these debts are paid off. Since %name1% has no money, the debt must be paid in work.>
		Msg2 <You'd do that? Personally, I don't care who pays, just as long as I get my money. %name1% owes me $ \VAL V1 .>
		Msg3 <This isn't a good place to speak about that. Come downstairs to my office and we can discuss things in private.>
		Msg4 <%name3% invited you downstairs to \PPR %3% office to discuss %name1%'s freedom.>
		Msg5 <The usual, you know. Narcotics, Vesuvian polywine, vintage Imperator Zeta pornoreels... of course \SPR %1% 's been pretty clean these days, now that I've cut off \PPR %1% source.>
		Msg6 <Ah, a big lump of cash is so much nicer than waiting for a lazy bum like %name1% to work off a debt. It's been a pleasure doing business with you.>
		Msg7 <Take your time. %name1% isn't going anywhere.>
		Msg8 <Wait, you're a member of %name6%!? Aw crap... well, I guess it's a fair cop.>
		Msg9 <You arrest %name3%.>
		Msg10 <All right, I give up- %name1% can go free. I officially cancel \PPR %1% debt.>
		Msg11 <I officially cancel %name1%'s debts. You can take \OPR %1% back to %name2% or wherever \SPR %1% wants to go, with my blessing.>
		Msg12 <Honestly speaking, I have no real interest in reclaiming %name1%'s debts. I just thought that I could frighten \OPR %1% away from \PPR %1% self-destructive habits. It was fun having a lackey for a while, but it got old really fast.>
		Msg13 <You didn't really think you'd be able to talk me out of this, did you? I just brought you down here so it'll be easier to clean up the blood after you're dead!>
		Msg14 <You arrested the smuggler %name3%.>
		Prompt1 <How about I pay off %name1%'s debts?>
		Prompt2 <I want you to release %name1% right away.>
		Prompt3 <What kind of services did you provide to \OPR %1% ?>
		Prompt4 <I can afford that. [Give money]>
		CPrompt4 <ifG PC$ V1 Accept>
		Prompt5 <I'll be back later.>
		Prompt6 <You admit that? In the name of %name6%, you're under arrest!>
		CPrompt6 <if= PCFac %6% Accept>
		Prompt7 <Wait, why are you giving up so easily?>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		inv
			STC Trapdoor
			name <To %name3%'s Office>
			desig <ENTRANCE %s4%>
		end

		MetaScene 4
		% L1 = E1 has explained about E3 (1), E3 has released E1 (2)
		% L2 = E3 has moved to this scene for possible fight (1)
		% L3 = Fight has started
		% L4 = Conversation Counter
		start <Print 1 if= L2 1 if= L4 0 ForceChat %3% L= 4 1>

		% If the PC has started the fight, leaving the room results in a loss.
		end <if= QuestStatus %qid% %id% if= L3 1 if# L1 2 ifNPCOK %3%  FreezeNPC %3% PCEnemy %3% DeleteNPC %1% SetQuestStatus %qid% -2>

		Surrender%3% <if= QuestStatus %qid% %id% ForceChat %3%>

		Msg1 <You enter %name3%'s office.>

		sub
			Team 1

			Team 2
			name <Citizens>
			Passive
		end
	end
	inv
		NPC Pirate
		job <Smuggler>
		job_desig <THIEF>
	end

Content
	name <I likes it like that>
	desc <E1 has no intention to change his ways; may ask PC to get E2 off his back.>
	requires <*:PersonalIntervention ~"Safe">

	% E1 is the prodigal target
	% E2 is the mission-giver

	% NPCVar %1% %id%01 will be set if the PC accepts E1's request to make
	% E2 leave him alone. If the PC convinces E1 to reform, remember to clear
	% this variable so that E2 can provide the nessecary conclusion.

	% NPCVar %1% %id%02 will be set if the PC succeeds at the above.

	% SubQuest1 is the victory if the PC does as E1 asks, rather than complete
	%  the original mission.
	Quest1 <*SuccessVsChar.Evasion 1 2>

	sub
		Persona 1
		rumor%id% <%name1% has been hanging out at the black market.>
		%  V%id%01 = Thanks counter
		greeting <if# QuestStatus %qid% -1 else .%id%_GoThanks if= QuestStatus %qid% %id% else %pop% if= NPCVar %1% %id%02 1 else .%id%_GoCheckQuest NewChat Say %id%01 AddChat %id%01>
		.%id%_GoCheckQuest <ifNPCOK %2% else .%id%_GoE2Died NewChat Say %id%02 AddChat %id%02 AddChat %id%03 AddChat %id%04>
		.%id%_GoE2Died <NewChat SayAnything SetQuestStatus %qid% -2>
		.%id%_GoThanks <if= v%id%01 0 else .%id%_GoChat V= %id%01 1 NewChat Say %id%05>
		.%id%_GoChat <NewChat SayAnything>
		.%id%_GoWinQuest <QMemo %prev_qid% %id%08 SetQuestStatus %qid% -1 NPCVar= %1% %id%01 0 MoveNPC %1% %s2%>
		result%id%01 <AddLawful -1  AddEasygoing -d8  Goto .%id1%_GoInit>
		result%id%02 <NewChat Say %id%03 AddChat %id%05 AddChat %id%06 AddChat %id%07>
		result%id%03 <NewChat Say %id%04 AddChat %id%05 AddChat %id%08>
		result%id%04 <NewChat Say %id%06 AddChat %id%09 AddChat %id%10>
		result%id%05 <ifConversation HardSkillTar %threat% else .%id%_GoR5Failed NewChat Say %id%07 Goto .%id%_GoWinQuest>
		.%id%_GoR5Failed <NewChat Say %id%09 AddChat %id%11 AddChat %id%12 AddChat %id%07>
		result%id%06 <NewChat Say %id%10 AddChat %id%03>
		result%id%07 <NewChat Say %id%11 NPCVar= %1% %id%01 1 QMemo %qid% %id%12>
		result%id%08 <ifIntimidation SkillTar %threat% else .%id%_GoR8Failed NewChat Say %id%13 Goto .%id%_GoWinQuest>
		.%id%_GoR8Failed <NewChat Say %id%14>
		result%id%09 <ifG PCLaw %threat% else .%id%_GoR9Fail NewChat Say %id%15 Goto .%id%_GoWinQuest>
		.%id%_GoR9Fail <NewChat Say %id%16>
		result%id%10 <NewChat Say %id%17>
		result%id%11 <Goto result%id%08>
		result%id%12 <AddLawful 1 NewChat Say %id%18 Goto .%id%_GoWinQuest>
		Msg%id%01 <So, did you meet with %name2%?>
		Msg%id%02 <Yeah, what do you want?>
		Msg%id%03 <Well you can go back and tell %name2% that I'm busy. The last thing I need is \OPR %2% nosing into my affairs.>
		Msg%id%04 <What are you trying to say, there?>
		Msg%id%05 <Thanks for your help, \PC . I'll remember it.>
		Msg%id%06 <You've just got to push harder! %name2% will give up eventually, \SPR %2% has to...>
		Msg%id%07 <I know you're right, and I've tried to pull myself away, but... Maybe I'll give sobriety one more chance. I'm going back to %name2%.>
		Msg%id%08 <You convinced %name1% to go back to %name2% at \SCENE %s2% .>
		Msg%id%09 <%name2% doesn't know anything about my life, and neither do you. I'm not in any trouble. I've got everything under control.>
		Msg%id%10 <Just hanging out, the usual. I spent the money I was supposed to give the loan shark, but that made me so nervous that I've been popping pills all day. So now I'm betting on a synthchicken fight to hopefully win the money back.>
		Msg%id%11 <Man, if you could get %name2% off my back, that'd be awesome. I'm sure I could find a little something for you if you did that.>
		Msg%id%12 <%name1% at \SCENE %s1% wants you to get %name2% to stop bothering \OPR %1% .>
		Msg%id%13 <Oh. Umm... Well, if you're going to put it like that, I think it would be best if I went back to %name2%.>
		Msg%id%14 <You don't scare me... at least not until after those pills I took wear off...>
		Msg%id%15 <I know, I know... just when I was starting to have a bit of fun for the first time in ages. I guess it is good to have one person who cares about me, even if \SPR %2% is an old stick in the mud... Alright. I'll go back.>
		Msg%id%16 <You're just saying that because you think it'll make me give in. Never mind %name2% ; there's more than enough alcohol around here to make me forget \OPR %2% .>
		Msg%id%17 <Let me know when you've succeeded, would ya? I'll have something for you.>
		Msg%id%18 <I know that life hasn't been good lately, but I'm on the right track! Aw, who am I kidding? I'll go back to %name2%, now while I still have a chance...>
		Prompt%id%01 <%name2% won't be bothering you again.>
		Prompt%id%02 <%name2% sent me to talk.>
		Prompt%id%03 <You'd better go home before you get hurt.>
		Prompt%id%04 <%name2% isn't going to budge.>
		CPrompt%id%04 <if= NPCVar %1% %id%01 1 Accept>
		Prompt%id%05 <You're going to get hurt if you keep this up.>
		Prompt%id%06 <What are you doing, anyhow?>
		Prompt%id%07 <I'll bet you'd like %name2% to leave you alone.>
		Prompt%id%08 <I mean, I'll personally hurt you if you don't go.>
		Prompt%id%09 <No, I really think you're stuck with \OPR %2% .>
		Prompt%id%10 <I'll do my best.>
		Prompt%id%11 <Go now or I may have to use force.>
		Prompt%id%12 <Believe me, I've been there. You're in trouble.>
		CPrompt%id%12 <ifPCCriminal Accept>

		MetaScene 1
		% Contains a check to end the quest if E1 dies
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		Persona 2
		greeting <if= QuestStatus %qid% %id% else %pop% if# NPCVar %1% %id%01 0 else %pop% if= NPCVar %1% %id%02 0 else .%id%_GoDidIt NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoDidIt <NewChat SayAnything>
		Result%id%01 <NewChat Say %id%02 AddChat %id%03 AddChat %id%04>
		Result%id%02 <NewChat Say %id%03 AddChat %id%05 AddChat %id%04 AddChat %id%06>
		Result%id%03 <AddHeroic -1 ifIntimidation SkillTar %threat% else .%id%_GoR3Fail NewChat Say %id%04 QMemo %qid% %id%05 NPCVar= %1% %id%02 1 SetQuestStatus %prev_qid% -2>
		.%id%_GoR3Fail <NewChat Say %id%06>
		Result%id%04 <NewChat Say %id%07>
		Result%id%05 <NewChat Say %id%08 QMemo %qid% %id%05 NPCVar= %1% %id%02 1 SetQuestStatus %prev_qid% -2>
		Result%id%06 <NewChat Say %id%07>
		Msg%id%01 <Do you have any news about %name1% yet?>
		Msg%id%02 <I don't understand... I thought you were going to help me reform %name1%.>
		Msg%id%03 <How can I accept these decisions? %name1% is on a self destructive track, but is too blinded to see. Without outside intervention I fear for \PPR %1% future.>
		Msg%id%04 <So, \SPR %1% 's gotten to you, too... I can see this is a lost cause. You can tell %name1% that from now on I'll leave \OPR %1% alone.>
		Msg%id%05 <%name2% agreed to stop bothering %name1% about \PPR %1% lifestyle.>
		Msg%id%06 <I won't be so easily intimidated. Unlike %name1%, I have some strength in my convictions.>
		Msg%id%07 <Come back when you have news about %name1%, then.>
		Msg%id%08 <I think this may end in disaster, but it's clear that nothing I do or say will make %name1% change \PPR %1% mind. Hopefully \SPR %2% can learn something before it's too late.>
		Prompt%id%01 <I think you better back off on %name1%.>
		Prompt%id%02 <Why don't you accept %name1%'s decisions?>
		CPrompt%id%02 <ifConversation HardSkillTar %threat% Accept>
		Prompt%id%03 <Messing with %name1% could be bad for your health.>
		Prompt%id%04 <Never mind.>
		Prompt%id%05 <%name1%'s got to learn from \PPR %1% own mistakes.>
		Prompt%id%06 <You could be right.>
	end

%%
%% *:MechaHeld Content
%%
%% E1's mecha is currently being held by someone else. In this subquest,
%% the PC will be able to find that someone and release the mecha... Or
%% maybe have it locked up perminantly. The NPC has a semi-legitimate
%% claim to holding the mecha... it may be in hock due to debts, or maybe
%% its ownership is disputed. The presence of this subquests implies a
%% certain amount of inexperience or irresponsibility on the part of the
%% pilot.
%%
%% To start this subquest, the calling quest should set the QuestID.
%% This quest should end with -1 if the mecha was released, or -2 if not.
%% The rumor should explain the situation.
%%
%% PARAM1: The NPC who lost his mecha.

Content
	name <Mecha Drifting in Space>
	desc <E1 ejected while in space; now his mecha is floating aimlessly outside.>
	requires <*:MechaHeld Spinner (!Ne|!Lo)>

	% E1 is the character that owns the mecha
	% E2 is the encounter representing the mecha
	Element2 <Prefab>
	Scene2 <Space Environs>

	inv
		STC QUEST-ENCOUNTER-AUTO-WANDER
		name <Abandoned Mecha>
		rumor%id% <%name1% was forced to eject outside the station.>
		EncounterMove 20
		ENCOUNTER_NonCombat
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifEncounterVisible Goto Use>
		USE <ifYesNo 1 2 3 else GoTakeMecha   Print 4   XPV 50  SetQuestStatus %qid% -1  DeleteItem %2%>
		GoTakeMecha <RandomMecha .mekdesc %threat% SetQuestStatus %qid% -2  DeleteItem %2%>
		.mekdesc <GENERAL>
		Msg1 <You spot a mecha floating in space. It's most likely the one lost by %name1%.>
		Msg2 <Mark its location for %name1%.>
		Msg3 <Take the mecha as your own.>
		Msg4 <You send the mecha coordinates to %name1%.>
	end

Content
	name <Mecha Impounded by L5Law>
	desc <E1 broke some traffic rules; must pass a test to get mecha back>
	requires <*:MechaHeld L5PAT ~"Safe">

	% E1 is the character who owns the mecha.
	% E2 is the police officer holding the mecha.
	Element2 <Prefab>
	Scene2 <Building Legit>
	Team2 <Citizens>
	TeamData2 <SD Ally>

	sub
		Persona 1
		greeting <if= QuestStatus %qid% %id% else %pop%  if= NPCVar %2% 1 1 else %pop%  NewChat Say %id%01 AddChat %id%01>
		result%id%01 <NewChat Say %id%02 AddCHat %id%02 AddCHat %id%03>
		result%id%02 <NewChat Say %id%03 NPCVar= %2% 1 2>
		result%id%03 <NewChat Say %id%04>
		Msg%id%01 <All these traffic rules... What do they really have to do with how well I can pilot a mecha? %name2% says I have to pass a test before I can get my machine back.>
		Msg%id%02 <Really, you'd do that? So, you know the answers to all of these questions, then?>
		Msg%id%03 <Thanks. With your help, I'm sure I'll do fine on the test.>
		Msg%id%04 <Be careful... if %name2% finds out, your mecha will be confiscated next.>
		Prompt%id%01 <I could help you to study, if you want.>
		Prompt%id%02 <No problem. Let me show you.>
		CPrompt%id%02 <ifMechaPiloting %sktar% Accept>
		Prompt%id%03 <Um... Actually I don't know either.>

		Persona 2
		% NPCVar %2% 1 = State Counter, if =1 have learned of test, if =2 have taught E1
		% V2 = Intimidation counter
		&Bribe <Threat %threat% 3>
		rumor%id% <%name2% impounded %name1%'s mecha due to multiple parking tickets.>
		greeting <if= QuestStatus %qid% %id% else GoNoQuest ifNPCDead %1% else GoCheckWin SetQuestStatus %qid% -2 Goto GoChat>
		GoCheckWin <if= NPCVar %2% 1 2 else GoMainQuest NewChat Say 14 SetQuestStatus %qid% -1>
		GoMainQuest <if= NPCVar %2% 1 1 else GoFirstTime NewChat Say 3 AddChat 3 AddChat 4>
		GoFirstTime <NewChat Say 1 AddChat 1 AddChat 2>
		*GoNoQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 AddChat 3 AddChat 4 NPCVar= %2% 1 1 QMemo %qid% 4>
		*result2 <*MISC_CHATTER>
		result3 <NewChat Say 5>
		result4 <NewChat Say 6 AddCHat 5 AddChat 6 AddCHat 7>
		result5 <ifNPCLawful else GoR5NoLaw NewChat Say 7 AddCHat 6 AddChat 3>
		GoR5NoLaw <NewChat Say 8 AddChat 8 AddChat 9 AddChat 10>
		result6 <AddLawful -5 AddHeroic -1 ifIntimidation %sktar_hard% else GoR6Fail NewChat Say 9 SetQuestStatus %qid% -1>
		GoR6Fail <NewChat Say 10 PCEnemy %2% V= 2 1>
		result7 <NewChat Say 11>
		result8 <NewChat Say 12 Cash+ - &Bribe SetQuestStatus %qid% -1 AddLawful -5>
		result9 <NewChat Say 13 AddReact d10 SetQuestStatus %qid% -1 AddLawful -3 AddEasygoing -d8>
		result10 <NewChat Say 11>
		Msg1 <Yes, may I help you?>
		Msg2 <I caught %name1% piloting in an improper manner. I confiscated \PPR %1% mecha, and will not return it until \SPR %1% completes a mecha safety course.>
		Msg3 <I'm still waiting for %name1% to complete \PPR %1% safety course.>
		Msg4 <%name2% confiscated %name1%'s mecha, and won't return it until \SPR %1% completes a safety course.>
		Msg5 <Please try. I've talked to \OPR %1% over and over but it never seems to sink in.>
		Msg6 <What exactly do you mean by that?>
		Msg7 <I'm not about to be bribed. You can tell %name1% that \SPR %1% can't get out of the test that easily.>
		Msg8 <That sounds alright... what can you offer me?>
		Msg9 <Yikes... Alright, you can tell %name1% that \PPR %1% mecha is being released. There's no need to make a big deal over this.>
		Msg10 <It's not a smart idea to try intimidating a law officer. You can tell %name1% that \SPR %1% can't get out of the test that easily.>
		Msg11 <That's too bad.>
		Msg12 <It's been a pleasure doing business with you. You can tell %name1% that \PPR %1% mecha is being released.>
		Msg13 <Yes, I do believe that would be a worthwhile trade. You can tell %name1% that \PPR %1% mecha is being released.>
		Msg14 <You can tell %name1% that \SPR %1% passed the exam. I'll release \PPR %1% mecha shortly.>
		Prompt1 <I'm here to talk about %name1%'s mecha.>
		Prompt2 <I'm just here to chat.>
		Prompt3 <I'll talk with \OPR %1% about that.>
		Prompt4 <I want you to return \PPR %1% mecha now.>
		Prompt5 <I can make it worth your while.>
		Prompt6 <It wouldn't be healthy to refuse me.>
		CPrompt6 <if= V2 0 Accept>
		Prompt7 <Nothing, I'm just saying...>
		Prompt8 <I can offer cash. [give $ \VAL &Bribe ]>
		CPrompt8 <ifG PC$ &Bribe Accept>
		Prompt9 <I can show you a good time.>
		CPrompt9 <ifChatNPCSexy ifFlirtation %sktar_hard% Accept>
		Prompt10 <I've got nothing...>

		Metascene 2
		% If the key NPC dies, resolve the quest as a failure.
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
	end
	inv
		NPC Police Officer
		SetFaction 9
	end


Content
	name <Meks in Hock>
	desc <E1 is in debt to E2; the mecha is in hock>
	requires <*:MechaHeld>

	% E1 is the character who owns the mecha.
	% E2 is the storekeeper holding the mecha.
	Element2 <Prefab>
	Scene2 <Building Garage>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	sub
		Persona 1
		% This conversation thread will activate after the PC asks E2 about the mecha.
		greeting <if= QuestStatus %qid% %id% else %pop% ifNPCOK %2% else .%id%_GoE2Died if= NPCVar %2% 1 1 else %pop%  NewChat Say %id%02 AddChat %id%01 AddChat %id%02>
		.%id%_GoE2Died <NewChat Say %id%01 SetQuestStatus %qid% -2>
		result%id%01 <NewChat Say %id%03 AddChat %id%03 AddCHat %id%04 AddChat %id%05>
		result%id%02 <NewChat Say %id%04>
		result%id%03 <NewChat Say %id%05 NPCVar= %2% 1 2>
		result%id%04 <NewChat Say %id%06>
		result%id%05 <NewChat Say %id%07 NPCVar= %2% 1 2>
		Msg%id%01 <I'll never get my mecha back from %name2% now...>
		Msg%id%02 <I wish there was some way to get my mecha back from %name2%...>
		Msg%id%03 <I can't afford to pay back everything I owe, and with my mecha gone I don't have any way to make more money. It's a tough situation.>
		Msg%id%04 <Please do that. Any help would be greatly appreciated.>
		Msg%id%05 <I could do that, if %name2% would agree to return my mecha. That way \SPR %2% gets paid and I still get to eat.>
		Msg%id%06 <That's so true.>
		Msg%id%07 <You're right... the only way to get past this is to bite the bullet and pay up. It's going to be painful in the short term, but really there's nothing else I can do. Tell %name2% I'll pay as much as I can now and the rest later.>
		Prompt%id%01 <Why don't you just pay the money you owe?>
		Prompt%id%02 <I'll talk with \OPR %2% , see if I can help.>
		Prompt%id%03 <Maybe you can work out an installment plan.>
		CPrompt%id%03 <ifConversation %sktar% Accept>
		Prompt%id%04 <That's a real problem.>
		Prompt%id%05 <Face responsibility and pay what you owe.>
		CPrompt%id%05 <ifIntimidation %sktar% accept>

		Persona 2
		% V1 = Amount owed
		% NPCVar %2% 1 = Communication variable. When =1, PC has talked to E2. WHen =2, E1 agreed to pay installments.
		rumor%id% <%name2% is holding %name1%'s mecha until all debts are paid.>
		greeting <if= QuestStatus %qid% %id% else GoNoQuest NewChat Say 1 AddChat 1 AddChat 2>
		GoE1Dead <NewChat Say 2 SetQuestStatus %qid% -2>
		result1 <ifNPCOK %1% else GoE1Dead NewChat Say 3 AddChat 3 AddChat 4 AddChat 5 AddCHat 6 if= NPCVar %2% 1 0 NPCVar= %2% 1 1 V= 1 Threat %threat% 2 QuestMemo %qid% 4>
		result2 <Goto GoShop>
		result3 <NewChat Say 6 AddChat 7 AddChat 8 AddChat 4>
		result4 <ifConversation %sktar_hard% else GoR4Fail NewChat Say 7 SetQuestStatus %qid% -1>
		GoR4Fail <NewChat Say 8>
		result5 <NewChat Say 5 SetQuestStatus %qid% -1>
		result6 <NewChat Say 9 AddChat 9 AddChat 8 AddCHat 7>
		result7 <ifConversation %sktar% else GoR7Fail NewChat Say 11 AddChat 10 AddChat 11>
		GoR7Fail <NewChat Say 10>
		result8 <NewChat Say 12>
		result9 <ifFlirtation %sktar_hard% else GoR9Fail NewChat Say 13 SetQuestSTatus %qid% -1 AddReact d12 AddHeroic -1>
		GoR9Fail <NewChat Say 14>
		result10 <NewChat Say 15 Cash+ - V1 SetQuestStatus %qid% -1>
		result11 <NewChat Say 16>
		*GoNoQuest <*NiceToMeetYou GoShop>
		*GoShop <*SHOP_Mecha GoBye>
		*GoBye <*GoodBye>
		Msg1 <Yes, may I help you?>
		Msg2 <I don't think %name1% is going to be needing a mecha any time soon... It's a shame that I'll never get the money I'm owed.>
		Msg3 <%name1% owes me money, quite a bit of it in fact... I'll be holding onto \PPR %1% mecha until that money is repaid in full.>
		Msg4 <%name2% is holding %name1%'s mecha until \SPR %1% can pay off \PPR %1% debt.>
		Msg5 <Is that so? As soon as I've recieved the first installment, I will return \PPR %1% mecha.>
		Msg6 <I'm owed $ \VAR V1 in total. If only %name1% would pay some installments, I wouldn't have needed to impound \ppr %1% mecha.>
		Msg7 <Hmm. You're right, I know. %name1% won't be able to pay me back so long as \SPR %1% doesn't have the money. Alright, you can go tell %name1% that \SPR %1% can have \PPR %1% mecha back.>
		Msg8 <That's hardly my problem, is it? Either I get that money or %name1%'s mecha stays where it is.>
		Msg9 <Look, I'm running a business here. I can't just give it back until I've been paid.>
		Msg10 <No way. This is %name1%'s debt, and \SPR %1% 's going to pay it off.>
		Msg11 <Sure, you could do that... I mean, you really want to?>
		Msg12 <You do that.>
		Msg13 <For you? Well, it's a lot of money, but... yeah, I could do that for you.>
		Msg14 <No. Definitely not.>
		Msg15 <Ah, it's been a pleasure doing business with you. You can go tell %name1% that \SPR %1% can pick up \PPR %1% mecha anytime \SPR %1% wants.>
		Msg16 <I didn't think so. Now, if you'll excuse me, I have other customer to attend to.>
		Prompt1 <I'd like to talk about %name1%'s mecha.>
		Prompt2 <I'd like to see what you have for sale.>
		Prompt3 <How much does \SPR %1% owe you?>
		Prompt4 <%name1% can't earn money without a mecha.>
		Prompt5 <%name1% has agreed to pay the money.>
		CPrompt5 <if= NPCVar %2% 1 2 Accept>
		Prompt6 <Why can't you just give the mecha back?>
		CPrompt6 <ifChatNPCSexy ifFlirtation %sktar_hard% Accept>
		Prompt7 <How about I pay off the debt myself?>
		CPrompt7 <ifG PC$ V1 Accept>
		Prompt8 <I'll go talk with %name1% about that.>
		Prompt9 <Couldn't you do it for me?>
		Prompt10 <Yes, I do. [pay $ \VAL V1 ]>
		Prompt11 <No, not really.>

		Metascene 2
		% If the key NPC dies, resolve the quest as a failure.
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <.#...+#.......1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end

	end
	inv
		NPC Shopkeeper
	end


%%
%% *ProveMyself Content
%%
%% The key NPC needs to prove himself, somehow. This may be to make up for
%% some past deed, as a rite of passage, or whatever. In any case the deed
%% done will be risky.
%%
%% To start this subquest, the calling quest should set the QuestID.
%% This quest should end with -1 if the NPC was successful, or -2 if not.
%% It can farm out the QID-setting to a "topper" subquest, if needed.
%%
%% PARAM1: The NPC who will be doing the proving.

Content
	name <Prove Myself vs Derelict Spaceship>
	% A derelict spaceship has been found. A local professor has offered a reward
	% for the first person to locate its cargo hold. The PC can take part in the mission,
	% but will be beaten there by E1... at which point in time the PC can allow him to
	% claim the prize, or fight him and take the prize himself.
	requires <*ProveMyself L5PAT ~"Safe">

	% E1 is the character who must prove himself
	% E2 is the professor offering the quest
	% E3 is the derelect spaceship entrance
	% E4 is the spaceship dungeon
	% E5 is the final goal room
	Element2 <Prefab>
	Scene2 <Building Public>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <Prefab>
	Scene3 <Environs>

	Scene4 <New *Dungeon.Derelict>

	Scene5 <New *CargoHold>

	sub
		Persona 1
		% v%id%01 = Have gotten win speech already
		greeting <if# QuestStatus %qid% -1 else .%id%_GoWinQuest if= QuestStatus %qid% %id% else %pop%  if= SceneVar %s5% 12 1 else .%id%_GoPreDungeon Goto .%id%_GoInDungeon>
		.%id%_GoWinQuest <if= v%id%01 0 else .%id%_GoChat NewChat Say %id%01 v= %id%01 1>
		.%id%_GoPreDungeon <NewChat Say %id%02>
		.%id%_GoInDungeon <if= SceneID %s5% else .%id%_GoBusy IfChatNPCSurrendered else .%id%_GoFirstDungeon AddHeroic 2 Goto .%id%_GoSurrender>
		.%id%_GoFirstDungeon <NewChat Say %id%03 AddChat %id%01 AddChat %id%02>
		*.%id%_GoBusy <*IAmBusy&OnPhone>
		*.%id%_GoChat <*MISC_CHATTER>
		result%id%01 <NewChat Say %id%04   Print %id%07  AddLawful 1 AddRenown -1 SetQuestStatus %qid% -1  MoveAndPacifyNPC %1% %s1%  PCAlly %1%>
		result%id%02 <AddLawful -5 ifIntimidation %sktar_hard% else .%id%_GoStartFight Goto .%id%_GoSurrender>
		.%id%_GoSurrender <EndChat Say %id%05   Print %id%07  SetQuestStatus %qid% -2  MoveAndPacifyNPC %1% %s1%  PCEnemy %1%  L= 13 1  UnSurrender %1%>
		.%id%_GoStartFight <AddHeroic -3 TeamAttack 2 EndChat  Say %id%06  SetQuestStatus %qid% -2  PCEnemy %1%>
		Msg%id%01 <Now that I've completed my first real mission, I feel that I'm ready to be a cavalier!>
		Msg%id%02 <What I really need to prove myself is a real mission. The trouble is, until I've completed a real mission, nobody's willing to hire me...>
		Msg%id%03 <I did it! I found the cargo hold of this spaceship for Professor %name2%, and it seems like I'm the first person here! I can't wait to get back to town and collect my reward.>
		Msg%id%04 <Thanks, \PC . I'll see you back in town.>
		Msg%id%05 <Alright, I see how this is going... I'll let you claim you were here first. The reward's not worth dying over.>
		Msg%id%06 <Do you expect me to give up on this without a fight? I think I can take you...>
		Msg%id%07 <%name1% leaves the area.>
		Prompt%id%01 <Congratulations, I concede victory to you.>
		Prompt%id%01_1 <Congratulations, you earned it.>
		Prompt%id%02 <Too bad you aren't getting out alive.>

		Persona 2
		rumor%id% <%name2% has discovered an old wrecked spaceship, and is offering a reward to whoever explores it.>
		% V1 = Have collected reward
		% V2 = Have initialized quest
		% V3 = E1 dead at start of mission
		greeting <if# SceneVar %s5% 13 0 else GoCheckQuest if= V1 0 else GoChat NewChat Say 1 AddChat 1 AddChat 2>
		GoCheckQuest <if= QuestStatus %qid% -1 else GoCheckBegin if= V1 0 else GoChat NewChat Say 4 v= 1 1>
		GoCheckBegin <if= QuestStatus %qid% %id% else GoNoQuest if= V2 0 else GoRemindQuest NewChat Say 5 QMemo %qid% 6  V= 2 1 Goto GoStartQuest>
		GoRemindQuest <NewChat say 7>
		GoStartQuest <SceneVar= %s5% 12 1  MoveNPC %1% %5%  SetNPCTeam %1% 2  SetQEncActive %3% ifNPCDead %1% V= 3 1>
		*GoNoQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 CashPrize Reward %threat% 100 V= 1 1>
		result2 <NewChat Say 3  AddHeroic 1 AddRenown -5   V= 1 1>
		Msg1 <Welcome back. Have you discovered anything of interest?>
		Msg2 <Ah, that's good to hear. Now I have a good place to begin my expedition. Here's a small reward for your trouble.>
		Msg3 <Is that so? In that case, I'll have to prepare a reward.>
		Msg4 <Did you hear? A young adventurer named %name1% has located the cargo hold. My expedition can start soon.>
		Msg5 <I've discovered an old space wreck not far from here. To find out about it, I really need to have a look at its cargo hold. I'm offering a reward for the first person to explore this spaceship and tell me exactly where it is.>
		Msg6 <Professor %name2% is offering a prize for the first person to locate the cargo hold of an old wreck \SPR %2% discovered.>
		Msg7 <I'm still waiting for someone to locate the cargo hold of that derelict spaceship I discovered.>
		Prompt1 <I've located the cargo hold.>
		Prompt2 <%name1% found the cargo hold.>
		CPrompt2 <if= V3 0 Accept>

		MetaScene 4
		sub
			room
			minimap <............2............>
			special <SHAREDPALETTE>
			sub
				Trapdoor
				desig <ENTRANCE %s5%>
				MiniMapComponent 2
			end
		end

		MetaScene 5
		% V11 = Quest initialization counter
		% V12 = SubQuest Status; if V12=1, quest has been initialized.
		% V13 = If nonzero, PC counts as first to cargo hold
		start <if= v11 0 else %pop%  V= 11 1  ifNPCOK %1% else GoStartNPCDead ForceChat %1% Goto %pop%>
		GoStartNPCDead <Print 11  V= 13 1  SetQuestStatus %qid% -2>
		SURRENDER%1% <if= V11 1 SetQuestStatus %qid% %id% Forcechat %1%>
		FAINT%1% <if= V11 1 V= 13 1 SetQuestStatus %qid% -2>
		% If the quest status hasn't been set by the time the scene is exited, do it then.
		End <if= V11 1   V= 11 2   ifNPCOK %1% else GoNPCDead  ifTeamHostile 2 else GoNoHostile  MoveAndPacifyNPC %1% %s1%  SetQuestStatus %qid% -2>
		GoNoHostile <if= QuestStatus %qid% %id%  MoveAndPacifyNPC %1% %s1%  SetQuestStatus %qid% -1>
		GoNPCDead <if= QuestStatus %qid% %id%  SetQuestStatus %qid% -2 V= 13 1>
		Msg11 <You enter the cargo hold. This must be the place that %name2% was searching for.>
		sub
			room
			minimap <......###..#2#...........>
			special <SHAREDPALETTE>
			sub
				Elevator
				name <Airlock>
				MiniMapComponent 2
				use <ifYesNo 1 2 3 Print 4 Exit %s3%>
				Msg1 <This airlock opens to the outside of the spaceship.>
				Msg2 <Leave the derelict.>
				Msg3 <Stay here for now.>
				Msg4 <You leave the derelict.>
			end
		end
	end
	inv
		NPC Professor

		STC QUEST-MAPMARKER-STATIONARY
		name <Derelict Spaceship>
		desig <Entrance %s4%>
	end

Content
	name <Prove Myself Vs Pirates>
	requires <*ProveMyself Spinner -1:CRIHN ~"Dangerous">

	% E1 is the character who must prove himself.
	% E2 is the character offering a bounty on pirates
	% E3 is the pirate combat zone
	% E4 is the entrance to the combat zone
	% E5 is the black market unlocked by this quest
	Element2 <Prefab>
	Scene2 <Building Public>
	Team2 <Citizens>
	TeamData2 <Ally Pass>

	Scene3 <New *BLANK.Space.Mecha>

	Element4 <Prefab>
	Scene4 <Space Environs>

	Element5 <Prefab>
	Scene5 <New *BlackMarket>
	Team5 <Citizens>
	TeamData5 <Ally Pass>

	Quest1 <*ThankYou 1>

	sub
		Persona 1
		greeting <if= QuestStatus %qid% %id% else %pop% if= SceneVar %3% 1 2 else .%id%_GoCheckBattle NewChat Say %id%01 AddChat %id%01>
		.%id%_GoCheckBattle <if= SceneVar %3% 1 1 else .%id%_GoCheckBusy EndChat Say %id%03 AddChat %id%02 AddChat %id%03>
		.%id%_GoCheckBusy <if# SceneVar %3% 2 0 else .%id%_GoFirst NewChat Say %id%02>
		*.%id%_GoFirst <*IMustProveMyself>
		Result%id%01 <Goto .%id1%_GoInit>
		*Result%id%02 <*LetsDefeatThem>
		Result%id%03 <EndChat Say %id%04>
		Msg%id%01 <We did it! I've survived a real mecha fight. I feel like I could take on the world!>
		Msg%id%02 <I'm busy right now.>
		Msg%id%03 <\PC ! Look, I've found the pirates that %name2% was looking for!>
		Msg%id%04 <No way. I have to prove myself... if I leave now, will I be running forever?>
		Prompt%id%01 <Don't get too excited.>
		Prompt%id%02 <I'll help you out.>
		Prompt%id%02_1 <I'll back you up.>
		Prompt%id%03 <Get out of here; it's too dangerous.>

		Persona 2
		rumor%id% <%name2% needs a mecha pilot to fight some pirates.>
		% This bounty hunter will give the PC a regular mission test, but it
		% doesn't matter whether the PC passes or not: Merely talking to this
		% character will set the plot into motion.
		% V1 = Initialization Counter
		% V2 = Have offered mission
		greeting <if= V1 0 else GoGreet2 V= 1 1 MoveNPC %1% %3% SetNPCTeam %1% 3 SceneVar= %3% 2 1 SetQEncActive %4% Goto GoGreet2>
		GoGreet2 <if= V2 1 else GoCheckOffer if= SceneVar %3% 1 2 else GoCheckLoss NewChat Say %id%01 CashPrize Reward %threat% 100 V= 2 2>
		GoCheckLoss <if= SceneVar %3% 1 3 else GoMissionReminder NewChat Say %id%02 V= 2 2>
		GoMissionReminder <NewChat Say %id%03>
		GoCheckOffer <if= SceneVar %3% 1 0 else GoAlreadyFought if= QuestStatus %qid% %id% else GoNoQuest Goto GoCheckAuto>
		GoAlreadyFought <V= 2 2 Goto GoNoQuest>
		*GoCheckAuto <*AutoMissionTest&Mecha GoMissionBriefing GoRejectMission GoCheckSkill na 8>
		*GoCheckSkill <*GenericMissionTest&Mecha  GoMissionBriefing na GoRejectMission na 8>
		*GoRejectMission <*RejectMission na>
		GoMissionBriefing <NewChat Say %id%04 QMemo %qid% %id%05>
		*GoNoQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		Msg%id%01 <The pirates have been defeated. Here's the reward you earned.>
		Msg%id%02 <You failed to defeat the pirates... now that they've moved camp, it's going to take ages to find them again.>
		Msg%id%03 <There are pirates on the loose in \SCENE %s4% . I'm still waiting for someone to take me up on the reward.>
		Msg%id%04 <There's a group of pirates hiding in \SCENE %s4% . If you could defeat them, there'd be a reward in it for you.>
		Msg%id%05 <%name2% asked you to defeat some pirates in \SCENE %s4% .>

		MetaScene 3
		% V1 = Initialization Counter
		% The first time the PC enters this scene, it should be for the battle.
		% Otherwise nothing will happen.
		%  1: Battle in progress
		%  2: PC has won against pirates
		%  3: PC has lost against pirates
		% V2 = Trigger Set
		% The NPC has been moved, everything should be ready for combat.
		start <if= V1 0 else GoBeenBefore V= 1 1 ifNPCInPlay %1% ForceChat %1%>
		GoBeenBefore <>
		nu1 <if= t1 0 Return if= L1 1 L= 1 3 SetQuestStatus %qid% -2 Goto GoMoveNPC>
		nu2 <if= t2 0 if= L1 1 L= 1 2 ifNPCInPlay %1% else GoNotInPlay  ForceChat %1% Goto GoMoveNPC>
		GoNotInPlay <SetQuestStatus %qid% -2 Goto GoMoveNPC>
		GoMoveNPC <ifNPCOK %1% MoveNPC %1% %s1%>
		sub
			Team 1
			name <Player Team>
			SetEnemy 2
			SetAlly 3

			Team 2
			name <Enemy Team>
			SetEnemy 1 3
			SetFaction 8
			Deploy <if= L1 0 WMecha 2 %threat% 220>

			Team 3
			name <E3 Team>
			SetAlly 1
			SetEnemy 2
		end
		inv
			stc DERELICT-1
			name <Shady Microstation>
			desig <Entrance %s5%>
		end

		Persona 5
		*greeting <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
	end
	inv
		NPC Bounty Hunter
		CharDesc Lawful

		STC QUEST-ENCOUNTER-WANDER
		name <Red Rover Pirates>
		desig <entrance %s3%>
		% After fighting the pirates, this encounter becomes perminantly visible.
		update <ifThisQEncActive else GoHide  SetStat STAT_MetaVisibility 0  if# SceneVar %3% 1 0 else GoSetOrders ShowEncounter Goto GoSetOrders>
		%% This encounter will only attack before the PC fights the pirates
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifG 1 SceneVar %3% 1   ifUStealth 15 else GoAutoAttack ifYesNo -3 -4 -5 else GoAvoidAttack Goto GoAutoAttack>

		NPC Pirate
		SetFaction 8
	end

%%
%% *:StolenMaterials Content
%%		&ConstructionSite = The materials stolen from a construction site
%%
%% Several tons of crap have been stolen from E1. The PC will be sent to get
%% them back, or may possibly collude with whoever stole them, or may even
%% steal a bit himself... Set the QuestID to -1 if the goods are returned to
%% their rightful owners, or to -2 if the materials are perminantly removed.
%% To start this subquest, the calling quest should set its QuestId to its
%% component ID.
%%
%% Param1: The boss NPC who sent the PC to find the problem

Content
	name <Never Received It>
	desc <The stuff that was supposedly stolen actually never showed up in the first place.>
	requires <*:StolenMaterials -"EARTH">

	% Element1 is the NPC who lost the materials
	% Element2 is the NPC who runs the shipping service
	% Element3 is the wonky robot that's been collecting stuff
	% Element4 is the Sentinel that's behind it all
	% Element5 is an artifact that came from Earth with the Sentinel
	Element2 <Prefab>
	Scene2 <Public SpacePort>
	Team2 <Citizens>
	TeamData2 <Ally Pass>

	Element3 <Prefab>
	Scene3 <New *BLANK.Basement>
	Team3 <Enemies>
	TeamData3 <SD Enemy>

	Element4 <Prefab>
	Scene4 <New *BLANK.BoxBasement>
	Team4 <Enemies>
	TeamData4 <SD Enemy>

	Element5 <Artifact Item Prezero>
	Scene5 <Grab 4>

	sub
		Persona 2
		rumor%id% <%name2% at the spaceport handles all deliveries for %name1%.>
		% V1 = Have explained situation and opened first lock
		greeting <if= QuestStatus %qid% %id% else GoNonQuest if= V1 1 else GoFirstTime NewChat Say 8 AddChat 5 AddChat 6>
		*GoNonQuest <*NiceToMeetYou GoExpo>
		GoExpo <NewChat Say 1>
		GoFirstTime <NewChat Say 2 AddChat 1 AddChat 2>
		GoOpenFirstDoor <QMemo %qid% 4 SceneVar= %s3% 1 1 V= 1 1 ItemLevel %3% %threat%>
		Result1 <NewChat Say 3 AddChat 3 Goto GoOpenFirstDoor>
		Result2 <NewChat Say 5 AddChat 3 Goto GoOpenFirstDoor>
		Result3 <NewChat Say 6 AddChat 4>
		Result4 <NewChat Say 7>
		Result5 <NewChat Say 9>
		Result6 <NewChat Say 10 QMemo %prev_qid% 11 AddChat 7 SetQuestStatus %qid% -1 SceneVar= %s4% 1 1>
		Result7 <NewChat Say 12>
		Msg1_1 <I handle deliveries to all the businesses around here. With my team of robotic loaders I've never missed a shipment.>
		CMsg1_1 <if# QuestStatus %qid% -1 Accept>
		Msg1_2 <I handle deliveries to all the businesses around here. My brand new team of state of the art robots should cut shipment time by half!>
		CMsg1_2 <if= QuestStatus %qid% -1 Accept>
		Msg2 <This is terrible... my reputation is going to be ruined...>
		Msg3 <All the shipments I was supposed to be making, including %name1%'s, never arrived at their destination! I found out that Sparky, my supposedly reliable delivery droid, has been diverting packages to the sub-basement.>
		Msg4 <%name2% revealed that %name1%'s supplies have been diverted by a rogue droid named Sparky. You can find this robot in the basement of \SCENE %s2% .>
		Msg5 <I know why the parts are missing: they were never even delivered! Sparky, my supposedly reliable delivery droid, has been diverting packages to the sub-basement.>
		Msg6 <What's worse is that he's apparently coerced my other robots into some kind of cybernetic rebellion. I don't understand what's going on- none of them are smart enough to have gone frankenstein.>
		Msg7 <Maybe you can. If you can disable Sparky, the other robots won't be too much of a problem. I'll open the hatch for you. Come see me when you're done.>
		Msg8 <So, have you disabled the robots yet?>
		Msg9 <Well, I certainly hope that you succeed. I'm a bit nervous about what they're planning down there.>
		Msg10 <It's a crying shame to put down a good droid like Sparky, but what else could have been done? You can go let %name1% know that \PPR %1% parts will be on the way shortly.>
		Msg11 <You solved %name2%'s robot problem, so %name1%'s parts will soon be delivered.>
		Msg12 <The trouble all started when I got an order of salvage parts from Earth. I don't know if that had anything to do with the robots going crazy, but just to be safe I'm going to keep away from the sub-basement for a while.>
		Prompt1 <Calm down, tell me what happened.>
		Prompt2 <%name1% sent me to find \PPR %1% missing parts.>
		Prompt3 <That's bad.>
		Prompt4 <Maybe I can help.>
		Prompt5 <Not yet.>
		Prompt6 <Sparky has been deactivated.>
		CPrompt6 <ifItemDestroyed %3% Accept>
		Prompt7 <Any idea why the robots went nuts?>

		MetaScene 2
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <2...####+#3.1.......##+##>
			special <SHAREDPALETTE>
			desig <HOME>
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s3%>
				use <if= SceneVar %s3% 1 1 else GoLocked Print 1 Exit StatVal 4>
				Clue32 <Print 101>
				Msg1 <You go down the trapdoor.>
				Msg101 <You see no way to pick this lock. The trapdoor appears to have been barred from the other side.>

				STC Computer-1
				MiniMapComponent 3
				PDir 1
				Use <Print 1>
				Clue32 <Print 2>
				Msg1 <You examine the computer. It looks like someone's been playing solitaire all day.>
				Msg2 <Examining the access records, it seems that someone tried to hack into this computer. The hostile signal originated from within this building.>
			end
		end

		MetaScene 3
		% L1 = E2 has opened the door to this level
		% L2 = Monster generation counter
		Start <Print 1 if= L2 0 if= QuestStatus %qid% %id% L= 2 1 WMonster 2 %threat%>
		Msg1 <You enter %name2%'s warehouse.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Enemies>
			SetEnemy 1
			type <Robot>

			Room
			minimap <........1...2............>
			desig <HOME>
			special <SHAREDPALETTE>
			sub
				Trapdoor
				desig <ENTRANCE %s4%>
				MiniMapComponent 2
				use <if= SceneVar %s4% 1 1 else GoLocked Print 1 Exit StatVal 4>
				Clue32 <Print 101>
				Msg1 <You go down the trapdoor.>
				Msg101 <You see no way to pick this lock. The trapdoor appears to have been barred from the other side.>
			end
		end

		MetaScene 4
		% L1 = E2 has opened the door to this level
		% L2 = have been here before
		Start <if= V2 0 else GoBeenBefore V= 2 1 Print 1>
		GoBeenBefore <Print 2>
		Msg1 <You enter the sub-basement of %name2%'s warehouse. An ominous grinding sound can be heard nearby.>
		Msg2 <You enter the sub-basement of %name2%'s warehouse.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Enemies>
			SetEnemy 1
		end
	end
	inv
		NPC Trucker

		Monster GuardBot
		name <Sparky>

		Monster Sentinel
	end

Content
	name <Monsters Ate It>
	requires <*:StolenMaterials &ConstructionSite>

	% Element1 is the NPC who lost the materials
	% Element2 is the coworker who knows what's happening
	% Element3 is the scene full of monsters

	Element2 <Prefab>
	Scene2 <Grab 1>
	Team2 <Citizens>
	TeamData2 <Pass>

	Scene3 <New *ExterminationBasement>

	sub
		Persona 2
		% V1 = Have given info/Have given thanks
		rumor%id% <%name2% knows something about %name1%'s construction problem.>
		greeting <if= QuestStatus %qid% -1 else GoCheckMission if= V1 1 else GoJustChat V= 1 2 NewChat Say 5 AddReact d10>
		GoCheckMission <if= QuestStatus %qid% %id% else GoJustChat if= V1 1 else GoFirstTime >
		*GoFirstTime <*IHaveInformation GoPresentInfo %1%>
		GoPresentInfo <NewChat Say 1 QMemo %qid% 6 V= 1 1 AddChat 1 AddChat 2>
		*GoJustChat <*MISC_CHATTER>

		Result1 <NewChat Say 2 AddChat 3>
		Result2 <NewChat Say 3 AddChat 3>
		result3 <NewChat Say 4>

		Msg1 <The materials that disappeared from the job site? They weren't stolen... something ate them.>
		Msg2 <I've seen them... I told some of the other workers, they thought I'd been drinking, but they're definitely there.>
		Msg3 <I can't exactly say what they were, but I expect some kind of a mutated beaver crossbred with a space fungus.>
		Msg4 <I've seen them all around, but the most common place is in \SCENE %s3% . The way down is over there.>
		Msg5 <I haven't seen any of those creatures in a while... I guess you must have cleared them up.>
		Msg6 <%name2% told you that creatures in the sub-basement are eating %name1%'s supplies.>

		Prompt1 <How do you know that?>
		Prompt2 <What kind of creature would eat plastic sheeting?>
		Prompt3 <Where can I find these creatures?>

		Metascene 2
		start <if= SceneVar %s3% 1 1 else %pop% SetQuestStatus %qid% -1 Goto %pop%>
		sub
			room
			minimap <......###..#2#........1..>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Elevator
				name <Sub-Basement>
				desig <Entrance %s3%>
				MiniMapComponent 2
				use <if= QuestStatus %qid% %id% else GoLocked Print 1 Exit Destination>
				GoLocked <Print 2>
				Msg2 <There's nothing interesting in here.>
			end
		end
	end
	inv
		NPC Construction Worker
	end

Content
	name <Pirates Took It>
	requires <*:StolenMaterials Spinner>

	% Element1 is the NPC who lost the materials
	% Element2 is a crooked shopkeeper who's been selling them
	% Element3 is the asteroid microstation where the materials are being stored
	% Element4 is the entrance to the scene, activated by E2

	element2 <Prefab>
	scene2 <Building Public Mall>
	Team2 <Citizens>
	TeamData2 <Pass>

	element3 <Prefab>
	scene3 <New *BLANK.Space.Mecha>

	element4 <Prefab>
	scene4 <Space Environs>

	sub
		Persona 1
		% V%id%01 = Have told about pirate base
		greeting <if= QuestStatus %qid% %id% else %pop% ifQEncActive %4% else %pop% if= V%id%01 0 else %pop% NewChat say %id%01 AddChat %id%01>
		result%id%01 <NewChat Say %id%02 V= %id%01 1>
		msg%id%01 <Any news on those missing supplies, yet?>
		msg%id%01_1 <You've been investigating this matter for a while. Have you found any leads on my missing supplies?>
		Cmsg%id%01_1 <ifNPCSociable Accept>
		msg%id%01_2 <Do you have anything to report?>
		Cmsg%id%01_2 <ifNPCShy Accept>
		msg%id%01_3 <So, have you found anything about the stuff I lost yet?>
		Cmsg%id%01_3 <ifNPCEasygoing Accept>
		msg%id%01_4 <Have you found my things yet? It's vitally important that they be returned at once...>
		Cmsg%id%01_4 <ifNPCPassionate Accept>
		msg%id%01_5 <I'm kind of enjoying my unintentional vacation, but have you learned anything about the goods that disappeared yet?>
		Cmsg%id%01_5 <ifNPCCheerful Accept>
		msg%id%01_6 <Have you found anything about the stuff I had stolen yet? If the materials aren't returned soon, I'll be ruined.>
		Cmsg%id%01_6 <ifNPCMelancholy Accept>
		msg%id%02 <In that case, you should try to find where they've hidden their loot and see if you can get it back.>
		msg%id%02_1 <I don't suppose these pirates will be in any mood to negotiate... and in any event, doing so would only embolden them to make further attacks. See if you can locate their base, and take back my property by any means nessecary.>
		Cmsg%id%02_1 <ifNPCSociable Accept>
		msg%id%02_2 <Locate these pilots and recover our property.>
		Cmsg%id%02_2 <ifNPCShy Accept>
		msg%id%02_3 <Well, it seems to me that this is a step in the right direction. All you have to do now is find the pirates and take my things back from them.>
		Cmsg%id%02_3 <ifNPCEasygoing Accept>
		msg%id%02_4 <In that case, why aren't you out looking for those pirates? Hurry, this might be the only chance we have to recover my supplies!>
		Cmsg%id%02_4 <ifNPCPassionate Accept>
		msg%id%02_5 <Great work, \PC ! Now all you have to do is locate these pirates and win the supplies back from them. It shouldn't be too tough, should it?>
		Cmsg%id%02_5 <ifNPCCheerful Accept>
		msg%id%02_6 <If only we could find where the pirates are keeping their loot, we might be able to get some of it back. I suppose it's too late for any of that already...>
		Cmsg%id%02_6 <ifNPCMelancholy Accept>
		Prompt%id%01 <I heard they've been stolen by pirates.>

		Persona 2
		rumor%id% <%name2% has been selling off mecha parts and repair supplies cheap.>
		% V1 = Have revealed the pirate base
		% V2 = Have tried to bluff the shopkeeper
		greeting <if= QuestSTatus %qid% %id% else GoGreet if= V1 0 else GoGreet NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoGreet <*NiceToMeetYou GoOpenShop>
		*GoOpenShop <*SHOP_MECHA GoGoodbye>
		*GoGoodbye <*GOODBYE>
		GoRevealBase <V= 1 1 SetQEncActive %4% QMemo %qid% 6>
		result1 <NewChat Say 3 AddChat 4 AddChat 5>
		result2 <Goto GoOpenShop>
		result3 <EndChat Say 2 Shop .wares Goto GoGoodbye>
		.wares <TOOL MEXTRA CONTRABAND>
		result4 <NewChat Say 4 AddChat 6 AddChat 7>
		result5 <V= 2 1 ifConversation SkillTar %threat% else GoR5Fail NewChat Say 5 Goto GoRevealBase>
		GoR5Fail <NewChat Say 7>
		result6 <NewChat Say 8 Goto GoRevealBase>
		result7 <NewChat Say 9 AddChat 8 AddChat 9>
		result8 <NewChat Say 10 SetQuestStatus %qid% -2 AddLawful -5 RandomMecha .mekfac %threat%>
		.mekfac <GENERAL>
		result9 <Goto result6>
		Msg1 <Hey, can I interest you in some great parts at a low low price?>
		Msg2 <Take a look through, I don't even know what all of this stuff is... If you notice the serial numbers filed off of anything that's just because it's, uh, a factory second. That's it.>
		Msg3 <I've got connections, you know. One of my associates recently came into a large quantity of, er, swag... I've been helping him find its way to market.>
		Msg4 <I didn't steal anything! And if I happen to be selling stolen goods then that's hardly my fault! Look, I'm sure we can come to some kind of deal...>
		Msg5 <I get this stuff from a pirate I know. They've got an asteroid base just outside the colony. You could try making a deal with him, but I should warn you that they aren't usually friendly to strangers.>
		Msg6 <%name2% told you there are pirates outside the spinner who may have stolen %name1%'s materials.>
		Msg7 <I'm afraid you're stuck, then. My supplier doesn't deal with the general public.>
		Msg8 <I don't know his name... There are some pirates who have an asteroid base just outside the station. I get this stuff from them... Now leave me alone, and don't cause any more headaches.>
		Msg9 <Well, look around. I've got a fortune of junk here. If you'll agree to keep hush about where it came from, I'll let you take your pick of the lot, no charge.>
		Msg10 <It's been a pleasure doing business with you.>
		Prompt1 <Where did you get all this junk?>
		Prompt2 <No, but I'd like to see what else you have.>
		Prompt3 <Sure, let me take a look.>
		Prompt4 <You stole all this from %name1%, didn't you?>
		Prompt5 <I need to buy more than you have in stock.>
		CPrompt5 <if= V2 0 Accept>
		Prompt6 <Tell me who your supplier is.>
		Prompt7 <What kind of deal are you thinking about?>
		Prompt8 <Sounds good. I'll do that.>
		Prompt9 <No deal. Tell me who your supplier is.>

		MetaScene 2
		% If the NPC is killed, automatically reveal the asteroid base. This is
		% kind of a strange thing to do but it'll prevent quest lockup.
		start <ifNPCDead %2% else %pop% if= QuestStatus %qid% %id% else %pop% SetQEncActive %4% Goto %pop%>
		sub
			room
			minimap <..............1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end

		MetaScene 3
		% The blank space scene with the asteroid base.
		%% Because it's a blank, this metascene holds all the important stuff.
		% L1 - Initialization counter
		% L2 = Victory Counter
		% L3 = Attack recharge timer for encounter
		start <if= L1 0 else GoBeenBefore Print 1 L= 1 1>
		GoBeenBefore <if= L2 0 Print 2>

		nu1 <if= T1 0 Return  if= L2 0    AddRenown -1>
		nu2 <if= T2 0         if= L2 0    L= 2 1   AddRenown 1  Print 3  XPV 100   Salvage   SetQuestStatus %qid% -1>

		Msg1 <You spot the asteroid base that %name2% must have been talking about.>
		Msg2 <The pirates have regrouped, and challenge you again.>
		Msg3 <You've defeated all the pirates, and can return %name1%'s materials now.>

		sub
			Team 1
			name <Player Team>
			ParaX 5
			ParaY 5
			SetEnemy 2

			Team 2
			name <Enemies>
			SetFaction 8
			Deploy <if= L2 0   WMecha 2 %threat% 100  L= 3 ComTime L+ 3 43200>
			ParaX 25
			ParaY 25
			SetEnemy 1
		end

	end
	inv
		NPC Shopkeeper
		chardesc Criminal
		skill 21 7

		STC DERELICT-1
		name <Asteroid Station>
		use <Print 1>
		Msg1 <This rudimentary station appears to contain all of %name1%'s building supplies.>

		STC QUEST-ENCOUNTER-STATIONARY
		name <Asteroid Base>
		desig <ENTRANCE %s3%>
		ATTACK <ifG StatVal STAT_MetaVisibility -1   if= SceneVar %s3% 2 0   ifG ComTime SceneVar %s3% 3    Alert 2 Exit Destination>
		Msg2 <You are attacked by pirates!>
		ENCOUNTER_HOSTILE
	end


%%
%% *SuccessGetItem Content
%%
%% The PC has retrieved E2 for NPC1.
%% The NPC who provided the task will now either reward the
%% PC or provide an additional task, or maybe both.
%% Note that by the time of this event E2 may very well be destroyed,
%% depending on what has come before.
%%
%% Param1: The character for whom the PC was working.
%%   Persona 1 must have a .%id%_GoInit script.
%% Param2: The item being given.
%%
%% SubTypes:
%% .giveitem	PC gave the item to the NPC
%% .keepitem	PC didn't give the item to the NPC
%% .NPCsecret	The item is potentially embarassing to the NPC

Content
	name <Good job, let's study>
	%% The PC will gain a new teacher.
	requires <*SuccessGetItem (1:MILIT|1:ADVEN)>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the item that was given.

	sub
		Persona 1
		Greeting <if= QuestStatus %qid% -1 else %pop% ifChatNPCArchEnemy else .%id%_GoNoEnemy SetQuestStatus %qid% -2 Goto .%id%_GoBlowOff>
		*.%id%_GoBlowOff <*BrushOff>
		.%id%_GoNoEnemy <NewChat Say %id%02 AddChat %id%01 AddChat %id%02>
		.%id%_GoInit <NewChat Say %id%01 SetQuestStatus %qid% -1 AddChat %id%03>
		*.%id%_GoBye <*GOODBYE>
		result%id%01 <EndChat Say %id%03 School .%id%_skills Goto .%id%_GoBye>
		.%id%_skills <1 2 3 4 5 6 7 8 9 10 11 12>
		result%id%02 <NewChat Say %id%04>
		result%id%03 <Goto result%id%01>
		Msg%id%01 <So this is the %name2%. You've proven yourself to be a great cavalier... If you want, I could train you to be even better.>
		Msg%id%02 <Hello \PC . Are you in the mood for some training?>
		Msg%id%03 <You're getting better all the time, but there are a few tricks you haven't picked up yet.>
		Msg%id%04 <Okay. I'll be here when you need me.>
		Prompt%id%01 <Absolutely. Let's go.>
		Prompt%id%02 <Not today, thanks.>
		Prompt%id%03 <Let's get started right away.>
	end

Content
	name <Basic Keep Item Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessGetItem .keepitem>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the item that was given.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 80 SetQuestStatus %qid% -1>
		Msg%id%01 <So this is it. Thanks for showing it to me... Here's some cash to help finance your next expedition, wherever that leads.>
	end

Content
	name <Basic Give Item Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessGetItem .giveitem>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the item that was given.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 200 SetQuestStatus %qid% -1>
		Msg%id%01 <So this is the %name2% . Here's a reward for bringing it to me.>
	end

%%
%% *SuccessRescue Content
%%
%% The PC has rescued a NPC. E1 is happy about this and will
%% provide a reward.
%%
%% Param1: The character who will provide the reward.
%%   Persona 1 must have a .%id%_GoInit script.
%% Param2: The character who was rescued.
%%

Content
	name <Basic Rescue Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessRescue>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the character who's been redeemed.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 300 SetQuestStatus %qid% -1>
		Msg%id%01 <Thank you so much for saving %name2%! Here, take this money as a reward.>
	end


%%
%% *SuccessVsChar Content
%%
%% The PC has completed a task of some kind against NPC2.
%% The NPC who provided the task will now either reward the
%% PC or provide an additional task, or maybe both.
%% Note that by the time of this event E2 may very well be dead,
%% depending on what has come before.
%%
%% Param1: The character for whom the PC was working.
%%   Persona 1 must have a .%id%_GoInit script.
%% Param2: The character against whom the PC was working.
%%
%% SubTypes:
%% .Evidence		The PC has brought evidence implicating E2 in a crime.
%% .ConJob		E2 has been conned by E1 and the PC.
%% .FoilPlot		The PC foiled a plot by E2 against E1.
%% .Capture		The PC has captured E2
%% .Redemption		The PC has saved E2 from himself, or whatnot
%% .Evasion		The NPC has evaded E2's plans

Content
	name <Redemption vs. Reward>
	%% The PC will be given a reward and that's it.
	%% It hardly seems like "vs" when the goal is redemption, does it?
	requires <*SuccessVsChar .Redemption>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the character who's been redeemed.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 200 SetQuestStatus %qid% -1>
		Msg%id%01 <%name2% is now on the right track. Thanks, \PC ... This wouldn't have been possible without you.>
	end

Content
	name <Capture vs. Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessVsChar .Capture>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the character who's been defeated.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 200 SetQuestStatus %qid% -1>
		Msg%id%01_10 <%name2% is now in custody. Thanks, \PC ... This wouldn't have been possible without you.>
		CMsg%id%01_10 <ifNPCOK %2% Accept>
		Msg%id%01_20 <%name2% won't be a threat any more. Thanks, \PC ... This wouldn't have been possible without you.>
	end

Content
	name <Evidence vs. Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessVsChar .Evidence>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the character who's been defeated.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 200 SetQuestStatus %qid% -1>
		Msg%id%01 <This evidence will make it impossible for %name2% to get away this time. Thanks for your help.>
	end

Content
	name <Basic vs. Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessVsChar -.Redemption>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the character who's been defeated.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 200 SetQuestStatus %qid% -1>
		Msg%id%01 <%name2% has been defeated! Here's a reward for your help.>
	end


%%
%% *Success1 Content
%%
%% The PC has completed a task of some kind. The NPC who provided the
%% task will now either reward the PC or provide an additional task,
%% or maybe both.
%%
%% Param1: The character for whom the PC was working.
%%   Persona 1 must have a .%id%_GoInit script.
%%
%% SubTypes:
%% .AgainstPirates	PC has defeated a pirate or pirates
%% .Extermination	PC has cleared an area of monsters
%% .FinishMission	PC has ridden this metaphorical horse as far as it'll go
%% .CriminalTransaction	PC has performed some kind of illegal business deal
%% .BusinessDeal	PC has done some kind of business for/with NPC
%% .TipOff              PC has provided useful information
%% .CharityWork		PC has done charity work, presumably without expecting a reward

Content
	name <Corporate Perks>
	requires <*Success1 .BusinessDeal (1:CORPO|1:TRADE|1:LABOR|1:CRAFT|1:FAITH)>
	desc <The PC will be rewarded with a mecha.>
	special <REUSABLE>

	% E1 is the character who provides the mecha.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 RandomMecha .%id%_factions %threat%  SetQuestStatus %qid% -1>
		.%id%_factions <GENERAL PDASS>
		Msg%id%01 <Because you've been such a big help to my business, I'd like for you to have this company mecha. It's got my logo printed on the sides.>
		Msg%id%01_1 <For helping me with my business, I got something for you. It's a new mecha. I know that cavaliers always need new mecha... I hope you don't mind, but I had my logo painted onto the side of it. That way it's a business expense.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <That deal went nicely. Here's a perk for your trouble, a new mecha with my company logo on it.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <Thanks. For helping my business, I bought you this new mecha. I know it seems like an extravagant gift, but since I had my logo painted onto the side it's actually advertising!>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <You've been a great help to me, and now I'll be a great help to you! In thanks for your help, I got you this new mecha. It's got my logo printed right on the side, so everyone will know how stylish and cool you are.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <That was great! Say, could you do me one more small favor? I bought this new mecha for you, and it's got my logo on the side. Just drive it around the city every once in a while.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <Hey, I didn't think that was going to work out, but you really came through. I got something to thank you... unfortunately the guy at the mecha store thought I was buying this mecha for myself, and stuck the company logo on it.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
	end


Content
	name <Driven Away Pirates>
	requires <*Success1 .AgainstPirates (1:CRAFT|1:TRADE|1:CORPO|1:LABOR|1:FAITH) ("spinner"|"asteroid")>
	desc <The PC has defeated some pirates for this person. Maybe the PC can now find pirate treasure!>
	special <REUSABLE>

	% E1 is the character who provides the (measly) reward.
	% E2 is the hard to find derelict ship the pirates were looking for
	% E3 is the treasure ship interior
	Element2 <Prefab>
	Scene2 <Environs>
	Scene3 <New *Dungeon.Derelict>

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 QMemo %qid% %id%02 SetQuestStatus %qid% %id% CashPrize Reward %threat% 100 SetQEncActive %2%>
		Msg%id%01 <Here's your reward. I expect the pirates could come back, though- according to rumor they were here searching for a lost treasure ship from the early days of colinization.>
		Msg%id%02 <%name1% told you that the pirates may have been looking for a derelict treasure ship in \SCENE %s2% .>

		MetaScene 3
		sub
			room
			special <SHAREDPALETTE>
			minimap <......#2#..3#4..#5#......>
			inv
				STC DISPLAYCASE-1
				MiniMapComponent 2
				RandomLoot
				Use <Print 1 OpenInv if= QuestStatus %qid% %id% SetQuestStatus %qid% -1>
				Msg1 <You examine the contents of the display case.>
				Loot_Category <ARTWORK ANTIQUE ARMOR MELEE ACCESSORY>
				Loot_Factions <CRIHN>

				STC DISPLAYCASE-1
				MiniMapComponent 3
				RandomLoot
				Use <Print 1 OpenInv if= QuestStatus %qid% %id% SetQuestStatus %qid% -1>
				Msg1 <You examine the contents of the display case.>
				Loot_Category <WEAPON JEWELRY GEMSTONE>
				Loot_Factions <CRIHN>

				STC DISPLAYCASE-1
				MiniMapComponent 4
				RandomLoot
				Use <Print 1 OpenInv if= QuestStatus %qid% %id% SetQuestStatus %qid% -1>
				Msg1 <You examine the contents of the display case.>
				Loot_Category <TREASURE ARTWORK ANTIQUE>
				Loot_Factions <CRIHN>

				STC DISPLAYCASE-1
				MiniMapComponent 5
				RandomLoot
				Use <Print 1 OpenInv if= QuestStatus %qid% %id% SetQuestStatus %qid% -1>
				Msg1 <You examine the contents of the display case.>
				Loot_Category <TREASURE WEAPON>
				Loot_Factions <CRIHN>
			end
		end
	end
	inv
		STC QUEST-ENCOUNTER-STATIONARY
		name <Ghostly Wreck>
		desig <ENTRANCE %s3%>
		update <ifThisQEncActive else GoHide  SetStat STAT_MetaVisibility SkillTar %threat%  Goto GoSetOrders>
		Attack < >
	end

Content
	name <You're good, now go fetch>
	requires <*Success1 (.BusinessDeal|.TipOff) (1:THIEF|1:ACADE) BRANCHES>

	%% E1 is the character previously helped.
	%% E2 is the item to be sought
	%%  S2 is the scene where it is hidden
	Element2 <Prefab>
	Scene2 <New *ItemCrypt>

	%% Q1 is the success branch for locating the item
	Quest1 <*SuccessGetItem.giveitem 1 2>

	sub
		Persona 1
		% V%id%01 = Have accepted quest.
		Greeting <if= QuestStatus %qid% %id% else %pop% ifChatNPCArchEnemy else GoNotEnemy SetQuestStatus %qid% -2 Goto .%id%_GoBlowOff>
		*%id%_GoBlowOff <*BrushOff>
		GoNotEnemy <NewChat Say %id%05 AddChat %id%03 AddChat %id%04 AddChat %id%05 AddChat %id%06>
		.%id%_GoInit <SetQuestStatus %qid% %id% Goto GoOfferQuest NewChat Say %id%01 AddChat %id%01 AddChat %id%02 CashPrize Threat %threat% 100 150 QMemo %qid% %id%02>
		result%id%01 <NewChat Say %id%03>
		result%id%02 <NewChat Say %id%04 SetQuestStatus %qid% -2>
		result%id%03 <SetQuestStatus %qid% -2  Goto .%id%_GoR7Final>
		*.%id%_GoR3Final <*YouLostItem>
		result%id%04 <SetQuestStatus %qid% -2 PCEnemy %1% Goto .%id%_Go04Finish>
		*.%id%_Go04Finish <*PCWillKeepItem %2%>
		result%id%05 <AddRenown 3   AddReact d5   DeleteItem %2%   Goto .%id1%_GoInit>
		result%id%06 <NewChat Say %id%06>
		Msg%id%01 <You've been a real help, \PC ... I may have some work for you. I'm looking for a sculpture known as "The Heart of Gold". If you could bring it to me, I'll give you another reward.>
		Msg%id%02 <%name1% in \SCENE %s1% asked you to locate the Heart of Gold, a supposed work of art.>
		Msg%id%03 <Very good. According to rumor, the Heart of Gold was last seen nearby. Come back and talk to me after you've located it.>
		Msg%id%04 <It's up to you. I'll probably find it myself, given enough time...>
		Msg%id%05 <Any luck finding the Heart of Gold?>
		Msg%id%06 <It's still out there, somewhere... Don't give up until you've found it.>
		Prompt%id%01 <I'll see what I can do.>
		Prompt%id%01_1 <I'll be back when I've found it.>
		Prompt%id%02 <No. I'm finished working for you.>
		Prompt%id%02_1 <No thanks. I'm out of here.>
		Prompt%id%03 <I'm afraid that I lost it...>
		CPrompt%id%03 <ifItemDestroyed %2% Accept>
		Prompt%id%04 <I've decided to keep it myself.>
		CPrompt%id%05 <ifKeyItem %2% ifChatNPCInPlay Accept>
		Prompt%id%05 <Here, I brought it to you.>
		CPrompt%id%05 <ifKeyItem %2% ifChatNPCInPlay Accept>
		Prompt%id%06 <I'm still looking for it.>
		CPrompt%id%06 <ifItemOK %2% Accept>
	end
	inv
		Treasure 100
		name <Heart of Gold>
		desc <A golden lump shaped vaguely like a heart. Some might call it art.>
		Mass 7
	end

Content
	name <Thanks for the Help>
	requires <*Success1 .CharityWork>
	special <REUSABLE>

	%% E1 is the character previously helped.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 100 AddHeroic 1 SetQuestStatus %qid% -1>
		Msg%id%01 <Thank you so much for the help. Here's a small gift, in appreciation of what you've done... Please take it.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <I'm not good with words but maybe this will speak better. I'd like to give you this small gift, in thanks for what you've done.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <Thanks for partially restoring my faith in humanity. Here's a small token of my appreciation.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
	end


Content
	name <How About A Quest?>
	requires <*Success1 (.FinishMission|.CharityWork) (1:ADVEN|1:MILIT|1:FAITH) ~1:SILKN BRANCHES>
	%% The PC has just completed a job for the NPC. The NPC will now suggest that
	%% the player has what it takes to go on a dangerous quest... if asked about
	%% it, the NPC will ask the PC to retrieve an item.

	%% E1 is the character previously helped.
	%% E2 is the artifact to be sought
	%% E3 is the city this quest is located in
	%%  S2 is the scene where it is hidden
	Element2 <ARTIFACT ITEM PREZERO>
	Scene2 <New *ItemCrypt>
	Element3 <.>

	%% Q1 is the success branch for locating the item
	Quest1 <*SuccessGetItem.keepitem 1 2>

	sub
		Persona 1
		% V%id%01 = Have accepted quest.
		Greeting <if= QuestStatus %qid% %id% else %pop% ifChatNPCArchEnemy else GoNotEnemy SetQuestStatus %qid% -2 Goto .%id%_GoBlowOff>
		*%id%_GoBlowOff <*BrushOff>
		GoNotEnemy <if= V%id%01 1 else GoOfferQuest NewChat Say %id%06 AddChat %id%05 AddChat %id%06 AddChat %id%07>
		.%id%_GoInit <SetQuestStatus %qid% %id% Goto GoOfferQuest>
		GoOfferQuest <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		result%id%01 <NewChat Say %id%03 QMemo %qid% %id%02 V= %id%01 1 AddChat %id%05>
		result%id%02 <NewChat Say %id%04 AddChat %id%03 AddChat %id%04>
		result%id%03 <Goto result%id%01>
		result%id%04 <NewChat Say %id%05 SetQuestStatus %qid% -2 CashPrize Threat %threat% 100 750 AddRenown -1>
		result%id%05 <AddRenown 3   AddReact d5   Goto .%id1%_GoInit>
		result%id%06 <NewChat Say %id%07>
		result%id%07 <SetQuestStatus %qid% -2  Goto .%id%_GoR7Final>
		*.%id%_GoR7Final <*YouLostItem>
		Msg%id%01 <Good job, \PC , but I can tell that it wasn't much of a challenge for you. What you need is a real adventure.>
		Msg%id%02 <%name1% told you that the %name2% is hidden somewhere in %name3%, and sent you to find it.>
		Msg%id%03 <The %name2% is a historical relic from the PreZero world. According to rumor, it was lost somewhere in this very city. Recovering it could be very good for your adventuring career.>
		Msg%id%04 <I could give you a cash reward, but is that what you really want? I'm offering you something far more important than money, a chance at personal betterment and spiritual growth.>
		Msg%id%05 <Well, if that's what you want, you were a great help to me, so... here you are.>
		Msg%id%06 <Hello, \PC . I trust that your adventure is going well?>
		Msg%id%07 <Come back here when you've found it, then. I'd like to have a look at it myself.>
		Prompt%id%01 <What do you have in mind?>
		Prompt%id%01_1 <Tell me what you have in mind.>
		Prompt%id%01_2 <I think you're right...>
		Prompt%id%02 <No adventure, just a reward please.>
		Prompt%id%02_1 <I've had enough adventure, I want money.>
		Prompt%id%02_2 <Wait, what about my reward?>
		Prompt%id%03 <Hmmm... Okay, I'll take your quest.>
		Prompt%id%03_1 <That does sound good. Tell me about it.>
		Prompt%id%04 <Nope. I'd rather take the money.>
		Prompt%id%04_1 <No thanks, money is fine.>
		Prompt%id%05 <I've found the %name2%.>
		CPrompt%id%05 <ifKeyItem %2% Accept>
		Prompt%id%06 <I'm still looking for the %name2%.>
		CPrompt%id%06 <ifItemOK %2% Accept>
		Prompt%id%07 <I'm afraid that I lost the %name2%...>
		CPrompt%id%07 <ifItemDestroyed %2% Accept>
	end

Content
	name <Exotic Animal Trade>
	requires <*Success1 .Extermination ("MAQUI"|"FCOMS"|"RISHI") "Spinner" BRANCHES>
	%% What are animals like the ones you just exterminated doing on a spinner,
	%% anyhow? PC will have a chance to work with L5Law to investigate, or to
	%% work with the smugglers to get the cargo safely away.

	%% E1 is the character previously helped.
	%% E2 is the L5Law officer working on the case.
	%% E3 is the smuggler who has all the animals.
	%% E4 is an encounter representing the smugglers to take the package
	%% E5 is an encounter representing the police looking for the package
	%% E6 is the crate
	Element2 <Prefab>
	Scene2 <Building Public Legit>
	Team2 <Guards>
	TeamData2 <SD Ally>

	Element3 <Prefab>
	Scene3 <Building Public Sleazy>
	Team3 <Citizens>
	TeamData3 <Pass>

	Element4 <Prefab>
	Scene4 <Space Environs>

	Element5 <Prefab>
	Scene5 <Space Environs>

	Element6 <Prefab>
	Scene6 <New *Blank.Basement>

	% SubQuest 1 is if the PC helps E2 catch E3.
	% SubQuest 2 is if the PC helps E3 deliver the exotic animals.
	Quest1 <*SuccessVsChar.Evidence 2 3>
	Quest2 <*Success1.CriminalTransaction 3>

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 AddChat %id%01 AddChat %id%02 CashPrize Reward %threat% 100 SetQuestStatus %qid% %id% QMemo %qid% %id%04>
		result%id%01 <NewChat   Say %id%02   AddReact d10>
		result%id%02 <NewChat   Say %id%03   AddCheerful d6>
		Msg%id%01 <Thanks for your help! It'll be a lot safer from now on. I just wonder... where could those creatures had come from?>
		Msg%id%02 <The only way they could have appeared is if they were brought by someone... But why? And how did they end up in my basement?>
		Msg%id%03 <That's not the point! We live on in a colony, there aren't supposed to be any animals like that here at all!>
		Msg%id%04 <%name1% wonders how the animals in \PPR %1% basement could have gotten into a space colony.>
		Prompt%id%01 <That is a real mystery...>
		Prompt%id%01_1 <I didn't even think we had those kinds of animals.>
		Prompt%id%02 <Maybe if you cleaned once in a while...>
		Prompt%id%02_1 <They must've been attracted by the smell down there.>

		Persona 2
		%% The Police Officer. Bring him the crate for a reward + some points of LAW.
		rumor%id% <%name2% is investigating the illegal animal trade.>
		% V1 = Have given investigation quest
		greeting <if= QuestStatus %qid% %id% else GoIntro if= V1 1 else GoFirstTime NewChat Say 8 AddChat 8 AddChat 9>
		GoFirstTime <NewChat Say 1  AddChat 1  AddChat 2>
		*GoIntro <*NiceToMeetYou GoChat>
		*GoChat <*Misc_Chatter>
		Result1 <NewChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		result2 <ifPCCriminal else GoR2Pass NewChat Say 4 AddChat 4 AddChat 5>
		GoR2Pass <NewChat Say 3>
		result3 <NewChat Say 5 V= 1 1 QMemo %qid% 10>
		result4 <NewChat Say 6 AddChat 6 AddChat 7>
		result5 <NewChat Say 7 AddChat 6 AddChat 7>
		result6 <NewChat Say 3>
		result7 <Goto result3>
		result8 <DeleteNPC %3% DeleteItem %4%  DeleteItem %5%  DeleteItem %6%  AddLawful 1  AddReact d10  XPV 100  Goto .%id1%_GoInit>
		result9 <NewChat Say 9 AddChat 8>
		Msg1 <Hello \PC . I hear that you helped %name1% clean out the basement.>
		Msg2 <I'm working with L5Law to bust an exotic animal smuggling ring. They're importing rare animals from Earth for sale in the colonies. It's vital that we find and stop them at once.>
		Msg3 <I see. Well, if you change your mind, you know where to find me.>
		Msg4 <I know you probably have reasons for not working with the law, but you did help %name1%, and these smugglers must be stopped. It's very important that we find them as soon as possible.>
		Msg5 <We need to locate the smugglers. If you can find any evidence connection someone to the ring, bring it to me.>
		Msg6 <There are many reasons. Some people might keep them as pets. Others could use them as guard animals, or for a private collection. As repulsive as it sounds, some of these animals could even be cut into parts and eaten.>
		Msg7 <The ecology of a spinner colony is very finely balanced. Animals brought from outside could spread diseases or parasites. They could breed in the service layer, destroying resources that the rest of us need to live.>
		Msg8 <I'm still trying to locate those animal smugglers. Hopefully some new evidence will turn up soon.>
		Msg9 <That's progress... but unfortunately we still need some evidence or we won't be able to make an arrest.>
		Msg10 <%name2% in \SCENE NPCScene %2% asked you to help investigate the illegal animal trade in \SCENE RootSceneID .>
		Prompt1 <That's right. How did you know?>
		Prompt2 <I don't want to talk about it.>
		Prompt3 <How can I help you with this?>
		Prompt4 <Why would anyone want to import animals?>
		Prompt5 <I don't see why importing animals is so bad.>
		Prompt6 <Sorry, I don't want to get involved.>
		Prompt7 <I'll help you with this case.>
		Prompt8 <I've got your evidence right here.>
		CPrompt8 <ifKeyItem %6% Accept>
		Prompt9 <I think %name3% may be involved.>
		CPrompt9 <if# NPCVar %3% 2 0 Accept>

		Persona 3
		%% The Smuggler. Do his delivery for a reward + some points of CRIMINAL.
		rumor%id% <%name3% has been known to smuggle all kinds of exotic goods.>
		% V1 = Have given delivery quest
		greeting <if= QuestStatus %qid% %id% else GoIntro  if= V1 1 else GoCheckReject NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoCheckReject <if= V1 -1 else GoFirstTime Goto GoBrushOff>
		*GoBrushOff <*BrushOff&Questioning>
		GoFirstTime <NewChat Say 4 AddChat 4 AddChat 5>
		*GoIntro <*NiceToMeetYou GoOpenShop>
		*GoOpenShop <*SHOP_BLACKMARKET GoByeBye>
		*GoByeBye <*Goodbye>
		result1 <NewChat Say 2>
		result2 <DeleteItem %4% DeleteItem %5% AddLawful -5   Goto .%id2%_GoInit>
		result3 <NewChat Say 3 AddReact -d20  DeleteItem %4% DeleteItem %5%  SetQuestStatus %qid% -2 ifNPCPassionate PCEnemy %3%>
		% If the PC isn't lawful, then no problem E3 will tell about the exotic animal
		% trade. Even if he is lawful, if he passes a decent conversation roll E3 will still
		% talk. Either way, the connection is made and the trap door activated.
		result4 <ifPCLawful else GoR4JobOffer ifConversation %sktar_hard% else GoR4Fail Goto GoR4JobOffer>
		GoR4JobOffer <NewChat Say 10  AddChat 9 AddChat 10>
		GoR4Fail <NewChat  Say 5  AddChat 6 AddChat 7  NPCVar= %3% 2 1   V= 1 -1   UpdateProps>
		result5 <Goto GoOpenShop>
		result6 <NewChat Say 6 AddCHat 8>
		result7 <NewChat Say 7 AddChat 8>
		result8 <NewChat Say 9>
		result9 <NewChat Say 11  AddChat 11   NPCVar= %3% 2 2   SetQEncActive %4%   V= 1 1   QMemo %qid% 16 UpdateProps>
		result10 <NewChat Say 12   AddLawful 1   NPCVar= %3% 2 1   V= 1 -1   QMemo %qid% 17 UpdateProps>
		result11 <NewChat Say 13 AddChat 12>
		result12 <NewChat Say 14  AddChat 13 AddChat 14>
		result13 <NewChat Say 15>
		result14 <Goto result10>
		Msg1 <Have you delivered the package yet?>
		Msg2 <Retrieve the "package" from my basement and deliver it to the "businessmen" who are working in \SCENE %s4% . Then return here and I will give you a "reward". Do I have to spell it out for you?>
		Msg3 <You lost it!? How does one lose a 20kg shipping container!? That's it... you and I, our business relationship is over.>
		Msg4 <Yes, may I help you? I carry absolutely everything, and what I don't have now I can easily get.>
		Msg5 <Exotic animals? Er... Never heard of that, sorry. Nope. I don't have any, and I don't know where to get them either...>
		Msg6 <Well that's common, isn't it? Ships coming and going all the time, from Earth, Mars... I had some critters escape in my basement too. Nothing to worry about, that.>
		Msg7 <When I said that I can get everything I obviously meant everything legally available, of course. If you're looking for exotic creatures you may want to ask %name1%... I hear they had some at \SCENE %s1% .>
		Msg9 <Still with the questions! I don't know. How would I? I'm a merchant, not some kind of xenobiologist... I've already said enough to you.>
		Msg10 <Hmmm... I might know something about that, but if you'll take my advice dealing with that kind of thing is more trouble than it's worth. How'd you like to help me out?>
		Msg11 <Well, I had this customer, who wanted some animals transported from Earth. Fair enough, I thought, but when they got here one of the crates malfunctioned and the damn things got out!>
		Msg12 <Suit yourself. Fortunately there will never be a shortage of thieves and cut-throats to do my dirty work.>
		Msg13 <If only that were the worst of it; they ended up in my basement too, and I even think they're breeding down there! I can't break out the heavy guns to get rid of them, or the law would be on me in a second...>
		Msg14 <I've set poison and traps; that should get most of the vermin, eventually, but for now I still have to conduct a business. There's a crate in the basement which has to be delivered to some "clients" waiting in \SCENE %s4% .>
		Msg15 <Great! Head down these stairs to the basement; there's likely to be some vermin about but you can handle those. Find the cold storage crate then bring it to the clients waiting outside the station. Afterwards you'll get a nice reward.>
		Msg16 <%name3% asked you to deliver a package to smugglers waiting in \SCENE %s4% .>
		Msg17 <%name3% seems to be involved in the exotic animal trade; %name2% would be interested.>
		Prompt1 <What exactly am I supposed to do again?>
		Prompt2 <It has been delivered.>
		CPrompt2 <if= NPCVar %3% 1 1 Accept>
		Prompt3 <I think I lost the package...>
		CPrompt3 <if# NPCVar %3% 1 1 ifItemDestroyed %6% Accept>
		Prompt4 <I'm looking for some exotic animals.>
		Prompt5 <Show me what you have for sale.>
		Prompt6 <%name1% had a problem with animals, maybe from Earth.>
		Prompt7 <I thought you said you could get anything.>
		Prompt8 <Any idea where they came from?>
		Prompt9 <How can I help you?>
		Prompt10 <I won't get involved in anything illegal.>
		Prompt11 <So that's how they got into %name1%'s basement.>
		Prompt12 <What do you want me to do?>
		Prompt13 <I'll get the crate and deliver it for you.>
		Prompt14 <I don't want anything to do with this.>

		MetaScene 3
		% Contains the entrance to the basement.
		sub
			room
			special <SHAREDPALETTE>
			desig <HOME>
			minimap <.......2....1.......&---&>
			inv
				TrapDoor
				desig <Entrance %s6%>
				MiniMapComponent 2
				Hide -1
				Update <if= NPCVar %3% 2 1 else GoCheckAuto   if= V1 0   V= 1 1  SetStat STAT_MetaVisibility %sktar%>
				GoCheckAuto <if= NPCVar %3% 2 2   V= 1 1  SetStat STAT_MetaVisibility 0>
			end
		end

		MetaScene 6
		% Contains the teams and scripts for the basement.
		% L1 = Initialization Counter
		% L2 = XP gained for getting case counter
		start <if= L1 0 else GoBeenBefore Print 1 L= 1 1 MonsterUp 2 WMThreat %threat%>
		GoBeenBefore <Print 2>
		Msg1 <You enter the basement. Strange noises can be heard coming from the darkness.>
		Msg2 <You enter the basement.>
		get%6% <if= L2 0 XPV 50 L= 2 1>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monsters>
			type <Animal Vermin Exotic Bug Reptile Fungus>
			SetEnemy 1
			Update <ifItemDestroyed %6% SetStat STAT_WMonster 0>

			Room
			% A room to hold the package.
			special <SharedPalette>
			desig <HOME>
			minimap <......#.#...1...#.#......>
		end

	end
	inv
		NPC Police Officer
		SetFaction 9

		NPC Thief
		job <Smuggler>
		% V1 = Have completed smuggling job
		% V2 = Have activated trapdoor

		STC QUEST-ENCOUNTER-WANDER
		% This encounter appears after activated by E3.
		% When it approaches the PC or is approached by the PC, it will
		% ask if the PC wants to make the transfer. If yes, then the crate
		% disappears and the PC can return to E3 for the reward.
		name <Smugglers>
		ENCOUNTER_NonCombat
		use <ifKeyItem %6% else GoRunAway Goto GoMakeSwitch>
		attack <ifKeyItem %6%             Goto GoMakeSwitch>
		GoRunAway <Print 1   SetStat STAT_MetaVisibility -1>
		GoMakeSwitch <ifYesNo 2 3 4 else GoRunAway Alert 5 DeleteItem %6% NPCVar= %3% 1 1 XPV 100 DeleteItem %4%>
		Msg1 <The smuggler's ship darts away too quickly for you to approach.>
		Msg2 <You are approached by a smuggler. Do you send over the crate?>
		Msg3 <Send if over.>
		msg4 <Keep it.>
		Msg5 <The smuggler's ship retrieves the crate and darts away.>

		Encounter
		% This encounter appears when this quest fragment starts.
		% If the PC is holding the package, he will be attacked by the police.
		% Whether he defeats the police or just runs away this encounter will
		% recharge in a few hours.
		% V1 = Recharge counter
		name <Police Dragnet>
		Special <NoMSID>
		update <if= QuestStatus %qid% %id% else GoHide  ifG ComTime V1 else GoHide Goto GoSetVisible>
		Use <ifKeyItem %6% else GoNoItem Goto Attack>
		GoNoItem <Alert 1 SetStat STAT_MetaVisibility -1 V= 1 ComTime V+ 1 3600 V+ 1 d7200>
		Attack <ifKeyItem %6% Alert 2 Print 3 V= 1 ComTime V+ 1 3600 V+ 1 d7200 Goto GoStartCombat>
		GoStartCombat <SavePos Dynamic 2 %threat% 120 .nu1 .nu2  DynaFaction 9>
		.nu1 <if= T1 0  Return   if= L1 0  L= 1 1  AddRenown -5>
		.nu2 <if= T2 0           if= L1 0  L= 1 1  XPV 50   AddLawful -1>
		EncounterMove 30
		ENCOUNTER_Defense
		Msg1 <Police Patrol: Smugglers are operating in the area. Please be cautious, citizen.>
		Msg2 <Police Patrol: Power down and surrender. You are transporting known contraband.>
		Msg3 <You are attacked by the police!>

		Treasure 10
		name <Cold Storage Crate>
		mass 39
		StolenGoods
		desc <A container for storing things at extremely low temperatures. It is often used to transport living material.>
	end

Content
	name <Destroy Pirate Base>
	requires <*Success1 .AgainstPirates BRANCHES (1:MILIT|1:POLIC|1:POLIT|1:CORPO)>

	% E1 is the character who will offer the mission
	% E2 is the local police faction
	% E3 is the pirate faction to be defeated
	Element2 <Faction Police !Ally 1>
	Element3 <Faction Crime !Enemy 2>
	Scene3 <New *DestroyBase>

	Quest1 <*SUCCESS1.FinishMission 1>

	Sub
		Persona 1
		Greeting <if= QuestStatus %qid% %id% else %pop%  NewChat Say %id%02 AddChat %id%01>
		.%id%_GoInit <NewChat Say %id%01   CashPrize Reward %threat% 100   SetQuestStatus %qid% %id% QMemo %qid% %id%03>
		Result%id%01 <Goto .%id1%_GoInit>
		Msg%id%01 <Good job... very good. The pirates must have a base somewhere around here. If we could destroy that, their attacks would be stopped for good.>
		Msg%id%02 <Our forces are still looking for the pirate base. Never fear, it will be located and destroyed soon.>
		Msg%id%03 <%name1% thinks there is a pirate base somewhere near \SCENE RootSceneID .>
		Prompt%id%01 <I've destroyed the pirate base myself.>
		CPrompt%id%01 <if= SceneVar %3% 1 1 Accept>
	end


Content
	name <Revenge For Rats>
	requires <*Success1 .Extermination (1:TRADE|1:CORPO|1:CRAFT) BRANCHES>
	%% Someone was behind the recent infestation... Will you bring the criminal
	%% to justice, or will you help him finish his plan?

	%% E1 is the character you just helped.
	%% E2 is his enemy.
	%% E3 is the spray bottle of spaceworm spores

	Element2 <Prefab>
	Scene2 <Building Public Mall>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <Prefab>
	Place3 <2>

	Quest1 <*SuccessVsChar.Evidence 1 2>
	Quest2 <*SuccessVsChar.ConJob   2 1>
	Quest3 <*SuccessVsChar.FoilPlot 1 2>

	sub
		Persona 1
		rumor%id% <%name2% has always hated %name1%.>
		greeting <if= QuestStatus %qid% %id% else %pop% if# NPCVar %2% 1 3 else .%id%_GoIntimidate ifKeyItem %3% else %pop% if= SceneID %s1% else %pop% Goto .%id%_GoHaveCan>
		.%id%_GoHaveCan <NewChat Say %id%02 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		.%id%_GoIntimidate <NewChat Say %id%02 AddChat %id%04>
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 100  SetQuestStatus %qid% %id% QMemo %qid% %id%05>
		Result%id%01 <NewChat Say %id%03>
		Result%id%02 <PCFriend %1% PCEnemy %2% DeleteItem %3%  NPCVar= %2% 1 4  XPV 50   Goto .%id1%_GoInit>
		Result%id%03 <NewChat Say %id%04 DeleteItem %3%  NPCVar= %2% 1 2 Qmemo %qid% %id%06>
		Result%id%04 <PCFriend %1% PCEnemy %2%     XPV 50   Goto .%id3%_GoInit>
		Msg%id%01 <Thanks for cleaning out the vermin. Somebody must have put them there; I only wish I knew who...>
		Msg%id%02 <You look like you have something to tell me...>
		Msg%id%03 <Well, that's a relief. I thought you were going to tell me the basement was full of rats again.>
		Msg%id%04 <Really? I guess I better use it, then... You did such a good job cleaning out the vermin the first time, I'm sure that whatever's in here will work great.>
		Msg%id%05 <%name1% in \SCENE %s1% thinks someone put the monsters in \PPR %1% basement.>
		Msg%id%06 <You gave the strange cannister to %name1% for %name2% in \SCENE %s2% .>
		Prompt%id%01 <No, nothing at all.>
		Prompt%id%02 <I got this strange cannister from %name2%...>
		Prompt%id%03 <This can of pest repellent will keep your basement safe!>
		CPrompt%id%03 <if= NPCVar %2% 1 1 Accept>
		Prompt%id%04 <%name2% isn't going to bother you any more.>

		Persona 2
		% V1 = Intimidation recharge counter
		% V2 = Conversation recharge counter
		greeting <ifG NPCVar %2% 1 2 else GoCheckMission ifNPCArchEnemy %2% else GoOpenStore NewChat Say 17>
		GoCheckMission <if= SceneID %s2% else GoHangUp ifNPCOK %1% else GoBreakQuest if= QuestStatus %qid% %id% else GoGreeting  ifG NPCVar %2% 0 else GoConfrontation Say 18 AddChat 12 AddChat 13>
		GoConfrontation <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoBreakQuest <SetQuestStatus %qid% -2 Goto GoGreeting>
		GoHangUp <NewChat Say 19>
		*GoGreeting <*NiceToMeetYou GoOpenStore>
		*GoOpenStore <*SHOP_GENERAL GoGoodbye>
		*GoGoodbye <*GOODBYE>
		result1 <NewChat Say 2 AddChat 4 AddChat 5 AddChat 8>
		result2 <Goto GoOpenStore>
		result3 <ifNPCArchEnemy %1% else GoR3Normal NewChat Say 3 AddChat 6 AddChat 7>
		GoR3Normal <NewChat Say 4  AddChat 4>
		result4 <AddReact -d10   ifIntimidation %sktar_hard% else GoIntFail Goto GoIntWorked>
		GoIntWorked <NewChat Say 5 NPCVar= %2% 1 3>
		GoIntFail <NewChat Say 6>
		result5 <ifConversation %sktar% else GoR5Fail NewChat Say 8 AddChat 9 AddChat 10>
		GoR5Fail <NewChat Say 9>
		result6 <NewChat Say 10 AddChat 9 AddChat 10>
		result7 <Goto result4>
		result8 <ifConversation %sktar% else GoR8Fail NewChat Say 11 AddChat 9 AddChat 10>
		GoR8Fail <NewChat Say 12>
		result9 <ifItemOK %3% else GoR9Fail NewChat Say 13 AddChat 11 GiveItem %3% NPCVar= %2% 1 1 QMemo %qid% 20>
		GoR9Fail <NewChat Say 14>
		result10 <NewChat Say 15>
		result11 <NewChat Say 16>
		result12 <Goto GoOpenStore>
		result13 <PCAlly %2% PCEnemy %1% AddHeroic -1 AddLawful -3 AddReact d10  XPV 50  Goto .%id2%_GoInit>
		Msg1 <You look like there's something you're after. What do you want?>
		Msg2 <Oh, do you? I don't know anything about that... And what business is it of yours, anyhow?>
		Msg3 <What do you care about that? The last I heard, you and %name1% weren't exactly on the best of terms...>
		Msg4 <Like I said earlier, I don't know anything about %name1%! Besides, anything that happened is probable \OPR %1% fault anyhow.>
		Msg5 <Okay, I admit it, I released the vermin into %name1%'s basement... I was just trying to even some old scores, that's all... I won't do anything like that again.>
		Msg6 <You're really clueless, aren't you? It's not my fault if bad things happen to to %name1%.>
		Msg8 <You don't say... Well, there could be some money for you in it, if you're willing to do me a favor... What do you say?>
		Msg9 <If you had any evidence linking me to the rats, then maybe you'd have the start of a decent extortion scam. As it is you've got nothing.>
		Msg10 <That's a great idea. If you're really interested, and not above a bit of underhandedness, I've got something you can do...>
		Msg11 <You think I need any help to deal with %name1%? Well, it would give an extra layer of plausible deniability... I do have a task, if you're willing to do a bit of underhanded work.>
		Msg12 <I don't need your help with anything, especially not anything that I don't even know about in the first place.>
		Msg13 <Take this can to %name1%... It contains spaceworm spores, but you can tell \OPR %1% it's rat poison instead. When \SPR %1% spreads it around \PPR %1% basement the worms will hatch and eat all \PPR %1% stock.>
		Msg14 <Er... I seem to have misplaced something very important... I guess I don't have anything for you to do after all.>
		Msg15 <Fine. I don't need any help dealing with %name1%, anyhow.>
		Msg16 <You'll be rewarded very handsomely, I promise you... Far more than that cheapskate %name1% would ever pay.>
		Msg17 <Go away. I have nothing to say to you.>
		Msg18 <So, how's the secret mission going? Anything to report?>
		Msg19 <What do you want? If you need to buy anything, come see me in \SCENE %s2% . Can't conduct business over the phone.>
		Msg20 <%name2% asked you to give a cannister of spaceworm spores to %name1% in \SCENE %s1% .>
		Prompt1 <I know you put monsters in %name1%'s basement.>
		Prompt2 <I'm just here to shop.>
		Prompt3 <I want you to leave %name1% alone from now on.>
		Prompt4 <If anything else happens, I promise you'll suffer.>
		Prompt5 <I thought this knowledge might be worth some money.>
		Prompt6 <You're right... Maybe I should be helping you instead.>
		Prompt7 <That doesn't matter. You're going to stop.>
		Prompt8 <I want to help you against \OPR %1% .>
		Prompt9 <Sounds good. What do you want me to do?>
		Prompt10 <On second thought, I'm not interested.>
		Prompt11 <What do I get in return?>
		Prompt12 <I need to go shopping before completing it.>
		Prompt13 <I've given %name1% the spores.>
		CPrompt13 <if= NPCVar %2% 1 2 Accept>

		MetaScene 2
		sub
			room
			minimap <..............1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end
	end
	inv
		NPC Shopkeeper
		%% NPCVar 1:	If V1=1, have accepted mission from E2.
		%%		If V1=2, have completed that mission.
		%%		If V1=3, have intimidated E2 into submission.
		%%		If V1=4, have sided with E1
		Chardesc Passionate
		Lawfulness 0

		Treasure 100
		name <Aerosol Container>
		desc <This unmarked aerosol container looks mighty suspicious.>
		use <Print 4>
		Clue16 <Print 2>
		Clue20 <Print 2>
		Clue22 <Print 3>
		Clue29 <Print 1>
		Msg1 <This type of container is used to transport biological samples. No idea why it would be connected to a spray head, though.>
		Msg2 <This type of container is used for storing and dispensing biological agents. As this one isn't marked, it probably doesn't contain medicine.>
		Msg3 <This type of container is used for storing and dispensing biological agents. Perhaps it's a weapon of some kind.>
		Msg4 <Better not just spray this around... There's no telling what it might contain.>
	end

Content
	name <Thanks for the Info>
	requires <*Success1 .TipOff>
	special <REUSABLE>

	%% E1 is the character previously helped.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward %threat% 300 SetQuestStatus %qid% -1>
		Msg%id%01 <Thanks for the information. Here's a little reward for your trouble.>
		Msg%id%01_1 <Ah, it's always helpful to be on top of the latest information. Thanks for letting me know. Here's something for you.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Thanks for the tip. Here's something for you.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <How interesting. Your information is worth something... about this much, I'd say.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <Aha! That's going to be very useful to know... Here, take this reward for your help.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <It's a good thing you told me that. Here, take this. It's my way of saying thanks.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <It would have been very bad if you hadn't told me that. Here, thanks for the tip.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>

	end

Content
	name <Basic Reward End>
	requires <*Success1 -.TipOff -.CharityWork>
	special <REUSABLE>
	%% This is the basic ending... The PC gets a reward and that's it.
	%% Afterwards, set the QuestStatus to -1 to indicate a successfully
	%% completed quest.

	%% E1 is the character to give the reward.

	sub
		Persona 1
		*.%id%_GoInit <*MissionWasSuccess&Reward .%id%_GoFinish ChatNPCFac na>
		.%id%_GoFinish <CashPrize Reward %threat% 250 SetQuestStatus %qid% -1>
	end




