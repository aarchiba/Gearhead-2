%%
%% STATIC ADVENTURE CONTENT
%%
%% This is used when a new campaign is started to fill each location with
%% randomly generated content. The content is hardcoded into the adventure
%% instead of being housed in a plot or story.
%%

%%
%% *:MechaHeld Content
%%
%% E1's mecha is currently being held by someone else. In this subquest,
%% the PC will be able to find that someone and release the mecha... Or
%% maybe have it locked up perminantly. The NPC has a semi-legitimate
%% claim to holding the mecha... it may be in hock due to debts, or maybe
%% its ownership is disputed. The presence of this subquests implies a
%% certain amount of inexperience or irresponsibility on the part of the
%% pilot.
%%
%% To start this subquest, the calling quest should set the QuestID.
%% This quest should end with -1 if the mecha was released, or -2 if not.
%% The rumor should explain the situation.
%%
%% PARAM1: The NPC who lost his mecha.

Content
	name <Mecha Drifting in Space>
	desc <E1 ejected while in space; now his mecha is floating aimlessly outside.>
	requires <*:MechaHeld Spinner (!Ne|!Lo)>

	% E1 is the character that owns the mecha
	% E2 is the encounter representing the mecha
	Element2 <Prefab>
	Scene2 <Space Environs>

	inv
		STC QUEST-ENCOUNTER-AUTO-WANDER
		name <Abandoned Mecha>
		rumor%id% <%name1% was forced to eject outside the station.>
		EncounterMove 20
		ENCOUNTER_NonCombat
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifEncounterVisible Goto Use>
		USE <ifYesNo 1 2 3 else GoTakeMecha   Print 4   XPV 50  SetQuestStatus %qid% -1  DeleteItem %2%>
		GoTakeMecha <RandomMecha .mekdesc %threat% SetQuestStatus %qid% -2  DeleteItem %2%>
		.mekdesc <GENERAL>
		Msg1 <You spot a mecha floating in space. It's most likely the one lost by %name1%.>
		Msg2 <Mark its location for %name1%.>
		Msg3 <Take the mecha as your own.>
		Msg4 <You send the mecha coordinates to %name1%.>
	end

Content
	name <Mecha Impounded by L5Law>
	desc <E1 broke some traffic rules; must pass a test to get mecha back>
	requires <*:MechaHeld L5PAT>

	% E1 is the character who owns the mecha.
	% E2 is the police officer holding the mecha.
	Element2 <Prefab>
	Scene2 <Building Legit>
	Team2 <Citizens>
	TeamData2 <SD Ally>

	sub
		Persona 1
		greeting <if= QuestStatus %qid% %id% else %pop%  if= NPCVar %2% 1 1 else %pop%  NewChat Say %id%01 AddChat %id%01>
		result%id%01 <NewChat Say %id%02 AddCHat %id%02 AddCHat %id%03>
		result%id%02 <NewChat Say %id%03 NPCVar= %2% 1 2>
		result%id%03 <NewChat Say %id%04>
		Msg%id%01 <All these traffic rules... What do they really have to do with how well I can pilot a mecha? %name2% says I have to pass a test before I can get my machine back.>
		Msg%id%02 <Really, you'd do that? So, you know the answers to all of these questions, then?>
		Msg%id%03 <Thanks. With your help, I'm sure I'll do fine on the test.>
		Msg%id%04 <Be careful... if %name2% finds out, your mecha will be confiscated next.>
		Prompt%id%01 <I could help you to study, if you want.>
		Prompt%id%02 <No problem. Let me show you.>
		CPrompt%id%02 <ifMechaPiloting %sktar% Accept>
		Prompt%id%03 <Um... Actually I don't know either.>

		Persona 2
		% NPCVar %2% 1 = State Counter, if =1 have learned of test, if =2 have taught E1
		% V2 = Intimidation counter
		&Bribe <Threat %threat% 3>
		rumor%id% <%name2% impounded %name1%'s mecha due to multiple parking tickets.>
		greeting <if= QuestStatus %qid% %id% else GoNoQuest ifNPCDead %1% else GoCheckWin SetQuestStatus %qid% -2 Goto GoChat>
		GoCheckWin <if= NPCVar %2% 1 2 else GoMainQuest NewChat Say 14 SetQuestStatus %qid% -1>
		GoMainQuest <if= NPCVar %2% 1 1 else GoFirstTime NewChat Say 3 AddChat 3 AddChat 4>
		GoFirstTime <NewChat Say 1 AddChat 1 AddChat 2>
		*GoNoQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 AddChat 3 AddChat 4 NPCVar= %2% 1 1 QMemo %qid% 4>
		*result2 <*MISC_CHATTER>
		result3 <NewChat Say 5>
		result4 <NewChat Say 6 AddCHat 5 AddChat 6 AddCHat 7>
		result5 <ifNPCLawful else GoR5NoLaw NewChat Say 7 AddCHat 6 AddChat 3>
		GoR5NoLaw <NewChat Say 8 AddChat 8 AddChat 9 AddChat 10>
		result6 <AddLawful -5 AddHeroic -1 ifIntimidation %sktar_hard% else GoR6Fail NewChat Say 9 SetQuestStatus %qid% -1>
		GoR6Fail <NewChat Say 10 PCEnemy %2% V= 2 1>
		result7 <NewChat Say 11>
		result8 <NewChat Say 12 Cash+ - &Bribe SetQuestStatus %qid% -1 AddLawful -5>
		result9 <NewChat Say 13 AddReact d10 SetQuestStatus %qid% -1 AddLawful -3 AddEasygoing -d8>
		result10 <NewChat Say 11>
		Msg1 <Yes, may I help you?>
		Msg2 <I caught %name1% piloting in an improper manner. I confiscated \PPR %1% mecha, and will not return it until \SPR %1% completes a mecha safety course.>
		Msg3 <I'm still waiting for %name1% to complete \PPR %1% safety course.>
		Msg4 <%name2% confiscated %name1%'s mecha, and won't return it until \SPR %1% completes a safety course.>
		Msg5 <Please try. I've talked to \OPR %1% over and over but it never seems to sink in.>
		Msg6 <What exactly do you mean by that?>
		Msg7 <I'm not about to be bribed. You can tell %name1% that \SPR %1% can't get out of the test that easily.>
		Msg8 <That sounds alright... what can you offer me?>
		Msg9 <Yikes... Alright, you can tell %name1% that \PPR %1% mecha is being released. There's no need to make a big deal over this.>
		Msg10 <It's not a smart idea to try intimidating a law officer. You can tell %name1% that \SPR %1% can't get out of the test that easily.>
		Msg11 <That's too bad.>
		Msg12 <It's been a pleasure doing business with you. You can tell %name1% that \PPR %1% mecha is being released.>
		Msg13 <Yes, I do believe that would be a worthwhile trade. You can tell %name1% that \PPR %1% mecha is being released.>
		Msg14 <You can tell %name1% that \SPR %1% passed the exam. I'll release \PPR %1% mecha shortly.>
		Prompt1 <I'm here to talk about %name1%'s mecha.>
		Prompt2 <I'm just here to chat.>
		Prompt3 <I'll talk with \OPR %1% about that.>
		Prompt4 <I want you to return \PPR %1% mecha now.>
		Prompt5 <I can make it worth your while.>
		Prompt6 <It wouldn't be healthy to refuse me.>
		CPrompt6 <if= V2 0 Accept>
		Prompt7 <Nothing, I'm just saying...>
		Prompt8 <I can offer cash. [give $ \VAL &Bribe ]>
		CPrompt8 <ifG PC$ &Bribe Accept>
		Prompt9 <I can show you a good time.>
		CPrompt9 <ifChatNPCSexy ifFlirtation %sktar_hard% Accept>
		Prompt10 <I've got nothing...>

		Metascene 2
		% If the key NPC dies, resolve the quest as a failure.
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
	end
	inv
		NPC Police Officer
		SetFaction 9
	end


Content
	name <Meks in Hock>
	desc <E1 is in debt to E2; the mecha is in hock>
	requires <*:MechaHeld>

	% E1 is the character who owns the mecha.
	% E2 is the storekeeper holding the mecha.
	Element2 <Prefab>
	Scene2 <Building Garage>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	sub
		Persona 1
		% This conversation thread will activate after the PC asks E2 about the mecha.
		greeting <if= QuestStatus %qid% %id% else %pop% ifNPCOK %2% else .%id%_GoE2Died if= NPCVar %2% 1 1 else %pop%  NewChat Say %id%02 AddChat %id%01 AddChat %id%02>
		.%id%_GoE2Died <NewChat Say %id%01 SetQuestStatus %qid% -2>
		result%id%01 <NewChat Say %id%03 AddChat %id%03 AddCHat %id%04 AddChat %id%05>
		result%id%02 <NewChat Say %id%04>
		result%id%03 <NewChat Say %id%05 NPCVar= %2% 1 2>
		result%id%04 <NewChat Say %id%06>
		result%id%05 <NewChat Say %id%07 NPCVar= %2% 1 2>
		Msg%id%01 <I'll never get my mecha back from %name2% now...>
		Msg%id%02 <I wish there was some way to get my mecha back from %name2%...>
		Msg%id%03 <I can't afford to pay back everything I owe, and with my mecha gone I don't have any way to make more money. It's a tough situation.>
		Msg%id%04 <Please do that. Any help would be greatly appreciated.>
		Msg%id%05 <I could do that, if %name2% would agree to return my mecha. That way \SPR %2% gets paid and I still get to eat.>
		Msg%id%06 <That's so true.>
		Msg%id%07 <You're right... the only way to get past this is to bite the bullet and pay up. It's going to be painful in the short term, but really there's nothing else I can do. Tell %name2% I'll pay as much as I can now and the rest later.>
		Prompt%id%01 <Why don't you just pay the money you owe?>
		Prompt%id%02 <I'll talk with \OPR %2% , see if I can help.>
		Prompt%id%03 <Maybe you can work out an installment plan.>
		CPrompt%id%03 <ifConversation %sktar% Accept>
		Prompt%id%04 <That's a real problem.>
		Prompt%id%05 <Face responsibility and pay what you owe.>
		CPrompt%id%05 <ifIntimidation %sktar% accept>

		Persona 2
		% V1 = Amount owed
		% NPCVar %2% 1 = Communication variable. When =1, PC has talked to E2. WHen =2, E1 agreed to pay installments.
		rumor%id% <%name2% is holding %name1%'s mecha until all debts are paid.>
		greeting <if= QuestStatus %qid% %id% else GoNoQuest NewChat Say 1 AddChat 1 AddChat 2>
		GoE1Dead <NewChat Say 2 SetQuestStatus %qid% -2>
		result1 <ifNPCOK %1% else GoE1Dead NewChat Say 3 AddChat 3 AddChat 4 AddChat 5 AddCHat 6 if= NPCVar %2% 1 0 NPCVar= %2% 1 1 V= 1 Threat %threat% 2 QuestMemo %qid% 4>
		result2 <Goto GoShop>
		result3 <NewChat Say 6 AddChat 7 AddChat 8 AddChat 4>
		result4 <ifConversation %sktar_hard% else GoR4Fail NewChat Say 7 SetQuestStatus %qid% -1>
		GoR4Fail <NewChat Say 8>
		result5 <NewChat Say 5 SetQuestStatus %qid% -1>
		result6 <NewChat Say 9 AddChat 9 AddChat 8 AddCHat 7>
		result7 <ifConversation %sktar% else GoR7Fail NewChat Say 11 AddChat 10 AddChat 11>
		GoR7Fail <NewChat Say 10>
		result8 <NewChat Say 12>
		result9 <ifFlirtation %sktar_hard% else GoR9Fail NewChat Say 13 SetQuestSTatus %qid% -1 AddReact d12 AddHeroic -1>
		GoR9Fail <NewChat Say 14>
		result10 <NewChat Say 15 Cash+ - V1 SetQuestStatus %qid% -1>
		result11 <NewChat Say 16>
		*GoNoQuest <*NiceToMeetYou GoShop>
		*GoShop <*SHOP_Mecha GoBye>
		*GoBye <*GoodBye>
		Msg1 <Yes, may I help you?>
		Msg2 <I don't think %name1% is going to be needing a mecha any time soon... It's a shame that I'll never get the money I'm owed.>
		Msg3 <%name1% owes me money, quite a bit of it in fact... I'll be holding onto \PPR %1% mecha until that money is repaid in full.>
		Msg4 <%name2% is holding %name1%'s mecha until \SPR %1% can pay off \PPR %1% debt.>
		Msg5 <Is that so? As soon as I've recieved the first installment, I will return \PPR %1% mecha.>
		Msg6 <I'm owed $ \VAR V1 in total. If only %name1% would pay some installments, I wouldn't have needed to impound \ppr %1% mecha.>
		Msg7 <Hmm. You're right, I know. %name1% won't be able to pay me back so long as \SPR %1% doesn't have the money. Alright, you can go tell %name1% that \SPR %1% can have \PPR %1% mecha back.>
		Msg8 <That's hardly my problem, is it? Either I get that money or %name1%'s mecha stays where it is.>
		Msg9 <Look, I'm running a business here. I can't just give it back until I've been paid.>
		Msg10 <No way. This is %name1%'s debt, and \SPR %1% 's going to pay it off.>
		Msg11 <Sure, you could do that... I mean, you really want to?>
		Msg12 <You do that.>
		Msg13 <For you? Well, it's a lot of money, but... yeah, I could do that for you.>
		Msg14 <No. Definitely not.>
		Msg15 <Ah, it's been a pleasure doing business with you. You can go tell %name1% that \SPR %1% can pick up \PPR %1% mecha anytime \SPR %1% wants.>
		Msg16 <I didn't think so. Now, if you'll excuse me, I have other customer to attend to.>
		Prompt1 <I'd like to talk about %name1%'s mecha.>
		Prompt2 <I'd like to see what you have for sale.>
		Prompt3 <How much does \SPR %1% owe you?>
		Prompt4 <%name1% can't earn money without a mecha.>
		Prompt5 <%name1% has agreed to pay the money.>
		CPrompt5 <if= NPCVar %2% 1 2 Accept>
		Prompt6 <Why can't you just give the mecha back?>
		CPrompt6 <ifChatNPCSexy ifFlirtation %sktar_hard% Accept>
		Prompt7 <How about I pay off the debt myself?>
		CPrompt7 <ifG PC$ V1 Accept>
		Prompt8 <I'll go talk with %name1% about that.>
		Prompt9 <Couldn't you do it for me?>
		Prompt10 <Yes, I do. [pay $ \VAL V1 ]>
		Prompt11 <No, not really.>

		Metascene 2
		% If the key NPC dies, resolve the quest as a failure.
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <.#...+#.......1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end

	end
	inv
		NPC Shopkeeper
	end


%%
%% *ProveMyself Content
%%
%% The key NPC needs to prove himself, somehow. This may be to make up for
%% some past deed, as a rite of passage, or whatever. In any case the deed
%% done will be risky.
%%
%% To start this subquest, the calling quest should set the QuestID.
%% This quest should end with -1 if the NPC was successful, or -2 if not.
%% It can farm out the QID-setting to a "topper" subquest, if needed.
%%
%% PARAM1: The NPC who will be doing the proving.

Content
	name <Prove Myself vs Derelict Spaceship>
	% A derelict spaceship has been found. A local professor has offered a reward
	% for the first person to locate its cargo hold. The PC can take part in the mission,
	% but will be beaten there by E1... at which point in time the PC can allow him to
	% claim the prize, or fight him and take the prize himself.
	requires <*ProveMyself L5PAT>

	% E1 is the character who must prove himself
	% E2 is the professor offering the quest
	% E3 is the derelect spaceship entrance
	% E4 is the spaceship dungeon
	% E5 is the final goal room
	Element2 <Prefab>
	Scene2 <Building Public>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <Prefab>
	Scene3 <Environs>

	Scene4 <New *Dungeon.Derelict>

	Scene5 <New *CargoHold>

	sub
		Persona 1
		% v%id%01 = Have gotten win speech already
		greeting <if# QuestStatus %qid% -1 else .%id%_GoWinQuest if= QuestStatus %qid% %id% else %pop%  if= SceneVar %s5% 12 1 else .%id%_GoPreDungeon Goto .%id%_GoInDungeon>
		.%id%_GoWinQuest <if= v%id%01 0 else .%id%_GoChat NewChat Say %id%01 v= %id%01 1>
		.%id%_GoPreDungeon <NewChat Say %id%02>
		.%id%_GoInDungeon <if= SceneID %s5% else .%id%_GoBusy IfChatNPCSurrendered else .%id%_GoFirstDungeon AddHeroic 2 Goto .%id%_GoSurrender>
		.%id%_GoFirstDungeon <NewChat Say %id%03 AddChat %id%01 AddChat %id%02>
		*.%id%_GoBusy <*IAmBusy&OnPhone>
		*.%id%_GoChat <*MISC_CHATTER>
		result%id%01 <NewChat Say %id%04   Print %id%07  AddLawful 1 AddRenown -1 SetQuestStatus %qid% -1  MoveAndPacifyNPC %1% %s1%  PCAlly %1%>
		result%id%02 <AddLawful -5 ifIntimidation %sktar_hard% else .%id%_GoStartFight Goto .%id%_GoSurrender>
		.%id%_GoSurrender <EndChat Say %id%05   Print %id%07  SetQuestStatus %qid% -2  MoveAndPacifyNPC %1% %s1%  PCEnemy %1%  L= 13 1  UnSurrender %1%>
		.%id%_GoStartFight <AddHeroic -3 TeamAttack 2 EndChat  Say %id%06  SetQuestStatus %qid% -2  PCEnemy %1%>
		Msg%id%01 <Now that I've completed my first real mission, I feel that I'm ready to be a cavalier!>
		Msg%id%02 <What I really need to prove myself is a real mission. The trouble is, until I've completed a real mission, nobody's willing to hire me...>
		Msg%id%03 <I did it! I found the cargo hold of this spaceship for Professor %name2%, and it seems like I'm the first person here! I can't wait to get back to town and collect my reward.>
		Msg%id%04 <Thanks, \PC . I'll see you back in town.>
		Msg%id%05 <Alright, I see how this is going... I'll let you claim you were here first. The reward's not worth dying over.>
		Msg%id%06 <Do you expect me to give up on this without a fight? I think I can take you...>
		Msg%id%07 <%name1% leaves the area.>
		Prompt%id%01 <Congratulations, I concede victory to you.>
		Prompt%id%01_1 <Congratulations, you earned it.>
		Prompt%id%02 <Too bad you aren't getting out alive.>

		Persona 2
		rumor%id% <%name2% has discovered an old wrecked spaceship, and is offering a reward to whoever explores it.>
		% V1 = Have collected reward
		% V2 = Have initialized quest
		% V3 = E1 dead at start of mission
		greeting <if# SceneVar %s5% 13 0 else GoCheckQuest if= V1 0 else GoChat NewChat Say 1 AddChat 1 AddChat 2>
		GoCheckQuest <if= QuestStatus %qid% -1 else GoCheckBegin if= V1 0 else GoChat NewChat Say 4 v= 1 1>
		GoCheckBegin <if= QuestStatus %qid% %id% else GoNoQuest if= V2 0 else GoRemindQuest NewChat Say 5 QMemo %qid% 6  V= 2 1 Goto GoStartQuest>
		GoRemindQuest <NewChat say 7>
		GoStartQuest <SceneVar= %s5% 12 1  MoveNPC %1% %5%  SetNPCTeam %1% 2  SetQEncActive %3% ifNPCDead %1% V= 3 1>
		*GoNoQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 CashPrize Reward Threat %threat% 100 100 V= 1 1>
		result2 <NewChat Say 3  AddHeroic 1 AddRenown -5   V= 1 1>
		Msg1 <Welcome back. Have you discovered anything of interest?>
		Msg2 <Ah, that's good to hear. Now I have a good place to begin my expedition. Here's a small reward for your trouble.>
		Msg3 <Is that so? In that case, I'll have to prepare a reward.>
		Msg4 <Did you hear? A young adventurer named %name1% has located the cargo hold. My expedition can start soon.>
		Msg5 <I've discovered an old space wreck not far from here. To find out about it, I really need to have a look at its cargo hold. I'm offering a reward for the first person to explore this spaceship and tell me exactly where it is.>
		Msg6 <Professor %name2% is offering a prize for the first person to locate the cargo hold of an old wreck \SPR %2% discovered.>
		Msg7 <I'm still waiting for someone to locate the cargo hold of that derelict spaceship I discovered.>
		Prompt1 <I've located the cargo hold.>
		Prompt2 <%name1% found the cargo hold.>
		CPrompt2 <if= V3 0 Accept>

		MetaScene 4
		sub
			room
			minimap <............2............>
			special <SHAREDPALETTE>
			sub
				Trapdoor
				desig <ENTRANCE %s5%>
				MiniMapComponent 2
			end
		end

		MetaScene 5
		% V11 = Quest initialization counter
		% V12 = SubQuest Status; if V12=1, quest has been initialized.
		% V13 = If nonzero, PC counts as first to cargo hold
		start <if= v11 0 else %pop%  V= 11 1  ifNPCOK %1% else GoStartNPCDead ForceChat %1% Goto %pop%>
		GoStartNPCDead <Print 11  V= 13 1  SetQuestStatus %qid% -2>
		SURRENDER%1% <if= V11 1 SetQuestStatus %qid% %id% Forcechat %1%>
		FAINT%1% <if= V11 1 V= 13 1 SetQuestStatus %qid% -2>
		% If the quest status hasn't been set by the time the scene is exited, do it then.
		End <if= V11 1   V= 11 2   ifNPCOK %1% else GoNPCDead  ifTeamHostile 2 else GoNoHostile  MoveAndPacifyNPC %1% %s1%  SetQuestStatus %qid% -2>
		GoNoHostile <if= QuestStatus %qid% %id%  MoveAndPacifyNPC %1% %s1%  SetQuestStatus %qid% -1>
		GoNPCDead <if= QuestStatus %qid% %id%  SetQuestStatus %qid% -2 V= 13 1>
		Msg11 <You enter the cargo hold. This must be the place that %name2% was searching for.>
		sub
			room
			minimap <......###..#2#...........>
			special <SHAREDPALETTE>
			sub
				Elevator
				name <Airlock>
				MiniMapComponent 2
				use <ifYesNo 1 2 3 Print 4 Exit %s3%>
				Msg1 <This airlock opens to the outside of the spaceship.>
				Msg2 <Leave the derelict.>
				Msg3 <Stay here for now.>
				Msg4 <You leave the derelict.>
			end
		end
	end
	inv
		NPC Professor

		STC QUEST-MAPMARKER-STATIONARY
		name <Derelict Spaceship>
		desig <Entrance %s4%>
	end

Content
	name <Prove Myself Vs Pirates>
	requires <*ProveMyself Spinner -1:CRIHN>

	% E1 is the character who must prove himself.
	% E2 is the character offering a bounty on pirates
	% E3 is the pirate combat zone
	% E4 is the entrance to the combat zone
	% E5 is the black market unlocked by this quest
	Element2 <Prefab>
	Scene2 <Building Public>
	Team2 <Citizens>
	TeamData2 <Ally Pass>

	Scene3 <New *BLANK.Space.Mecha>

	Element4 <Prefab>
	Scene4 <Space Environs>

	Element5 <Prefab>
	Scene5 <New *BlackMarket>
	Team5 <Citizens>
	TeamData5 <Ally Pass>

	Quest1 <*ThankYou 1>

	sub
		Persona 1
		greeting <if= QuestStatus %qid% %id% else %pop% if= SceneVar %3% 1 2 else .%id%_GoCheckBattle NewChat Say %id%01 AddChat %id%01>
		.%id%_GoCheckBattle <if= SceneVar %3% 1 1 else .%id%_GoCheckBusy EndChat Say %id%03 AddChat %id%02 AddChat %id%03>
		.%id%_GoCheckBusy <if# SceneVar %3% 2 0 else .%id%_GoFirst NewChat Say %id%02>
		*.%id%_GoFirst <*IMustProveMyself>
		Result%id%01 <Goto .%id1%_GoInit>
		*Result%id%02 <*LetsDefeatThem>
		Result%id%03 <EndChat Say %id%04>
		Msg%id%01 <We did it! I've survived a real mecha fight. I feel like I could take on the world!>
		Msg%id%02 <I'm busy right now.>
		Msg%id%03 <\PC ! Look, I've found the pirates that %name2% was looking for!>
		Msg%id%04 <No way. I have to prove myself... if I leave now, will I be running forever?>
		Prompt%id%01 <Don't get too excited.>
		Prompt%id%02 <I'll help you out.>
		Prompt%id%02_1 <I'll back you up.>
		Prompt%id%03 <Get out of here; it's too dangerous.>

		Persona 2
		rumor%id% <%name2% needs a mecha pilot to fight some pirates.>
		% This bounty hunter will give the PC a regular mission test, but it
		% doesn't matter whether the PC passes or not: Merely talking to this
		% character will set the plot into motion.
		% V1 = Initialization Counter
		% V2 = Have offered mission
		greeting <if= V1 0 else GoGreet2 V= 1 1 MoveNPC %1% %3% SetNPCTeam %1% 3 SceneVar= %3% 2 1 SetQEncActive %4% Goto GoGreet2>
		GoGreet2 <if= V2 1 else GoCheckOffer if= SceneVar %3% 1 2 else GoCheckLoss NewChat Say %id%01 CashPrize Reward Threat %threat% 100 100 V= 2 2>
		GoCheckLoss <if= SceneVar %3% 1 3 else GoMissionReminder NewChat Say %id%02 V= 2 2>
		GoMissionReminder <NewChat Say %id%03>
		GoCheckOffer <if= SceneVar %3% 1 0 else GoAlreadyFought if= QuestStatus %qid% %id% else GoNoQuest Goto GoCheckAuto>
		GoAlreadyFought <V= 2 2 Goto GoNoQuest>
		*GoCheckAuto <*AutoMissionTest&Mecha GoMissionBriefing GoRejectMission GoCheckSkill na 8>
		*GoCheckSkill <*GenericMissionTest&Mecha  GoMissionBriefing na GoRejectMission na 8>
		*GoRejectMission <*RejectMission na>
		GoMissionBriefing <NewChat Say %id%04 QMemo %qid% %id%05>
		*GoNoQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		Msg%id%01 <The pirates have been defeated. Here's the reward you earned.>
		Msg%id%02 <You failed to defeat the pirates... now that they've moved camp, it's going to take ages to find them again.>
		Msg%id%03 <There are pirates on the loose in \SCENE %s4% . I'm still waiting for someone to take me up on the reward.>
		Msg%id%04 <There's a group of pirates hiding in \SCENE %s4% . If you could defeat them, there'd be a reward in it for you.>
		Msg%id%05 <%name2% asked you to defeat some pirates in \SCENE %s4% .>

		MetaScene 3
		% V1 = Initialization Counter
		% The first time the PC enters this scene, it should be for the battle.
		% Otherwise nothing will happen.
		%  1: Battle in progress
		%  2: PC has won against pirates
		%  3: PC has lost against pirates
		% V2 = Trigger Set
		% The NPC has been moved, everything should be ready for combat.
		start <if= V1 0 else GoBeenBefore V= 1 1 ifNPCInPlay %1% ForceChat %1%>
		GoBeenBefore <>
		nu1 <if= t1 0 Return if= L1 1 L= 1 3 SetQuestStatus %qid% -2 Goto GoMoveNPC>
		nu2 <if= t2 0 if= L1 1 L= 1 2 ifNPCInPlay %1% else GoNotInPlay  ForceChat %1% Goto GoMoveNPC>
		GoNotInPlay <SetQuestStatus %qid% -2 Goto GoMoveNPC>
		GoMoveNPC <ifNPCOK %1% MoveNPC %1% %s1%>
		sub
			Team 1
			name <Player Team>
			SetEnemy 2
			SetAlly 3

			Team 2
			name <Enemy Team>
			SetEnemy 1 3
			SetFaction 8
			Deploy <if= L1 0 WMecha 2 Threat %threat% 220>

			Team 3
			name <E3 Team>
			SetAlly 1
			SetEnemy 2
		end
		inv
			stc DERELICT-1
			name <Shady Microstation>
			desig <Entrance %s5%>
		end

		Persona 5
		*greeting <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
	end
	inv
		NPC Bounty Hunter
		EquipChar 35000
		CharDesc Lawful

		STC QUEST-ENCOUNTER-WANDER
		name <Red Rover Pirates>
		desig <entrance %s3%>
		% After fighting the pirates, this encounter becomes perminantly visible.
		update <ifThisQEncActive else GoHide  SetStat STAT_MetaVisibility 0  if# SceneVar %3% 1 0 else GoSetOrders ShowEncounter Goto GoSetOrders>
		%% This encounter will only attack before the PC fights the pirates
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifG 1 SceneVar %3% 1   ifUStealth 15 else GoAutoAttack ifYesNo -3 -4 -5 else GoAvoidAttack Goto GoAutoAttack>

		NPC Pirate
		SetFaction 8
		EquipChar 25000
	end

%%
%% *:StolenMaterials Content
%%		&ConstructionSite = The materials stolen from a construction site
%%
%% Several tons of crap have been stolen from E1. The PC will be sent to get
%% them back, or may possibly collude with whoever stole them, or may even
%% steal a bit himself... Set the QuestID to -1 if the goods are returned to
%% their rightful owners, or to -2 if the materials are perminantly removed.
%% To start this subquest, the calling quest should set its QuestId to its
%% component ID.
%%
%% Param1: The boss NPC who sent the PC to find the problem

Content
	name <Monsters Ate It>
	requires <*:StolenMaterials &ConstructionSite>

	% Element1 is the NPC who lost the materials
	% Element2 is the coworker who knows what's happening
	% Element3 is the scene full of monsters

	Element2 <Prefab>
	Scene2 <Grab 1>
	Team2 <Citizens>
	TeamData2 <Pass>

	Scene3 <New *ExterminationBasement>

	sub
		Persona 2
		% V1 = Have given info/Have given thanks
		rumor%id% <%name2% knows something about %name1%'s construction problem.>
		greeting <if= QuestStatus %qid% -1 else GoCheckMission if= V1 1 else GoJustChat V= 1 2 NewChat Say 5 AddReact d10>
		GoCheckMission <if= QuestStatus %qid% %id% else GoJustChat if= V1 1 else GoFirstTime >
		*GoFirstTime <*IHaveInformation GoPresentInfo %1%>
		GoPresentInfo <NewChat Say 1 QMemo %qid% 6 V= 1 1 AddChat 1 AddChat 2>
		*GoJustChat <*MISC_CHATTER>

		Result1 <NewChat Say 2 AddChat 3>
		Result2 <NewChat Say 3 AddChat 3>
		result3 <NewChat Say 4>

		Msg1 <The materials that disappeared from the job site? They weren't stolen... something ate them.>
		Msg2 <I've seen them... I told some of the other workers, they thought I'd been drinking, but they're definitely there.>
		Msg3 <I can't exactly say what they were, but I expect some kind of a mutated beaver crossbred with a space fungus.>
		Msg4 <I've seen them all around, but the most common place is in \SCENE %s3% . The way down is over there.>
		Msg5 <I haven't seen any of those creatures in a while... I guess you must have cleared them up.>
		Msg6 <%name2% told you that creatures in the sub-basement are eating %name1%'s supplies.>

		Prompt1 <How do you know that?>
		Prompt2 <What kind of creature would eat plastic sheeting?>
		Prompt3 <Where can I find these creatures?>

		Metascene 2
		start <if= SceneVar %s3% 1 1 else %pop% SetQuestStatus %qid% -1 Goto %pop%>
		sub
			room
			minimap <......###..#2#........1..>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Elevator
				name <Sub-Basement>
				desig <Entrance %s3%>
				MiniMapComponent 2
				use <if= QuestStatus %qid% %id% else GoLocked Print 1 Exit Destination>
				GoLocked <Print 2>
				Msg2 <There's nothing interesting in here.>
			end
		end
	end
	inv
		NPC Construction Worker
	end

Content
	name <Pirates Took It>
	requires <*:StolenMaterials Spinner>

	% Element1 is the NPC who lost the materials
	% Element2 is a crooked shopkeeper who's been selling them
	% Element3 is the asteroid microstation where the materials are being stored
	% Element4 is the entrance to the scene, activated by E2

	element2 <Prefab>
	scene2 <Building Public Mall>
	Team2 <Citizens>
	TeamData2 <Pass>

	element3 <Prefab>
	scene3 <New *BLANK.Space.Mecha>

	element4 <Prefab>
	scene4 <Space Environs>

	sub
		Persona 1
		% V%id%01 = Have told about pirate base
		greeting <if= QuestStatus %qid% %id% else %pop% ifQEncActive %4% else %pop% if= V%id%01 0 else %pop% NewChat say %id%01 AddChat %id%01>
		result%id%01 <NewChat Say %id%02 V= %id%01 1>
		msg%id%01 <Any news on those missing supplies, yet?>
		msg%id%01_1 <You've been investigating this matter for a while. Have you found any leads on my missing supplies?>
		Cmsg%id%01_1 <ifNPCSociable Accept>
		msg%id%01_2 <Do you have anything to report?>
		Cmsg%id%01_2 <ifNPCShy Accept>
		msg%id%01_3 <So, have you found anything about the stuff I lost yet?>
		Cmsg%id%01_3 <ifNPCEasygoing Accept>
		msg%id%01_4 <Have you found my things yet? It's vitally important that they be returned at once...>
		Cmsg%id%01_4 <ifNPCPassionate Accept>
		msg%id%01_5 <I'm kind of enjoying my unintentional vacation, but have you learned anything about the goods that disappeared yet?>
		Cmsg%id%01_5 <ifNPCCheerful Accept>
		msg%id%01_6 <Have you found anything about the stuff I had stolen yet? If the materials aren't returned soon, I'll be ruined.>
		Cmsg%id%01_6 <ifNPCMelancholy Accept>
		msg%id%02 <In that case, you should try to find where they've hidden their loot and see if you can get it back.>
		msg%id%02_1 <I don't suppose these pirates will be in any mood to negotiate... and in any event, doing so would only embolden them to make further attacks. See if you can locate their base, and take back my property by any means nessecary.>
		Cmsg%id%02_1 <ifNPCSociable Accept>
		msg%id%02_2 <Locate these pilots and recover our property.>
		Cmsg%id%02_2 <ifNPCShy Accept>
		msg%id%02_3 <Well, it seems to me that this is a step in the right direction. All you have to do now is find the pirates and take my things back from them.>
		Cmsg%id%02_3 <ifNPCEasygoing Accept>
		msg%id%02_4 <In that case, why aren't you out looking for those pirates? Hurry, this might be the only chance we have to recover my supplies!>
		Cmsg%id%02_4 <ifNPCPassionate Accept>
		msg%id%02_5 <Great work, \PC ! Now all you have to do is locate these pirates and win the supplies back from them. It shouldn't be too tough, should it?>
		Cmsg%id%02_5 <ifNPCCheerful Accept>
		msg%id%02_6 <If only we could find where the pirates are keeping their loot, we might be able to get some of it back. I suppose it's too late for any of that already...>
		Cmsg%id%02_6 <ifNPCMelancholy Accept>
		Prompt%id%01 <I heard they've been stolen by pirates.>

		Persona 2
		rumor%id% <%name2% has been selling off mecha parts and repair supplies cheap.>
		% V1 = Have revealed the pirate base
		% V2 = Have tried to bluff the shopkeeper
		greeting <if= QuestSTatus %qid% %id% else GoGreet if= V1 0 else GoGreet NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoGreet <*NiceToMeetYou GoOpenShop>
		*GoOpenShop <*SHOP_MECHA GoGoodbye>
		*GoGoodbye <*GOODBYE>
		GoRevealBase <V= 1 1 SetQEncActive %4% QMemo %qid% 6>
		result1 <NewChat Say 3 AddChat 4 AddChat 5>
		result2 <Goto GoOpenShop>
		result3 <EndChat Say 2 Shop .wares Goto GoGoodbye>
		.wares <TOOL MEXTRA CONTRABAND>
		result4 <NewChat Say 4 AddChat 6 AddChat 7>
		result5 <V= 2 1 ifConversation SkillTar %threat% else GoR5Fail NewChat Say 5 Goto GoRevealBase>
		GoR5Fail <NewChat Say 7>
		result6 <NewChat Say 8 Goto GoRevealBase>
		result7 <NewChat Say 9 AddChat 8 AddChat 9>
		result8 <NewChat Say 10 SetQuestStatus %qid% -2 AddLawful -5 RandomMecha .mekfac %threat%>
		.mekfac <GENERAL>
		result9 <Goto result6>
		Msg1 <Hey, can I interest you in some great parts at a low low price?>
		Msg2 <Take a look through, I don't even know what all of this stuff is... If you notice the serial numbers filed off of anything that's just because it's, uh, a factory second. That's it.>
		Msg3 <I've got connections, you know. One of my associates recently came into a large quantity of, er, swag... I've been helping him find its way to market.>
		Msg4 <I didn't steal anything! And if I happen to be selling stolen goods then that's hardly my fault! Look, I'm sure we can come to some kind of deal...>
		Msg5 <I get this stuff from a pirate I know. They've got an asteroid base just outside the colony. You could try making a deal with him, but I should warn you that they aren't usually friendly to strangers.>
		Msg6 <%name2% told you there are pirates outside the spinner who may have stolen %name1%'s materials.>
		Msg7 <I'm afraid you're stuck, then. My supplier doesn't deal with the general public.>
		Msg8 <I don't know his name... There are some pirates who have an asteroid base just outside the station. I get this stuff from them... Now leave me alone, and don't cause any more headaches.>
		Msg9 <Well, look around. I've got a fortune of junk here. If you'll agree to keep hush about where it came from, I'll let you take your pick of the lot, no charge.>
		Msg10 <It's been a pleasure doing business with you.>
		Prompt1 <Where did you get all this junk?>
		Prompt2 <No, but I'd like to see what else you have.>
		Prompt3 <Sure, let me take a look.>
		Prompt4 <You stole all this from %name1%, didn't you?>
		Prompt5 <I need to buy more than you have in stock.>
		CPrompt5 <if= V2 0 Accept>
		Prompt6 <Tell me who your supplier is.>
		Prompt7 <What kind of deal are you thinking about?>
		Prompt8 <Sounds good. I'll do that.>
		Prompt9 <No deal. Tell me who your supplier is.>

		MetaScene 2
		% If the NPC is killed, automatically reveal the asteroid base. This is
		% kind of a strange thing to do but it'll prevent quest lockup.
		start <ifNPCDead %2% else %pop% if= QuestStatus %qid% %id% else %pop% SetQEncActive %4% Goto %pop%>
		sub
			room
			minimap <..............1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end

		MetaScene 3
		% The blank space scene with the asteroid base.
		%% Because it's a blank, this metascene holds all the important stuff.
		% L1 - Initialization counter
		% L2 = Victory Counter
		% L3 = Attack recharge timer for encounter
		start <if= L1 0 else GoBeenBefore Print 1 L= 1 1>
		GoBeenBefore <if= L2 0 Print 2>

		nu1 <if= T1 0 Return  if= L2 0    AddRenown -1>
		nu2 <if= T2 0         if= L2 0    L= 2 1   AddRenown 1  Print 3  XPV 100   Salvage   SetQuestStatus %qid% -1>

		Msg1 <You spot the asteroid base that %name2% must have been talking about.>
		Msg2 <The pirates have regrouped, and challenge you again.>
		Msg3 <You've defeated all the pirates, and can return %name1%'s materials now.>

		sub
			Team 1
			name <Player Team>
			ParaX 5
			ParaY 5
			SetEnemy 2

			Team 2
			name <Enemies>
			SetFaction 8
			Deploy <if= L2 0   WMecha 2 Threat %threat% 100  L= 3 ComTime L+ 3 43200>
			ParaX 25
			ParaY 25
			SetEnemy 1
		end

	end
	inv
		NPC Shopkeeper
		chardesc Criminal
		skill 21 7

		STC DERELICT-1
		name <Asteroid Station>
		use <Print 1>
		Msg1 <This rudimentary station appears to contain all of %name1%'s building supplies.>

		STC QUEST-ENCOUNTER-STATIONARY
		name <Asteroid Base>
		desig <ENTRANCE %s3%>
		ATTACK <ifG StatVal STAT_MetaVisibility -1   if= SceneVar %s3% 2 0   ifG ComTime SceneVar %s3% 3    Alert 2 Exit Destination>
		Msg2 <You are attacked by pirates!>
		ENCOUNTER_HOSTILE
	end

%%
%% *:ConstructionProblem Content
%%
%% There's a problem at a construction site. To start this subquest, the calling
%% quest should set its QuestId to its component ID. At the end the QuestID will
%% be set to -1 if the construction problem was solved, or -2 if the construction
%% problem has been rendered unsolvable.
%%
%% Param1: The boss NPC who sent the PC to find the problem
%% Param2: Construction Site Scene

Content
	name <Stupid Fungus>
	requires <*:ConstructionProblem (Spinner|Asteroid)>
	% The construction site has been infested with spaceworms, and the boss hasn't
	% provided the funds for extermination.

	% E1 is the boss
	% E2 is the construction site
	% E3 is the foreman
	% E4 is the extermination scene
	element3 <Prefab>
	scene3 <Grab 2>
	team3 <Citizens>
	teamdata3 <Pass Ally>

	scene4 <New *BLANK.Basement>

	sub
		% Both the boss and the foreman get speaking roles this time around- if the
		% PC returns to speak with the boss, he may be able to convince him to pay for
		% a sensible de-fung'ing of the construction site.
		Persona 1
		% V%id%01 = Have argued the point already
		% If the PC has spoken to character 3, then he can talk to E1 and maybe get money.
		greeting <if= QuestStatus %qid% %id% else %pop% if= NPCVar %3% 1 1 else %pop% Goto .%id%_GoArgue>
		.%id%_GoArgue <if= v%id%01 0 else .%id%_GoArgue2 NewChat Say %id%01 V= %id%01 1 AddChat %id%01>
		.%id%_GoArgue2 <NewChat Say %id%02 AddChat %id%02>
		result%id%01 <NewChat Say %id%03 AddChat %id%03>
		result%id%02 <NewChat Say %id%04 AddChat %id%04 AddChat %id%05 AddChat %id%06>
		result%id%03 <Goto result%id%02>
		result%id%04 <NewChat Say %id%05>
		result%id%05 <NewChat Say %id%06 NPCVar= %3% 1 2>
		result%id%06 <NewChat Say %id%07 NPCVar= %3% 1 2>
		Msg%id%01 <Have you found out from %name3% why construction has been delayed?>
		Msg%id%02 <I'm still waiting for %name3% to finish construction.>
		Msg%id%03 <That shouldn't be a big problem. I already provided funds for an exterminator.>
		Msg%id%04 <This project is over budget already. There must be a better way... You're a cavalier, couldn't you just go kill all the fungus?>
		Msg%id%05 <You do that.>
		Msg%id%06 <You're probably right... some added expense at this point will save us headaches down the road. Tell %name3% that I'll arrange for a more thorough extermination.>
		Msg%id%07 <Seriously? In that case, I guess there's nothing I can do... Tell %name3% that I'll provide extra funds for extermination.>
		Prompt%id%01 <There's a fungus infestation at the site.>
		Prompt%id%02 <%name3%'s still waiting for the exterminator.>
		Prompt%id%03 <Apparently, the funds weren't enough.>
		Prompt%id%04 <I'll see what I can do.>
		Prompt%id%05 <Really, I think we need a professional.>
		CPrompt%id%05 <ifConversation %sktar_hard% Accept>
		Prompt%id%06 <Without proper treatment they'll be back in a week.>
		CPrompt%id%06 <ifScience %sktar% Accept>

		Persona 3
		% NPCVar %3% 1 = Have told about problem (1), have secured funding (2)
		% V%id%01 = Have discussed problem
		% V%id%02 = PC is a biotech expert qualified to do the extermination
		rumor%id% <%name3% has been having problems with %name1%'s construction project.>
		greeting <if= QuestStatus %qid% %id% else GoChat  if= v%id%01 1 else GoFirstTime NewChat Say 10 AddChat 9 AddChat 10 AddChat 11>
		GoFirstTime <NewChat Say 1 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 V= %id%01 1 NPCVar= %3% 1 1 QMemo %qid% 6 AddChat 3 AddChat 4 AddChat 5>
		result2 <AddEasygoing -d8 Goto result1>
		result3 <NewChat Say 3 AddChat 6 AddChat 7 AddChat 8>
		result4 <NewChat Say 4>
		result5 <NewChat Say 5>
		result6 <NewChat Say 7>
		result7 <NewChat Say 8 V= %id%02 1>
		result8 <NewChat Say 9>
		result9 <NewChat Say 11>
		result10 <NewChat Say 12 XPV 100 SetQuestStatus %qid% -1>
		result11 <SetQuestStatus %qid% -1  if= v%id%02 1 else GoR11Fail NewChat Say 12 XPV 100>
		GoR11Fail <NewChat Say 13 XPV 50>
		Msg1 <Yes, were you sent here by %name1%? It's about time that something was done...>
		Msg2 <Construction has halted due to a fungus infestation. It's not even safe for the workbots to go there. %name1% hired an exterminator once, but now the fungi are back and stronger than ever.>
		Msg3 <I don't think that's a good idea. We already tried that, remember? After a while the fungus came back... What we really need is some kind of quasinatural organism expert to clear the place out.>
		Msg4 <I think that would be best. Let me know what happens.>
		Msg5 <Come back and let me know if you find some way to help.>
		Msg6 <%name3% revealed that %name1%'s construction site is infested with space fungus.>
		Msg7 <Fine. Do whatever you think is best... Come back and let me know when we can get back to work.>
		Msg8 <Really? In that case, you should be qualified to handle the extermination. Come back and let me know when you're satisfied that the construction zone is safe.>
		Msg9 <I know I am. If only %name3% were as reasonable as you...>
		Msg10 <We will be able to begin construction as soon as the fungi are cleared out. Do you have any progress to report?>
		Msg11 <Well, come see me when you do.>
		Msg12 <Fantastic. You can tell %name1% that construction will resume as soon as possible.>
		Msg13 <Thanks. Hopefully this time they'll stay gone, at least long enough for us to finish the work. You can go tell %name1% that construction will resume shortly.>
		Prompt1 <Hold on, what's the matter?>
		Prompt2 <I'm here. Everything will be fine now.>
		Prompt3 <Why don't I just kill the fungi for you?>
		Prompt4 <I'll talk to %name1% about the exterminator.>
		Prompt5 <Let me see what I can do.>
		Prompt6 <We don't need no stinking expert!>
		Prompt7 <Hey, I'm an expert on quasinatural organisms.>
		CPrompt7 <ifBiotech %sktar% Accept>
		Prompt8 <You're probably right.>
		Prompt9 <Nothing yet.>
		Prompt10 <%name1% has agreed to provide extra funds.>
		CPrompt10 <if= NPCVar %3% 1 2 Accept>
		Prompt11 <I've exterminated all the fungus myself.>
		CPrompt11 <if= SceneVar %s4% 1 1 Accept>


		MetaScene 3
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %3% else %pop% SetQuestSTatus %qid% -2 Goto %pop%>
		end   <if= QuestStatus %qid% %id% else %pop% ifNPCDead %3% else %pop% SetQuestSTatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <......###..#2#........1..>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Elevator
				name <Sub-Basement>
				desig <Entrance %s4%>
				MiniMapComponent 2
				use <if= V1 1 else GoLocked Print 1 Exit Destination>
				Update <if= QuestStatus %qid% %id% V= 1 1>
				GoLocked <Print 2>
				Msg2 <There's nothing interesting in here.>
			end
		end

		MetaScene 4
		% L1 = Victory Indicator
		% L2 = Initialization Counter
		content <Fill Sub *STORAGE_X na>
		% Do two generations of monsters- one regular, and one at double strength.
		% Clearing out this area by brute force is going to be a challenge.
		Start <Print 1 if= L2 0 if= QuestStatus %qid% %id% L= 2 1 WMonster 2 * 2 %threat% WMonster 2 %threat%>
		nu2 <if= t2 0 if= L2 1 if= L1 0 Print 2 XPV 100 L= 1 1>
		Msg1 <You enter the basement.>
		Msg2 <That seems to be the last of the fungi.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monsters>
			type <Fungus>
			SetEnemy 1
		end

	end
	inv
		NPC Construction Worker
		job <Foreman>
	end


Content
	name <Supplies Went Missing>
	requires <*:ConstructionProblem BRANCHES>
	% The trouble with construction is that someone stole the materials!

	% E1 is the boss
	% E2 is the construction site
	% E3 is the foreman
	element3 <Prefab>
	scene3 <Grab 2>
	team3 <Citizens>
	teamdata3 <Pass Ally>

	Quest1 <*:StolenMaterials&ConstructionSite 3>

	sub
		Persona 3
		% V1 = Have revealed problem
		greeting <if= QuestStatus %qid% %id% else GoChat if= V1 1 else GoFirstTime NewChat Say 3 AddChat 2 AddChat 5>
		GoFirstTime <NewChat Say 1 AddChat 1>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat V= 1 1 Say 2 AddChat 3 AddChat 4 SetQuestStatus %qid1% %id1% QMemo %qid% 4>
		result2 <NewChat Say 5 SetQuestStatus %qid% -1 ifG React %threat% PCFriend %3%>
		result3 <NewChat Say 6>
		result4 <NewChat Say 7 AddCheerful -d10>
		result5 <NewChat Say 8 SetQuestStatus %qid% -2>
		Msg1 <Yes, may I help you? We're very busy here right now.>
		Msg2 <I wish I could tell you that things are proceeding well, but they aren't. The trouble is, we've been losing building materials.>
		Msg3 <Have you found anything about the missing building materials?>
		Msg4 <%name3% revealed that materials are being stolen from %name1%'s construction site.>
		Msg5 <Fantastic! We'll be able to get back to work soon... You should report to %name1% .>
		Msg6 <Ask around and see if you can uncover any clues. I'll help you in whatever way I can.>
		Msg7 <If you can't find the materials and bring them back, then it'll be both of us who have failed, won't it? Search around and see what you can find out. I'll keep working on the project.>
		Msg8 <The our project is lost... report back to %name1%.>
		Prompt1 <I was send to hurry up the construction.>
		Prompt1_1 <%name1% sent me to check your progress.>
		Prompt2 <I've solved your materials problem.>
		CPrompt2 <if= QuestStatus %qid1% -1 Accept>
		Prompt3 <I could find out what happened to them.>
		Prompt4 <%name1% won't be pleased at your failure.>
		Prompt5 <I think you better give up on those supplies...>
		CPrompt5 <if= QuestStatus %qid1% -2 Accept>
	end
	inv
		NPC Construction Worker
		job <Foreman>
	end

%%
%% *SuccessGetItem Content
%%
%% The PC has retrieved E2 for NPC1.
%% The NPC who provided the task will now either reward the
%% PC or provide an additional task, or maybe both.
%% Note that by the time of this event E2 may very well be destroyed,
%% depending on what has come before.
%%
%% Param1: The character for whom the PC was working.
%%   Persona 1 must have a .%id%_GoInit script.
%% Param2: The item being given.
%%
%% SubTypes:
%% .giveitem	PC gave the item to the NPC
%% .keepitem	PC didn't give the item to the NPC

Content
	name <Good job, let's study>
	%% The PC will gain a new teacher.
	requires <*SuccessGetItem (1:MILIT|1:ADVEN)>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the item that was given.

	sub
		Persona 1
		Greeting <if= QuestStatus %qid% -1 else %pop% ifChatNPCArchEnemy else .%id%_GoNoEnemy SetQuestStatus %qid% -2 Goto .%id%_GoBlowOff>
		*.%id%_GoBlowOff <*BrushOff>
		.%id%_GoNoEnemy <Say %id%02 AddChat %id%01 AddChat %id%02>
		.%id%_GoInit <NewChat Say %id%01 SetQuestStatus %qid% -1 AddChat %id%03>
		*.%id%_GoBye <*GOODBYE>
		result%id%01 <EndChat Say %id%03 School .%id%_skills Goto .%id%_GoBye>
		.%id%_skills <1 2 3 4 5 6 7 8 9 10 11 12>
		result%id%02 <NewChat Say Msg%id%04>
		result%id%03 <Goto result%id%01>
		Msg%id%01 <So this is the %name2%. You've proven yourself to be a great cavalier... If you want, I could train you to be even better.>
		Msg%id%02 <Hello \PC . Are you in the mood for some training?>
		Msg%id%03 <You're getting better all the time, but there are a few tricks you haven't picked up yet.>
		Msg%id%04 <Okay. I'll be here when you need me.>
		Prompt%id%01 <Absolutely. Let's go.>
		Prompt%id%02 <Not today, thanks.>
		Prompt%id%03 <Let's get started right away.>
	end

Content
	name <Basic Keep Item Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessGetItem .keepitem>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the item that was given.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward Threat %threat% 100 80 SetQuestStatus %qid% -1>
		Msg%id%01 <So this is it. Thanks for showing it to me... Here's some cash to help finance your next expedition, wherever that leads.>
	end

Content
	name <Basic Give Item Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessGetItem .giveitem>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the item that was given.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward Threat %threat% 100 200 SetQuestStatus %qid% -1>
		Msg%id%01 <So this is the %name2% . Here's a reward for bringing it to me.>
	end


%%
%% *SuccessVsChar Content
%%
%% The PC has completed a task of some kind against NPC2.
%% The NPC who provided the task will now either reward the
%% PC or provide an additional task, or maybe both.
%% Note that by the time of this event E2 may very well be dead,
%% depending on what has come before.
%%
%% Param1: The character for whom the PC was working.
%%   Persona 1 must have a .%id%_GoInit script.
%% Param2: The character against whom the PC was working.
%%
%% SubTypes:
%% .Evidence		The PC has brought evidence implicating E2 in a crime.
%% .ConJob		E2 has been conned by E1 and the PC.
%% .FoilPlot		The PC foiled a plot by E2 against E1.
%% .Capture		The PC has captured E2

Content
	name <Basic vs. Reward>
	%% The PC will be given a reward and that's it.
	requires <*SuccessVsChar>
	special <REUSABLE>

	%% E1 is the character to give the reward.
	%% E2 is the character who's been defeated.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward Threat %threat% 100 200 SetQuestStatus %qid% -1>
		Msg%id%01 <%name2% has been defeated! Here's a reward for your help.>
	end


%%
%% *Success1 Content
%%
%% The PC has completed a task of some kind. The NPC who provided the
%% task will now either reward the PC or provide an additional task,
%% or maybe both.
%%
%% Param1: The character for whom the PC was working.
%%   Persona 1 must have a .%id%_GoInit script.
%%
%% SubTypes:
%% .AgainstPirates	PC has defeated a pirate or pirates
%% .Extermination	PC has cleared an area of monsters
%% .FinishMission	PC has ridden this metaphorical horse as far as it'll go
%% .CriminalTransaction	PC has performed some kind of illegal business deal
%% .BusinessDeal	PC has done some kind of business for/with NPC
%% .TipOff              PC has provided useful information
%% .CharityWork		PC has done charity work, presumably without expecting a reward

Content
	name <You're good, now go fetch>
	requires <*Success1 (.BusinessDeal|.TipOff) (1:THIEF|1:ACADE) BRANCHES>

	%% E1 is the character previously helped.
	%% E2 is the item to be sought
	%%  S2 is the scene where it is hidden
	Element2 <Prefab>
	Scene2 <New *ItemCrypt>

	%% Q1 is the success branch for locating the item
	Quest1 <*SuccessGetItem.giveitem 1 2>

	sub
		Persona 1
		% V%id%01 = Have accepted quest.
		Greeting <if= QuestStatus %qid% %id% else %pop% ifChatNPCArchEnemy else GoNotEnemy SetQuestStatus %qid% -2 Goto .%id%_GoBlowOff>
		*%id%_GoBlowOff <*BrushOff>
		GoNotEnemy <NewChat Say %id%05 AddChat %id%03 AddChat %id%04 AddChat %id%05 AddChat %id%06>
		.%id%_GoInit <SetQuestStatus %qid% %id% Goto GoOfferQuest NewChat Say %id%01 AddChat %id%01 AddChat %id%02 CashPrize Threat %threat% 100 150 QMemo %qid% %id%02>
		result%id%01 <NewChat Say %id%03>
		result%id%02 <NewChat Say %id%04 SetQuestStatus %qid% -2>
		result%id%03 <SetQuestStatus %qid% -2  Goto .%id%_GoR7Final>
		*.%id%_GoR3Final <*YouLostItem>
		result%id%04 <SetQuestStatus %qid% -2 PCEnemy %1% Goto .%id%_Go04Finish>
		*.%id%_Go04Finish <*PCWillKeepItem %2%>
		result%id%05 <AddRenown 3   AddReact d5   DeleteItem %2%   Goto .%id1%_GoInit>
		result%id%06 <NewChat Say %id%06>
		Msg%id%01 <You've been a real help, \PC ... I may have some work for you. I'm looking for a sculpture known as "The Heart of Gold". If you could bring it to me, I'll give you another reward.>
		Msg%id%02 <%name1% in \SCENE %s1% asked you to locate the Heart of Gold, a supposed work of art.>
		Msg%id%03 <Very good. According to rumor, the Heart of Gold was last seen nearby. Come back and talk to me after you've located it.>
		Msg%id%04 <It's up to you. I'll probably find it myself, given enough time...>
		Msg%id%05 <Any luck finding the Heart of Gold?>
		Msg%id%06 <It's still out there, somewhere... Don't give up until you've found it.>
		Prompt%id%01 <I'll see what I can do.>
		Prompt%id%01_1 <I'll be back when I've found it.>
		Prompt%id%02 <No. I'm finished working for you.>
		Prompt%id%02_1 <No thanks. I'm out of here.>
		Prompt%id%03 <I'm afraid that I lost it...>
		CPrompt%id%03 <ifItemDestroyed %2% Accept>
		Prompt%id%04 <I've decided to keep it myself.>
		CPrompt%id%05 <ifKeyItem %2% ifChatNPCInPlay Accept>
		Prompt%id%05 <Here, I brought it to you.>
		CPrompt%id%05 <ifKeyItem %2% ifChatNPCInPlay Accept>
		Prompt%id%06 <I'm still looking for it.>
		CPrompt%id%06 <ifItemOK %2% Accept>
	end
	inv
		Treasure 100
		name <Heart of Gold>
		desc <A golden lump shaped vaguely like a heart. Some might call it art.>
		Mass 7
	end

Content
	name <Thanks for the Help>
	requires <*Success1 .CharityWork>
	special <REUSABLE>

	%% E1 is the character previously helped.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward Threat %threat% 100 100 AddHeroic 1 SetQuestStatus %qid% -1>
		Msg%id%01 <Thank you so much for the help. Here's a small gift, in appreciation of what you've done... Please take it.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <I'm not good with words but maybe this will speak better. I'd like to give you this small gift, in thanks for what you've done.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <Thanks for partially restoring my faith in humanity. Here's a small token of my appreciation.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
	end


Content
	name <How About A Quest?>
	requires <*Success1 (.FinishMission|.CharityWork) (1:ADVEN|1:MILIT|1:FAITH) ~1:SILKN BRANCHES>
	%% The PC has just completed a job for the NPC. The NPC will now suggest that
	%% the player has what it takes to go on a dangerous quest... if asked about
	%% it, the NPC will ask the PC to retrieve an item.

	%% E1 is the character previously helped.
	%% E2 is the artifact to be sought
	%% E3 is the city this quest is located in
	%%  S2 is the scene where it is hidden
	Element2 <ARTIFACT ITEM PREZERO>
	Scene2 <New *ItemCrypt>
	Element3 <Scene town>

	%% Q1 is the success branch for locating the item
	Quest1 <*SuccessGetItem.keepitem 1 2>

	sub
		Persona 1
		% V%id%01 = Have accepted quest.
		Greeting <if= QuestStatus %qid% %id% else %pop% ifChatNPCArchEnemy else GoNotEnemy SetQuestStatus %qid% -2 Goto .%id%_GoBlowOff>
		*%id%_GoBlowOff <*BrushOff>
		GoNotEnemy <if= V%id%01 1 else GoOfferQuest NewChat Say %id%06 AddChat %id%05 AddChat %id%06 AddChat %id%07>
		.%id%_GoInit <SetQuestStatus %qid% %id% Goto GoOfferQuest>
		GoOfferQuest <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		result%id%01 <NewChat Say %id%03 QMemo %qid% %id%02 V= %id%01 1 AddChat %id%05>
		result%id%02 <NewChat Say %id%04 AddChat %id%03 AddChat %id%04>
		result%id%03 <Goto result%id%01>
		result%id%04 <NewChat Say %id%05 SetQuestStatus %qid% -2 CashPrize Threat %threat% 100 750 AddRenown -1>
		result%id%05 <AddRenown 3   AddReact d5   Goto .%id1%_GoInit>
		result%id%06 <NewChat Say %id%07>
		result%id%07 <SetQuestStatus %qid% -2  Goto .%id%_GoR7Final>
		*.%id%_GoR7Final <*YouLostItem>
		Msg%id%01 <Good job, \PC , but I can tell that it wasn't much of a challenge for you. What you need is a real adventure.>
		Msg%id%02 <%name1% told you that the %name2% is hidden somewhere in %name3%, and sent you to find it.>
		Msg%id%03 <The %name2% is a historical relic from the PreZero world. According to rumor, it was lost somewhere in this very city. Recovering it could be very good for your adventuring career.>
		Msg%id%04 <I could give you a cash reward, but is that what you really want? I'm offering you something far more important than money, a chance at personal betterment and spiritual growth.>
		Msg%id%05 <Well, if that's what you want, you were a great help to me, so... here you are.>
		Msg%id%06 <Hello, \PC . I trust that your adventure is going well?>
		Msg%id%07 <Come back here when you've found it, then. I'd like to have a look at it myself.>
		Prompt%id%01 <What do you have in mind?>
		Prompt%id%01_1 <Tell me what you have in mind.>
		Prompt%id%01_2 <I think you're right...>
		Prompt%id%02 <No adventure, just a reward please.>
		Prompt%id%02_1 <I've had enough adventure, I want money.>
		Prompt%id%02_2 <Wait, what about my reward?>
		Prompt%id%03 <Hmmm... Okay, I'll take your quest.>
		Prompt%id%03_1 <That does sound good. Tell me about it.>
		Prompt%id%04 <Nope. I'd rather take the money.>
		Prompt%id%04_1 <No thanks, money is fine.>
		Prompt%id%05 <I've found the %name2%.>
		CPrompt%id%05 <ifKeyItem %2% Accept>
		Prompt%id%06 <I'm still looking for the %name2%.>
		CPrompt%id%06 <ifItemOK %2% Accept>
		Prompt%id%07 <I'm afraid that I lost the %name2%...>
		CPrompt%id%07 <ifItemDestroyed %2% Accept>
	end

Content
	name <Exotic Animal Trade>
	requires <*Success1 .Extermination ("MAQUI"|"FCOMS"|"RISHI") "Spinner" BRANCHES>
	%% What are animals like the ones you just exterminated doing on a spinner,
	%% anyhow? PC will have a chance to work with L5Law to investigate, or to
	%% work with the smugglers to get the cargo safely away.

	%% E1 is the character previously helped.
	%% E2 is the L5Law officer working on the case.
	%% E3 is the smuggler who has all the animals.
	%% E4 is an encounter representing the smugglers to take the package
	%% E5 is an encounter representing the police looking for the package
	%% E6 is the crate
	Element2 <Prefab>
	Scene2 <Building Public Legit>
	Team2 <Guards>
	TeamData2 <SD Ally>

	Element3 <Prefab>
	Scene3 <Building Public Sleazy>
	Team3 <Citizens>
	TeamData3 <Pass>

	Element4 <Prefab>
	Scene4 <Space Environs>

	Element5 <Prefab>
	Scene5 <Space Environs>

	Element6 <Prefab>
	Scene6 <New *Blank.Basement>

	% SubQuest 1 is if the PC helps E2 catch E3.
	% SubQuest 2 is if the PC helps E3 deliver the exotic animals.
	Quest1 <*SuccessVsChar.Evidence 2 3>
	Quest2 <*Success1.CriminalTransaction 3>

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 AddChat %id%01 AddChat %id%02 CashPrize Reward Threat %threat% 100 100 SetQuestStatus %qid% %id% QMemo %qid% %id%04>
		result%id%01 <NewChat   Say %id%02   AddReact d10>
		result%id%02 <NewChat   Say %id%03   AddCheerful d6>
		Msg%id%01 <Thanks for your help! It'll be a lot safer from now on. I just wonder... where could those creatures had come from?>
		Msg%id%02 <The only way they could have appeared is if they were brought by someone... But why? And how did they end up in my basement?>
		Msg%id%03 <That's not the point! We live on in a colony, there aren't supposed to be any animals like that here at all!>
		Msg%id%04 <%name1% wonders how the animals in \PPR %1% basement could have gotten into a space colony.>
		Prompt%id%01 <That is a real mystery...>
		Prompt%id%01_1 <I didn't even think we had those kinds of animals.>
		Prompt%id%02 <Maybe if you cleaned once in a while...>
		Prompt%id%02_1 <They must've been attracted by the smell down there.>

		Persona 2
		%% The Police Officer. Bring him the crate for a reward + some points of LAW.
		rumor%id% <%name2% is investigating the illegal animal trade.>
		% V1 = Have given investigation quest
		greeting <if= QuestStatus %qid% %id% else GoIntro if= V1 1 else GoFirstTime NewChat Say 8 AddChat 8 AddChat 9>
		GoFirstTime <NewChat Say 1  AddChat 1  AddChat 2>
		*GoIntro <*NiceToMeetYou GoChat>
		*GoChat <*Misc_Chatter>
		Result1 <NewChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		result2 <ifPCCriminal else GoR2Pass NewChat Say 4 AddChat 4 AddChat 5>
		GoR2Pass <NewChat Say 3>
		result3 <NewChat Say 5 V= 1 1 QMemo %qid% 10>
		result4 <NewChat Say 6 AddChat 6 AddChat 7>
		result5 <NewChat Say 7 AddChat 6 AddChat 7>
		result6 <NewChat Say 3>
		result7 <Goto result3>
		result8 <DeleteNPC %3% DeleteItem %4%  DeleteItem %5%  DeleteItem %6%  AddLawful 1  AddReact d10  XPV 100  Goto .%id1%_GoInit>
		result9 <NewChat Say 9 AddChat 8>
		Msg1 <Hello \PC . I hear that you helped %name1% clean out the basement.>
		Msg2 <I'm working with L5Law to bust an exotic animal smuggling ring. They're importing rare animals from Earth for sale in the colonies. It's vital that we find and stop them at once.>
		Msg3 <I see. Well, if you change your mind, you know where to find me.>
		Msg4 <I know you probably have reasons for not working with the law, but you did help %name1%, and these smugglers must be stopped. It's very important that we find them as soon as possible.>
		Msg5 <We need to locate the smugglers. If you can find any evidence connection someone to the ring, bring it to me.>
		Msg6 <There are many reasons. Some people might keep them as pets. Others could use them as guard animals, or for a private collection. As repulsive as it sounds, some of these animals could even be cut into parts and eaten.>
		Msg7 <The ecology of a spinner colony is very finely balanced. Animals brought from outside could spread diseases or parasites. They could breed in the service layer, destroying resources that the rest of us need to live.>
		Msg8 <I'm still trying to locate those animal smugglers. Hopefully some new evidence will turn up soon.>
		Msg9 <That's progress... but unfortunately we still need some evidence or we won't be able to make an arrest.>
		Msg10 <%name2% in \SCENE NPCScene %2% asked you to help investigate the illegal animal trade in \SCENE RootSceneID .>
		Prompt1 <That's right. How did you know?>
		Prompt2 <I don't want to talk about it.>
		Prompt3 <How can I help you with this?>
		Prompt4 <Why would anyone want to import animals?>
		Prompt5 <I don't see why importing animals is so bad.>
		Prompt6 <Sorry, I don't want to get involved.>
		Prompt7 <I'll help you with this case.>
		Prompt8 <I've got your evidence right here.>
		CPrompt8 <ifKeyItem %6% Accept>
		Prompt9 <I think %name3% may be involved.>
		CPrompt9 <if# NPCVar %3% 2 0 Accept>

		Persona 3
		%% The Smuggler. Do his delivery for a reward + some points of CRIMINAL.
		rumor%id% <%name3% has been known to smuggle all kinds of exotic goods.>
		% V1 = Have given delivery quest
		greeting <if= QuestStatus %qid% %id% else GoIntro  if= V1 1 else GoCheckReject NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoCheckReject <if= V1 -1 else GoFirstTime Goto GoBrushOff>
		*GoBrushOff <*BrushOff&Questioning>
		GoFirstTime <NewChat Say 4 AddChat 4 AddChat 5>
		*GoIntro <*NiceToMeetYou GoOpenShop>
		*GoOpenShop <*SHOP_BLACKMARKET GoByeBye>
		*GoByeBye <*Goodbye>
		result1 <NewChat Say 2>
		result2 <DeleteItem %4% DeleteItem %5% AddLawful -5   Goto .%id2%_GoInit>
		result3 <NewChat Say 3 AddReact -d20  DeleteItem %4% DeleteItem %5%  SetQuestStatus %qid% -2 ifNPCPassionate PCEnemy %3%>
		% If the PC isn't lawful, then no problem E3 will tell about the exotic animal
		% trade. Even if he is lawful, if he passes a decent conversation roll E3 will still
		% talk. Either way, the connection is made and the trap door activated.
		result4 <ifPCLawful else GoR4JobOffer ifConversation %sktar_hard% else GoR4Fail Goto GoR4JobOffer>
		GoR4JobOffer <NewChat Say 10  AddChat 9 AddChat 10>
		GoR4Fail <NewChat  Say 5  AddChat 6 AddChat 7  NPCVar= %3% 2 1   V= 1 -1   UpdateProps>
		result5 <Goto GoOpenShop>
		result6 <NewChat Say 6 AddCHat 8>
		result7 <NewChat Say 7 AddChat 8>
		result8 <NewChat Say 9>
		result9 <NewChat Say 11  AddChat 11   NPCVar= %3% 2 2   SetQEncActive %4%   V= 1 1   QMemo %qid% 16 UpdateProps>
		result10 <NewChat Say 12   AddLawful 1   NPCVar= %3% 2 1   V= 1 -1   QMemo %qid% 17 UpdateProps>
		result11 <NewChat Say 13 AddChat 12>
		result12 <NewChat Say 14  AddChat 13 AddChat 14>
		result13 <NewChat Say 15>
		result14 <Goto result10>
		Msg1 <Have you delivered the package yet?>
		Msg2 <Retrieve the "package" from my basement and deliver it to the "businessmen" who are working in \SCENE %s4% . Then return here and I will give you a "reward". Do I have to spell it out for you?>
		Msg3 <You lost it!? How does one lose a 20kg shipping container!? That's it... you and I, our business relationship is over.>
		Msg4 <Yes, may I help you? I carry absolutely everything, and what I don't have now I can easily get.>
		Msg5 <Exotic animals? Er... Never heard of that, sorry. Nope. I don't have any, and I don't know where to get them either...>
		Msg6 <Well that's common, isn't it? Ships coming and going all the time, from Earth, Mars... I had some critters escape in my basement too. Nothing to worry about, that.>
		Msg7 <When I said that I can get everything I obviously meant everything legally available, of course. If you're looking for exotic creatures you may want to ask %name1%... I hear they had some at \SCENE %s1% .>
		Msg9 <Still with the questions! I don't know. How would I? I'm a merchant, not some kind of xenobiologist... I've already said enough to you.>
		Msg10 <Hmmm... I might know something about that, but if you'll take my advice dealing with that kind of thing is more trouble than it's worth. How'd you like to help me out?>
		Msg11 <Well, I had this customer, who wanted some animals transported from Earth. Fair enough, I thought, but when they got here one of the crates malfunctioned and the damn things got out!>
		Msg12 <Suit yourself. Fortunately there will never be a shortage of thieves and cut-throats to do my dirty work.>
		Msg13 <If only that were the worst of it; they ended up in my basement too, and I even think they're breeding down there! I can't break out the heavy guns to get rid of them, or the law would be on me in a second...>
		Msg14 <I've set poison and traps; that should get most of the vermin, eventually, but for now I still have to conduct a business. There's a crate in the basement which has to be delivered to some "clients" waiting in \SCENE %s4% .>
		Msg15 <Great! Head down these stairs to the basement; there's likely to be some vermin about but you can handle those. Find the cold storage crate then bring it to the clients waiting outside the station. Afterwards you'll get a nice reward.>
		Msg16 <%name3% asked you to deliver a package to smugglers waiting in \SCENE %s4% .>
		Msg17 <%name3% seems to be involved in the exotic animal trade; %name2% would be interested.>
		Prompt1 <What exactly am I supposed to do again?>
		Prompt2 <It has been delivered.>
		CPrompt2 <if= NPCVar %3% 1 1 Accept>
		Prompt3 <I think I lost the package...>
		CPrompt3 <if# NPCVar %3% 1 1 ifItemDestroyed %6% Accept>
		Prompt4 <I'm looking for some exotic animals.>
		Prompt5 <Show me what you have for sale.>
		Prompt6 <%name1% had a problem with animals, maybe from Earth.>
		Prompt7 <I thought you said you could get anything.>
		Prompt8 <Any idea where they came from?>
		Prompt9 <How can I help you?>
		Prompt10 <I won't get involved in anything illegal.>
		Prompt11 <So that's how they got into %name1%'s basement.>
		Prompt12 <What do you want me to do?>
		Prompt13 <I'll get the crate and deliver it for you.>
		Prompt14 <I don't want anything to do with this.>

		MetaScene 3
		% Contains the entrance to the basement.
		sub
			room
			special <SHAREDPALETTE>
			desig <HOME>
			minimap <.......2....1.......&---&>
			inv
				TrapDoor
				desig <Entrance %s6%>
				MiniMapComponent 2
				Hide -1
				Update <if= NPCVar %3% 2 1 else GoCheckAuto   if= V1 0   V= 1 1  SetStat STAT_MetaVisibility %sktar%>
				GoCheckAuto <if= NPCVar %3% 2 2   V= 1 1  SetStat STAT_MetaVisibility 0>
			end
		end

		MetaScene 6
		% Contains the teams and scripts for the basement.
		% L1 = Initialization Counter
		% L2 = XP gained for getting case counter
		start <if= L1 0 else GoBeenBefore Print 1 L= 1 1 MonsterUp 2 WMThreat %threat%>
		GoBeenBefore <Print 2>
		Msg1 <You enter the basement. Strange noises can be heard coming from the darkness.>
		Msg2 <You enter the basement.>
		get%6% <if= L2 0 XPV 50 L= 2 1>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monsters>
			type <Animal Vermin Exotic Bug Reptile Fungus>
			SetEnemy 1
			Update <ifItemDestroyed %6% SetStat STAT_WMonster 0>

			Room
			% A room to hold the package.
			special <SharedPalette>
			desig <HOME>
			minimap <......#.#...1...#.#......>
		end

	end
	inv
		NPC Police Officer
		SetFaction 9
		EquipChar 10000

		NPC Thief
		job <Smuggler>
		% V1 = Have completed smuggling job
		% V2 = Have activated trapdoor

		STC QUEST-ENCOUNTER-WANDER
		% This encounter appears after activated by E3.
		% When it approaches the PC or is approached by the PC, it will
		% ask if the PC wants to make the transfer. If yes, then the crate
		% disappears and the PC can return to E3 for the reward.
		name <Smugglers>
		ENCOUNTER_NonCombat
		use <ifKeyItem %6% else GoRunAway Goto GoMakeSwitch>
		attack <ifKeyItem %6%             Goto GoMakeSwitch>
		GoRunAway <Print 1   SetStat STAT_MetaVisibility -1>
		GoMakeSwitch <ifYesNo 2 3 4 else GoRunAway Alert 5 DeleteItem %6% NPCVar= %3% 1 1 XPV 100 DeleteItem %4%>
		Msg1 <The smuggler's ship darts away too quickly for you to approach.>
		Msg2 <You are approached by a smuggler. Do you send over the crate?>
		Msg3 <Send if over.>
		msg4 <Keep it.>
		Msg5 <The smuggler's ship retrieves the crate and darts away.>

		Encounter
		% This encounter appears when this quest fragment starts.
		% If the PC is holding the package, he will be attacked by the police.
		% Whether he defeats the police or just runs away this encounter will
		% recharge in a few hours.
		% V1 = Recharge counter
		name <Police Dragnet>
		Special <NoMSID>
		update <if= QuestStatus %qid% %id% else GoHide  ifG ComTime V1 else GoHide Goto GoSetVisible>
		Use <ifKeyItem %6% else GoNoItem Goto Attack>
		GoNoItem <Alert 1 SetStat STAT_MetaVisibility -1 V= 1 ComTime V+ 1 3600 V+ 1 d7200>
		Attack <ifKeyItem %6% Alert 2 Print 3 V= 1 ComTime V+ 1 3600 V+ 1 d7200 Goto GoStartCombat>
		GoStartCombat <SavePos Dynamic 2  Threat %threat% 120 .nu1 .nu2  DynaFaction 9>
		.nu1 <if= T1 0  Return   if= L1 0  L= 1 1  AddRenown -5>
		.nu2 <if= T2 0           if= L1 0  L= 1 1  XPV 50   AddLawful -1>
		EncounterMove 30
		ENCOUNTER_Defense
		Msg1 <Police Patrol: Smugglers are operating in the area. Please be cautious, citizen.>
		Msg2 <Police Patrol: Power down and surrender. You are transporting known contraband.>
		Msg3 <You are attacked by the police!>

		Treasure 10
		name <Cold Storage Crate>
		mass 39
		StolenGoods
		desc <A container for storing things at extremely low temperatures. It is often used to transport living material.>
	end

Content
	name <Destroy Pirate Base>
	requires <*Success1 .AgainstPirates BRANCHES>

	% E1 is the character who will offer the mission
	% E2 is the local police faction
	% E3 is the pirate faction to be defeated
	Element2 <Faction Police !Ally 1>
	Element3 <Faction Crime !Enemy 2>
	Scene3 <New *DestroyBase>

	Quest1 <*SUCCESS1.FinishMission 1>

	Sub
		Persona 1
		Greeting <if= QuestStatus %qid% %id% else %pop%  NewChat Say %id%02 AddChat %id%01>
		.%id%_GoInit <NewChat Say %id%01   CashPrize Reward Threat %threat% 100 100   SetQuestStatus %qid% %id% QMemo %qid% %id%03>
		Result%id%01 <Goto .%id1%_GoInit>
		Msg%id%01 <Good job... very good. The pirates must have a base somewhere around here. If we could destroy that, their attacks would be stopped for good.>
		Msg%id%02 <Our forces are still looking for the pirate base. Never fear, it will be located and destroyed soon.>
		Msg%id%03 <%name1% thinks there is a pirate base somewhere near \SCENE RootSceneID .>
		Prompt%id%01 <I've destroyed the pirate base myself.>
		CPrompt%id%01 <if= SceneVar %3% 1 1 Accept>
	end


Content
	name <Revenge For Rats>
	requires <*Success1 .Extermination (1:TRADE|1:CORPO|1:CRAFT) BRANCHES>
	%% Someone was behind the recent infestation... Will you bring the criminal
	%% to justice, or will you help him finish his plan?

	%% E1 is the character you just helped.
	%% E2 is his enemy.
	%% E3 is the spray bottle of spaceworm spores

	Element2 <Prefab>
	Scene2 <Building Public Mall>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <Prefab>
	Place3 <2>

	Quest1 <*SuccessVsChar.Evidence 1 2>
	Quest2 <*SuccessVsChar.ConJob   2 1>
	Quest3 <*SuccessVsChar.FoilPlot 1 2>

	sub
		Persona 1
		rumor%id% <%name2% has always hated %name1%.>
		greeting <if= QuestStatus %qid% %id% else %pop% if# NPCVar %2% 1 3 else .%id%_GoIntimidate ifKeyItem %3% else %pop% if= SceneID %s1% else %pop% Goto .%id%_GoHaveCan>
		.%id%_GoHaveCan <NewChat Say %id%02 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		.%id%_GoIntimidate <NewChat Say %id%02 AddChat %id%04>
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward Threat %threat% 100 100  SetQuestStatus %qid% %id% QMemo %qid% %id%05>
		Result%id%01 <NewChat Say %id%03>
		Result%id%02 <PCFriend %1% PCEnemy %2% DeleteItem %3%  NPCVar= %2% 1 4  XPV 50   Goto .%id1%_GoInit>
		Result%id%03 <NewChat Say %id%04 DeleteItem %3%  NPCVar= %2% 1 2 Qmemo %qid% %id%06>
		Result%id%04 <PCFriend %1% PCEnemy %2%     XPV 50   Goto .%id3%_GoInit>
		Msg%id%01 <Thanks for cleaning out the vermin. Somebody must have put them there; I only wish I knew who...>
		Msg%id%02 <You look like you have something to tell me...>
		Msg%id%03 <Well, that's a relief. I thought you were going to tell me the basement was full of rats again.>
		Msg%id%04 <Really? I guess I better use it, then... You did such a good job cleaning out the vermin the first time, I'm sure that whatever's in here will work great.>
		Msg%id%05 <%name1% in \SCENE %s1% thinks someone put the monsters in \PPR %1% basement.>
		Msg%id%06 <You gave the strange cannister to %name1% for %name2% in \SCENE %s2% .>
		Prompt%id%01 <No, nothing at all.>
		Prompt%id%02 <I got this strange cannister from %name2%...>
		Prompt%id%03 <This can of pest repellent will keep your basement safe!>
		CPrompt%id%03 <if= NPCVar %2% 1 1 Accept>
		Prompt%id%04 <%name2% isn't going to bother you any more.>

		Persona 2
		% V1 = Intimidation recharge counter
		% V2 = Conversation recharge counter
		greeting <ifG NPCVar %2% 1 2 else GoCheckMission ifNPCArchEnemy %2% else GoOpenStore NewChat Say 17>
		GoCheckMission <if= SceneID %s2% else GoHangUp ifNPCOK %1% else GoBreakQuest if= QuestStatus %qid% %id% else GoGreeting  ifG NPCVar %2% 0 else GoConfrontation Say 18 AddChat 12 AddChat 13>
		GoConfrontation <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoBreakQuest <SetQuestStatus %qid% -2 Goto GoGreeting>
		GoHangUp <NewChat Say 19>
		*GoGreeting <*NiceToMeetYou GoOpenStore>
		*GoOpenStore <*SHOP_GENERAL GoGoodbye>
		*GoGoodbye <*GOODBYE>
		result1 <NewChat Say 2 AddChat 4 AddChat 5 AddChat 8>
		result2 <Goto GoOpenStore>
		result3 <ifNPCArchEnemy %1% else GoR3Normal NewChat Say 3 AddChat 6 AddChat 7>
		GoR3Normal <NewChat Say 4  AddChat 4>
		result4 <AddReact -d10   ifIntimidation %sktar_hard% else GoIntFail Goto GoIntWorked>
		GoIntWorked <NewChat Say 5 NPCVar= %2% 1 3>
		GoIntFail <NewChat Say 6>
		result5 <ifConversation %sktar% else GoR5Fail NewChat Say 8 AddChat 9 AddChat 10>
		GoR5Fail <NewChat Say 9>
		result6 <NewChat Say 10 AddChat 9 AddChat 10>
		result7 <Goto result4>
		result8 <ifConversation %sktar% else GoR8Fail NewChat Say 11 AddChat 9 AddChat 10>
		GoR8Fail <NewChat Say 12>
		result9 <ifItemOK %3% else GoR9Fail NewChat Say 13 AddChat 11 GiveItem %3% NPCVar= %2% 1 1 QMemo %qid% 20>
		GoR9Fail <NewChat Say 14>
		result10 <NewChat Say 15>
		result11 <NewChat Say 16>
		result12 <Goto GoOpenStore>
		result13 <PCAlly %2% PCEnemy %1% AddHeroic -1 AddLawful -3 AddReact d10  XPV 50  Goto .%id2%_GoInit>
		Msg1 <You look like there's something you're after. What do you want?>
		Msg2 <Oh, do you? I don't know anything about that... And what business is it of yours, anyhow?>
		Msg3 <What do you care about that? The last I heard, you and %name1% weren't exactly on the best of terms...>
		Msg4 <Like I said earlier, I don't know anything about %name1%! Besides, anything that happened is probable \OPR %1% fault anyhow.>
		Msg5 <Okay, I admit it, I released the vermin into %name1%'s basement... I was just trying to even some old scores, that's all... I won't do anything like that again.>
		Msg6 <You're really clueless, aren't you? It's not my fault if bad things happen to to %name1%.>
		Msg8 <You don't say... Well, there could be some money for you in it, if you're willing to do me a favor... What do you say?>
		Msg9 <If you had any evidence linking me to the rats, then maybe you'd have the start of a decent extortion scam. As it is you've got nothing.>
		Msg10 <That's a great idea. If you're really interested, and not above a bit of underhandedness, I've got something you can do...>
		Msg11 <You think I need any help to deal with %name1%? Well, it would give an extra layer of plausible deniability... I do have a task, if you're willing to do a bit of underhanded work.>
		Msg12 <I don't need your help with anything, especially not anything that I don't even know about in the first place.>
		Msg13 <Take this can to %name1%... It contains spaceworm spores, but you can tell \OPR %1% it's rat poison instead. When \SPR %1% spreads it around \PPR %1% basement the worms will hatch and eat all \PPR %1% stock.>
		Msg14 <Er... I seem to have misplaced something very important... I guess I don't have anything for you to do after all.>
		Msg15 <Fine. I don't need any help dealing with %name1%, anyhow.>
		Msg16 <You'll be rewarded very handsomely, I promise you... Far more than that cheapskate %name1% would ever pay.>
		Msg17 <Go away. I have nothing to say to you.>
		Msg18 <So, how's the secret mission going? Anything to report?>
		Msg19 <What do you want? If you need to buy anything, come see me in \SCENE %s2% . Can't conduct business over the phone.>
		Msg20 <%name2% asked you to give a cannister of spaceworm spores to %name1% in \SCENE %s1% .>
		Prompt1 <I know you put monsters in %name1%'s basement.>
		Prompt2 <I'm just here to shop.>
		Prompt3 <I want you to leave %name1% alone from now on.>
		Prompt4 <If anything else happens, I promise you'll suffer.>
		Prompt5 <I thought this knowledge might be worth some money.>
		Prompt6 <You're right... Maybe I should be helping you instead.>
		Prompt7 <That doesn't matter. You're going to stop.>
		Prompt8 <I want to help you against \OPR %1% .>
		Prompt9 <Sounds good. What do you want me to do?>
		Prompt10 <On second thought, I'm not interested.>
		Prompt11 <What do I get in return?>
		Prompt12 <I need to go shopping before completing it.>
		Prompt13 <I've given %name1% the spores.>
		CPrompt13 <if= NPCVar %2% 1 2 Accept>

		MetaScene 2
		sub
			room
			minimap <..............1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end
	end
	inv
		NPC Shopkeeper
		%% NPCVar 1:	If V1=1, have accepted mission from E2.
		%%		If V1=2, have completed that mission.
		%%		If V1=3, have intimidated E2 into submission.
		%%		If V1=4, have sided with E1
		Chardesc Passionate
		Lawfulness 0

		Treasure 100
		name <Aerosol Container>
		desc <This unmarked aerosol container looks mighty suspicious.>
		use <Print 4>
		Clue16 <Print 2>
		Clue20 <Print 2>
		Clue22 <Print 3>
		Clue29 <Print 1>
		Msg1 <This type of container is used to transport biological samples. No idea why it would be connected to a spray head, though.>
		Msg2 <This type of container is used for storing and dispensing biological agents. As this one isn't marked, it probably doesn't contain medicine.>
		Msg3 <This type of container is used for storing and dispensing biological agents. Perhaps it's a weapon of some kind.>
		Msg4 <Better not just spray this around... There's no telling what it might contain.>
	end

Content
	name <Thanks for the Info>
	requires <*Success1 .TipOff>
	special <REUSABLE>

	%% E1 is the character previously helped.

	sub
		Persona 1
		.%id%_GoInit <NewChat Say %id%01 CashPrize Reward Threat %threat% 100 300 SetQuestStatus %qid% -1>
		Msg%id%01 <Thanks for the information. Here's a little reward for your trouble.>
		Msg%id%01_1 <Ah, it's always helpful to be on top of the latest information. Thanks for letting me know. Here's something for you.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Thanks for the tip. Here's something for you.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <How interesting. Your information is worth something... about this much, I'd say.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <Aha! That's going to be very useful to know... Here, take this reward for your help.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <It's a good thing you told me that. Here, take this. It's my way of saying thanks.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <It would have been very bad if you hadn't told me that. Here, thanks for the tip.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>

	end

Content
	name <Basic Reward End>
	requires <*Success1 -.TipOff -.CharityWork>
	special <REUSABLE>
	%% This is the basic ending... The PC gets a reward and that's it.
	%% Afterwards, set the QuestStatus to -1 to indicate a successfully
	%% completed quest.

	%% E1 is the character to give the reward.

	sub
		Persona 1
		*.%id%_GoInit <*MissionWasSuccess&Reward .%id%_GoFinish ChatNPCFac na>
		.%id%_GoFinish <CashPrize Reward Threat %threat% 100 250 SetQuestStatus %qid% -1>
	end



%%   *:FindMoney Contents
%%		.Investor = The NPC needs the money to invest in a new product
%%		.Charity = The NPC needs the money to save an orphanage or whatever
%%	Param1: The character who needs the money
%%
%%	When the time comes to go scrounging for funds, the parent quest fragment
%%	must set the the QuestStatus of the FindMoney component to its id.

Content
	name <Charity Mecha Tournament>
	desc <The PC can enter and maybe even win a charity mecha tournament.>
	requires <*:FindMoney .Charity>

	% E1 is the character who needs the money
	% E2 is the organizer of the tournament
	% E3 is the tournament encounter
	Element2 <Prefab>
	Scene2 <Building Public Legit>
	Team2 <Citizens>
	TeamData2 <Pass>

	Element3 <Prefab>
	Scene3 <Environs>

	sub
		Persona 2
		rumor%id% <%name2% has organized a charity mecha tournament.>
		% NPCVar %2% 1 = Have won the fight
		% V1 = Match recharge timer
		% V2 = Registration counter
		greeting <if= QuestStatus %qid% %id% else GoGreet if= NPCVar %2% 1 1 else GoCheckStatus NewChat Say 9 SetQuestStatus %qid% -1 History 10>
		GoCheckStatus <if= ItemVar %3% 11 1 else GoCheckRecharge NewChat Say 7 AddChat 3>
		GoCheckRecharge <ifG V1 ComTime else GoCheckFirst NewChat Say 6>
		GoCheckFirst <if= V2 1 else GoRegister NewChat Say 4 AddChat 2>
		GoRegister <NewChat Say 1 AddChat 1>
		GoSetCombat <ItemVar= %3% 11 1 V= 1 ComTime V+ 1 86400>
		*GoGreet <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 QMemo %qid% 3 V= 2 1 Goto GoSetCombat>
		result2 <NewChat Say 5 Goto GoSetCombat>
		result3 <NewChat Say 8>
		Msg1 <This is the \SCENE RootSceneID Charity Mecha Spectacular. Pilots from across the solar system have come to compete for their favorite charities. Would you like to register?>
		Msg2 <Very good. There's a match coming up soon. Go to the arena in \SCENE %s3% and do your best.>
		Msg3 <You joined %name2%'s charity mecha tournament to earn money for %name1%.>
		Msg4 <Welcome to the \SCENE RootSceneID Charity Mecha Spectacular. Are you ready to fight?>
		Msg5 <There's another match coming up shortly. I trust you remember where to find the arena.>
		Msg6 <You lost the match, but you aren't out of the running yet... You can try in a different heat tomorrow.>
		Msg7 <What are you doing here? You're supposed to be in the arena!>
		Msg8 <You can find the arena entrance in \SCENE %s3% . It should be pretty obvious.>
		msg9 <You did it! You've won the \SCENE RootSceneID Charity Mecha Spectacular. I've arranged for the prize money to be sent to %name1%... you must be very proud.>
		Msg10 <You won the \SCENE RootSceneID Charity Mecha Spectacular.>
		Prompt1 <I'm here to help %name1%.>
		Prompt2 <I'm ready.>
		Prompt3 <Where was that again?>

		% Death check
		Metascene 2
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 DeleteItem %3% Goto %pop%>
	end
	inv
		NPC Arena Pilot
		chardesc Heroic Sociable
		EquipChar 15000

		STC QUEST-MAPMARKER-STATIONARY
		name <Charity Arena>
		%% V11 = Primed for combat
		update <if# V11 0 else GoHide  SetStat STAT_MetaVisibility 0  ShowEncounter  Goto GoSetOrders>
		use <ifG StatVal STAT_MetaVisibility -1  Print 1  Goto GoStartCombat>
		GoStartCombat <V= 11 0  SavePos Dynamic 2 Threat V2 100 .nu1 .nu2 ComposeD Msg1 .Msg1>
		.nu1 <if= T1 0  Return   if= L1 0  L= 1 1  AddRenown -5>
		.nu2 <if= T2 0           if= L1 0  L= 1 1  XPV 100  AddRenown 1   NPCVar= %2% 1 1  QMemo %qid% 1  DeleteItem %3%>
		.msg1 <You have won the charity mecha tournament, and should inform %name2% in \SCENE %s2% .>
		Msg1 <You enter the arena.>
	end

Content
	name <Tycoon Party>
	desc <If the PC can crash a party of the station's wealthiest residents, maybe he can get funding.>
	requires <*:FindMoney SPINNER BRANCHES>

	% Element 1 will be the NPC who needs some money
	% Element 2 will be the tycoon who can maybe provide the money
	element2 <Prefab>
	Scene2 <New *PartyYacht>
	Team2 <Citizens>
	TeamData2 <Pass>

	sub
		Persona 2
		rumor%id% <%name2% is hosting a fancy yacht party for all the spinner's richest inhabitants.>
		% V1 = Have applied reaction boost.
		greeting <if= QuestStatus %qid% %id% else GoChat ifG PartyScore 2 else GoBadParty NewChat Say 1 AddChat 1 AddChat 2 if= V1 0 V= 1 1 AddReact 20>
		GoBadParty <NewChat Say 2 QMemo %qid% 6>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 3 AddChat 3>
		result2 <NewChat Say 4>
		result3 <NewChat Say 5  SetQuestStatus %qid% -1>
		Msg1 <Ah, \PC ! I've heard that you're the life of the party. It's such a pleasure to see you here at my little event!>
		Msg2 <How disappointing... So far, this party has been one colossal bore.>
		Msg3 <For you, \PC , anything... as long as it isn't too expensive, or too perverted.>
		Msg4 <You're very welcome. Please, stay as long as you'd like.>
		Msg5 <So, not only a pretty face, you also have business sense... I respect that. Alright, I'll help %name1% .>
		Msg6 <%name2% isn't enjoying \PPR %2% yacht party in \SCENE RootSceneID very much.>
		Prompt1 <%name2%, I need to ask you a favor.>
		Prompt2 <I'm glad to hear that.>
		Prompt3 <I want you to finance %name1%.>
		CPrompt3 <ifNPCOK %1% Accept>
	end
	inv
		NPC Corporate Executive
		job <Tycoon>
		chardesc Renowned Renowned Old Sociable Cheerful
	end

Content
	name <Celebrity in Distress>
	desc <The PC will encounter a celebrity being attacked, and may help.>
	%% Upon saving E1, he will be sent to a nearby building. If the PC speaks with
	%% him again, he'll get to ask for a reward.
	requires <*:FindMoney SPINNER>

	% E1 is the character who needs the money
	% E2 is the character the PC will be helping
	% E3 is the entrance to the space scene
	% E4 is the scene where E1 will go after the fight
	element2 <Prefab>
	scene2 <New *BLANK.Space.Mecha>
	team2 <Allies>
	teamdata2 <SD Ally>

	element3 <Prefab>
	scene3 <SPACE ENVIRONS>

	Scene4 <Building Public>

	sub
		Persona 2
		special <UNLISTED>
		% V1 = Victory Counter
		% V2 = Have asked for date counter
		rumor%id% <the famous performer %name2% has been seen flying outside the colony in a sports mecha.>
		greeting <if= QuestStatus %qid% %id% else GoChat if= V1 1 else GoCheckVictory  Goto GoThanks>
		*GoThanks <*ThanksForRescue GoReward>
		GoReward <NewChat Say 5 AddChat 1 AddChat 2 AddChat 3>
		GoCheckVictory <if= SceneVar %s2% 2 1 else GoKeepFighting V= 1 1 QMemo %qid% 4  EndChat Say 3>
		GoKeepFighting <ifChatNPCKnown else GoFirstTime EndChat Say 1>
		*GoFirstTime <*HelpMeVsMecha GoWillHelp GoWontHelp>
		*GoWillHelp <*LetsDefeatThem 8>
		GoWontHelp <EndChat   AddReact -15   Say 2>
		*GoChat <*MISC_CHATTER>
		result1 <SetQuestStatus %qid% -1 if= V2 0 else GoR1JustEnd NewChat Say 7 AddChat 2 AddChat 4>
		GoR1JustEnd <NewChat Say 6>
		result2 <if= SceneVar %s2% 3 1 else GoR2RealFail ifFlirtation skilltar %threat% else GoR2Fail ifG React 20 else GoR2Fail V= 2 1 NewChat Say 8 PCLover %2% AddChat 1 AddChat 3 AddChat 5>
		GoR2Fail <V= 2 -1 ifG React 15 else GoR2RealFail PCFriend %2% NewChat Say 9 AddChat 1 AddChat 3 AddChat 5>
		GoR2RealFail <V= 2 -1 NewChat Say 10 AddChat 1 AddChat 3 AddChat 5>
		result3 <NewChat Say 11 SetQuestStatus %qid% -2  CashPrize Reward Threat %threat% 100 200>
		result4 <NewChat Say 12 ifChatNPCSexy AddRenown 1>
		result5 <NewChat Say 13>
		Msg1 <The pirates aren't defeated yet! Let's keep fighting!>
		Msg2 <Fine, I don't need any help!>
		Msg3 <We did it! Come meet me at %name4% later and I'll be able to thank you properly.>
		Msg4 <You rescued %name2% from space pirates; \SPR %2% said to meet \OPR %2% at %name4% later for a reward.>
		Msg5 <I'd really like to thank you for what you did. Is there anything you need?>
		Msg6 <%name1%? Yes, I think I can help \OPR %1% out. Once again, thanks so much for your help. It was a lot of fun flying with you.>
		Msg7 <Alright, I can help %name1% with a little money. What about for you, though? There must be something that you want for yourself.>
		Msg8 <To be honest with you, I was hoping you'd ask that... But it doesn't seem like much of a reward for the person who came flying to my rescue. Isn't there something else that you need?>
		Msg9 <I'll keep that in mind. Maybe we can go out someday. For right now, though, isn't there anything I could give you?>
		Msg10 <I don't think so, I'm far too busy to go on dates... Isn't there anything else that you want?>
		Msg11 <That's easy enough to do. I hope this reward will be enough... Thank you again, \PC . I won't forget your help.>
		Msg12 <That's very noble of you.>
		Msg13 <I won't forget your help. Thank you again.>
		Prompt1 <I don't need anything, but %name1% does.>
		CPrompt1 <ifNPCOK %1% if= QuestStatus %qid% %id% Accept>
		Prompt2 <How would you like to go on a date sometime?>
		CPrompt2 <ifChatNPCSexy if= V2 0 Accept>
		Prompt3 <You can pay me in cash.>
		CPrompt3 <if= QuestStatus %qid% %id% Accept>
		Prompt4 <There's nothing that I need.>
		Prompt5 <You've already given me enough.>
		CPrompt5 <if# QuestStatus %qid% %id% Accept>

		MetaScene 2
		%% Because scene 1 is a blank, this metascene holds all the important stuff.
		% L1 = Conversation Initializer
		% L2 = Victory Counter
		% L3 = NPC in play at end of battle
		% L4 = Value of E1's cargo
		start <if= L1 0  Print 2 ForceChat %2% L= 1 1>
		% If the PC leaves the scene before defeating all enemies, there's no second chance.
		nu1 <if= T1 0 Return  if= L2 0   L= 2 -1  DeleteNPC %2%  SetQuestStatus %qid% -2>
		nu2 <if= T2 0         if= L2 0   XPV 100   L= 2 1   ifNPCOK %2%  Print 2 ForceChat %2% ifNPCInPlay %2% else GoMove L= 3 1 Goto GoMove>
		GoMove <MoveAndPacifyNPC %2% %s4%>
		Faint%2% <SetQuestStatus %qid% -2>

		Msg2 <%name2% contacts you.>

		sub
			Team 1
			name <Player Team>
			ParaX 5
			ParaY 5
			SetAlly 3
			SetEnemy 2

			Team 2
			name <Enemies>
			SetFaction 8
			Deploy <if= L2 0   WMecha 2 Threat %threat% 170>
			ParaX 25
			ParaY 25
			SetEnemy 1 3

			Team 3
			name <Allies>
			SetEnemy 2
			SetAlly 1
			ParaX 35
			ParaY 10
		end
	end
	inv
		NPC Celebrity
		mecha <Trailblazer.txt>
		% This NPC has a small assortment of combat skills.
		skill 1 5
		skill 2 5
		skill 3 5
		skill 4 5
		skill 5 8
		skill 10 10
		SetTeam 3
		% This NPC will be a member of the Pro Duelist League.
		SetFaction 5

		STC QUEST-ENCOUNTER-AUTO-STATIONARY
		name <Sporty Mecha>
		desig <ENTRANCE %s2%>
		% This entrance will only be visible while the quest is active;
		% afterwards it should disappear forever.
		update <if= QuestStatus %qid% %id% else GoHide  if= SceneVar %s2% 2 0 else GoHide Goto GoSetVisible>
	end

Content
	name <Insane Venture Capitalist>
	% The PC needs a few million dollars to help a friend. This rich shopkeeper will
	% give him the money if the PC will clear the rats out of his basement. Fairly
	% standard RPG plot, except the PC will be able to call the NPC on his clear insanity.
	requires <*:FindMoney>

	% E1 is the character who needs the money
	% E2 is the shopkeeper who might be able to provide it
	% E3 is the basement full of rats

	element2 <Prefab>
	Scene2 <Mall Public>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Scene3 <NEW *ExterminationBasement>

	sub
		Persona 2
		rumor%id% <%name2% is very rich, but doesn't seem to be all that bright.>
		% V1 = Have accepted quest
		greeting <if= QuestStatus %qid% %id% else GoFirstTime if= V1 1 else GoQuestIntro NewChat Say 14 AddChat 14 AddChat 15>
		GoQuestIntro <ifNPCOK %1% else GoCancel NewChat Say 1 AddChat 1 AddChat 2>
		GoCancel <SetQuestStatus %qid% -2 goto GoFirstTime>
		*GoFirstTime <*NiceToMeetYou GoOpenShop>
		*GoOpenShop <*SHOP_ARMOR GoGoodbye>
		*GoGoodbye <*GOODBYE>
		result1 <NewChat Say 2 V= 1 1 AddChat 3 AddChat 4>
		result2 <Goto GoOpenShop>
		result3 <ifChatNPCInPlay else GoR3NotHere NewChat Say 3 QMemo 5 AddChat 13>
		GoR3NotHere <NewChat Say 4 QMemo 5 AddChat 13>
		result4 <NewChat Say 6 AddChat 5 AddChat 6>
		result5 <Goto result3>
		result6 <NewChat Say 7 AddChat 5 AddChat 7>
		result7 <NewChat Say 8 AddChat 5 AddChat 8>
		result8 <NewChat Say 9 AddChat 5 AddChat 9>
		result9 <NewChat Say 10 AddChat 5 AddChat 10>
		result10 <NewChat Say 11 AddChat 5 AddChat 11>
		result11 <NewChat Say 12 AddChat 5 AddChat 12>
		result12 <NewChat Say 13 AddChat 5>
		result13 <AddRenown 1 AddReact -10 Goto result14>
		result14 <NewChat Say 15 AddReact 10 SetQuestStatus %qid% -1>
		result15 <Goto GoOpenShop>
		Msg1 <Yes, may I help you with something?>
		Msg1_1 <You look like you want to ask something... Hurry up and spit it out, I don't have all day.>
		Msg2 <Donations, huh? Well, it sounds like a worthwhile cause... I may be able to help %name1%, if you do me a little favor. My storage room has been infested with rats. If you can get rid of them, then I'll agree to help your friend.>
		Msg3 <The way down to the storage room is right behind this door. Come back when you've cleared out all the mice.>
		Msg4 <You'll have to come to \SCENE %s2% to exterminate the mice- I know technology is great but I don't think you can do that over the phone just yet.>
		Msg5 <%name2% in \SCENE %s2% agreed to fund %name1% if you clear the vermin out of \PPR %2% storage room.>
		Msg6 <Yes, that's the deal. Take it or leave it.>
		Msg7 <Why should I hire an exterminator when I have you right here? And you have to do what I say, or else %name1% won't get the money!>
		Msg8 <A million dollars isn't a lot when you have as much money as I do. That's why I can afford to help people like %name1%... plus I can use it as a tax write-off.>
		Msg9 <Careful! Don't upset me, or I'll make you clean the storage room with no donation for %name1%!>
		Msg10 <I see what you mean... So, it seems we have a bit of a standoff here, neither one of us are capable of breaking our original agreement.>
		Msg11 <When I was younger I used to play adventure games, and there was always a rich king or a merchant who would give the adventurer some fantastic treasure in exchange for killing rats. I've always wanted that...>
		Msg12 <Oh hell no. I was crap at those games... my adventurer always died. I wanted to be the king-merchant character... Imagine, being able to send people on any mundane yet dangerous assignments that strike my fancy...>
		Msg13 <That may well be... but, I'm your only chance to help %name1%, short of becoming a millionaire yourself... or finding someone else to help... or winning a lottery...>
		Msg14 <How goes the progress on your mouse-killing, adventurer?>
		Msg14_1 <Any progress in cleaning out my storage room, yet?>
		Msg14_2 <Have you cleared the mice, or whatever they are, from the storage room yet?>
		Msg15 <Thanks! You can tell %name1% that the money's on the way.>
		Prompt1 <I'm soliciting donations for %name1%.>
		Prompt2 <I'd like to see what you have for sale.>
		Prompt3 <It's a deal.>
		Prompt4 <You'll give the money if I clean downstairs...?>
		Prompt5 <Well, I've heard enough.>
		Prompt5_1 <That's enough rambling.>
		Prompt5_2 <Well, that's enough for me.>
		Prompt5_3 <That's all I needed to hear.>
		Prompt6 <But you can hire an exterminator for $200, $300 tops...>
		Prompt7 <We're talking millions of dollars here...>
		Prompt8 <You're completely blazing insane.>
		Prompt9 <I'm not going to clean there for free.>
		Prompt10 <Why are you doing this, anyhow?>
		Prompt11 <So you want to be a cavalier?>
		Prompt12 <I said it before, you're out of your mind.>
		Prompt13 <I think you'll find the mice are gone already.>
		CPrompt13 <if= SceneVar %3% 1 1 Accept>
		Prompt14 <The mice are all dead.>
		Prompt14_1 <They won't be bothering you again.>
		CPrompt14 <if= SceneVar %3% 1 1 Accept>
		Prompt15 <I need some supplies for my adventure.>

		MetaScene 2
		sub
			room
			minimap <2#...+#.......1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Door
				update <if= V1 0 else GoCheckQuest V= 1 1 SetStat STAT_Lock %sktar_hard%>
				GoCheckQuest <ifG QuestStatus %qid% 0 SetStat STAT_Lock 0>
			end
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s3%>
			end
		end
	end
	inv
		NPC Shopkeeper
		Knowledge 8
		Ego 14
	end


