%%
%% *CS_PressConference Content
%%
%% A press conference is being called for some reason or another. The PC may have been directed
%% here by a superior, or may just have heard about it on the news.
%%
%% Param1: The scene where the conference will take place.
%% Param2: The NPC who will be leading the press conference.

%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************



%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************



%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************

Content
	name <Let's Find It Together>
	requires <*CS_PressConference +Pla R:-->
	Size 1

	% E1 is the scene where this conference takes place
	% E2 is the person giving the conference
	% E3 is a scene where the NPC will suggest they gather information
	Element3 <Scene Public !Near -7>

	start <if= PlotStatus %id% else %pop% ifNPCDead %2% LoseComp 0 .%id%_Loss>
	.%id%_Loss <+T-- +F-->

	sub
		Persona 2
		%% V%id%01 = Question Counter
		greeting <if= PlotStatus %id% else %pop% ifChatNPCInPlay else .%id%_GoBrushOff Goto .%id%_GoIntro>
		*.%id%_GoBrushOff <*IAmBusy&OnPhone>
		*.%id%_GoIntro <*PressConferenceGreeting&AboutItem .%id%_GoTell &TargetItem>
		.%id%_GoTell <if= v%id%01 0 else .%id%_GoOffer NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03 AddChat %id%04>
		.%id%_GoOffer <NPCLevel %2% StoryDL V= %id%01 1 NewChat Say %id%02 AddChat %id%05 AddChat %id%06>
		result%id%01 <AlterContext .%id%_Money Goto .%id%_GoOffer>
		.%id%_Money <+Gmo>
		result%id%02 <AddEasygoing -d8         Goto .%id%_GoOffer>
		result%id%03 <AddEasygoing  d8         Goto .%id%_GoOffer>
		result%id%04 <Goto .%id%_GoOffer>
		result%id%05 <NewChat Say %id%03 History %id%04 SMemo %id%05 LevelNPC %2% StoryDL &SetPartnerNPC %2% WinComp %3% .%id%_WinComp>
		.%id%_WinComp <+Tgs +Fin>
		result%id%06 <NewChat Say %id%06 History %id%07 SMemo 0 NTBLoseComp 0 .%id%_LoseComp ifNPCDead &EnemyNPC &SetEnemyNPC %2% FreezeNPC %2%>
		.%id%_LoseComp <+T-- +F-->
		Msg%id%01 <Tell me, \PC ... Why is it that you're interested in the \ITEM &TargetItem ?>
		Msg%id%02 <It seems we have some common interests. I propose that we join forces. Working together, it will be much easier to find the \ITEM &TargetItem .>
		Msg%id%03 <Great. Recently I've been following a lead which has led me to %name3%; I recommend that we head there first.>
		Msg%id%04 <%name2% joined forces with you to locate the \ITEM &TargetItem .>
		Msg%id%05 <%name2% suggested that the two of you go to %name3% to look for information about the \ITEM &TargetItem .>
		Msg%id%06 <Is that so... Well, it's not like I would have needed your help to locate it. Good luck in your search.>
		Msg%id%07 <%name2% suggested a partnership, but you refused \OPR %2% .>
		Prompt%id%01 <It's worth a lot of money.>
		Prompt%id%02 <I'm seeking it for the adventure!>
		Prompt%id%03 <I don't really have a reason...>
		Prompt%id%04 <I can't let \PERSONA &EnemyNPC get it.>
		CPrompt%id%04 <ifNPCOK &EnemyNPC Accept>
		Prompt%id%05 <That's a good idea.>
		Prompt%id%06 <Sorry, but I work better alone.>
	end

Content
	name <You should speak to E3>
	requires <*CS_PressConference +Pla>
	Size 1

	% E1 is the scene where this conference takes place
	% E2 is the person giving the conference
	% E3 is an expert who knows more about the item
	Element3 <Prefab>

	% v%id%01 = Initialization counter
	update <if= PlotStatus %id% else %pop% v= %id%01 1 FreezeNPC %3%>
	start <if= PlotStatus %id% else %pop% ifNPCDead %2% LoseComp 0 .%id%_Loss>
	.%id%_Loss <+T-- +F-->

	sub
		Persona 2
		greeting <if= PlotStatus %id% else %pop% ifChatNPCInPlay else .%id%_GoBrushOff Goto .%id%_GoIntro>
		*.%id%_GoBrushOff <*IAmBusy&OnPhone>
		*.%id%_GoIntro <*PressConferenceGreeting&AboutItem .%id%_GoTell &TargetItem>
		.%id%_GoTell <NewChat Say %id%01 StoryNote %id%02 AddChat %id%01 AddChat %id%02 &SetTargetNPC %3% WinComp 0 .%id%_Next>
		.%id%_Next <+Tgt +Fin>
		result%id%01 <NewChat Say %id%03>
		result%id%02 <NewChat Say %id%04 AddChat %id%01>
		Msg%id%01 <If you're hoping to find where the \ITEM &TargetItem , you should probably speak with Professor %name3% . It's been said that \SPR %3% is one of the leading experts on it.>
		Msg%id%02 <%name2% told you that %name3% knows about the \ITEM &TargetItem .>
		Msg%id%03 <I'm not entirely sure, but \SPR %3% shouldn't be too hard to find. The last I heard \SPR %3% was in this town doing research.>
		Msg%id%04 <I know that it's very valuable, and a lot of people would love to get their hands on it. Really, the person you ought to be speaking with is %name3%.>
		Prompt%id%01 <Any idea where I can find %name3%?>
		Prompt%id%02 <What do you know about the \ITEM &TargetItem ?>
	end
	inv
		NPC Professor
	end


