%%
%% *CS_DelayedVictory Content
%% *CS_DelayedNegation Content
%%
%% Victory is going to be delayed for several hours. This subplot is generally used when
%% a mission-giver asks the PC to go relax for a while before the next mission.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%% Before calling this subplot, the story status must have been set with AlterContext.
%%

Content
	name <Default Delay>
	desc <Several hours will pass before the next component is generated.>
	requires <*CS_DelayedNegation>

	% P%id%01 = Time Limit
	update <if= PlotStatus %id% else %pop% if= P%id%01 0 P= %id%01 ComTime P+ %id%01 7200 P+ %id%01 d86400>
	hour <if= PlotStatus %id% else %pop% ifG ComTime P%id%01 NegateComp &CompScene na>

Content
	name <Default Delay>
	desc <Several hours will pass before the next component is generated.>
	requires <*CS_DelayedVictory>

	% P%id%01 = Time Limit
	update <if= PlotStatus %id% else %pop% if= P%id%01 0 P= %id%01 ComTime P+ %id%01 7200 P+ %id%01 d86400>
	hour <if= PlotStatus %id% else %pop% ifG ComTime P%id%01 WinComp &CompScene na>


%%
%% *CS_FriendFired_Win Content
%%
%% The PC's friend has just been fired from a faction, and for whatever reason this
%% is a win for the PC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC who has been fired
%% Param2: The faction he's been fired from
%%

Content
	name <What did I do?>
	desc <The friend wonders what he did wrong.>
	requires <*CS_FriendFired_Win>

	% E1 is the friend
	% E2 is the faction he's been fired from
	% E3 is a public scene for the meeting
	Element3 <Scene Building Public !NEar -7>

	% p%id%01 = Initialization Counter
	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_Next>
	.%id%_Next <+T-- +G-->
	update <if= PlotStatus %id% else %pop% if= P%id%01 0 P= %id%01 1 MoveAndPacifyNPC %1% %3% SetNPCFaction %1% 0 Email %id%01>

	Msg%id%01_1 <%name1%@ \NARRATIVE 7 :// I've received bad news from %name2%... come meet me in %name3%.>
	Msg%id%01_2 <%name1%@ \NARRATIVE 7 :// I've got some bad news. Come see me in %name3%.>
	Msg%id%01_3 <%name1%@ \NARRATIVE 7 :// This has been a terrible day... I'd like to talk with you in %name3%.>

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 NTBWinComp 0 .%id%_next StoryNote %id%03 AddChat %id%01 AddChat %id%02>
		.%id%_next <+T-- +F-->
		result%id%01 <NewChat Say %id%02  AddCheerful d6>
		result%id%02 <NewChat Say %id%02>
		Msg%id%01 <I don't understand what happened; I've been fired from %name2%.>
		Msg%id%01_1 <I have some terrible news... I've been fired from %name2%. I don't know why, but they told me to go and never come back.>
		Msg%id%01_2 <I've been fired from %name2%. It was really a shock... I thought I was doing so well.>
		Msg%id%02 <I know I'll find something else in time, but for right now I'm feeling very disappointed.>
		Msg%id%02_1 <The worst part is, I made a lot of new friends while at %name2%. I guess maybe I'll see them again, but I don't know...>
		CMsg%id%02_1 <ifNPCSociable Accept>
		Msg%id%02_2 <I think I probably will do better by myself. I was never much of a joiner, anyhow.>
		CMsg%id%02_2 <ifNPCShy Accept>
		Msg%id%02_3 <Eh, it's not that bad. I'll find something else, or I won't... either way is fine.>
		CMsg%id%02_3 <ifNPCEasygoing Accept>
		Msg%id%02_4 <Those idiots at %name2% don't know what they're doing! I don't need them! I can do better by myself!>
		CMsg%id%02_4 <ifNPCPassionate Accept>
		Msg%id%02_5 <You know, I had been looking at this like an ending, but instead I think it's a beginning. Now that I'm free from %name2% I can choose my own destiny, wherever that may lead.>
		CMsg%id%02_5 <ifNPCCheerful Accept>
		Msg%id%02_6 <I guess I'm just not good enough to make it as a member of %name2%... I'll have to learn to accept that.>
		CMsg%id%02_6 <ifNPCMelancholy Accept>
		Msg%id%03 <%name1% was fired from %name2%, much to your relief.>
		Prompt%id%01 <It's probably for the best.>
		Prompt%id%01_1 <You're better off without them.>
		Prompt%id%01_2 <You're too good to work for them, anyhow.>
		Prompt%id%02 <Oh, that's too bad... I wonder what happened.>
		Prompt%id%02_1 <How surprising... I wonder what happened.>
		Prompt%id%02_2 <That's a shame... I wonder what happened.>
	end

%%
%% *CS_ThanksForRescue Content
%%	&FromCSEnemy	You rescued the NPC directly from the core story enemy NPC
%%
%% The PC has rescued the NPC. Hooray.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The one who was rescued
%%

Content
	name <The Treasure Hunter>
	desc <The NPC you rescued was looking for treasure.>
	requires <*CS_ThanksForRescue (+P--|+Pun) -1:ENEMY -1:FRIEND ~+Gkn>

	% E1 is the NPC being sought
	% E2 is the artifact that E1 was seeking
	element2 <Artifact Item PreZero>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		Greeting <if= PlotStatus %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*ThanksForRescue .%id%_GoReveal>
		.%id%_GoReveal <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoWin <StoryNote %id%03 &SetTargetItem %2% FreezeItem %2% NTBWinComp 0 .%id%_next>
		.%id%_next <+Pla +T-- +F-->
		result%id%01 <NewChat Say %id%02 AddChat %id%03 AddChat %id%04 Goto .%id%_GoWin>
		result%id%02 <NewChat Say %id%04 AddEasygoing -d8   AddChat %id%03 AddChat %id%04 Goto .%id%_GoWin>
		result%id%03 <NewChat Say %id%05 AddChat %id%05>
		result%id%04 <NewChat Say %id%06 AddChat %id%05>
		result%id%04 <NewChat Say %id%07>
		Msg%id%01 <If it wasn't for you, I might not even be here right now.>
		Msg%id%02 <They're probably after me because they think I know where the %name2% is.>
		Msg%id%03 <You learned about the %name2% from %name1%.>
		Msg%id%04 <That's the problem, you see. Right now I'm searching for the %name2%. Once I find it I'll have plenty of cash, but until that time I'm frankly broke.>
		Msg%id%05 <I thought there'd be some clue as to its location around here.>
		Msg%id%06 <The %name2% is worth more than just money. \ITEM_DESC %2% \ITEM_USAGE %2%>
		Msg%id%07 <I had planned on continuing my search, but this brush with death has made me reconsider. I just might take up chess.>
		Prompt%id%01 <Do you know why they were after you?>
		Prompt%id%01_1 <Any idea why those guys were after you?>
		Prompt%id%02 <You should give me some kind of reward, then.>
		Prompt%id%02_1 <How about a reward for my effort, then?>
		Prompt%id%03 <What do you know about this %name2%?>
		Prompt%id%03_1 <What can you tell me about the %name2%?>
		Prompt%id%04 <That must be pretty valuable.>
		Prompt%id%04_1 <Sounds like it must be valuable.>
		Prompt%id%05 <What are you going to do now?>

	end

Content
	name <Let Me Join You>
	desc <The NPC wants revenge against the enemy character, himself.>
	requires <*CS_ThanksForRescue &FromCSEnemy>

	% E1 is the character who's been rescued

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		Greeting <if= PlotStatus %id% else %pop% NTBWinComp 0 .%id%_Next goto .%id%_GoThanks>
		.%id%_Next <+T-- +F-->
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoReward>
		.%id%_GoReward <ifG ChatNPCRelationship NAV_Friend else .%id%_GoJoin  Goto .%id%_GoOffer>
		.%id%_GoJoin <PCAlly %1%  Goto .%id%_GoOffer>
		.%id%_GoOffer <NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03>

		result%id%01 <ChatNPCJoinLance   StoryNote %id%02  Goto .%id%_GoWelcome>
		*.%id%_GoWelcome <*WelcomeToLance>
		result%id%02 <NewChat Say %id%03>
		result%id%03 <NewChat Say %id%03 if= ChatNPCRelationship NAV_ArchAlly  ifChatNPCSexy  ifFlirtation SkillTar StoryDL PCLover %1%>

		Msg%id%01 <If it's alright with you, I'd like to join your lance. It's the least I could do to repay you.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable ifNPCOK &EnemyChar Accept>
		Msg%id%01_2 <I want to come with you. I could repay you for saving me, and get my revenge against \PERSONA &EnemyChar at the same time.>
		CMsg%id%01_2 <ifNPCShy ifNPCOK &EnemyChar Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCEasygoing ifNPCOK &EnemyChar Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCPassionate ifNPCOK &EnemyChar Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful ifNPCOK &EnemyChar Accept>
		Msg%id%01_6 <Let me join your lance... I must have revenge against \PERSONA &EnemyChar .>
		CMsg%id%01_6 <ifNPCMelancholy ifNPCOK &EnemyChar Accept>

		Msg%id%02_1 <%name1% joined your lance to help fight \PERSONA &EnemyChar .>
		CMsg%id%02_1 <ifNPCOK &EnemyChar Accept>
		Msg%id%02_2 <%name1% joined your lance.>
		CMsg%id%02_2 <ifNPCDead &EnemyChar Accept>
		Msg%id%03 <Always remember, I'll be right here if you need me.>
		Msg%id%03_1 <>
		Msg%id%03_2 <>
		Prompt%id%01 <I'd love to have you at my side.>
		CPrompt%id%01 <ifChatNPCCanJoinLance Accept>
		Prompt%id%01_1 <Alright, come along.>
		Prompt%id%02 <Maybe some other time.>
		Prompt%id%02_1 <Sorry, but now's not a good time.>
		Prompt%id%03 <I think you'd be safer here.>
		Prompt%id%03_1 <It's too dangerous to come with me.>
	end

Content
	name <Generic Thanks>
	desc <The PC has just rescued someone, and will get a cash prize. Lame.>
	requires <*CS_ThanksForRescue>

	% E1 is the character who's been rescued

	% P1 = Initialization Counter
	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		Greeting <if= PlotStatus %id% else %pop% goto .%id%_GoThanks>
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoReward>
		.%id%_GoReward <ifG 1 ChatNPCRelationship else .%id%_GoCash PCFriend %1% Goto .%id%_GoCash>
		.%id%_GoCash <NewChat Say %id%01 CashPrize Reward Threat StoryDL 100 250 NTBWinComp 0 .%id%_Next>
		.%id%_Next <+T-- +F-->
		Msg%id%01 <Here's a little reward, in appreciation of what you've done.>
		Msg%id%01_1 <I'd like for you to have this reward. I know that it isn't much, but it's the least I could do to thank you for saving my life.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Here, I'd like you to have this. Thanks again.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <We all know that nothing says "thank you" like a big handfull of money. Unfortunately I don't have a big handfull, but I can give you this little one. Thanks for saving me.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <Nothing I could ever do would repay you for saving my life, but I can at least give you this money as a token of appreciation.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <It's really lucky that you showed up when you did. Here, take this- it's my way of saying thanks.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <If not for you, I would be dead right now. Here's a small token of my gratitude.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
	end

Content
	name <Rescued an Ally>
	desc <The PC has just rescued an ally, who will suggest that he go speak with another member of the faction.>
	requires <*CS_ThanksForRescue 1:Ally -1:PDASS>

	% E1 is the character who's been rescued
	% E2 is a public scene in this city, to be used as home scene if a
	%    pre-existing E4 can't be found.
	% E3 is another character allied with E2
	element2 <Scene Public !Okay -3 Building !Near -7>
	element3 <Character !Comrade 1 !Near 2 Neverfail>
	NeverFail3 <Mecha Pilot>

	% P1 = Initialization Counter
	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		% V1 = Can only get reward once
		Greeting <if= PlotStatus %id% else %pop% goto .%id%_GoThanks>
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoReward>
		.%id%_GoReward <if= V%id%01 0 else .%id%_GoBriefing V= %id%01 1 CashPrize Reward Threat StoryDL 100 200 NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoBriefing <ifNPCOK %3% else .%id%_GoE3Dead NewChat Say %id%02 StoryNote %id%03 &SetTargetChar %3%  WinComp NPCScene %3% .%id%_changes>
		.%id%_changes <+Tgt +Fmi>
		.%id%_GoE3Dead <NewChat Say %id%05 SMemo 0 NTBWinComp 0 .%id%_changes2>
		result%id%01 <Goto .%id%_GoBriefing>
		result%id%02 <if= ChatNPCFac PCFac else .%id%_GoR2Reject if# PCFac 0 else .%id%_GoR2Reject NewChat Say %id%04 AddChat %id%03>
		*.%id%_GoR2Reject <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <NTBWinComp 0 .%id%_changes2>
		.%id%_changes2 <+T-- +F-->
		result%id%03 <Goto .%id%_GoBriefing>
		Msg%id%01 <Here's a reward for your quick thinking. This matter needs to be dealt with quickly, before it gets out of hand. We could use someone like you on our side.>
		Msg%id%02 <Go speak with %name3% at \Scene NPCScene %3% . I think \SPR %3% 'll have a mission you can help with.>
		Msg%id%03 <%name1% sent you to speak with %name3% at \SCENE NPCScene %3% .>
		Msg%id%04 <This is now official \FACTION ChatNPCFac business. All your other missions can wait; this is top priority.>
		Msg%id%05 <With %name3% dead, all our operatives will have to be extra careful. Stay on your toes for the next little while.>
		Prompt%id%01 <Tell me what you need.>
		Prompt%id%02 <Forget it, I'm finished here.>
		Prompt%id%03 <Understood. Tell me what I have to do.>
		Prompt%id%03_1 <Yeah, I'll be a good little quest-bitch...>
		CPrompt%id%03_1 <ifPCPassionate ifPCMelancholy Accept>
	end


Content
	name <Thanks for the Help>
	desc <The PC has rescued someone, who now wants to join the lance.>
	requires <*CS_ThanksForRescue -1:Ally -1:Enemy ~+Glo>

	%% E1 is the NPC that the PC rescued
	%% E2 is a nice scene for a date
	element2 <Scene !Near -7 Building Public Meeting>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .loss>
	.loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% Goto .%id%_GoStart>
		*.%id%_GoStart <*ThanksForRescue .%id%_GoOffer>
		.%id%_GoOffer <PCFriend %1% ifG React 30 else .%id%_GoNoOffer ifG PCCharm 10 else .%id%_GoNoOffer NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		.%id%_GoNoOffer <ifChatNPCSexy else .%id%_GoCashAward ifFlirtation SkillTar StoryDL else .%id%_GoCashAward ifG React d10 else .%id%_GoCashAward NewChat Say %id%08 AddChat %id%06 AddChat %id%07>
		.%id%_GoCashAward <NewChat Say %id%07  StoryNote %id%05 CashPrize Reward Threat StoryDL 150 100 NTBWinComp 0 .%id%_next>
		result%id%01 <NewChat Say %id%02 StoryNote %id%03 PCAlly %1% WinComp 0 .%id%_next>
		.%id%_next <+T-- +F-->
		result%id%02 <NewChat Say %id%04 StoryNote %id%05 CashPrize Reward Threat StoryDL 150 100 NTBWinComp 0 .%id%_next>
		result%id%03 <NewChat Say %id%06 AddChat %id%04 AddChat %id%05>
		result%id%04 <Goto result%id%01>
		result%id%05 <Goto result%id%02>
		result%id%06 <NewChat Say %id%09 StoryNote %id%010 &SetTargetChar %1% WinComp %2% .%id%_date>
		.%id%_date <+Tda +F-->
		result%id%07 <Goto result%id%02>
		Msg%id%01 <I thought that to repay you for saving my life, I could join your team and maybe help out. What do you think?>
		Msg%id%02 <Thanks! I won't let you down!>
		Msg%id%03 <%name1% joined your lance in thanks for saving \PPR %1% life.>
		Msg%id%04 <I understand. Well, the least I can do is give you some money. Thanks for your help. I won't forget it.>
		Msg%id%05 <%name1% rewarded you for saving \PPR %1% life.>
		Msg%id%06 <I know, but... I have other talents that could be useful. And you could teach me to fight. Can't I come along with you, please?>
		Msg%id%07 <In thanks for saving my life, I'd like you to have this money.>
		Msg%id%08 <In thanks for saving my life, I'd like... honestly, I'd like to go out with you sometime.>
		Msg%id%09 <Great! Meet me in %name2% later on.>
		Msg%id%010 <After saving \PPR %1% life, %name1% invited you to %name2% for a date.>
		Prompt%id%01 <I think it's a good idea.>
		Prompt%id%01_1 <I'd be honored to have you join me.>
		Prompt%id%02 <No thanks. I don't need any help.>
		Prompt%id%02_1 <Sorry, I'm not looking for another lancemate.>
		Prompt%id%03 <Thanks, but aren't you a bit useless?>
		CPrompt%id%03 <if= NPCSkill E2 10 0 Accept>
		Prompt%id%03_1 <Thanks, but you aren't much of a fighter.>
		Prompt%id%04 <Alright. You can join me.>
		Prompt%id%05 <I don't have time to look after you.>
		Prompt%id%06 <I'd like that too.>
		Prompt%id%07 <This isn't a good time for that.>
	end

%%
%% *ForcedInduction Content
%%
%% The PC has been forced into the ranks of the target faction against his will.
%% This component will move the PC to a induction location, alter the plot state,
%% store the PC's current faction and set the new faction. The new propp state
%% will be "+Psh".
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC doing the induction
%% Param2: The faction the PC will be inducted into
%%

Content
	name <Induction Plus Mission>
	requires <*ForcedInduction>
	desc <You are forcibly inducted into a faction and told to report for a mission.>

	% Element1 is the NPC doing the induction
	% Element2 is the faction the PC will be inducted into
	% Element3 is the scene where the induction will take place
	% Element4 is the NPC you must report to
	Element3 <Scene Building Public !Near -7 !Ally 2>
	Element4 <Character !Ally 2 !Near -7>

	% v%id%01 = Initialization/Conversation Counter

	update <if= PlotStatus %id% else %pop% if= v%id%01 0 V= %id%01 1 Exit %3% MoveAndPacifyNPC %1% %3% MoveNPC %4% %3%>

	start <if= v%id%01 1 else %pop% Alert %id%01 ForceChat %1% V= %id%01 2 Goto %pop%>
	Msg%id%01 <You are taken to %name3%.>

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop%  &SetAllyFac %2% StoreOldFaction SetPCFac %2% &SetTargetChar %4% NegateComp %3% .%id%_next  StoryNote %id%02 Goto .%id%_GoGreet>
		*.%id%_GoGreet <*CS_ForcedInduction_Greeting .%id%_GoBrief %2%>
		.%id%_next <+Tgt +Fmi +Psh>
		.%id%_GoBrief <ifNPCOK %4% else .%id%_GoScrapMission NewChat Say %id%01 SMemo %id%04>
		.%id%_GoScrapMission <NewChat Say %id%03 AlterContext .%id%_ScrapContext>
		.%id%_ScrapContext <+T-- +F-->
		Msg%id%01 <Go talk with %name4% for your first mission; \SPR %4% should be somewhere in this building.>
		Msg%id%01_1 <You have to speak with %name4% about your first mission. I believe \SPR %4% can be found somewhere in this building.>
		Msg%id%01_2 <%name4% has the details of your first mission. You'll need to go talk with \OPR %4% right over there.>
		Msg%id%02 <You were conscripted into %name2%.>
		Msg%id%03 <There was a task planned for you, but I'm afraid it's been scrapped. I'll let you know when there's something for you to do.>
		Msg%id%04 <You have to see %name4% in %name3% about your first mission for %name2%.>
	end

Content
	name <Basic Induction>
	requires <*ForcedInduction>
	desc <You are forcibly inducted into a faction, but given no orders at this time.>

	% Element1 is the NPC doing the induction
	% Element2 is the faction the PC will be inducted into
	% Element3 is the scene where the induction will take place
	Element3 <Scene Building Public !Near -7 !Ally 1>

	% v%id%01 = Initialization/Conversation Counter

	update <if= PlotStatus %id% else %pop% if= v%id%01 0 V= %id%01 1 Exit %3% MoveAndPacifyNPC %1% %3%>

	start <if= v%id%01 1 else %pop% Alert %id%01 ForceChat %1% V= %id%01 2 Goto %pop%>
	Msg%id%01 <You are taken to %name3%.>

	% The only subplot is a delay before the next mission.
	SubPlot1 <*CS_DelayedNegation>

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop%  &SetAllyFac %2% StoreOldFaction SetPCFac %2% &SetCompScene 0 AlterCOntext .%id%_next SetPlotStatus %id1%  StoryNote %id%02 Goto .%id%_GoGreet>
		*.%id%_GoGreet <*CS_ForcedInduction_Greeting .%id%_GoBrief %2%>
		.%id%_next <+T-- +F-- +Psh>
		.%id%_GoBrief <NewChat Say %id%01>
		Msg%id%01 <We'll let you know when there's some work that needs to be done. For the time being you're free to go.>
		Msg%id%01_1 <For the time being you're free to do whatever you'd like. I'll let you know when it's time for your first mission.>
		Msg%id%01_2 <You are free to go, for the moment. I'll call you back when we have a mission for you.>
		Msg%id%02 <You were conscripted into %name2%.>
	end

Content
	name <Secret Base Induction>
	requires <*ForcedInduction>
	desc <You are forcibly inducted into a faction, but given no orders at this time.>

	% Element1 is the NPC doing the induction
	% Element2 is the faction the PC will be inducted into
	% Element3 is an urban scene for the secret base
	% Element4 is the secret base
	Element3 <Scene Urban !Near -7>
	Element4 <Prefab>
	Place4 <3>

	% v%id%01 = Initialization/Conversation Counter

	update <if= PlotStatus %id% else %pop% if= v%id%01 0 V= %id%01 1 Exit %4% MoveAndPacifyNPC %1% %4%>

	start <if= v%id%01 1 else %pop% Alert %id%01 ForceChat %1%  V= %id%01 2  Goto %pop%>
	Msg%id%01 <You are taken to a secret base of %name2%.>

	% The only subplot is a delay before the next mission.
	SubPlot1 <*CS_DelayedNegation>

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop%  &SetAllyFac %2% StoreOldFaction SetPCFac %2% &SetCompScene 0 AlterCOntext .%id%_next SetPlotStatus %id1%  StoryNote %id%02 Goto .%id%_GoGreet>
		*.%id%_GoGreet <*CS_ForcedInduction_Greeting .%id%_GoBrief %2%>
		.%id%_next <+T-- +F-- +Psh>
		.%id%_GoBrief <NewChat Say %id%01>
		Msg%id%01 <We'll let you know when there's some work that needs to be done. For the time being you're free to go.>
		Msg%id%01_1 <For the time being you're free to do whatever you'd like. I'll let you know when it's time for your first mission.>
		Msg%id%01_2 <You are free to go, for the moment. I'll call you back when we have a mission for you.>
		Msg%id%02 <You were conscripted into %name2%.>

		STC MS_Office
		SetID 4
	end
	inv
		STC ENCOUNTER-WANDER
		name <Secret Base>
		update <if= PlotStatus %id% else GoHide SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	end

%%
%% *CS_Win_MechaRetribution
%%
%% The PC has won a mecha retribution encounter. This subplot is free to modify
%% the story as it sees fit.
%%
%% Param1: The Mission-giver

Content
	name <There Are Others>
	desc <The NPC will refer the PC to someone else who's had problems.>
	requires <*CS_Win_MechaRetribution>

	% E1 is the NPC.
	% E2 is the other NPC who's had troubles
	% E3 is the scene for the next meeting
	Element2 <Character !Near -7 !Okay -3 !Xclude -2 !Okay 1>
	Element3 <Scene Building Public !Okay 2>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 AddChat %id%01 AddChat %id%02 SMemo 0 Goto .%id%_GoEnd>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 500 SMemo 0 NTBWinComp 0 .%id%_win>
		.%id%_win <+T-- +F-->
		Result%id%01 <ifNPCOK %2% else .%id%_GoFail NewChat Say %id%02 StoryNote %id%03 &SetTargetChar %2% &SetCompScene %3% AlterContext .%id%_GoHelp>
		.%id%_GoHelp <+Tgt +Fmi>
		.%id%_GoFail <NewChat Say %id%05>
		Result%id%02 <NewChat  AddSociable -d10  Say %id%04>
		Msg%id%01_10 <You've made \FACTION &EnemyFac pay for what they did here, but they haven't been stopped. I know there are others who have suffered at their hands.>
		CMsg%id%01_10 <ifFactionExists &EnemyFac Accept>
		Msg%id%01_20 <You've made those raiders pay, but I fear that this is only the beginning. Other people have also been attacked.>
		CMsg%id%01_20 <if= &EnemyFac 0 Accept>
		Msg%id%02 <In that case, go see %name2% at %name3% . I hear that \SPR %2% has also suffered from unprovoked mecha attacks.>
		Msg%id%03 <%name1% sent you to help %name2% at %name3% .>
		Msg%id%04 <Given time, it's likely to become everyone's problem.>
		Msg%id%05 <I'm sure that given time you'll get that chance.>
		Prompt%id%01 <I'd like to help them, too.>
		Prompt%id%02 <That's not my problem.>
	end

Content
	name <Justice Has Been Served>
	desc <The NPC will pay the PC for the job.>
	requires <*CS_Win_MechaRetribution -1:THIEF>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 SMemo 0 Goto .%id%_GoEnd>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 500 SMemo 0 NTBWinComp 0 .%id%_win>
		.%id%_win <+T-- +F-->
		Msg%id%01 <They have been punished. Thanks for your help.>
	end



%%
%% *CS_Win_UnexpectedMeeting Content
%%	&Rescue		The PC rescued the NPC
%%
%% During the last mission, the PC met someone. The mission giver will now probably
%% tell the PC to go look for that person, or something.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%% Param2: The NPC who was met
%%

Content
	name <He'll want to thank you>
	desc <The PC will be sent to find the NPC, because he'll want to thank him.>
	requires <*CS_Win_UnexpectedMeeting &Rescue>

	% E1 is the mission-giver.
	% E2 is the NPC previously met

	start <if= PlotStatus %id% else %pop% ifNPCDead %1%  SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% CashPrize CashPrize Reward Threat StoryDL 100 300 ifNPCOK %2% else .%id%_GoNPCGone Goto .%id%_GoSearchNPC>
		.%id%_GoSearchNPC <NewChat Say %id%01 SMemo %id%02 &SetTargetChar %2% WinComp 0 .%id%_Win>
		*.%id%_GoNPCGone <*MissionWasSuccess&Reward .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <SMemo 0 WinComp 0 .%id%_end>
		.%id%_win <+Tlt +Frt>
		.%id%_end <+T-- +F-->
		Msg%id%01 <Good work on that mission. You should probably try to find that person you rescued; I'll bet \SPR %2% will want to reward you.>
		Msg%id%01_1 <I'm quite satisfied with the results of that last mission, but I'm quite sure there's someone else who will want to thank you as well. Have you spoken to %name2% since getting back? I think you should.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Good job. I have nothing else for you to do now, although I'm sure %name2% will want to speak with you.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <That went fairly well, I think. If you're looking for something else to do now, you should try and find that person you rescued. Maybe you can get another reward from \OPR %2% .>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <Brilliant work, \PC . I'll bet %name2% will want to thank you for rescuing \OPR %2% .>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <I knew you'd come through on that mission. Here's a reward... You might want to see if you can find %name2%, since I'm certain \SPR %2% 'll want to reward you as well.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <The mission was a success. In the name of completeness, though, you should try to find %name2% and make sure \SPR %2% got back alright.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Msg%id%02 <You should try to find %name2%, whom you recently rescued.>
	end

Content
	name <Find out what he knows>
	desc <The PC is asked to locate the previously met NPC, and find out what he knows.>
	requires <*CS_Win_UnexpectedMeeting>

	% E1 is the mission-giver.
	% E2 is the NPC previously met

	start <if= PlotStatus %id% else %pop% ifNPCDead %1%  SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_win <+Tlt +Fin>
		.%id%_meh <+T-- +F-->
		result%id%01 <ifNPCOK %2% else .%id%_GoTargetDead NewChat Say %id%02 &SetTargetChar %2% WinComp 0 .%id%_win CashPrize Reward Threat StoryDL 100 200 AddReact d10 SMemo %id%04>
		.%id%_GoTargetDead <NewChat Say %id%03 SMemo 0 WinComp 0 .%id%_meh CashPrize Threat StoryDL 100 300>
		result%id%02 <NewChat Say %id%05 SMemo 0 WinComp 0 .%id%_meh CashPrize Threat StoryDL 100 150 AddSociable -d10>
		Msg%id%01 <Welcome back, \PC . How did the mission go?>
		Msg%id%01_1 <What's the progress on the mission I assigned to you? Do you have anything to report?>
		Msg%id%01_2 <Did you complete the mission I assigned to you? I've been waiting for your report.>
		Msg%id%02 <%name2% might know something about our enemy. You should try to find \OPR %2% and see what you can learn.>
		Msg%id%02_1 <Interesting. %name2% might have some useful information. You should try to find \OPR %2% and see what you can learn.>
		Msg%id%03 <I'm sure that's not important. Here's your reward for completing the mission.>
		Msg%id%04 <You should seek %name2% to find out what \SPR %2% knows.>
		Msg%id%05 <I see. Here's your reward, then.>
		Msg%id%05_1 <I knew you wouldn't have any problem. Here's a reward for your efforts.>
		CMsg%id%05_1 <ifG React 40 Accept>
		Msg%id%05_2 <I find that hard to believe... but as long as the job has been done, here's your reward.>
		CMsg%id%05_2 <ifG 10 React Accept>
		Prompt%id%01 <I met someone named %name2%.>
		Prompt%id%01_1 <During the mission I met %name2%.>
		Prompt%id%02 <It was completed without incident.>
		Prompt%id%02_1 <There's nothing special to report.>
	end

%%
%% *CS_Loss_UnexpectedMeeting Content
%%
%% During the last mission, the PC met someone. Unfortunately that person apparently
%% died or something.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%% Param2: The NPC who was met
%%

Content
	name <Too bad we couldn't find that pilot...>
	desc <The mission-giver laments the loss of E2.>
	requires <*CS_Loss_UnexpectedMeeting>

	% E1 is the mission-giver.
	% E2 is the NPC who was lost

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop%  NewChat Say %id%01 AddChat %id%01  AddChat %id%02>

		.%id%_GoEnd <SMemo 0  NTBLoseComp 0 .%id%_loss>
		.%id%_loss <+T-- +F-->
		.%id%_GoContinue <SMemo %id%05  &SetTargetChar %2% LoseComp 0 .%id%_find>
		.%id%_find <+Tlt +Fin>

		result%id%01 <ifNPCDead %2% else .%id%_GoE2Find NewChat Say %id%02 Goto .%id%_GoEnd>
		.%id%_GoE2Find <NewChat Say %id%03  Goto .%id%_GoContinue>
		*result%id%02 <*MissionWasFailure .%id%_GoR2Finish &AllyFac &EnemyFac>
		.%id%_GoR2Finish <AddSociable -d8  Goto .%id%_GoEnd>

		Msg%id%01 <I understand that things didn't go well on the mission.>
		Msg%id%02 <Whatever information %name2% might have had is now gone as well...>
		Msg%id%03 <Reports indicate that %name2% is still out there; if you could find \OPR %2% , maybe \SPR %2% could give us some useful information.>

		Msg%id%05 <You should seek %name2% to find out what \SPR %2% knows.>
		Prompt%id%01 <I couldn't save %name2%.>
		Prompt%id%01_1 <I failed; %name2% was lost.>
		Prompt%id%02 <I don't want to talk about it.>
		Prompt%id%02_1 <You don't want to know.>
	end

%%
%% *CS_Win_ToUnknownEnemy Content
%%
%% The PC has won a mission and the plot state has changed to +Pun.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%%

Plot
	name <City in Trouble>
	desc <PC asked to help defend against the unknown enemy.>
	requires <*CS_Win_ToUnknownEnemy (P:--|P:COR|P:PDASS) (F:MIL|F:CRM|F:--) (1:POLIT|1:POLIC|1:MILIT) -L:ENEMY>

	% E1 is the target NPC.
	% E3 is the next NPC to meet about a mission
	% E3 is the scene for the next mission
	% E4 is the local government faction
	element2 <Character !Near -7 !Ally -7>
	element3 <MetaScene !Near -7 *Building NeverFail>
	element4 <Faction Government !Comrade -7>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		% V%id%01 = Have given reward
		greeting <if= PlotSTatus %id% else %pop% if= V%id%01 0 else .%id%_GoNewMission CashPrize Reward Threat StoryDL 100 500 V= %id%01 1 Goto .%id%_GoGetReward>
		*.%id%_GoGetReward <*MissionWasSuccess&Reward .%id%_GoRewardOp N3 N2>
		.%id%_GoRewardOp <AddChat %id%03>
		.%id%_GoNewMission <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		result%id%01 <ifNPCOK %2% else .%id%_GoNoMission NewChat Say %id%03 &SetTargetChar %2% &SetAllyFac %4% StoryNote %id%02 WinComp %3% .%id%_changes>
		.%id%_changes <+Tgt +Fmi>
		.%id%_GoNoMission <NewChat Say %id%04 &SetAllyFac %4% StoryNote %id%05 WinComp 0 .%id%_changes2>
		*result%id%02 <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <WinComp 0 .%id%_changes2>
		.%id%_changes2 <+T-->
		result3 <Goto .%id%_GoNewMission>
		Msg%id%01 <We still don't know where these invaders are coming from, nor what they want. You know, %name4% could use a good pilot like you to help face this threat.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Msg%id%02 <%name1% sent you to speak with %name2% in %name3%.>
		Msg%id%03 <In that case you should go to %name3% and speak with %name2%. I believe \SPR E2 has something you could help with.>
		Msg%id%04 <That's good to hear. Go rest up, and get your stuff repaired. There's no telling when we could be attacked again.>
		Msg%id%05 <You accepted %name1%'s request to help defend %name4%.>
		Prompt%id%01 <It would be an honor to help.>
		Prompt%id%02 <Sorry, I'm not signing up for anyone's war.>
		Prompt%id%03 <Thank you.>
	end


%%
%% *CS_Loss_ToUnknownEnemy Content
%%
%% Catch-all lose mission component.
%% The PC has lost a mission provided by the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%%

Content
	name <Default Loss>
	desc <The NPC will debrief the PC on the loss.>
	requires <*CS_Loss_ToUnknownEnemy>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 SMemo 0 NTBLoseComp 0 .%id%_loss>
		.%id%_loss <+T-- +F-->
		Msg%id%01 <You have failed in the mission. Unless we can learn what our enemy is after, we have no chance of success.>
		Msg%id%01_1 <We have lost, and even worse we don't know the purpose behind these attacks.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <You lost the mission. Our enemy moves in secret, but soon all will be revealed.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <That last mission didn't go so well. It'd be a lot easier to deal with these attacks if we knew what they were after.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <You failed. There must be some purpose behind the attacks, but until we figure it out we are forced to play sitting ducks.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <Too bad about that last mission, but don't let it get you down. We have to keep our guard up. There's no telling when another attack could come.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <Your mission was a complete failure. This is madness... how are we supposed to defend ourselves, when we don't even know what the enemy is after?>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Msg%id%01_10 <We have to learn what \FACTION &EnemyFac is up to. It seems like that's the only chance we have to stop them.>
		CMsg%id%01_10 <ifFactionExists &EnemyFac Accept>
		Msg%id%01_20 <It's clear now that \PERSONA &EnemyChar has some reason for these attacks, but what could it be?>
		CMsg%id%01_20 <ifNPCOK &EnemyChar Accept>
	end

%%
%% *CS_Win_RevealEnemyNPC
%%
%% The PC has defeated a new enemy NPC; this NPC should _not_ have been set as
%% the story enemy by the calling component! That will probably but not nessecarily
%% be done here. This component may alter the story state.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%% Param2: The enemy met; check to make sure he's not dead
%%

Content
	name <Begin the Manhunt>
	desc <The PC will begin a manhunt for the enemy NPC.>
	requires <*CS_Win_RevealEnemyNPC (C:POLIC|P:POL|2:THIEF|E:++)>

	% E1 is the mission-giver.
	% E2 is the target.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportSuccess>
		.%id%_GoReportSuccess <NewChat Say %id%01 AddChat %id%01 Goto .%id%_GoEnd>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 500 ifNPCOK %2% else .%id%_GoTrivialEnd SMemo %id%02  &SetTargetChar %2%  WinComp 0 .%id%_win>
		.%id%_GoTrivialEnd <SMemo 0  NTBWinComp 0 .%id%_meh>
		.%id%_win <+Tht +Far>
		.%id%_meh <+T-- +F-->
		Result%id%01 <NewChat Say %id%03>
		Msg%id%01 <Good work on the mission. Here's a reward.>
		Msg%id%02 <%name2% could still be in \SCENE &EpisodeScene ; you should probably try to find \OPR %2% .>
		Msg%id%03 <If %name2% is still nearby, it could be dangerous. It's important that \SPR %2% be found before \SPR %2% can attack again.>
		Prompt%id%01 <I fought someone named %name2%.>
		CPrompt%id%01 <ifNPCOK %2% Accept>
	end

Content
	name <E2 is a priority>
	desc <The new enemy will become the core story enemy.>
	requires <*CS_Win_RevealEnemyNPC E:-->

	% E1 is the mission-giver.
	% E2 is the target.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% ifNPCOK %2% else .%id%_GoReportSuccess &SetEnemyChar %2% gOTO .%id%_GoReportSuccess>
		*.%id%_GoReportSuccess <*MissionWasSuccess&Reward .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 500 SMemo 0 NTBWinComp 0 .%id%_win>
		.%id%_win <+T-- +F-->
	end

%%
%% *CS_Loss_RevealEnemyNPC
%%
%% The PC has lost to a new enemy NPC; this NPC should _not_ have been set as
%% the story enemy by the calling component! That will probably but not nessecarily
%% be done here. This component may alter the story state.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%% Param2: The enemy met; check to make sure he's not dead
%%

Content
	name <Never Again>
	desc <The PC will make up for this loss by finding E2.>
	requires <*CS_Loss_RevealEnemyNPC>

	% E1 is the mission-giver.
	% E2 is the target.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 LOseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportFailure>
		.%id%_GoReportFailure <NewChat Say %id%01 AddChat %id%01 SMemo 0  NTBLoseComp 0 .%id%_loss>
		.%id%_hunt <+Tht +Far>
		.%id%_loss <+T-- +F-->
		Result%id%01 <NewChat Say %id%03 SMemo %id%02  &SetTargetChar %2%  AlterContext .%id%_hunt>
		Msg%id%01 <You were defeated by %name2%. This is very bad.>
		Msg%id%02 <%name2% could still be in \SCENE &EpisodeScene ; you should probably try to find \OPR %2% .>
		Msg%id%03 <I certainly hope that you can. If \SPR %2% is still in \SCENE &EpisodeScene we could be in for some more trouble.>
		Prompt%id%01 <I will find %name2%, and finish this.>
		CPrompt%id%01 <ifNPCOK %2% Accept>
	end

Content
	name <Default Loss to Enemy NPC>
	desc <The NPC will debrief the PC on the loss; E2 becomes the core enemy.>
	requires <*CS_Loss_RevealEnemyNPC E:-->

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportLoss>
		*.%id%_GoReportLoss <*MissionWasFailure .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <SMemo 0 NTBLoseComp 0 .%id%_loss ifNPCOK %2% &SetEnemyChar %2%>
		.%id%_loss <+T-- +F-->
	end


%%
%% *CS_CAWinMission Content
%%	&RevealEF	The enemy faction has been revealed
%%
%% Catch-all win mission component.
%% The PC has won a mission provided by the NPC. Note that the mission should not
%% have altered the state of the story; therefore, this reward is free to do so
%% itself.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%%

Content
	name <Pass the Buck>
	desc <The NPC will send the PC to talk with someone else.>
	requires <*CS_CAWinMission>

	% E1 is the NPC.
	% E2 is another NPC the PC can speak with
	% E3 is a scene for the meeting
	Element2 <Character !Near -7 !Ally 1 !Okay -3>
	Element3 <Scene Building Public !Near -7 !Okay 2>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% SMemo 0  ifNPCOK %2% else .%id%_GoReportSuccess  if# ChatNPCFac 0 else .%id%_GoReportSuccess NewChat Say %id%01 SMemo %id%02 Goto .%id%_GoEnd>
		*.%id%_GoReportSuccess <*MissionWasSuccess&Reward .%id%_GoMeh &AllyFac &EnemyFac>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 500 &SetTargetChar %2% WinComp %3% .%id%_win>
		.%id%_win <+Tgt +Fmi>
		.%id%_GoMeh <CashPrize Reward Threat StoryDL 100 500 NTBWinComp 0 .%id%_meh>
		.%id%_meh <+T-- +F-->
		Msg%id%01 <Good work, \PC . If you want to see this through, I believe %name2% at %name3% has another mission you could help with.>
		Msg%id%01_1 <Thanks \PC . We're going to need a lot of help to fight this problem... I believe %name2% at %name3% is looking for someone to run another mission, if you're interested.>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Good job. %name2% at %name3% has another mission for you, if you're interested.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <Back for your pay, I see. I believe %name2% at %name3% has another mission if you're planning to continue this.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <One battle's over, but we're a long way from the end yet... %name2% could use some help over at %name3% , if you're up for another mission.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Msg%id%02 <%name1% sent you to %name2% at %name3% for another mission.>
	end

Content
	name <Here, have some money>
	desc <The NPC will pay the PC for the job.>
	requires <*CS_CAWinMission>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportSuccess>
		*.%id%_GoReportSuccess <*MissionWasSuccess&Reward .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 500 SMemo 0 NTBWinComp 0 .%id%_win>
		.%id%_win <+T-- +F-->
	end


%%
%% *CS_CALoseMission Content
%%	&RevealEF	The enemy faction has been revealed
%%
%% Catch-all lose mission component.
%% The PC has lost a mission provided by the NPC. Note that the mission should not
%% have altered the state of the story; therefore, this reward is free to do so
%% itself.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%%

Content
	name <Default Loss>
	desc <The NPC will debrief the PC on the loss.>
	requires <*CS_CALoseMission>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportLoss>
		*.%id%_GoReportLoss <*MissionWasFailure .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <SMemo 0 NTBLoseComp 0 .%id%_loss>
		.%id%_loss <+T-- +F-->
	end


%%
%% *CS_WinDuelVs Content
%%
%%
%% The PC has won a duel against the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Challenger
%%

Content
	name <The Sore Loser>
	desc <The NPC can't believe he lost; in fact, he will become CS Enemy.>
	requires <*CS_WinDuelVs E:-- -1:ALLY -1:PDASS ~1:ENEMY>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop%  NewChat Say %id%01 FreezeNPC %1% StoryNote %id%02 &SetEnemyChar %1% WinComp 0 .%id%_win ifNPCPassionate PCEnemy %1%>
		.%id%_win <+T-- +F-->
		Msg%id%01 <I can't accept losing the duel to you... From this point on I'm going to train hard, then someday I'll be back and I'll defeat you!>
		Msg%id%01_1 <People are already talking about how you defeated me... I can't accept that! You may have won this time, but I promise you that the next time we meet I will be the victor!>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Enjoy your victory, for now. I promise you that it won't last long.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <Things used to be going so well... I can't accept being defeated by you, and I won't. We'll face each other again, sometime.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <You cheater! That's the only way you could have possibly beaten me... From this moment on, I pledge to destroy you. Sleep with one eye open \PC .>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <What, you've come here to rub your victory in my face? Get lost... the next time we meet won't be nearly so pleasant for you as today was.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <You ruined everything! How did you beat me? You must have cheated... From this day forth I dedicate my life to making yours miserable. Don't think I'll ever forget what you've done.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Msg%id%02 <%name1% swore revenge on you for beating \OPR %1% in a duel.>
	end

Content
	name <You should go see MILIT...>
	desc <The NPC will reccomend that the PC go ask someone about a job.>
	requires <*CS_WinDuelVs -7:ENEMY -C:THIEF>

	% E1 is the NPC.
	% E2 is the person to see about the job
	% E3 is a public scene for the meeting.
	Element2 <Character "MILIT" !Near -7 !Ally -7 !Okay -3 !Okay 1>
	Element3 <Scene Building Public !Near -7 !Ally 2>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% ifNPCOK %2% else .%id%_GoE2Died NewChat Say %id%01 SMemo %id%02 &SetTargetChar %2% WinComp %3% .%id%_win>
		.%id%_GoE2Died <NewChat Say %id%03 SMemo 0 WinComp 0 .%id%_draw>
		.%id%_win <+Tgt +Fmi>
		.%id%_draw <+T-- +F-->
		Msg%id%01 <You beat me. You should go talk with %name2% in %name3%; I hear that \SPR %2% 's looking for a good pilot.>
		Msg%id%01_1 <You won our duel. A fighter with your talents could really go places... I hear that %name2% is looking for a pilot over at %name3%. You should go talk with \OPR %2% .>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <Good match. I've got a tip for you- %name2% in %name3% is looking for a mecha pilot. You should definitely check it out.>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <You did half decent out there, I thought. If you're interested I know of a job opening. %name2% in %name3% needs a pilot. Tell \OPR %2% I sent you.>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <You've shown me the true art of fighting. I was going to go to %name3% and do a mission for %name2% , but you should go instead. You've proven yourself the better pilot.>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <That was a great fight. I've got a tip for you- %name2% in %name3% is looking for a pilot. You've proven yourself capable, I think you could get the job.>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <I can't believe I lost to you... I was supposed to run a mission for %name2% at %name3% today, but now that my mecha's in the shop I can't. If you wanted you could go take the mission yourself.>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Msg%id%02 <%name1% reccomended that you go ask %name2% in %name3% for a mission.>
		Msg%id%03 <You won the duel. Congratulations.>
	end

%%
%% *CS_LoseDuelVs Content
%%
%%
%% The PC has lost a duel against the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Challenger
%%

Content
	name <Ha ha!>
	desc <The NPC will just chide the PC, then end the plot.>
	requires <*CS_LoseDuelVs>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% SMemo 0 LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 SMemo 0 NTBLoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
		Msg%id%01 <You lost the duel. Good for me.>
	end
