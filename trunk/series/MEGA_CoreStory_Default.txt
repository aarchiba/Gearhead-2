%% *CS_Duel Content
%%
%%
%% The PC is about to duel somebody as part of the core story.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%% This component should move the opponent from its current location to the
%% contest location at update, and then move the opponent back to its
%% original location at the end of the battle.
%%
%% Param1: The Challenger
%% Param2: The Challenger's home scene, for the purpose of this component
%% Param3: The outdoor scene where the duel will take place
%%
%% NEEDED SCRIPTS: To deal with the ending, the parent plot must define two
%% scripts:
%% 	.%id%_%plotid%_GoWin
%% 	.%id%_%plotid%_GoLose
%% %id% is the ID of the duel layer; so, if it was generated as subplot 1,
%% the calling plot would use %id1%.
%% Please note that it is possible this component will not exit to either of
%% the above labels, if a complete derailment of the story takes place.


Content
	name <A Duel Interrupted>
	Size 8
	desc <When you go to fight, you find some other mecha there.>
	requires <*CS_Duel (+P--|+Pme) ~F:-- ~T:Ally ~T:Friend -L:Enemy ~+Gpe>

	% E1 is the challenger
	% E2 is the challenger's home scene
	% E3 is the outdoor scene where the duel will take place
	% E4 is the faction whose mecha are present
	% E5 is the faction of the current city
	% E6 is the duel scene itself
	element4 <Faction Military !Enemy -7 !Xclude 1>
	% The faction of the root scene must not be an enemy of the character
	% you're dueling...
	element5 <Faction !Comrade -7 !Okay 1>
	Element6 <Prefab>
	Place6 <%e3%>

	% P%id%01 = Initialization Counter
	% P%id%02 = Duel target skill level calculator

	update <if=  P%id%01 0  P= %id%01 1  MoveNPC %1% %6%  SetNPCTeam %1% 3  V= %id%02 StoryDL  V+ %id%02 d5  NPCLevel %1% V%id%02>

	sub
		MetaScene 6 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		% L4 = Identity of enemy NPC
		MapWidth 30
		MapHeight 30

		special <ARENA>

		Start <if= L2 0 L= 2 1   ForceChat %1%   AlterContext .change L= 4 %1%>
		nu1 <if= T1 0  Return   if= L1 0 L= 1 1   StoryNote 1   Trigger0 .%id%_%plotid%_GoLose>
		nu2 <if= T2 0           if= L1 0 L= 1 2   StoryNote 1   AddRenown 1  XPV 100   SALVAGE  ifTeamNotHostile 3 else GoFoughtNPC   Trigger0 .%id%_%plotid%_GoWin>
		GoFoughtNPC <WinComp 0 .Next>
		.next <+T-- +F-->
		.change <+Pun>

		end <ifNPCOK %1% MoveNPC %1% %2%>

		Msg1 <Your duel with %name1% was interrupted by %name4%.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			SetEnemy 1 3
			Deploy <SetSelfFaction %4%   WMecha 2 Threat StoryDL 200>
			ParaX 15
			ParaY 25

			team 3
			SetEnemy 2
			ParaX 25
			ParaY 10
		end

		Persona 1
		% V%id%01 = Have spoken already
		greeting <if= PlotStatus %id% else %pop% if= V%id%01 1 else .%id%_GoFirstTime EndChat Say %id%01>
		.%id%_GoFirstTime <ifChatNPCInPlay else .%id%_GoWaiting EndChat Say %id%02 AddChat %id%01 AddChat %id%02 AddChat %id%03 V= %id%01 1>
		*.%id%_GoWaiting <*WaitingForDuel %3%>
		result%id%01 <EndChat Say %id%03 AddHeroic 1        PCFriend %1% &SetAllyFac %5% &SetEnemyFac %4% PCFEnemy %4%>
		result%id%02 <EndChat Say %id%04 AddEasygoing -10   AddHeroic -1  PCEnemy %1%  TeamAttack 3 if= &EnemyFac 0 &SetEnemyFac %4% PCFEnemy %4%>
		result%id%03 <EndChat Say %id%05 AddRenown -1 AddReact -d20 if= &EnemyFac 0 &SetEnemyFac %4% PCFEnemy %4%>
		Msg%id%01 <Keep your mind on the game.>
		Msg%id%02 <It looks like we're not alone... those mecha are from %name4%. They can't possibly be up to any good. How about we fight them together?>
		Msg%id%02_1 <As much as I'd like to fight you, I'm afraid that we just got something bigger to worry about... Those mecha are from %name4%. Obviously they're here on some kind of covert mission. We should stop them before they do any damage.>
		CMsg%id%02_1 <ifNPCSociable Accept>
		Msg%id%02_2 <Look at that... a raiding party from %name4%. Let's defeat them together.>
		CMsg%id%02_2 <ifNPCShy Accept>
		Msg%id%02_3 <Sorry to interrupt our duel, but there's a group of mecha from %name4% right here. Most likey they're planning something destructive. Shall we defeat them together?>
		CMsg%id%02_3 <ifNPCEasygoing Accept>
		Msg%id%02_4 <Our fight will have to wait, there's something more important to take care of first... Look, it's a scout team from %name4%. This could be bad. If we team up we should be able to defeat them easily.>
		CMsg%id%02_4 <ifNPCPassionate Accept>
		Msg%id%02_5 <I was hoping our duel would be interesting, but I just found something a whole lot more fun. My sensors picked up a raiding party from %name4%. If we join forces, they won't stand a chance against us.>
		CMsg%id%02_5 <ifNPCCheerful Accept>
		Msg%id%02_6 <I've got some bad news... Look over there, those mecha were sent by %name4%. Is this the start of an invasion? Let's put our duel aside and defeat them before they have a chance to kill anyone.>
		CMsg%id%02_6 <ifNPCMelancholy Accept>
		Msg%id%03 <You're an honorable pilot, \PC . Let's do this.>
		Msg%id%03_1 <It'll be a pleasure to fight by your side. Let's go.>
		Msg%id%04 <Your hatred means so much that you'd side with the enemies of \NARRATIVE 7 ? Then let's see who can kill you first, %name4% or me...>
		Msg%id%04_1 <How spiteful are you? These mecha are here to destroy \NARRATIVE 7 , and still you can't think of anything but our fued. If it's a fight you want then it's a fight you're going to get!>
		Msg%id%05 <You're just going to run? You coward! Do the lives of your fellow citizens mean nothing to you?>
		Msg%id%05_1 <You're going to leave me to face them all by myself!? Coward! Traitor!>
		Prompt%id%01 <I'll help you, for \NARRATIVE 7 's sake.>
		Prompt%id%01_1 <It's up to us to protect \NARRATIVE 7 .>
		Prompt%id%02 <No way, you aren't getting off that easy.>
		CPrompt%id%02 <ifNPCArchEnemy E2 Accept>
		Prompt%id%02_1 <Forget it. I hate you more than %name4%.>
		Prompt%id%03 <You do it yourself. I'm out of here.>
		Prompt%id%03_1 <You can fight them, I'm getting out of here.>
	end
	inv
		STC ENCOUNTER-DUEL
		name <%name1%'s Mecha>
		update <if= PlotStatus %id% else GoHide ShowEncounter SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	end


Content
	name <Regular Duel>
	desc <The PC and the NPC will fight. Nothing special happens.>
	requires <*CS_Duel>
	Size 5

	% E1 is the challenger
	% E2 is the challenger's home scene
	% E3 is the outdoor scene where the duel will take place
	% E4 is the duel encounter
	Element4 <Prefab>
	Place4 <%e3%>

	% P%id%01 = Initialization Counter
	update <if= PlotStatus %id% else %pop% if= P%id%01 0 MoveNPC %1% %4% SetNPCTeam %1% 2 NPCLevel %1% StoryDL P= %id%01 1>

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop%  ifChatNPCInPlay else .%id%_GoNotInPlay Goto .%id%_GoChallenge>
		*.%id%_GoChallenge <*ArenaChallenge .%id%_GoThemeInfo>
		*.%id%_GoThemeInfo <*THEME_EXPO&Enemy NA>
		*.%id%_GoNotInPlay <*WaitingForDuel %3%>

		MetaScene 4 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		% L4 = Identity of enemy NPC
		MapWidth 30
		MapHeight 30

		special <ARENA>

		Start <if= L2 0 L= 2 1   ForceChat %1%>
		nu1 <if= T1 0  Return   MoveNPC %1% %2%  if= L1 0 L= 1 1   SMemo 1   History 4  Print 2   Trigger0 .%id%_%plotid%_GoLose>
		nu2 <if= T2 0           MoveNPC %1% %2%  if= L1 0 L= 1 2   SMemo 3   History 5  Print 2 AddRenown 1   XPV 100   Trigger0 .%id%_%plotid%_GoWin>

		.next <+T-- +F-->

		Msg1 <You were defeated by %name1% in a duel; \SPR %1% is waiting for you at %name2%.>
		Msg2 <%name1% returns to %name2%.>
		Msg3 <You defeated %name1% in a duel; \SPR %1% is waiting for you at %name2%.>
		Msg4 <You were defeated by %name1% in a duel.>
		Msg5 <You defeated %name1% in a duel.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			ParaX 25
			ParaY 25
		end

	end
	inv
		STC ENCOUNTER-DUEL
		name <%name1%'s Mecha>
		update <if= PlotStatus %id% else GoHide ShowEncounter SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	end

%%
%% *CS_CAWinMission Content
%%	&RevealEF	The enemy faction has been revealed
%%
%% Catch-all win mission component.
%% The PC has won a mission provided by the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%%

Content
	name <Here, have some money>
	desc <The NPC will pay the PC for the job.>
	requires <*CS_CAWinMission>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportSuccess>
		*.%id%_GoReportSuccess <*MissionWasSuccess&Reward .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <CashPrize Reward Threat StoryDL 100 200 WinComp 0 .%id%_win>
		.%id%_win <+T-- +F-->
	end

%%
%% *CS_CALoseMission Content
%%	&RevealEF	The enemy faction has been revealed
%%
%% Catch-all lose mission component.
%% The PC has lost a mission provided by the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Mission-giver
%%

Content
	name <Default Loss>
	desc <The NPC will debrief the PC on the loss.>
	requires <*CS_CALoseMission>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% gOTO .%id%_GoReportLoss>
		*.%id%_GoReportLoss <*MissionWasFailure .%id%_GoEnd &AllyFac &EnemyFac>
		.%id%_GoEnd <LoseComp 0 .%id%_loss>
		.%id%_loss <+T-- +F-->
	end


%%
%% *CS_WinDuelVs Content
%%
%%
%% The PC has won a duel against the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Challenger
%%

Content
	name <You should go see MILIT...>
	desc <The NPC will reccomend that the PC go ask someone about a job.>
	requires <*CS_WinDuelVs>

	% E1 is the NPC.
	% E2 is the person to see about the job
	% E3 is a public scene for the meeting.
	Element2 <Character "MILIT" !Near -7 !Ally -7 !Okay -3>
	Element3 <Scene Building Public !Okay 2>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% WinComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% ifNPCOK %2% else .%id%_GoE2Died NewChat Say %id%01 SMemo %id%02 &SetTargetChar %2% WinComp %3% .%id%_win>
		.%id%_GoE2Died <NewChat Say %id%03 WinComp 0 .%id%_draw>
		.%id%_win <+Tgt +Fmi>
		.%id%_draw <+T-- +F-->
		Msg%id%01 <You beat me. You should go talk with %name2% in %name3%; I hear that \SPR %2% 's looking for a good pilot.>
		Msg%id%02 <%name1% reccomended that you go ask %name2% in %name3% for a mission.>
		Msg%id%03 <You won the duel. Congratulations.>
	end

%%
%% *CS_LoseDuelVs Content
%%
%%
%% The PC has lost a duel against the NPC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The Challenger
%%

Content
	name <Ha ha!>
	desc <The NPC will just chide the PC, then end the plot.>
	requires <*CS_LoseDuelVs>

	% E1 is the NPC.

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% NewChat Say %id%01 LoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
		Msg%id%01 <You lost the duel. Good for me.>
	end
