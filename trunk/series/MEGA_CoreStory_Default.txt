%%
%% *:CS_SubBossRoadblock
%%
%% This is an odd little utility component. In order for something to happen,
%% the PC will need to locate and defeat a certain NPC. The battle will happen
%% in an invisible wandering encounter; at the start of the fight, the NPC will
%% contact the PC.
%%
%% If the PC loses the battle, no problem: the fight will repeat every 3-5 hours
%% until the PC wins. If the boss NPC dies, this counts as a win even if the PC
%% doesn't finish off the lancemates.
%%
%% NOTE: THIS SUBPLOT DOES NOT INCLUDE A PERSONA FOR THE NPC! It's up to the
%%  parent plot to provide that.
%%
%% When this subplot concludes, it sets the following trigger:
%%  .%id%_%plotid%_GoWin
%%
%% PARAM1: The outdoors scene for the encounter
%% PARAM2: The NPC to be met
%%

Content
	name <Default Roadblock>
	desc <Fight the sub-boss and some lancemates.>
	requires <*:CS_SubBossRoadblock>
	Size 6

	% E1 is the outdoors scene for the encounter
	% E2 is the NPC to be met
	% E3 is the encounter itself
	Element3 <Prefab>
	Place3 <1>

	% P%id%01 = Initialization Counter
	update <if= P%id%01 0 P= %id%01 1 NPCLevel %2% StoryDL MoveNPC %2% %3% SetNPCTeam %2% 2>

	sub
		MetaScene 3 2
		rumor%id% <%name2% has been seen in %name1%.>
		special <UNCHARTABLE>
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter; resets each time
		MapWidth 50
		MapHeight 50

		Start <if= L2 0 L= 2 1   L= 1 0   ForceChat %2%>
		nu1 <if= T1 0   Return  if= L1 0   L= 1 1   AddRenown -3>
		nu2 <if= T2 0   if= L1 0   L= 1 1   Goto GoVictory>
		Faint%2% <L= 1 1 Goto GoVictory>
		GoVictory <XPV 100 History 1 Trigger0 .%id%_%plotid%_GoWin WinSubPlot %plotid%>
		end <L= 2 0>
		Msg1 <You defeated %name2% in mecha combat.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <if= PlotStatus %plotid% %id% SetSelfFaction NPCFac %2%   WMecha 2 StoryDL 60>
			ParaX 45
			ParaY 45
		end
	end
	inv
		Encounter
		name <%name2%'s Lance>
		% V1 = Recharge Counter
		% This encounter will appear once every 3-5 hours, as long as this is the active subplot.
		update <if= PlotStatus %plotid% %id% else GoHide if# V1 0 else GoSetTimer ifG ComTime V1 else GoHide SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		GoSetTimer <V= 1 ComTime V+ 1 1800 Goto GoHide>
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		use <ifG StatVal STAT_MetaVisibility -1  V= 1 ComTime V+ 1 10800 V+ 1 d7200  Print -1 Exit Destination>
		GoAutoAttack <V= 1 ComTime V+ 1 10800 V+ 1 d7200  Alert -2 Exit Destination>
		GoAvoidAttack <V= 1 ComTime V+ 1 7200 V+ 1 d3600  Print -6 AddSociable -1 UpdateProps>
	end

%%
%% *:CS_DifficultMeeting
%%	&IsEnemy	The NPC to be met is an enemy
%%	&Protected	The NPC shouldn't get killed, by the PC or anything else
%%
%% The PC needs to meet with someone, but arranging a meeting is difficult.
%% Hence the name of the component. Duh. This subplot will set a PLACE for
%% the relevant NPC and may or may not start a conversation with FORCECHAT,
%% but will not contain the NPC's persona or make any other assumptions
%% about what's going on.
%%
%% One note about the persona: The conversation should start with an acknowledgment
%% that the PC was looking for E1.
%%
%% The parent subplot should disable this subplot when the conversation is over.
%%
%% PARAM1: The NPC the PC is seeking
%%

Content
	name <I wouldn't need to do this if you had a cell phone>
	desc <The PC can fight E1's henchmen; afterwards E1 contacts the PC>
	requires <*:CS_DifficultMeeting &IsEnemy>
	Size 5

	% E1 is the NPC that the PC is seeking
	% E2 is an outdoors scene for the combat
	% E3 is the combat encounter itself
	Place1 </>
	Element2 <Scene Outdoors !Near -7>
	Element3 <Prefab>
	Place3 <2>

	sub
		MetaScene 3 2
		rumor%id% <%name1% is doing something in %name2%.>

		% V1 = Encounter Over Counter
		% V2 = Initialization Counter
		MapWidth 50
		MapHeight 50

		% Defeat the henchmen, and E1 shows up. Get defeated by them, and lose your chance
		% forever.
		start <if= L2 0 L= 2 1 TeamMonologue 2 1>
		nu1 <if= T1 0   Return  if= V1 0 V= 1 1 LoseSubPlot %plotid%>
		nu2 <if= T2 0   if= V1 0   V= 1 1   Alert 2 XPV 100  ForceChat %1%>

		Msg1 <Look, it's \PC ! >
		Msg2 <You have defeated all of %name1%'s henchmen. That ought to get \PPR %1% attention.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <if= PlotStatus %plotid% %id% SetSelfFaction NPCFac %1%   WMecha 2 StoryDL 100>
			ParaX 45
			ParaY 45
		end
	end
	inv
		STC CORE-INVISIBLEENCOUNTER
		name <%name1%'s Henchmen>
		EncounterMove 20
	end


%%
%% *:CS_DiscoverNPCHasItem
%%	&NotReally	The NPC doesn't actually have it
%%
%% In this subplot, the PC will learn that an item being sought is in the possession
%% of a certain NPC.
%%
%% When this subplot concludes, the following trigger must be set:
%%		.%id%_%plotid%_GoDiscover
%%
%% PARAM1: The NPC who has it
%% PARAM2: The item being sought
%%

Content
	name <Trivial DNHI>
	desc <Ask an NPC, get told who has it. Boring, but quick to write.>
	requires <*:CS_DiscoverNPCHasItem>
	Size 1

	% E1 is the NPC with the item
	% E2 is the item
	% E3 is a local public scene
	% E4 is an NPC who knows something about it
	Element3 <Scene Building Public !Near -7>
	Element4 <Character Knowledge !Near 3 -ENEMY NEVERFAIL>
	NEVERFAIL4 <Professor>

	% FAIL CONDITIONS
	% - E4 dies before revealing the secret
	start <ifNPCDead %4% LoseSubPlot %plotid%>

	sub
		Persona 4
		rumor%id% <%name4% knows something about the %name2%.>
		greeting <if= PlotStatus %plotid% %id% else GoChat Goto GoGreet>
		GoChat <NewChat SayAnything>
		*GoGreet <*LookingForItem %2% GoExplain>
		GoExplain <NewChat Say 1 SMemo 2 History 3   WinSubPlot %plotid% Trigger0 .%id%_%plotid%_GoDiscover>
		Msg1_1 <As far as I know, the %name2% is currently in the possession of %name1%. More than this I couldn't say.>
		Msg1_2 <The last I heard, %name1% is the one who has the %name2%. I'm afraid that I really don't know anything else.>
		Msg1_3 <They say that the %name2% is being held by someone called %name1%. Maybe this information can be of use to you.>
		Msg2 <You learned from %name4% that %name1% is in possession of the %name2%.>
		Msg3 <You learned that %name1% had the %name2%.>
	end

%%
%% *:CS_DiscoverNPCKidnappedByCSE
%%	&NotReally	It wasn't really a kidnapping, or some other misunderstanding
%%
%% A very specific subplot. The NPC you're searching for has been kidnapped
%% by the core story enemy... or apparently so, anyhow.
%%
%% When this subplot concludes, the following trigger must be set:
%%		.%id%_%plotid%_GoDiscover
%%
%% PARAM1: The scene where the NPC supposedly is
%% PARAM2: The NPC being sought
%%

Plot
	name <Enemy Team Was Just Here>
	desc <Upon searching for NPC, you learn that they've disappeared. Then you get attacked.>
	requires <*:CS_DiscoverNPCKidnappedByCSE>
	Size 3

	% E1 is the scene where the NPC supposedly is
	% E2 is the NPC being sought
	% E3 is a character in E1 who can tell the PC that E2 has left
	Element3 <Character Lawful !Near -7 NoMecha !Okay 2 NEVERFAIL>
	Place3 <1 (Citizens) pass ally>

	% This plot cannot fail. E3 isn't actually important. You don't even need to win
	% the fight against the core story enemy.
	% Entering the location primes the attack; exiting to the city map executes the attack.
	% The attack will be handled with a dynamic encounter.

	% p%id%01 = Attack Primed
	START <if= p%id%01 1 else .%id%_GoCheckPrimer ifScene .%id%_desc Goto .%id%_GoStartAttack>
	.%id%_desc <Outdoors>
	.%id%_GoCheckPrimer <if= SceneID %1% p= %id%01 1>
	.%id%_GoStartAttack <Alert %id%01 PMemo %id% %id%02 History %id%03 SavePos Dynamic 2 StoryDL 100 .%id%_nu1 .%id%_nu2  DynaFaction &EnemyFac WinSubPlot %plotid% Trigger0 .%id%_%plotid%_GoDiscover>
	.%id%_nu1 <if= T1 0 Return>
	.%id%_nu2 <if= T2 0 if= L1 0 L= 1 1 XPV 100 Salvage>

	Msg%id%01_1 <As you leave the building, you are suddenly attacked! These mecha must be connected with the disappearance of %name2%.>
	CMsg%id%01_1 <if= &EnemyFac 0 ifNPCDead &EnemyNPC Accept>
	Msg%id%01_2 <As you leave the building, you are suddenly attacked by \PERSONA &EnemyNPC 's henchmen! It's clear that they must be responsible for the disappearance of %name2%.>
	CMsg%id%01_2 <if= &EnemyFac 0 ifNPCOK &EnemyNPC Accept>
	Msg%id%01_3 <As you leave the building, you are suddenly attacked! It's clear that \FACTION &EnemyFac must have something to do with the disappearance of %name2%.>
	CMsg%id%01_3 <if# &EnemyFac 0 Accept>
	Msg%id%02_1 <You learned that %name2% has been kidnapped.>
	CMsg%id%02_1 <if= &EnemyFac 0 ifNPCDead &EnemyNPC Accept>
	Msg%id%02_2 <You learned that %name2% has been kidnapped by \PERSONA &EnemyNPC .>
	CMsg%id%02_2 <if= &EnemyFac 0 ifNPCOK &EnemyNPC Accept>
	Msg%id%02_3 <You learned that %name2% has been kidnapped by \FACTION &EnemyFac .>
	CMsg%id%02_3 <if# &EnemyFac 0 Accept>
	Msg%id%03_1 <You learned that %name2% had been kidnapped.>
	CMsg%id%03_1 <if= &EnemyFac 0 ifNPCDead &EnemyNPC Accept>
	Msg%id%03_2 <You learned that %name2% had been kidnapped by \PERSONA &EnemyNPC .>
	CMsg%id%03_2 <if= &EnemyFac 0 ifNPCOK &EnemyNPC Accept>
	Msg%id%03_3 <You learned that %name2% had been kidnapped by \FACTION &EnemyFac .>
	CMsg%id%03_3 <if# &EnemyFac 0 Accept>

	sub
		Persona 3
		greeting <if= PlotStatus %plotid% %id% else GoChat Goto GoGreet>
		GoChat <NewChat SayAnything>
		*GoGreet <*LookingForNPC&NoFailure %2% GoExplain GoExplain>
		GoExplain <NewChat Say 1 PMemo %id% 2>
		Msg1 <%name2% was here earlier, but I saw \OPR %2% talking to some big guys and they left in a hurry. Is \SPR %2% in any trouble?>
		Msg2 <You learned that %name2% has been kidnapped.>
	end

%%
%% *CS_ThanksForAssist
%%
%% The PC didn't exactly rescue this NPC, but he did assist.
%%
%% PARAM1: The NPC who was aided.
%%

Plot
	name <I've got info for you.>
	desc <The NPC will tell the PC a secret.>
	requires <*CS_ThanksForAssist>

	% Element1 is the NPC to meet

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	% SubPlot1 is a revelation
	SubPlot1 <*CS_Revelation&Friendly 1>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop% NewChat Say %id%01 AddChat %id%01>
		result%id%01 <Goto %id1%_GoInit>
		Msg%id%01 <Thanks for your help back there. You know, I've got some information that you just might find useful.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCCheerful Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCMelancholy Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCEasygoing Accept>
		Msg%id%01_6 <>
		CMsg%id%01_6 <ifNPCPassionate Accept>
		Prompt%id%01 <Tell me what it is.>
		Prompt%id%01_1 <Really? Great.>
		Prompt%id%01_2 <Go on, then. Don't leave me waiting.>
	end

%%
%% *CS_Pla_GetItem
%%	&Derelict	The item was found in a wreck
%%
%% In the +Pla plot state, the PC is about to come into posession of the
%% target item. This subplot will add some consequences for this; Note that
%% this may also be triggered if the target item is destroyed or otherwise
%% lost. Make sure you check for that, too.
%%
%% PARAM1: The item obtained.
%%

Content
	name <Do Not Want>
	desc <The enemy didn't want the artifact; he wanted the info from the derelict's computer.>
	requires <*CS_Pla_GetItem &Derelict E:++ F:++>
	Size 3

	% E1 is the item obtained
	% E2 is the enemy character
	Element2 <Grab 1>
	Place2 </>

	% P%id%01 = Message counter

	start <if= PlotStatus %plotid% %id% else %pop% ifScene .%id%_Scene if= P%id%01 0 P= %id%01 1 ForceChat %2%>
	.%id%_Scene <OUTDOORS>

	sub
		Persona 2
		special <UNLISTED NOESCAPE>
		greeting <EndChat Say 1 AddChat 1>
		result1 <EndChat Say 2 StoryNote 3 Print 4 &SetTargetItem 0 WinComp 0 .next  SavePos Dynamic 2 StoryDL 100 .nu1 .nu2 DynaFaction &EnemyFac>
		.next <+Pew +T-- +F-->
		.nu1 <if= T1 0  Return   if= L2 0  L= 2 1  AddRenown -5>
		.nu2 <if= T2 0           if= L2 0  L= 2 1  XPV 100  AddRenown 1   Return>
		Msg1 <\PC , I should have expected to find you here. Well, it hardly matters- you're too late to stop me.>
		Msg2 <It wasn't the %name1% that I was after- it was the computer on board the ship that was carrying it. This information will be a great help to our new weapons program. Unfortunately, you will not live to see the glorious rise of \FACTION &EnemyFac !>
		Msg3 <%name2% revealed that \FACTION &EnemyFac were working on a new weapons program.>
		Msg4 <You are attacked by %name2%'s men.>
		Prompt1 <Too late? I beat you to the %name1%!>
	end


%%
%% *CS_DelayedVictory Content
%% *CS_DelayedNegation Content
%%
%% Victory is going to be delayed for several hours. This subplot is generally used when
%% a mission-giver asks the PC to go relax for a while before the next mission.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%% Before calling this subplot, the story status must have been set with AlterContext.
%%

Content
	name <Default Delay>
	desc <Several hours will pass before the next component is generated.>
	requires <*CS_DelayedNegation>

	% P%id%01 = Time Limit
	update <if= PlotStatus %plotid% %id% else %pop% if= P%id%01 0 P= %id%01 ComTime P+ %id%01 7200 P+ %id%01 d7200>
	hour <if= PlotStatus %plotid% %id% else %pop% ifG ComTime P%id%01 NegateComp &CompScene na>

Content
	name <Default Delay>
	desc <Several hours will pass before the next component is generated.>
	requires <*CS_DelayedVictory>

	% P%id%01 = Time Limit
	update <if= PlotStatus %plotid% %id% else %pop% if= P%id%01 0 P= %id%01 ComTime P+ %id%01 7200 P+ %id%01 d7200>
	hour <if= PlotStatus %plotid% %id% else %pop% ifG ComTime P%id%01 WinComp &CompScene na>


%%
%% *CS_FriendFired_Win Content
%%
%% The PC's friend has just been fired from a faction, and for whatever reason this
%% is a win for the PC.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC who has been fired
%% Param2: The faction he's been fired from
%%

Content
	name <What did I do?>
	desc <The friend wonders what he did wrong.>
	requires <*CS_FriendFired_Win>

	% E1 is the friend
	% E2 is the faction he's been fired from
	% E3 is a public scene for the meeting
	Element3 <Scene Building Public !NEar -7>

	% p%id%01 = Initialization Counter
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_Next>
	.%id%_Next <+T-- +G-->
	update <if= PlotStatus %plotid% %id% else %pop% if= P%id%01 0 P= %id%01 1 MoveAndPacifyNPC %1% %3% SetNPCFaction %1% 0 Email %id%01>

	Msg%id%01_1 <%name1%@ \NARRATIVE 7 :// I've received bad news from %name2%... come meet me in %name3%.>
	Msg%id%01_2 <%name1%@ \NARRATIVE 7 :// I've got some bad news. Come see me in %name3%.>
	Msg%id%01_3 <%name1%@ \NARRATIVE 7 :// This has been a terrible day... I'd like to talk with you in %name3%.>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop% NewChat Say %id%01 NTBWinComp 0 .%id%_next StoryNote %id%03 AddChat %id%01 AddChat %id%02>
		.%id%_next <+T-- +F-->
		result%id%01 <NewChat Say %id%02  AddCheerful d6>
		result%id%02 <NewChat Say %id%02>
		Msg%id%01 <I don't understand what happened; I've been fired from %name2%.>
		Msg%id%01_1 <I have some terrible news... I've been fired from %name2%. I don't know why, but they told me to go and never come back.>
		Msg%id%01_2 <I've been fired from %name2%. It was really a shock... I thought I was doing so well.>
		Msg%id%02 <I know I'll find something else in time, but for right now I'm feeling very disappointed.>
		Msg%id%02_1 <The worst part is, I made a lot of new friends while at %name2%. I guess maybe I'll see them again, but I don't know...>
		CMsg%id%02_1 <ifNPCSociable Accept>
		Msg%id%02_2 <I think I probably will do better by myself. I was never much of a joiner, anyhow.>
		CMsg%id%02_2 <ifNPCShy Accept>
		Msg%id%02_3 <Eh, it's not that bad. I'll find something else, or I won't... either way is fine.>
		CMsg%id%02_3 <ifNPCEasygoing Accept>
		Msg%id%02_4 <Those idiots at %name2% don't know what they're doing! I don't need them! I can do better by myself!>
		CMsg%id%02_4 <ifNPCPassionate Accept>
		Msg%id%02_5 <You know, I had been looking at this like an ending, but instead I think it's a beginning. Now that I'm free from %name2% I can choose my own destiny, wherever that may lead.>
		CMsg%id%02_5 <ifNPCCheerful Accept>
		Msg%id%02_6 <I guess I'm just not good enough to make it as a member of %name2%... I'll have to learn to accept that.>
		CMsg%id%02_6 <ifNPCMelancholy Accept>
		Msg%id%03 <%name1% was fired from %name2%, much to your relief.>
		Prompt%id%01 <It's probably for the best.>
		Prompt%id%01_1 <You're better off without them.>
		Prompt%id%01_2 <You're too good to work for them, anyhow.>
		Prompt%id%02 <Oh, that's too bad... I wonder what happened.>
		Prompt%id%02_1 <How surprising... I wonder what happened.>
		Prompt%id%02_2 <That's a shame... I wonder what happened.>
	end


%%
%% *ForcedInduction Content
%%
%% The PC has been forced into the ranks of the target faction against his will.
%% This component will move the PC to a induction location, alter the plot state,
%% store the PC's current faction and set the new faction. The new propp state
%% will be "+Psh".
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC doing the induction
%% Param2: The faction the PC will be inducted into
%%

Content
	name <Induction Plus Mission>
	requires <*ForcedInduction>
	desc <You are forcibly inducted into a faction and told to report for a mission.>

	% Element1 is the NPC doing the induction
	% Element2 is the faction the PC will be inducted into
	% Element3 is the scene where the induction will take place
	% Element4 is the NPC you must report to
	Element3 <Scene Building Public !Near -7 !Ally 2>
	Element4 <Character !Ally 2 !Near -7>

	% v%id%01 = Initialization/Conversation Counter

	update <if= PlotStatus %plotid% %id% else %pop% if= v%id%01 0 V= %id%01 1 Exit %3% MoveAndPacifyNPC %1% %3% MoveNPC %4% %3%>

	start <if= v%id%01 1 else %pop% Alert %id%01 ForceChat %1% V= %id%01 2 Goto %pop%>
	Msg%id%01 <You are taken to %name3%.>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop%  &SetAllyFac %2% StoreOldFaction SetPCFac %2% &SetTargetNPC %4% NegateComp %3% .%id%_next  StoryNote %id%02 Goto .%id%_GoGreet>
		*.%id%_GoGreet <*CS_ForcedInduction_Greeting .%id%_GoBrief %2%>
		.%id%_next <+Tgt +Fmi +Psh>
		.%id%_GoBrief <ifNPCOK %4% else .%id%_GoScrapMission NewChat Say %id%01 SMemo %id%04>
		.%id%_GoScrapMission <NewChat Say %id%03 AlterContext .%id%_ScrapContext>
		.%id%_ScrapContext <+T-- +F-->
		Msg%id%01 <Go talk with %name4% for your first mission; \SPR %4% should be somewhere in this building.>
		Msg%id%01_1 <You have to speak with %name4% about your first mission. I believe \SPR %4% can be found somewhere in this building.>
		Msg%id%01_2 <%name4% has the details of your first mission. You'll need to go talk with \OPR %4% right over there.>
		Msg%id%02 <You were conscripted into %name2%.>
		Msg%id%03 <There was a task planned for you, but I'm afraid it's been scrapped. I'll let you know when there's something for you to do.>
		Msg%id%04 <You have to see %name4% in %name3% about your first mission for %name2%.>
	end

Content
	name <Basic Induction>
	requires <*ForcedInduction>
	desc <You are forcibly inducted into a faction, but given no orders at this time.>

	% Element1 is the NPC doing the induction
	% Element2 is the faction the PC will be inducted into
	% Element3 is the scene where the induction will take place
	Element3 <Scene Building Public !Near -7 !Ally 1>

	% v%id%01 = Initialization/Conversation Counter

	update <if= PlotStatus %plotid% %id% else %pop% if= v%id%01 0 V= %id%01 1 Exit %3% MoveAndPacifyNPC %1% %3%>

	start <if= v%id%01 1 else %pop% Alert %id%01 ForceChat %1% V= %id%01 2 Goto %pop%>
	Msg%id%01 <You are taken to %name3%.>

	% The only subplot is a delay before the next mission.
	SubPlot1 <*CS_DelayedNegation>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop%  &SetAllyFac %2% StoreOldFaction SetPCFac %2% &SetCompScene 0 AlterCOntext .%id%_next SetPlotStatus %plotid% %id1%  StoryNote %id%02 Goto .%id%_GoGreet>
		*.%id%_GoGreet <*CS_ForcedInduction_Greeting .%id%_GoBrief %2%>
		.%id%_next <+T-- +F-- +Psh>
		.%id%_GoBrief <NewChat Say %id%01>
		Msg%id%01 <We'll let you know when there's some work that needs to be done. For the time being you're free to go.>
		Msg%id%01_1 <For the time being you're free to do whatever you'd like. I'll let you know when it's time for your first mission.>
		Msg%id%01_2 <You are free to go, for the moment. I'll call you back when we have a mission for you.>
		Msg%id%02 <You were conscripted into %name2%.>
	end

Content
	name <Secret Base Induction>
	requires <*ForcedInduction>
	desc <You are forcibly inducted into a faction, but given no orders at this time.>

	% Element1 is the NPC doing the induction
	% Element2 is the faction the PC will be inducted into
	% Element3 is an urban scene for the secret base
	% Element4 is the secret base
	Element3 <Scene Urban !Near -7>
	Element4 <Prefab>
	Place4 <3>

	% v%id%01 = Initialization/Conversation Counter

	update <if= PlotStatus %plotid% %id% else %pop% if= v%id%01 0 V= %id%01 1 Exit %4% MoveAndPacifyNPC %1% %4%>

	start <if= v%id%01 1 else %pop% Alert %id%01 ForceChat %1%  V= %id%01 2  Goto %pop%>
	Msg%id%01 <You are taken to a secret base of %name2%.>

	% The only subplot is a delay before the next mission.
	SubPlot1 <*CS_DelayedNegation>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop%  &SetAllyFac %2% StoreOldFaction SetPCFac %2% &SetCompScene 0 AlterCOntext .%id%_next SetPlotStatus %plotid% %id1%  StoryNote %id%02 Goto .%id%_GoGreet>
		*.%id%_GoGreet <*CS_ForcedInduction_Greeting .%id%_GoBrief %2%>
		.%id%_next <+T-- +F-- +Psh>
		.%id%_GoBrief <NewChat Say %id%01>
		Msg%id%01 <We'll let you know when there's some work that needs to be done. For the time being you're free to go.>
		Msg%id%01_1 <For the time being you're free to do whatever you'd like. I'll let you know when it's time for your first mission.>
		Msg%id%01_2 <You are free to go, for the moment. I'll call you back when we have a mission for you.>
		Msg%id%02 <You were conscripted into %name2%.>

		STC MS_Office
		SetID 4
	end
	inv
		STC ENCOUNTER-WANDER
		name <Secret Base>
		update <if= PlotStatus %plotid% %id% else GoHide SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	end


