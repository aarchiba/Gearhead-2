%
%  ************************************
%  ***      CORE  STORY  PLOTS      ***
%  ***      EASY  MONEY  TRACK      ***
%  ************************************
%
% The PC is looking for easy money- steer them towards a *CS_SEEKPLUNDER component.
%

Plot
	name <Thief Tipoff>
	desc <An unaligned will tip off the PC.>
	requires <*CORE_EMO_ ~P:-->

	% E1 is a local public scene
	% E2 is the thief
	element1 <Scene !Near -7 public (building|meeting)>
	element2 <Character !Near 1 THIEF Criminal NeverFail>
	Place2 <1 (Citizens) pass>
	NeverFail2 <Thief>

	end <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseEpisode>

	% SubPlot1 is the plunder-seeking bit.
	subplot1 <*CS_SeekPlunder>

	sub
		Persona 2
		rumor0 <%name2% knows something that you might want to know.>
		greeting <if= PlotStatus %plotid% 0 else .%id%_GoChat Goto .%id%_GoGreet>
		.%id%_GoChat <RevertPersona>
		*.%id%_GoGreet <*YouCouldUseHelp .%id%_GoBriefing>
		.%id%_GoBriefing <NewChat Say %id%01 SMemo %id%02 History %id%03 SetPlotStatus %plotid1% %id1%>
		Msg%id%01 <\HINT %id1% I figured that you might want to know.>
		Msg%id%02 <%name2% gave you a hot tip. \HINT %id1%>
		Msg%id%03 <%name2% gave you a hot tip.>
	end


Plot
	name <PCFac Tipoff>
	desc <A member of the PC's faction will tip them off about it.>
	requires <*CORE_EMO_ P:++ ~P:PCFAC>

	% E1 is a local public scene
	% E2 is a member of the PC's faction
	element1 <Scene !Near -7 public (building|meeting)>
	element2 <Character !Near 1 !Comrade -3 NeverFail>
	Place2 <1 (Guards) SD>

	5min <if= PlotStatus %plotid% 0  if= RootSceneID &EpisodeScene  EMail %id%01  SetPlotStatus %plotid% %id%>
	end <if= PlotStatus %plotid% %id% ifNPCDead %2% LoseEpisode>

	Msg%id%01_1 <%name2%@ \SCENE &EpisodeScene :// If you're in town I'd love to speak with you.>
	Msg%id%01_2 <%name2%@ \SCENE &EpisodeScene :// Got a hot tip for you, \PC . There could be easy money involved.>
	Msg%id%01_3 <%name2%@ \SCENE &EpisodeScene :// I've got some info. Come to \EXACT_SCENE %1% .>
	Msg%id%01_4 <%name2%@ \SCENE &EpisodeScene :// I've been asked to tell you something, but this is way too sensitive for email.>
	Msg%id%01_5 <%name2%@ \SCENE &EpisodeScene :// Umcae to \EXACT_SCENE %1% for an Othae Iptae.>

	% SubPlot1 is the plunder-seeking bit.
	subplot1 <*CS_SeekPlunder>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% %id% else .%id%_GoChat Goto .%id%_GoGreet>
		.%id%_GoChat <RevertPersona>
		*.%id%_GoGreet <*HowAreYou .%id%_GoBriefing>
		.%id%_GoBriefing <NewChat Say %id%01 SMemo %id%02 History %id%03 SetPlotStatus %plotid1% %id1%>
		Msg%id%01 <\HINT %id1% I figured that you might want to know.>
		Msg%id%02 <%name2% told you about some easy money. \HINT %id1%>
		Msg%id%03 <%name2% told you about some easy money.>
	end

Plot
	name <Welcome Wagon>
	desc <A police officer inadvertently gives the PC a tip.>
	requires <*CORE_EMO_>

	% E1 is a local scene
	% E2 is the police officer
	Element1 <Scene !Near -7 public (building|meeting)>
	Element2 <Character POLIC Sociable HasMecha !Near 1 -Friend -Nemesis -Lover -Family NeverFail>
	NeverFail2 <Police Officer>

	start <if= PlotStatus %plotid% 0  ifNPCOK %2% else .%id%_GoEndPlot  if= RootSceneID &EpisodeScene  ifScene .%id%_scenetype  Alert %id%01  ForceChat %2%>
	.%id%_GoEndPlot <LoseEpisode>
	.%id%_scenetype <outdoors>

	Msg%id%01 <Before you even have the chance to do anything illegal, you are accosted by a large force of police mecha.>

	% SubPlot1 is the plunder-seeking bit.
	subplot1 <*CS_SeekPlunder>

	sub
		Persona 2
		special <UNLISTED NOESCAPE>
		greeting <if= PlotStatus %plotid% 0 else .%id%_GoChat EndChat Say %id%01 AddChat %id%01 AddReact -15>
		.%id%_GoChat <RevertPersona>
		result%id%01 <EndChat Say %id%02 SMemo %id%03 History %id%04 AddChat %id%02 AddChat %id%03 AddChat %id%04 SetPlotStatus %plotid1% %id1%>
		result%id%02 <EndChat Say %id%05>
		result%id%03 <EndChat Say %id%06>
		result%id%04 <EndChat Say %id%07 AddReact d10>
		Msg%id%01 <Don't think I don't know what you're doing in town, \PC , and don't think for one minute that you're going to get away with it...>
		Msg%id%02 <Oh, come on. \HINT %id1% That's the reason you came here, isn't it?>
		Msg%id%03 <%name2% inadvertently gave you some useful info. \HINT %id1%>
		Msg%id%04 <%name2% inadvertently gave you some useful info.>
		Msg%id%05 <What!? Forget I said anything... and whatever you do, don't try to act on it...>
		Msg%id%06 <Just don't break any laws and you won't have anything to worry about, then.>
		Msg%id%07 <Make sure you don't... I'll have my eyes on you.>
		Prompt%id%01 <What are you talking about?>
		Prompt%id%01_1 <I have no idea what you're talking about.>
		Prompt%id%02 <You don't say..? Thanks for the tip.>
		Prompt%id%02_1 <Is that so..? Thanks for telling me.>
		Prompt%id%03 <Hey, don't think I'm some common crook...>
		CPrompt%id%03 <if# PCFac PRIVA Accept>
		Prompt%id%03_1 <Are you calling me a crook?>
		Prompt%id%04 <Don't worry. I won't overstep my letter of marque.>
		CPrompt%id%04 <if= PCFac PRIVA Accept>
	end

Plot
	name <Easy Money Mentor>
	desc <An experienced pirate will give you a tip.>
	requires <*CORE_EMO_1 ~!Ne>

	% E1 is a local public scene
	% E2 is the EMO Mentor
	element1 <Scene !Near -7 public (building|meeting)>
	element2 <Prefab>
	place2 <1 pass>

	% P%id%01 = Email Counter
	5Min <if= RootSceneID &EpisodeScene  if= P%id%01 0 P= %id%01 1 EMail 1>

	%% FAIL CONDITIONS:
	%% - E2 dies before revealing secret.
	end <if= PlotStatus %plotid% 0 ifNPCDead %2% LoseEpisode>

	Msg1_1  <%name2%@ \NARRATIVE 7 :// Looking for some easy money? Come see me at \EXACT_SCENE %1% .>
	Msg1_2  <%name2%@ \NARRATIVE 7 :// This old timer has a few hints for you. Let's talk at \EXACT_SCENE %1% .>
	Msg1_3  <%name2%@ \NARRATIVE 7 :// I request a parley with you at \EXACT_SCENE %1% .>

	% SubPlot1 is the plunder-seeking bit.
	subplot1 <*CS_SeekPlunder>

	sub
		Persona 2
		greeting <if= PlotStatus %plotid% 0 else GoChat Goto .%id%_GoGreet>
		rumor%id% <%name2% has been looking for you.>
		GoChat <NewChat SayAnything>
		*.%id%_GoGreet <*YouCouldUseHelp .%id%_GoBriefing>
		.%id%_GoBriefing <NewChat Say %id%01 SMemo %id%02 History %id%03 AddChat %id%01 AddChat %id%02 AddChat %id%03 SetPlotStatus %plotid1% %id1% SetXXRAttitude %2% XXR_A_IsSenior SetNPCBio %2% Msg%id%06>
		result%id%01 <NewChat Say %id%04>
		result%id%02 <NewChat Say %id%05 AddCheerful -d10  AddReact d20>
		result%id%03 <NewChat Say %id%07 AddCheerful -d10  AddReact -d20>
		Msg%id%01 <I've got some info for you. \HINT %id1% This could be a great opportunity...>
		Msg%id%01_1 <I've got a hot tip for you. \HINT %id1% Get there first and it'll all be yours.>
		Msg%id%01_2 <Here's something you should know. \HINT %id1% Sounds like this could be a big chance for you.>
		Msg%id%02 <%name2% gave you a hot tip. \HINT %id1%>
		Msg%id%03 <%name2% gave you a hot tip.>
		Msg%id%04 <Eh, maybe someday you can return the favor... just remember, there ain't no such thing as easy money.>
		Msg%id%05 <You're suspicious... that's good. I'm just telling you what I know. What you do with it is up to you.>
		Msg%id%06 <%name2% is a retired pirate who now gives out advice to beginners. You got a hot tip from \OPR %2% in \SCENE &EpisodeScene .>
		Msg%id%07 <I'm just here to get you off your arse, not to do your work for you. Now get out there and see if you can track down any rumors!>
		Prompt%id%01 <Thanks for the info.>
		Prompt%id%01_1 <Thanks for letting me know.>
		Prompt%id%02 <Why are you telling me this?>
		Prompt%id%02_1 <How do I know I can trust your info?>
		Prompt%id%03 <That's it!? How about something useful?>
		Prompt%id%03_1 <Oh yeah, that info's a big help...>
	end
	inv
		NPC Pirate
		chardesc Sociable Old Old Old Old Old Renowned Renowned
	end



