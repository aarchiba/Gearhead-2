%%
%%  *ENT_SpaceWreck
%%
%% The PC is looking for a wrecked spaceship.

Content
	name <Just Go Look>
	requires <*ENT_SpaceWreck>
	special <REUSABLE>

	% E1 is the key scene
	% E2 is the entrance of the key scene

	Scene1 <Key>
	Element2 <Prefab>
	Scene2 <SPACE ENVIRONS>

	inv
		STC QUEST-ENCOUNTER-AUTOENTRANCE-STATIONARY
		desig <entrance %1%>
		name <%name1%>
		rumor%prev_id% <there's an old wrecked spaceship floating outside the station. People say it's loaded with treasure.>
	end

%%
%%  *ENT_SecretTomb
%%
%% There's a secret tomb somewhere in town. Maybe someone can tell the PC where it is.
%% The Key Item is the item secured in the tomb.

Content
	name <Grave Robbers in Space>
	requires <*ENT_SecretTomb SPINNER>

	% E1 is the key item/tomb scene
	% E2 is the secret entrance to the tomb
	% E3 is the thief who can tell the PC about it
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <SPACE ENVIRONS>
	Element3 <Prefab>
	Scene3 <Building Public>
	Team3 <Citizens>
	TeamData3 <Pass>

	sub
		Persona 3
		% V1 = Have told about the tomb.
		% V2 = Reward for giving item to this NPC
		rumor%prev_id% <%name3% isn't above a bit of grave robbing.>
		greeting <if= V1 1 else GoCheckFirst ifItemOK %1% else GoEndQuest NewChat Say 6 AddChat 4 AddChat 5>
		GoEndQuest <V= 1 2 Goto GoChat>
		GoCheckFirst <if= QuestStatus %qid% %prev_id% else GoGreet Goto GoFirstTime>
		*GoFirstTime <*LookingForItem %1% GoGiveInfo>
		GoGiveInfo <V= 1 1 NewChat Say 1 AddChat 1 AddChat 2>
		*GoGreet <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 AddChat 3>
		result2 <NewChat Say 3 AddChat 3>
		result3 <NewChat Say 4 ItemQSubMemo %2% %prev_id% 5 SetQEncActive %2%>
		result4 <NewChat Say 4>
		result5 <NewChat V= 2 Reward Threat %threat% 100 150 Say 7 AddChat 6 AddChat 7>
		result6 <ifG React 25 else GoR6Fail NewChat Say 10 V= 1 2 DeleteItem %1% CashPrize V2 AddLawful -1 PCFriend %3%>
		GoR6Fail <NewChat Say 8 V= 1 2 CashPrize V2 DeleteItem %1% AddLawful -1>
		result7 <NewChat Say 9>
		Msg1 <I was looking for the \ITEM %1% myself, once... only thing is, once I found where it was, I realized it wouldn't be worth my time trying to get it.>
		Msg2 <Sure, I guess I could do that, not like it's going to cost me anything... the \item %1% was owned by one of the station founders, now it's part of a reliquary in a mortuary satellite outside.>
		Msg3 <The \item %1% 's locked in a mortuary satellite near the station. It wouldn't be worth my while to go after it because the crypt is loaded with defense robots and worse. It's hard to spend money after you die.>
		Msg4 <Here, let me give you the navigation coordinates. It's just outside the station. If you manage to find the \item %1% , come back and tell me... I'd just like to, you know, see it...>
		Msg5 <The \ITEM %1% can be found in a mortuary satellite outside of \SCENE rootsceneid .>
		Msg6 <Oh, it's you again. What do you want this time?>
		Msg7 <Now isn't that interesting... I'm still kind of interested in it, to be honest. I'll offer you $ \VAL V2 for the \ITEM %1% . What do you say? You're not going to get a better offer elsewhere.>
		Msg8 <Very nice... here's your money.>
		msg9 <You'll want to make up your mind quickly, then. I can't say how long I'll be willing to wait.>
		Msg10 <This is very nice... my brains and your brawn make a very effective combination, don't they? We'll have to work together again sometime. Here's your cash.>
		Prompt1 <Could you tell me where it is?>
		Prompt2 <Why wouldn't it be worth your time?>
		Prompt3 <Tell me where to find this satellite.>
		Prompt4 <Tell me about the mortuary satellite again.>
		Prompt5 <I've got the \ITEM %1% right here.>
		CPrompt5 <ifKeyItem %1% Accept>
		Prompt6 <Sounds good. [Give Item]>
		Prompt7 <I think I'll keep it for a while yet.>
	end
	inv
		STC QUEST-MAPMARKER-STATIONARY
		desig <ENTRANCE %s1%>
		name <Mortuary Station>

		NPC Thief
		mecha <something.txt>
	end

Content
	name <Buried in the Mine>
	requires <*ENT_SecretTomb>
	special <REUSABLE>
	% E1 is the key item/tomb scene
	% E2 is an entrance, to be placed in the mine

	Element1 <Key>
	Scene2 <Dungeon Mine>
	Element3 <Prefab>
	Scene3 <Grab 2>

	inv
		TrapDoor
		desig <ENTRANCE %s1%>
		Hide
		update <if= QuestStatus %qid% %prev_id% if= V1 0 SetStat 5 3 V= 1 1>
		rumor%prev_id% <there's an old tomb in %name2%.>
	end

Content
	name <The Professor Knows>
	requires <*ENT_SecretTomb>

	% E1 is the key item/tomb scene
	% E2 is the secret entrance to the tomb
	% E3 is the professor who can tell the PC about it
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <Building Public>
	Element3 <Prefab>
	Scene3 <Building Public Legit>
	Team3 <Citizens>
	TeamData3 <Pass>

	sub
		Persona 3
		% V1 = Have told about the tomb.
		rumor%prev_id% <%name3% knows where to find the %name1%.>
		greeting <if= QuestStatus %qid% %prev_id% else GoGreet if= V1 1 else GoFirstTime NewChat Say 6 AddChat 4>
		*GoFirstTime <*LookingForItem %1% GoGiveInfo>
		GoGiveInfo <V= 1 1 NewChat Say 1 AddChat 1 AddChat 2>
		*GoGreet <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 AddChat 3>
		result2 <NewChat Say 3 AddChat 3>
		result3 <NewChat Say 4 ItemQSubMemo %2% %prev_id% 5>
		result4 <NewChat Say 4>
		Msg1 <So, you are interested in the \ITEM %1% , are you? I know something about that. It was once in the posession of famed architect Sam Pham, but when he died it was buried with him.>
		Msg2 <Not nessecarily. Pham intended for his tomb to serve as a lasting tribute to his architectural genius, and it was for a while open to the public. It has since fallen into disrepair, sadly.>
		Msg3 <That may have been Pham's intention when he requested to be buried with the \item %1% ... He wanted his tomb to serve as a lasting tribute to his architectural genius. It has since fallen into disrepair, sadly.>
		Msg4 <The entrance to the tomb can be found in \SCENE %s2% . It has not been used in years, and may be difficult to find.>
		Msg5 <The \ITEM %1% can be found in a tomb. The entrance can be found in \SCENE %s2% .>
		Msg6 <Is there anything else that I can do for you?>
		Prompt1 <So I guess it's lost forever...>
		Prompt2 <Where's the tomb? I'll dig it up!>
		Prompt3 <Tell me where to find this tomb.>
		Prompt4 <I want to hear about that tomb again.>
	end
	inv
		TrapDoor
		desig <ENTRANCE %s1%>
		Hide
		update <if= QuestStatus %qid% %prev_id% if= V1 0 SetStat 5 SkillTar %threat% V= 1 1>

		NPC Professor
		chardesc Easygoing
	end

%%
%% *ENT_PartyYacht
%%
%% The PC wants to get onboard a yacht, but there's usually something in the way...

Content
	name <Troublesome Bouncer>
	requires <*ENT_PartyYacht>

	% E1 is the party yacht itself
	% E2 is the bouncer, in the scene containing the entrance to the yacht
	% E3 is the scene containing the entrance to E1
	Scene1 <Key>

	Element2 <pREFAB>
	Scene2 <New *MicroStation>
	Team2 <Guards>
	TeamData2 <SD>

	Scene3 <Space Environs>

	sub
		Persona 2
		% E2 is the NPC who might allow the PC access to the yacht. Maybe. If the
		% PC is really nice.
		% L1 = Have given PC access.
		Greeting <if= L1 1 else GoCheckSurrender NewChat Say 1>
		GoCheckSurrender <ifChatNPCSurrendered else GoIntro NewChat Say 2 TeamNeutral 2 TeamNeutral 3 Goto GoOpenDoor>
		*GoIntro <*LetMeIn %threat% GoLetIn GoGuardEnraged>
		GoLetIn <NewChat Say 3 Goto GoOpenDoor>
		GoGuardEnraged <ifTaunt %sktar_hard% else GoJustGoodbye EndChat Say 4 TeamAttack 3>
		*GoJustGoodbye <*BrushOff&GetOutOfHere>
		GoOpenDoor <L= 1 1>
		Msg1 <You can use the elevator now.>
		Msg2 <Alright, I give... I'll let you use the elevator.>
		Msg3 <Alright, I guess you can go up. I'll authorize you for the elevator.>
		Msg4 <That's it... Now you've gone and made me angry.>

		Metascene 2
		sub
			room
			special <SHAREDPALETTE>
			desig <HOME>
			minimap <.1.....##...2#...##......>
			inv
				Elevator
				desig <ENTRANCE %s1%>
				MiniMapComponent 2
				% This is the elevator to the main hall of the yacht. In order
				% to use it, the guard E2 must authorize things.
				Use <if= L1 1 else GoCantUse Print 1 Exit Destination>
				GoCantUse <Print 2>
				% The PC can try to pick the lock, but if the guard sees him it's bad.
				Clue32 <if= L1 0 ifNPCCanSeePC %2% else GoMaybePick  Print 3  TeamAttack 3>
				GoMaybePick <Mental ifCodeBreaking %sktar_hard% else GoCBFail Print 4 L= 1 1 XPV 50>
				GoCBFail <Print 5>
				Clue29 <Print 6>
				Clue37 <Print 6>
				Msg1 <You ride the elevator up to the main level.>
				Msg2 <This elevator won't open for you. There must be some kind of security measure.>
				Msg3 <The guard saw you!>
				Msg4 <You disable the lock on the elevator.>
				Msg5 <You don't manage to disable the lock.>
				Msg6 <This elevator has an expensive somato-biometric lock system with AI-enhanced legitimacy checking. It must be almost impossible to bypass.>
			end
		end

		MetaScene 3
		inv
			STC QUEST-ENCOUNTER-AUTOENTRANCE-STATIONARY
			name <Luxury Yacht>
			desig <ENTRANCE %s2%>
		end
	end
	inv
		NPC Soldier
		job <Bouncer>
		job_desig <LABOR>
		EquipChar 15000
	end

Content
	name <Secret Spot>
	requires <*ENT_SpaceSecret>
	special <REUSABLE>

	% E1 is the key scene
	% E2 is the entrance of the key scene

	Scene1 <Key>
	Element2 <Prefab>
	Scene2 <SPACE ENVIRONS>

	inv
		STC QUEST-ENCOUNTER-AUTOENTRANCE-STATIONARY
		desig <entrance %1%>
		name <Space Anomoly %1%>
		rumor%prev_id% <the defense sensors have detected something floating nearby.>
	end


Content
	name <Secret Pirate Base>
	requires <*ENT_SpaceSecret S:CRIHN>

	% E1 is the key scene
	% E2 is the entrance of the key scene
	% E3 is the character who can tell you where it is

	Scene1 <Key>
	Element2 <Prefab>
	Scene2 <SPACE ENVIRONS>

	Element3 <Prefab>
	Scene3 <Building Public>
	Team3 <Citizens>
	TeamData 3 <Pass Ally>

	sub
		Persona 3
		% V1 = Have revealed the location of the base.
		rumor%prev_id% <%name3% has had dealings with pirates.>
		greeting <if= QuestStatus %qid% %prev_id% else GoIntroduce if= V1 0 else GoChat NewChat Say 1 AddChat 1>
		*GoIntroduce <*NiceToMeetYou GoChat>
		*GoChat <*Misc_Chatter>
		result1 <NewChat Say 2 V= 1 1 ItemQSubMemo %2% %prev_id% 3 SetQEncActive %3%>
		Msg1 <What do you want?>
		Msg2 <That I do... There's a pirate base in \SCENE %s2% . Normally I wouldn't be giving that info out for free, but the bastards cheated me out of a trailerload of Joytek Pornbots, so to hell with them!>
		Msg3 <You learned from %name3% that there's a secret pirate base in \SCENE %s2% .>
		Prompt1 <I hear you know a thing or two about pirates.>
	end
	inv
		STC QUEST-ENCOUNTER-STATIONARY
		desig <entrance %s1%>
		name <Pirates!>

		NPC Citizen
		chardesc Criminal
	end

Content
	name <Secret Region of Space>
	requires <*ENT_SpaceSecret C:++>
	special <REUSABLE>

	% E1 is the key character the PC is looking for
	% E2 is the key scene that this provides the entrance to
	% E3 is the entrance, actually a quest encounter
	% E4 is the character who can tell the PC where to look
	Element1 <Key>

	Scene2 <Key>

	Element3 <Prefab>
	Scene3 <SPACE ENVIRONS>

	Element4 <Prefab>
	Scene4 <Building Public>
	Team4 <Citizens>
	TeamData 4 <Pass Ally>

	sub
		Persona 4
		% V1 = Have revealed secret already
		% V2 = Cash offer
		% V3 = Have tried intimidation already
		rumor%prev_id% <%name4% knows where %name1% is.>
		Greeting <if= QuestStatus %qid% %prev_id% else GoFirst  if= V1 0 else GoFind  Goto GoLookFor>
		*GoLookFor <*LookingForNPC %1% GoFind GoReject>
		GoFind <NewChat   Say 1   SetQEncActive %3%   ItemQSubMemo %3% %prev_id% 6 V= 1 1>
		GoReject <NewChat V= 2 Threat %threat% 10  Say 2 AddReact -d3  AddChat 1 AddChat 2 AddChat 3>
		*GoFirst <*NiceToMeetYou GoChat>
		*GoChat <*Misc_Chatter>
		result1 <NewChat Say 3>
		result2 <V= 3 1 ifIntimidation %sktar-hard% else GoR2Fail NewChat Say 4 AddChat 4 V= 1 1>
		GoR2Fail <NewChat Say 5>
		result3 <NewChat Say 6  V= 1 1  cash+ -V2  AddChat 5>
		result4 <Goto GoFind>
		result5 <Goto GoFind>
		Msg1 <%name1% is hiding in \SCENE %s3% .>
		Msg2 <I can't say that I know anyone by that name... Though, for $ \VAL V2 I might be able to remember.>
		Msg3 <It's your choice.>
		Msg4 <Alright, I'll tell you...>
		Msg5 <You hurt me and you'll never get this info.>
		Msg6 <You learned from %name4% that %name1% is hiding in \SCENE %s3% .>
		Prompt1 <No way, that's too much money!>
		Prompt2 <How about I just beat the info from you instead?>
		CPrompt2 <if= V3 0 Accept>
		Prompt3 <Alright, here you go. [give money]>
		CPrompt3 <ifG PC$ V2 Accept>
		Prompt4 <You're very smart.>
		Prompt5 <So, where is \OPR %1% ?>
	end
	inv
		STC QUEST-ENCOUNTER-STATIONARY
		desig <entrance %s2%>
		name <%name1%?>

		NPC Citizen
	end

