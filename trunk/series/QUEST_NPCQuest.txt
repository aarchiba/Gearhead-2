%%
%% *:NPCQuest Content
%%
%% The Key NPC is up to something. The PC may be able to help out or oppose. Or something.
%% Always make sure to do an *ENEMY_CHECK or equivalent in these quests.

Content
	name <Entertain the Diplomat>
	requires <*:NPCQuest (C:POLIT|C:MILIT|C:TRADE) "Financial" ~"Safe">
	desc <There's a dignitary who wants to be entertained, and only the PC can do it.>

	% E1 is the person who will be giving this quest
	% E2 is the Diplomat to be entertained
	% E3 is the entrance to the party yacht
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <New *PartyYacht>
	Team2 <Citizens>
	TeamData2 <Pass>
	Element3 <Prefab>
	Scene3 <Urban>

	% SubQuest1 is the success branch
	Quest1 <*Success1.BusinessDeal 1>

	sub
		Persona 1
		Rumor0 <%name1% has been trying to convince a visiting diplomat to invest in our city.>
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst NewChat Say 8 AddChat 4>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat Goto GoEnemyCheck>
		GoChat <NewChat SayAnything>
		*GoEnemyCheck <*ENEMY_CHECK GoMaybeOfferQuest ChatNPCFac GoMakeEnemy>
		GoMakeEnemy <SetQuestStatus %qid% -2 ifG d20 React PCEnemy %1%>
		% The PC can get this quest if he's an ally of the NPC, if his Renown
		% is sufficiently high, or if he can make a decent Conversation roll.
		GoMaybeOfferQuest <ifChatNPCAlly else GoCheckRenown                NewChat Say 1 Goto GoStartQuest>
		GoCheckRenown <ifG PCRenown %threat% else GoCheckConversation      NewChat Say 2 Goto GoStartQuest>
		GoCheckConversation <ifConversation SkillTar %threat% else GoChat  NewChat Say 3 Goto GoStartQuest>
		GoStartQuest <QMemo %qid% 4 SetQuestStatus %qid% %id% AddChat 1 AddChat 2 AddChat 3>
		Result1 <AddCheerful -d6   NewChat Say 5>
		Result2 <NewChat Say 6>
		Result3 <AddSociable d8    NewChat Say 7>
		Result4 <Goto .%id1%_GoInit>
		Msg1 <\PC , how would you like to go to a party? There's a diplomat in town and we're trying to convince \OPR %2% to invest here. There's a reception being held on a yacht above the city. All public servants have been invited to attend.>
		Msg2 <You're a pretty well known pilot, \PC . The city is holding a party for a visiting diplomat, and I'd like for you to attend. We're hoping to impress \OPR %2% so \SPR %2% 'll invest here. Make sure \SPR %2% has a good time and you'll be rewarded.>
		Msg3 <\PC , may I have a moment? There's a diplomat visiting our city and we're hoping \SPR %2% 'll invest here. I'm throwing a party for \OPR %2% tonight which I'd like for you to attend. Make sure \SPR %2% has a good time and there'll be a reward.>
		Msg4 <You've been invited to a party thrown by %name1% to convince a visiting diplomat to invest in the city.>
		Msg5_1 <Of course you will. I like you, how could %name2% not? You better get over there soon before all the free booze is gone.>
		CMsg5_1 <ifG React 20 Accept>
		Msg5_2 <Good point. No, I'm not particularly sure of that... I'm sure it'll be fine.>
		CMsg5_2 <ifG 21 React Accept>
		Msg6 <I'm sure there will be food, yes. Just don't eat all of it yourself.>
		Msg7 <Great. You can find the yacht floating over \SCENE RootSceneID . Just radio to them and the shuttle will pick you up.>
		Msg8 <I hope that %name2% is enjoying the party...>
		Prompt1 <Are you sure I'd make a good impression?>
		Prompt2 <Will there be free food?>
		Prompt3 <A party? I'm there.>
		Prompt4 <I'm here to tell you that \SPR %2% 'll invest.>
		CPrompt4 <if= SceneVar %s2% 11 1 Accept>

		Persona 2
		special <UNLISTED>
		greeting <if= QuestStatus %qid% %id% else GoChat if= SceneVar %s2% 11 0 else GoChat ifG PartyScore 2 else GoNotYet NewChat Say 1 QMemo %qid% 2  XPV 100  SceneVar= %s2% 11 1>
		GoChat <NewChat SayAnything>
		GoNotYet <NewChat Say 3>
		Msg1 <I must say, this is a fine party that %name1% has thrown. I'm more convinced than ever to proceed with our mutual investment.>
		Msg2 <%name2% was impressed by the amusements provided by %name1%. You should go back to \SCENE %s1% and report on your success.>
		Msg3 <I heard that the people of \SCENE RootSceneID were hospitable, but so far I'm just not seeing it.>

		% Some checks to make sure the quest ends if the NPC is killed...
		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		MetaScene 2
		% L11 = Have amused the diplomat
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
	end
	inv
		NPC Politician
		job <Diplomat>
		chardesc old cheerful sociable renowned

		STC QUEST-MAPMARKER-AUTO-WANDER
		name <Diplomatic Yacht>
		desig <ENTRANCE %s2%>
	end

Content
	name <Re-Construction Problems>
	requires <*:NPCQuest (C:POLIT|C:CORPO|C:LABOR|C:CRAFT|C:TRADE) "DISASTER">
	desc <The city is rebuilding from a previous disaster, but something's going wrong.>

	% E1 is the key character
	% E2 is the construction site
	% E3 is the entry to the construction site
	% E4 is the root scene
	element1 <Key>
	Scene2 <New *ConstructionSite>
	element3 <Prefab>
	Scene3 <Urban>
	Scene4 <Town>

	Quest1 <*:ConstructionProblem 1 2>
	Quest2 <*Success1.FinishMission 1>

	sub
		Persona 1
		rumor0 <%name1%'s reconstruction effort has hit a problem.>
		greeting <if= QuestStatus %qid% %id% else GoCheckQuest if= QuestStatus %qid1% -1 else GoCheckResult EndChat Say 5 AddChat 1>
		GoCheckResult <NewChat Say 3 AddChat 3 AddChat 2>
		GoCheckQuest <if= QuestStatus %qid% 0 else GoChat Goto GoCheckEnemy>
		*GoCheckEnemy <*ENEMY_CHECK GoOfferQuest ChatNPCFac GoMakeEnemy>
		GoMakeEnemy <SetQuestStatus %qid% -2 ifG d20 React PCEnemy %1%>
		*GoOfferQuest <*NiceToMeetYou GoExplainQuest>
		*GoChat <*MISC_CHATTER>
		GoExplainQuest <NewChat Say 1 AddChat 4 AddChat 5>
		Result1 <Goto .%id2%_GoInit>
		Result2 <NewChat Say 4 SetQuestStatus %qid% -2>
		Result3 <NewChat Say 6>
		Result4 <NewChat Say 7  AddCheerful -d10  SetQuestStatus %qid% %id%  SetQuestStatus %qid1% %id1%  QMemo %qid% 2>
		Result5 <NewChat Say 8  SetQuestStatus %qid% %id%  SetQuestStatus %qid1% %id1%  QMemo %qid% 2>
		Msg1 <As you probably know, we're still rebuilding from the damage we suffered in the disaster. I'm in charge of reconstruction efforts in \SCENE %s3% .>
		Msg2 <%name1% is in charge of the reconstruction of \SCENE %s3% . Unfortunately, the one of the projects has hit a problem.>
		Msg3 <Do you have any good news for me about the reconstruction work?>
		Msg4 <This is just what we need; hasn't \SCENE %s3% suffered enough?>
		Msg5 <I hear that you've solved our construction problem. Congratulations.>
		Msg6 <Please do. The citizens of %name4% are counting on this.>
		Msg7 <Maybe not, but you might be interested to know that there's been a problem at one of the reconstruction sites. I'm offering a reward to anyone who can get the project back on track.>
		Msg8 <Not so well, I'm afraid. There's been a problem at one of the reconstruction sites; work has all but halted. If you could find out what's going wrong and fix it, it would be a great help to us.>
		Prompt1 <[Continue]>
		Prompt2 <The reconstruction has halted.>
		CPrompt2 <if= QuestStatus %qid1% -2 Accept>
		Prompt3 <I'll be back when I have something to report.>
		Prompt4 <I don't care; I don't live here.>
		CPrompt4 <if# PCHomeTown %4% Accept>
		Prompt5 <How is the work coming along?>
	end
	inv
		STC Building
		name <%name1%'s Construction Site>
		desig <ENTRANCE %s2%>
	end

Content
	name <A Spy In Hiding>
	requires <*:NPCQuest C:POLIT "City" ~"Capitol">

	% There's an enemy spy in the city.

	% E1 is the NPC offering the quest
	% E2 is the NPC being sought
	% E3 is the root scene; it's just needed for messages
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <New *Hideout>
	Team2 <Guards>
	TeamData2 <SD>
	Scene3 <Town>

	% SQ1 is the victory for capturing the spy
	% SQ2 is the victory for aiding the spy
	Quest1 <*SuccessVsChar.Capture 1 2>
	Quest2 <*SuccessVsChar.Evasion 2 1>

	sub
		Persona 1
		% SceneVar %s2% 11 = Arrest status; if 1, E2 surrendered. If -1, E2 escaped.
		%                if -2, PC agreed to lie about E2
		% This quest doesn't have an enemy check at the beginning, so if the PC
		% is an enemy of E1's faction he can catch the spy to either remove the
		% enemy status (faction relations still apply) or can aid E2.
		rumor0 <there's an enemy spy hiding in %name3%.>
		greeting <if= QuestStatus %qid% %id% else GoCheckQuest if= SceneVar %s2% 12 0 else GoChat NewChat Say 3 AddChat 1 AddChat 2 AddChat 3>
		GoCheckQuest <if= QuestStatus %qid% 0 else GoChat Goto GoOfferQuest>
		*GoChat <*MISC_CHATTER>
		GoOfferQuest <NewChat Say 1 QMemo %qid% 2 SetQuestStatus %qid% %id%>
		Result1 <if= FacRelationship ChatNPCFac NAV_ArchEnemy else .%id1%_GoInit GrabFaction ChatNPCFac GSetNAtt NAG_Relationship 0 0 Goto .%id1%_GoInit>
		result2 <NewChat Say 4 SetQuestStatus %qid% -2>
		result3 <NewChat Say 5 QMemo %qid% 6 SceneVar= %s2% 12 1>
		Msg1 <There's an enemy spy operating in %name3%. We have learned that \PPR %2% name is %name2%, but so far have not been able to catch \OPR %2% . There's a sizable reward available for anyone who manages to do so.>
		Msg2 <%name1% revealed that there's a reward available for capturing or killing the spy %name2%.>
		msg3 <As of this time %name2% remains at large.>
		Msg4 <It's most unfortunate that we were not able to capture %name2%, but I guess that's someone else's problem now.>
		Msg5 <It's a shame that %name2% couldn't have been captured alive; we could have learned so much from \OPR %2% . There will be no reward as you have provided no evidence that you were the one who did this.>
		Msg6 <You lied to %name1%, telling \OPR %1% that %name2% is dead.>
		Prompt1 <I've stopped %name2%.>
		CPrompt1 <if= SceneVar %s2% 11  1  Accept>
		Prompt2 <%name2% is no longer in the area.>
		CPrompt2 <if= SceneVar %s2% 11 -1  Accept>
		Prompt3 <%name2% is dead.>
		CPrompt3 <if= SceneVar %s2% 11 -2  ifNPCOK %2%  Accept>


		Persona 2
		Special <UNLISTED>
		% SceneVar %s2% 12 = Have lied to E1 about E2's death
		greeting <if= QuestStatus %qid% %id% else GoChat if= SceneVar %s2% 12 1 else GoCheckOffer NewChat Say 12 AddChat 8>
		GoCheckOffer <if= SceneVar %s2% 11 -2 else GoCheckSurrender NewChat Say 9 AddChat 6 AddChat 7>
		GoCheckSurrender <ifChatNPCSurrendered else GoBeginFight ifFactionAlly NPCFac %1% else GoMaybeDeal EndChat Say 2 AddChat 1>
		GoMaybeDeal <EndChat Say 2 AddChat 1 AddChat 2>
		GoChat <NewChat SayAnything>
		GoBeginFight <SoloAttack %2% Goto GoThreat>
		*GoThreat <*IntruderGreeting&Arrest GoDie>
		GoDie <EndChat Say 1>
		result1 <EndChat Say 3 FreezeNPC %2% PCEnemy %2% SceneVar= %s2% 11 1 Print 4 QMemo %qid% 5 XPV 100>
		result2 <NewChat Say 6 UnSurrender %2% SetNPCTeam %2% 2 SceneVar= %s2% 11 -2 QMemo %qid% 7 AddChat 3 AddChat 4 AddChat 5>
		result3 <NewChat Say 8>
		result4 <PCFriend %2% Goto result3>
		result5 <ifChatNPCSexy else result3 ifFlirtation SkillTar %threat% else result3 PCLover %2% Goto result3>
		result6 <EndChat Say 10 AddHeroic -5 SoloAttack %2% SceneVar= %s2% 11 0>
		result7 <NewChat Say 11>
		result8 <Goto .%id2%_GoInit>
		Msg1 <You made a big mistake in seeking me out. I've studied thirty two varieties of martial arts... Killing you will be no problem.>
		Msg2 <I am defeated... Do with me what you want with me, but I'll reveal nothing.>
		Msg3 <As you wish. Don't think you've seen the last of me.>
		Msg4 <You capture %name2%.>
		Msg5 <You should go tell %name1% that %name2% has been captured.>
		Msg6 <You're letting me go!? But why would you want to do that?>
		Msg7 <You agreed to tell %name1% that %name2% died, thereby saving \OPR %2% from \OPR %1% .>
		Msg8 <I don't know whether you are crazy or simply a fool, but I won't refuse any help. Go back to %name1% and tell \OPR %1% that I've been killed. It's important that \SPR %1% suspect nothing.>
		Msg9 <Have you spoken with %name1% already?>
		Msg10 <You liar! I should have known better than to trust someone from \SCENE PCHomeTown ...>
		Msg11 <Be very careful, a single mistake could mean the death of us both.>
		Msg12 <Do you come bearing any good news?>
		Prompt1 <I'm bringing you back to %name1%.>
		Prompt2 <I want to help you, not capture you.>
		Prompt3 <I'm no fan of %name1%.>
		Prompt4 <I'm an enemy of %name1% myself.>
		CPrompt4 <ifNPCArchEnemy %1% Accept>
		Prompt5 <No reason. I just like you.>
		Prompt6 <I've decided I should kill you after all.>
		Prompt7 <No, not yet.>
		Prompt8 <%name1% thinks you're dead.>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% if= SceneVar %s2% 12 0 else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		MetaScene 2
		Surrender%2% <Forcechat %2%>
		Faint%2% <if= QuestStatus %qid% %id% ifNPCOK %1% else GoEndQuest L= 11 1>
		GoEndQuest <SetQuestStatus %qid% -2>
		end <if= QuestStatus %qid% %id% if= L11 0 ifNPCOK %2% DeleteNPC %2% L= 11 -1>
	end
	inv
		NPC Spy
		Talent 5
		Talent 6
	end


Content
	name <Construction Problems>
	requires <*:NPCQuest (C:POLIT|C:CORPO|C:LABOR|C:CRAFT|C:TRADE)>
	desc <The NPC has a building under construction, but there's something going wrong.>

	% E1 is the key character
	% E2 is the construction site
	% E3 is the entry to the construction site
	element1 <Key>

	Scene2 <New *ConstructionSite>

	element3 <Prefab>
	Scene3 <Urban>

	Quest1 <*:ConstructionProblem 1 2>
	Quest2 <*Success1.FinishMission 1>

	sub
		Persona 1
		rumor0 <%name1%'s building project is not going very well.>
		greeting <if= QuestStatus %qid% %id% else GoCheckQuest NewChat Say 3 AddChat 1 AddChat 2>
		GoCheckQuest <if= QuestStatus %qid% 0 else GoChat Goto GoCheckEnemy>
		*GoCheckEnemy <*ENEMY_CHECK GoOfferQuest ChatNPCFac GoMakeEnemy>
		GoMakeEnemy <SetQuestStatus %qid% -2 ifG d20 React PCEnemy %1%>
		*GoOfferQuest <*NiceToMeetYou GoExplainQuest>
		*GoChat <*MISC_CHATTER>
		GoExplainQuest <NewChat Say 1 SetQuestStatus %qid% %id%  SetQuestStatus %qid1% %id1%  QMemo %qid% 2>
		Result1 <Goto .%id2%_GoInit>
		Result2 <NewChat Say 4 SetQuestStatus %qid% -2>
		Msg1 <There's been a problem at the site of our new construction project. If anyone could find out what's going on and solve matters, I'd be willing to provide a generous reward.>
		Msg2 <%name1% in \SCENE %s1% has ordered the construction of a building, but progress has mysteriously halted.>
		Msg3 <Construction still hasn't been finished on my new building.>
		Msg4 <I have anticipated this possibility. It is a minor setback, but will not affect our long term goals.>
		Prompt1 <Actually, I've fixed the problem.>
		CPrompt1 <if= QuestStatus %qid1% -1 Accept>
		Prompt2 <I don't think it ever will be.>
		CPrompt2 <if= QuestStatus %qid1% -2 Accept>
	end
	inv
		STC Building
		name <%name1%'s Construction Site>
		desig <ENTRANCE %s2%>
	end


Content
	name <Military Teacher Shortage>
	requires <*:NPCQuest C:MILIT ~"Safe">
	desc <There's a shortage of drill instructors, so the PC will be asked to teach some stuff to three recruits.>

	% E1 is the key character
	% E2 through E4 are the students
	Element1 <Key>

	Element2 <Prefab>
	Scene2 <Grab 1>
	Team2 <Guards>
	TeamData2 <SD Ally>

	Element3 <Prefab>
	Scene3 <Grab 1>
	Team3 <Guards>
	TeamData3 <SD Ally>

	Element4 <Prefab>
	Scene4 <Grab 1>
	Team4 <Guards>
	TeamData4 <SD Ally>

	% SubQuest1 is the success when the PC has trained all the soldiers.
	Quest1 <*Success1.FinishMission 1>

	sub
		Persona 1
		rumor0 <%name1% has been training some new recruits.>
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst NewChat Say 2 AddChat 3 AddChat 4 AddChat 5 AddChat 6 AddChat 7>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat Goto GoCheckEnemy>
		*GoCheckEnemy <*ENEMY_CHECK GoOfferQuest ChatNPCFac GoDenyQuest>
		*GoChat <*MISC_CHATTER>
		GoOfferQuest <NewChat Say 1 AddChat 1 AddChat 2>
		GoDenyQuest <SetQuestStatus %qid% -2>
		Result1 <NewChat Say 3 QMemo %qid% 4 SetQuestStatus %qid% %id%>
		Result2 <NewChat Say 5 SetQuestStatus %qid% -2>
		Result3 <NewChat Say 6>
		Result4 <NewChat Say 7 SetQuestStatus %qid% -2>
		result5 <Goto result3>
		result6 <Goto result3>
		result7 <XPV 100  Goto .%id1%_GoInit>
		Msg1 <Right now we don't have enough qualified instructors for all the new recruits. I've been training some of them myself, but it's slow going.>
		Msg2 <How is the training going?>
		Msg3 <It would be appreciated if you could. The three recruits are %name2%, %name3%, and %name4%. They're stationed here at \SCENE %s1% along with me.>
		Msg4 <%name1% asked you to train three recruits at \SCENE %s1% : %name2%, %name3%, and %name4%.>
		Msg5 <No reason why it should. Carry on.>
		Msg6 <Come back and speak to me when you're finished; I'll see what kind of reward I can requisition.>
		Msg7 <What the hell did you teach them!? It's probably best if we cut the training short, before any more recruits get hurt.>
		Prompt1 <Maybe I could give you a hand.>
		Prompt2 <That doesn't concern me.>
		Prompt3 <I'm still working on it.>
		Prompt4 <%name2% is dead.>
		CPrompt4 <ifNPCDead %2% Accept>
		Prompt5 <%name3% is dead.>
		CPrompt5 <ifNPCDead %3% Accept>
		Prompt6 <%name4% is dead.>
		CPrompt6 <ifNPCDead %4% Accept>
		Prompt7 <All three recruits are ready for their test.>
		CPrompt7 <if= NPCVar %2% 1 1 if= NPCVar %3% 1 1 if= NPCVar %4% 1 1 Accept>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		Persona 2
		% The ranged combat student
		% Upon succeeding in this quest, set NPCVar ChatNPCID 1 to 1 and
		%  induct into E1's faction.
		greeting <if= QuestStatus %qid% %id% else GoChat if= NPCVar ChatNPCID 1 1 else GoTrain NewChat Say 1>
		GoChat <NewChat SayAnything>
		GoTrain <NewChat Say 2 AddChat 1 AddChat 2>
		GoFail <NewChat Say 4>
		GoWin <NewChat Say 3 Mental NPCVar= ChatNPCID 1 1  SetNPCFaction ChatNPCID NPCFac %1%>
		result1 <ifSkillTest 6 SkillTar %threat% else GoFail Goto GoWin>
		result2 <ifSkillTest 7 SkillTar %threat% else GoFail Goto GoWin>
		Msg1 <I'm ready to take on all comers, thanks to you.>
		Msg2 <I've got to work on my ranged combat skills if I want to get accepted into the sniper program.>
		Msg3 <Hey, thanks. With your help I think I can do it.>
		Msg4 <I think I'm better off training on my own.>
		Prompt1 <Let me teach you about Small Arms.>
		Prompt2 <Let me teach you about Heavy Weapons.>

		Persona 3
		% The close combat student
		% Upon succeeding in this quest, set NPCVar ChatNPCID 1 to 1 and
		%  induct into E1's faction.
		greeting <if= QuestStatus %qid% %id% else GoChat if= NPCVar ChatNPCID 1 1 else GoTrain NewChat Say 1>
		GoChat <NewChat SayAnything>
		GoTrain <NewChat Say 2 AddChat 1 AddChat 2>
		GoFail <NewChat Say 4>
		GoWin <NewChat Say 3 Mental NPCVar= ChatNPCID 1 1  SetNPCFaction ChatNPCID NPCFac %1%>
		result1 <ifSkillTest 8 SkillTar %threat% else GoFail Goto GoWin>
		result2 <ifSkillTest 9 SkillTar %threat% else GoFail Goto GoWin>
		Msg1 <I'm ready to deploy! Woo-hoo!>
		Msg2 <Man, I can't wait to be finished with this training so I can get out there and taste some real combat, you know?>
		Msg3 <You're pretty good at this. Thanks for the pointers.>
		Msg4 <Woah, are you trying to get me killed? I think I better train by myself.>
		Prompt1 <Let me teach you about Armed Combat.>
		Prompt2 <Let me teach you about Martial Arts.>

		Persona 4
		% The defense student
		% Upon succeeding in this quest, set NPCVar ChatNPCID 1 to 1 and
		%  induct into E1's faction.
		greeting <if= QuestStatus %qid% %id% else GoChat if= NPCVar ChatNPCID 1 1 else GoTrain NewChat Say 1>
		GoChat <NewChat SayAnything>
		GoTrain <NewChat Say 2 AddChat 1 AddChat 2>
		GoFail <NewChat Say 4>
		GoWin <NewChat Say 3 Mental NPCVar= ChatNPCID 1 1  SetNPCFaction ChatNPCID NPCFac %1%>
		result1 <ifSkillTest 10 SkillTar %threat% else GoFail Goto GoWin>
		result2 <ifSkillTest 36 SkillTar %threat% else GoFail Goto GoWin>
		Msg1 <Thanks to you, I'm all ready to go out. Hopefully I can even come back alive.>
		Msg2 <I signed up to help protect my home, but now that I'm getting close to the end of training I'm really worried about dying in combat...>
		Msg3 <Thanks for those tips. I feel a bit more confident now.>
		Msg4 <Stop. It's clear that you don't know any better than me, so listening to your advice could easily get me killed.>
		Prompt1 <Let me teach you about Dodge.>
		Prompt2 <Let me teach you about Resistance.>
	end
	inv
		NPC Soldier
		CharDesc Young Shy

		NPC Soldier
		CharDesc Young Passionate

		NPC Soldier
		CharDesc Young Melancholy
	end

Content
	name <Charity Orphanage>
	requires <*:NPCQuest (C:MEDIC|C:FAITH) ~"poor" ~"dystopia">

	% E1 is the NPC offering the quest.
	Element1 <KEY>
	% E2 through E4 are orphans living here.
	Element2 <Prefab>
	Scene2 <Grab 1>
	Team2 <Citizens>
	TeamData2 <ALLY PASS>
	Element3 <Prefab>
	Scene3 <Grab 1>
	Team3 <Citizens>
	TeamData3 <ALLY PASS>
	Element4 <Prefab>
	Scene4 <Grab 1>
	Team4 <Citizens>
	TeamData4 <ALLY PASS>

	% Quest1 is the FindMoney task.
	% Quest2 is the success track.
	Quest1 <*:FindMoney.Charity 1>
	Quest2 <*Success1.CharityWork 1>

	sub
		Persona 1
		rumor0 <%name1% needs money to help build an orphanage.>
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst NewChat Say 7 AddChat 6 AddChat 3>
		*GoCheckFirst <*NiceToMeetYou GoOfferQuest>
		GoOfferQuest <if= QuestStatus %qid% 0 else GoChat NewChat Say 1 AddChat 1 AddChat 2 AddChat 3 QMemo %qid% 3 SetQuestStatus %qid% %id% SetQuestStatus %qid1% %id1%>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2>
		result2 <NewChat Say 4 SetQuestStatus %qid% -2 SetQuestStatus %qid1% -2>
		result3 <NewChat Say 5 AddChat 4 AddChat 5>
		result4 <Cash+ -5000000 AddHeroic 2 SetQuestStatus %qid1% -2 Goto .%id2%_GoInit>
		result5 <NewChat Say 6>
		result6 <Goto .%id2%_GoInit>
		Msg1 <This city desperately needs a new orphanage. We are caring for many children here at \SCENE %s1% , but they lack adequate housing and support...>
		Msg2 <Could you do that? Any help would be greatly appreciated.>
		Msg3 <%name1% in \SCENE %s1% needs money to build a new orphanage.>
		Msg4 <It's a sad day for the world when we cast off our own children, and nobody cares...>
		Msg5 <Building a new orphanage isn't cheap... it would cost at least five million credits. Are you certain you're willing to do that?>
		Msg6 <Please do your best.>
		Msg7 <There are so many needy, but I fear we don't yet have the resources to help them all.>
		Prompt1 <I could help you find the money.>
		Prompt2 <I don't care about your stinky orphans.>
		Prompt3 <Why don't I just buy you an orphanage myself?>
		CPrompt3 <ifG PC$ 4999999 Accept>
		Prompt4 <Sure. I'm good for it. [give $5M]>
		Prompt5 <I'll see if I can find anything else first.>
		Prompt6 <I've found a sponsor for your orphanage.>
		CPrompt6 <if= QuestStatus %qid1% -1 Accept>

		% Death check
		Metascene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		Persona 2
		*Greeting <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>

		Persona 3
		*Greeting <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>

		Persona 4
		*Greeting <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
	end
	inv
		NPC Student
		Age -4

		NPC Student
		Age -3

		NPC Student
		Age -2
	end

Content
	name <Infectious Outbreak>
	requires <*:NPCQuest C:MEDIC ~"resort">
	% There's a disease outbreak. In order to fight the disease, it's nessecary to
	% find the principal carrier. Unfortunately, the supposed patient zero has gone
	% into hiding...

	% E1 is the NPC offering the quest
	% E2 is the carrier of a virus
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <New *Hideout>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	% Quest1 is the success track.
	Quest1 <*Success1.CharityWork 1>

	sub
		Persona 1
		rumor0 <%name1% has been fighting the outbreak of a weird disease.>
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst if# NPCVar %2% 1 0 else GoCheckDeath NewChat Say 8 AddChat 4>
		GoCheckDeath <ifNPCDead %2% else GoCheckProgress NewChat Say 7 SetQuestStatus %qid% -2 XPV 100>
		GoCheckProgress <NewChat Say 6>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat NewChat Say 1 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 2 QMemo %qid% 3 SetQuestStatus %qid% %id%>
		result2 <NewChat Say 4 AddChat 3>
		result3 <NewChat Say 5 QMemo %qid% 3 SetQuestStatus %qid% %id%>
		result4 <Goto .%id1%_GoInit>
		Msg1 <There's been an outbreak of malboro syndrome. You are advised to seek immediate medical attention if you suffer from confusion, blindness, muteness, fits of rage, or halitosis.>
		Msg2 <It's believed that the virus was brought here by a trucker named %name2%; unfortunately \SPR %2% 's either left town or gone into hiding. if \SPR %2% could be examined here, it would help us to identify the strain of the virus.>
		Msg3 <Malboro syndrome was brought to \SCENE RootSceneID by a trucker named %name2%. %name1% in \SCENE %s1% wants to examine \OPR %2% to identify the strain of the virus.>
		Msg4 <Please do. The virus is highly contagious... Also, if you happen to meet a trucker named %name2%, please come back to let me know.>
		Msg5 <It's thought that \SPR %2% 's the one who first brought the virus here. If we could examine \OPR %2% , we could identify the strain we're dealing with. Unfortunately %name2% seems to have either skipped town or gone into hiding.>
		Msg6 <New cases of malboro syndrome are turning up every day. The situation is getting desperate.>
		Msg7 <It's very sad what happened to %name2%... Now that we have the body, hopefully we can prevent any more lives from being lost.>
		Msg8 <%name2% has arrived here and is being treated. Thanks to \PPR %2% help, we've identified the strain of the virus and many more lives will be saved.>
		Prompt1 <What caused this outbreak?>
		Prompt2 <I'll be careful.>
		Prompt3 <Who is this %name2%?>
		Prompt4 <Don't forget my part in this.>

		% Death check
		Metascene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		Persona 2
		% NPCVar %2% 1 = Have agreed to go to hospital
		% V1 = First time counter.
		special <UNLISTED>
		greeting <if# NPCVar %2% 1 2 else GoChat if= NPCVar %2% 1 1 else GoCheckFirst NewChat Say 1 NPCVar= %2% 1 2>
		GoCheckFirst <if= QuestStatus %qid% %id% else GoChat  ifChatNPCSurrendered else GoArgue EndChat Say 2 Goto GoSurrender>
		GoArgue <if= V1 1 else GoFirstTime NewChat Say 10 AddChat 3 AddChat 4>
		GoFirstTime <NewChat Say 5 AddChat 1 AddChat 2 V= 1 1>
		GoSurrender <Print 3 QMemo %qid% 4 MoveAndPacifyNPC %2% %s1% UnSurrender %2% XPV 100 NPCVar= %2% 1 1>
		GoAttack <SoloAttack %2%>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 6 AddCHat 3 AddChat 4 AddCheerful -d8>
		result2 <NewChat Say 7 AddCHat 3 AddChat 4>
		result3 <NewChat Say 8 AddChat 5 AddChat 6>
		result4 <NewChat Say 9>
		result5 <ifIntimidation %sktar% else GoR5Fail EndChat Say 11 Goto GoSurrender>
		GoR5Fail <EndChat Say 14 Goto GoAttack>
		result6 <ifConversation %sktar% else GoR6Fail NewChat Say 12 AddChat 7 AddChat 8>
		GoR6Fail <EndChat Say 15 Goto GoAttack>
		result7 <EndChat Say 13 Goto GoSurrender>
		result8 <EndChat Say 15 Goto GoAttack>
		% Apparently, the malboro virus causes people to talk in fluent Kitteh.
		Msg1 <Hey, I'm sorry about before. I wasn't in my right mind, but things are starting to clear up... I guess if I had stayed in hiding I'd be dead by now. Thanks for making me come here.>
		Msg2 <I give up... I'll go peacefully...>
		Msg3 <%name2% goes to the hospital.>
		Msg4 <%name2% went to \SCENE %s1% . You should go there and speak with %name1%.>
		Msg5 <What are you? Who are you doing in my house? This isn't right... I don't need a doctor!>
		Msg6 <My breath? How would you know... How do you know it's not your nose that smells, eh? My nose has smelled many things... Some things are bigger than my head!>
		Msg7 <No, that's right... They sent you too, eh? The wily doctors... They sent you to collect my precious bodily fluids!>
		Msg8 <They wants to cut me up! They blames me for bringing the sick, but it's not my fault, I only catches it... Evil doctors want to punish %name2%...>
		Msg9 <Yes, you be going, before you die... They all die... I die too, I suppose.>
		Msg10 <Yes, what you do want? I dreamed you before, don't I?>
		Msg11 <Cut up here or cut up there... what's the difference? I be going then...>
		Msg12 <You promises they won't vivisect %name2%?>
		Msg13 <I has better be goes then... If you says it be safe.>
		Msg14 <Die here or die there... I chooses die here!>
		Msg15 <Stops the lies! I stops you... must protect precious bodily fluids!>
		Prompt1 <Woah, your breath stinks.>
		Prompt2 <You're %name2%, aren't you?>
		Prompt3 <I think you should go to the hospital.>
		Prompt4 <Well, it's been nice talking with you.>
		Prompt5 <Go to the hospital, or you can die right now.>
		Prompt6 <Don't worry, they only want to help you.>
		Prompt7 <I promise.>
		Prompt8 <Uh, actually I guess I can't promise...>

		MetaScene 2
		Surrender%2% <if= QuestStatus %qid% %id% ForceChat %2%>
	end
	inv
		NPC Trucker
	end


Content
	name <Charity Hospital>
	requires <*:NPCQuest C:MEDIC ~"poor">

	% E1 is the NPC offering the quest.
	% E2 is a person whose brother benefitted from the hospital expansion; after the mission is
	%   solved, he will provide a reward.
	Element1 <KEY>
	Element2 <Prefab>
	Scene2 <Grab 1>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	% Quest1 is the FindMoney task.
	% Quest2 is the success track.
	Quest1 <*:FindMoney.Charity 1>
	Quest2 <*Success1.CharityWork 2>

	sub
		Persona 1
		rumor0 <%name1% needs money to expand the hospital.>
		% V1 = Have been told to speak with E2
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst if= V1 0 else GoCheckDeath NewChat Say 8 AddChat 3 AddChat 5>
		% If E2 dies after being told to speak with him, end the quest.
		GoCheckDeath <ifNPCDead %2% else GoChat SetQuestStatus %qid% -2 Goto GoChat>
		*GoCheckFirst <*NiceToMeetYou GoOfferQuest>
		GoOfferQuest <if= QuestStatus %qid% 0 else GoChat NewChat Say 1 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		Result1 <NewChat Say 2 AddChat 3 AddChat 4 QMemo %qid% 3 SetQuestStatus %qid% %id% SetQuestStatus %qid1% %id1%>
		Result2 <NewChat Say 4 AddSociable -10 AddCheerful -10 SetQuestStatus %qid% -2 ifG PCHeroism 0 AddHeroic -1>
		result3 <NewChat Say 5 SetQuestStatus %qid1% -1 V= 1 1 Cash+ -5000000 AddHeroic 3 QMemo %qid% 6 NPCVar= %2% 1 1 ifNPCDead %2% SetQuestStatus %qid% -2>
		result4 <NewCHat Say 7>
		result5 <NewChat Say 9 V= 1 1 AddHeroic 1 QMemo %qid% 6 NPCVar= %2% 1 1 ifNPCDead %2% SetQuestStatus %qid% -2>
		Msg1 <We really need to expand and modernize this hospital. The problem is, we don't have the money to do it. I don't know what we're going to do.>
		Msg2 <Millions of credits, at least... It's too much for any one person to do, but if you wanted to help you could try and find some donations.>
		Msg3 <%name1% in \SCENE %s1% needs money to expand the hospital.>
		Msg4 <If everyone else feels the same way you do, I fear all hope will be lost.>
		Msg5 <This is truly exceptional! Thank you, with your generous help we can begin the expansions at once... You must go speak with %name2% . I'm certain \SPR %2% will want to thank you as well.>
		Msg6 <After helping %name1%, \SPR %1% told you to go speak with %name2%.>
		Msg7 <Please, try your best. There are many people counting on this hospital.>
		Msg8 <The hospital expansion is desperately needed. We have so many people depending on it.>
		Msg9 <This is great news! Thank you, we can now begin the expansions... You must go speak with %name2% . I'm certain \SPR %2% will want to thank you as well.>
		Prompt1 <How much money do you need?>
		Prompt2 <I don't care about your problems.>
		Prompt3 <I could contribute five million. [Give Money]>
		CPrompt3 <ifG PC$ 4999999 Accept>
		Prompt4 <I'll see what I can do.>
		Prompt5 <I've found someone to donate money.>
		CPrompt5 <if= QuestStatus %qid1% -1 Accept>

		Persona 2
		% NPCVar %2% 1 = Have completed quest
		Greeting <if= QuestStatus %qid% %id% else GoGreet if= NPCVar %2% 1 1 else GoTellStory NewChat Say 1 AddCHat 1>
		GoTellStory <NewChat Say 2>
		*GoGreet <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <Goto .%id2%_GoInit>
		Msg1 <Hey, I hear you're the one who found the money for the hospital expansion.>
		Msg2 <My brother is in the hospital. I'm praying for him.>
		Prompt1 <That's right.>
	end
	inv
		NPC Priest
		%% Originally this character was to be a citizen instead of a priest, but I decided
		%% to make it a priest since this will open extra quest branches.
	end

Content
	name <Infiltrators>
	requires <*:NPCQuest C:MILIT>

	% E1 is the NPC offering the quest.
	% E2 is the encounter that the PC must destroy.
	% E3 is the enemy faction
	element1 <KEY>
	element2 <Prefab>
	Scene2 <ENVIRONS>
	element3 <Faction !Enemy 1>

	% Quest1 is the continuation after the PC has defeated the pirates so many times.
	Quest1 <*SUCCESS1.FinishMission 1>

	sub
		Persona 1
		rumor0 <there are enemy mecha infiltrating our city; %name1% will protect us.>
		% NPCVar %1% 1 = Complete quest counter
		greeting <if= QuestStatus %qid% %id% else GoNoQuest Goto GoQuestInProgress>
		*GoQuestInProgress <*ENEMY_CHECK GoCheckSuccess ChatNPCFac GoLoseQuest>
		GoCheckSuccess <NewChat Say 3 AddChat 1>
		GoLoseQuest <DeleteItem %2% SetQuestStatus %qid% -2>
		GoNoQuest <if= QuestStatus %qid% 0 else GoBasicChat Goto GoStartQuest>
		*GoStartQuest <*ENEMY_CHECK GoFirstGreeting ChatNPCFac GoLoseQuest>
		*GoFirstGreeting <*DoYouWantAJob&Mecha GoOfferMission>
		GoOfferMission <NewChat Say 1  SetQuestStatus %qid% %id%  QMemo %qid% 2>
		*GoBasicChat <*MISC_CHATTER>
		result1 <Goto .%id1%_GoInit>
		Msg1 <Enemy infiltrators have penetrated our city's defenses. I suspect they were sent by %name3%, but have no direct confirmation of this. If you were to locate and destroy them I could guarantee a generous reward.>
		Msg2 <%name1% in \SCENE %s1% told you that there are enemy infiltrators in \SCENE rootsceneid . There's a reward for defeating them.>
		Msg3 <My troops are continuing to search for the enemy infiltrators. So far, our efforts have led to nothing.>
		Prompt1 <I've found them myself.>
		CPrompt1 <if# NPCVar %1% 1 0 Accept>
		Prompt1_1 <Call off the search, they've been stopped.>
	end
	inv
		Encounter
		name <Infiltrators>
		% V1 = Recharge Timer; needed to prevent double bounce when losing combat.
		Special <NoMSID>
		update <if= QuestStatus %qid% %id% else GoHide  ifG ComTime V1 else GoHide Goto GoSetVisible>
		GoSetVisible <SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		% Sets this encounter's orders to Passive
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		EncounterMove 50
		use <ifG StatVal STAT_MetaVisibility -1  Print -1  Goto GoStartCombat>
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifUStealth 15 else GoAutoAttack ifYesNo -3 -4 -5 else GoAvoidAttack Goto Use>
		GoAutoAttack <Alert -2 Goto GoStartCombat>
		GoAvoidAttack <Print -6 AddSociable -1>
		GoStartCombat <V= 1 0  SavePos Dynamic 2 %threat% 100 .nu1 .nu2 ComposeD Msg1 .Msg1 DynaFaction %3%>
		.nu1 <if= T1 0  Return   if= L1 0  L= 1 1  AddRenown -5  ItemVar= %2% 1 ComTime ItemVar+ %2% 1 d86400 ItemVar+ %2% 1 43200>
		.nu2 <if= T2 0           if= L1 0  L= 1 1  Print 1 XPV 100  AddRenown 1   NPCVar= %1% 1 1  DeleteItem %2%  QMemo %qid% 1>
		EncounterMove 50
		Msg-1 <You attack the infiltrators!>
		Msg-2 <You are attacked by infiltrators!>
		.Msg1 <You have defeated the infiltrators and should report to %name1% in \SCENE %s1% .>
	end

Content
	name <Where's My Battleship?>
	desc <The NPC has ordered a battleship, but there are problems at the construction yard.>
	requires <*:NPCQuest C:MILIT Spinner ~industrial>

	% E1 is the key character
	% E2 is the construction site
	% E3 is the entry to the construction site
	element1 <Key>

	Scene2 <New *ConstructionSite>

	element3 <Prefab>
	Scene3 <Space Environs>

	Quest1 <*:ConstructionProblem 1 2>
	Quest2 <*Success1.FinishMission 1>

	sub
		Persona 1
		greeting <if= QuestStatus %qid% %id% else GoFirstTime NewChat Say 3 AddChat 3 AddChat 4 AddChat 5>
		*GoFirstTime <*ENEMY_CHECK GoGreeting ChatNPCFac GoMakeEnemy>
		GoMakeEnemy <PCEnemy %1% SetQuestStatus %qid% -2>
		*GoGreeting <*NiceToMeetYou GoCheckFaction>
		GoCheckFaction <if= QuestStatus %qid% 0 else GoChat  if= PCFac ChatNPCFac else GoCheckReact  Goto GoOfferMission>
		GoCheckReact <ifG React %threat% else GoChat  Goto GoOfferMission>
		*GoChat <*MISC_CHATTER>
		GoOfferMission <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <NewChat Say 2 SetQuestStatus %qid% %id%  SetQuestStatus %qid1% %id1%  QMemo %qid% 5>
		result2 <NewCHat Say 4 SetQuestStatus %qid% -2>
		result3 <Goto .%id2%_GoInit>
		result4 <NewChat Say 6>
		result5 <NewChat Say 7 SetQuestStatus %qid% -2 AddReact -10>
		Msg1 <As you may know, we are constructing a new battleship for the fleet. Unfortunately progess has been slower than expected. I could use someone to infiltrate the shipyard and find out what the problem is.>
		Msg2 <Very good. The ship is being constructed at an orbital platform outside the station. Report back here when you've learned what's going on.>
		Msg3 <Have you found out anything about the battleship yet?>
		Msg4 <I see.>
		Msg5 <%name1% in \SCENE %s1% asked you to find out why the battleship \SPR %1% ordered hasn't been completed yet.>
		Msg6 <Well come back here when you have something to report, then.>
		Msg7 <This is a black day for us all...>
		Prompt1 <I could do that for you.>
		Prompt2 <That doesn't sound like my kind of job.>
		CPrompt2 <if# PCFac ChatNPCFac Accept>
		Prompt3 <Your ship will be completed shortly.>
		CPrompt3 <if= QuestStatus %qid1% -1 Accept>
		Prompt4 <I'm still working on it.>
		Prompt5 <I don't think you'll be getting a battleship...>
		CPrompt5 <if= QuestStatus %qid1% -2 Accept>
	end
	inv
		STC QUEST-MAPMARKER-AUTO-STATIONARY
		name <%name1%'s Shipyard>
		desig <ENTRANCE %s2%>
	end

Content
	name <Rat Killing in Government>
	desc <The PC has to kill a rat for the NPC.>
	requires <*:NPCQuest C:POLIT ~"Capitol" -!Hi -!Ex>

	% Quest1 is the continuation after the PC has cleared the basement.
	Quest1 <*SUCCESS1.Extermination 1>

	% E1 is the key character
	% E2 is the basement that has to be cleared
	element1 <Key>

	Scene2 <NEW *ExterminationBasement>
	Team2 <Monsters>
	TeamData2 <SD Enemy>

	sub
		Persona 1
		rumor0 <%name1% has an embarassing problem.>
		greeting <if= QuestStatus %qid% %id% else GoFirstTime NewChat Say 1 AddChat 1 AddChat 2>
		*GoFirstTime <*ENEMY_CHECK GoGreeting ChatNPCFac GoMakeEnemy>
		GoMakeEnemy <PCEnemy %1% SetQuestStatus %qid% -2>
		*GoGreeting <*NiceToMeetYou GoCheckRenown>
		GoCheckRenown <if= QuestStatus %qid% 0 else GoChat  ifG PCRenown d%threat% else GoCheckWangtta   Goto GoOfferMission>
		GoCheckWangtta <ifG React d10 else GoChat ifG -d10 PCRenown else GoChat   Goto GoOfferMission>
		*GoOfferMission <*AreYouHereAboutJob GoMissionDesc>
		GoMissionDesc <NewChat Say 2 AddChat 3>
		*GoChat <*MISC_CHATTER>
		Result1 <NewChat Say 6>
		Result2 <Goto .%id1%_GoInit>
		Result3 <NewChat Say 3  AddChat 4 AddChat 5>
		result4 <NewChat Say 4  SetQuestStatus %qid% -2>
		result5 <NewChat Say 5  SetQuestStatus %qid% %id% UpdateProps QMemo %qid% 7>
		Msg1 <Any progress to report about the basement? It's very important that the documents be protected...>
		Msg2 <As you know, there are many very important documents stored in this building. In the basement we have decades worth of records on optical tape.>
		Msg3 <Well, the basement has become infested with... vermin. I hate to say it, but our seat of government has rats living in the basement. Your job, if you accept it, will be to rid us of these creatures.>
		Msg4 <I understand. We'll find someone more suitable for the job.>
		Msg5 <You will find the stairs down to the basement in the supply closet. Come back here when you're finished.>
		Msg6 <Let me know when you do have something to report, then.>
		Msg7 <%name1% in \SCENE %s1% asked you to clear the basement of vermin.>
		Prompt1 <Nothing to report yet.>
		Prompt2 <The basement has been cleared.>
		CPrompt2 <if= SceneVar %2% 1 1 Accept>
		Prompt3 <Okay. Following you so far.>
		Prompt3_1 <So, you want me to do something about these tapes?>
		CPrompt3_1 <ifPCSociable Accept>
		Prompt3_2 <Go on.>
		CPrompt3_2 <ifPCShy Accept>
		Prompt3_3 <Uh-huh. How do I figure into this?>
		CPrompt3_3 <ifPCEasygoing Accept>
		Prompt3_4 <Boring. Get to the good stuff.>
		CPrompt3_4 <ifPCPassionate Accept>
		Prompt3_5 <Sounds good.>
		CPrompt3_5 <ifPCCheerful Accept>
		Prompt3_6 <Does this history lesson have a point?>
		CPrompt3_6 <ifPCMelancholy Accept>
		Prompt4 <No way. I'm not interested.>
		Prompt4_1 <Rats? No way, find someone else.>
		Prompt5 <Alright, I'll do it.>
		Prompt5_1 <So you want me to be... a rat-killer?>

		MetaScene 1
		sub
			room
			minimap <######.2.##..3###+###...#>
			special <SHAREDPALETTE>
			sub
				Door
				update <if= V1 0 else GoCheckQuest V= 1 1 SetStat STAT_Lock %sktar_hard%>
				GoCheckQuest <ifG QuestStatus %qid% 0 SetStat STAT_Lock 0>
			end
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s2%>

				Monster Cleanbot
				MiniMapComponent 3
				teamdata <Pass>
			end
		end
	end

Content
	name <Wanted Murderer>
	requires <*:NPCQuest C:POLIC>

	% There's a wanted murderer hiding somewhere in the city. The PC can find
	% him and fight. Nice and easy, since there's no possible negotiation
	% with the criminal.

	% E1 is the NPC offering the quest
	% E2 is the NPC being sought
	% E3 is the root scene; it's just needed for messages
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <New *Hideout>
	Team2 <Guards>
	TeamData2 <SD>
	Scene3 <Town>

	Quest1 <*SuccessVsChar.Capture 1 2>

	sub
		Persona 1
		% NPCVar %1% 1 = Arrest status; if 1, E2 surrendered. If -1, E2 escaped.
		rumor0 <%name1% is offering a reward for anyone who can bring %name2% to justice.>
		greeting <if= QuestStatus %qid% %id% else GoGreet NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoGreet <*NiceToMeetYou GoCheckFirst>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat NewChat Say 1 QMemo %qid% 2 SetQuestStatus %qid% %id%>
		*GoChat <*MISC_CHATTER>
		result1 <AddLawful 1 ifChatNPCArchEnemy else .%id1%_GoInit NewChat Say 5 SetQuestStatus %qid% -2 CashPrize Reward %threat% 100>
		result2 <NewChat Say 3 SetQuestStatus %qid% -2>
		result3 <NewChat Say 4 SetQuestStatus %qid% -2 CashPrize Reward %threat% 50>
		Msg1 <Escaped murderer %name2% is on the loose in %name3%. I want this criminal brought in alive; there's a reward for anyone who can do that.>
		Msg2 <%name1% in \SCENE %s1% is offering a bounty for the capture of wanted murderer %name2%.>
		Msg3 <That's terrible news. Now it's certain that %name2% will kill again.>
		Msg4 <Well, the bounty was for a live capture, but I suppose justice has been served... Here's a small reward for your efforts.>
		Msg5 <You and I aren't usually on the same side of the law, but you did a great service for %name3% today. Here's a reward.>
		Prompt1 <%name2% has been captured.>
		CPrompt1 <if= NPCVar %1% 1 1 Accept>
		Prompt2 <I think %name2% got away.>
		CPrompt2 <if= NPCVar %1% 1 -1 Accept>
		Prompt3 <%name2% is dead.>
		CPrompt3 <if= NPCVar %1% 1 0 ifNPCDead %2% Accept>

		Persona 2
		Special <UNLISTED>
		greeting <if= QuestStatus %qid% %id% else GoChat IfChatNPCSurrendered else GoConfront EndChat Say 4 Goto GoCapture>
		*GoChat <*MISC_CHATTER>
		*GoConfront <*IntruderGreeting&Arrest GoTalk>
		GoTalk <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoStartFight <SoloAttack %2%>
		GoCapture <NPCVar= %1% 1 1 Trigger SURRENDER %2%>
		result1 <EndChat Say 2 AddHeroic 1 Goto GoStartFight>
		result2 <ifIntimidation %sktar_hard% else GoR2Fail EndChat Say 4 Goto GoCapture>
		GoR2Fail <EndChat Say 3 Goto GoStartFight>
		result3 <NewChat Say 5>
		Msg1 <What do you want?>
		Msg2 <I've seen what happens to their prisoners... There's no way I'll be taken alive, not while I still have time to kill!>
		Msg3 <Is that so? I rather think that you're here to become my next victim.>
		Msg4 <I'm not going to fight when it's clear that I'll lose... I'd rather try my luck with the psycho-surgery. You can take me in.>
		Msg5 <So, you know who I am... Stay out of my business or you're likely to become my next victim.>
		Prompt1 <%name1% sent me to collect you.>
		Prompt2 <I'm here to kill you for your crimes.>
		Prompt3 <I just wanted to see a real murderer.>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>

		MetaScene 2
		Surrender%2% <Print 11 NPCVar= %1% 1 1 DeleteNPC %2%>
		nu1 <if= t1 0 Return ifNPCOK %2% DeleteNPC %2% NPCVar= %1% 1 -1>
		Msg11 <%name2% has been captured.>
	end
	inv
		NPC bandit
		job <murderer>
		chardesc Criminal Villainous
	end

Content
	name <Under Surveillance>
	requires <*:NPCQuest C:POLIC>

	% E1 is the NPC offering the quest
	% E2 is the NPC being sought
	Element1 <Key>
	Element2 <Prefab>
	Scene2 <New *BlackMarket>
	Team2 <Citizens>
	TeamData2 <Pass>

	Quest1 <*SuccessVsChar.Evidence 1 2>
	Quest2 <*Success1.TipOff 2>

	sub
		Persona 1
		rumor0 <%name1% has been looking for %name2%.>
		% NPCVar %1% 1 = Have spoken with E2.
		greeting <if= QuestStatus %qid% %id% else GoGreet ifChatNPCArchEnemy else GoSeekInfo ifNPCDead %2% else GoBrushOff SetQuestStatus %qid% -2 Goto GoBrushOff>
		GoSeekInfo <NewChat Say 1 AddChat 1>
		*GoBrushOff <*BrushOff&GetOutOfHere>
		*GoGreet <*NiceToMeetYou GoCheckFirst>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat ifNPCOK %2% else GoLoseQuest Goto GoStartQuest>
		GoStartQuest <NewChat Say 1 QMemo %qid% 2 SetQuestStatus %qid% %id% AddChat 1>
		GoLoseQuest <SetQuestStatus %qid% -2 Goto GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <AddLawful 1 XPV 100 PCEnemy %2% Goto .%id1%_GoInit>
		Msg1 <The crime lord %name2% has moved into the area. There's a reward being offered for information leading to \PPR %2% capture.>
		Msg2 <%name1% in \SCENE %s1% is offering a reward for information about %name2%.>
		Prompt1 <%name2% is hiding in the black market.>
		CPrompt1 <if# NPCVar %1% 1 0 Accept>

		Persona 2
		special <UNLISTED>
		rumor%id% <there's a black market in town where criminals can lay low.>
		greeting <NPCVar= %1% 1 1 if= QuestStatus %qid% %id% else GoPreQuest NewChat Say 1 AddCHat 1 AddChat 2>
		*GoPreQuest <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		result1 <XPV 100 PCFriend %2% AddLawful -5 Goto .%id2%_GoInit>
		*result2 <*BrushOff>
		Msg1 <Yeah, can I help you?>
		Prompt1 <%name1% is looking for you.>
		Prompt2 <No, I'm just looking around.>
	end
	inv
		NPC Thief
		Chardesc Renowned
		Combatant
	end


Content
	name <Pirate Attack>
	requires <*:NPCQuest "Spinner" (C:POLIT|C:POLIC)>

	% E1 is the NPC offering the quest.
	% E2 is the encounter that the PC must destroy.
	element1 <KEY>
	element2 <Prefab>
	Scene2 <SPACE ENVIRONS>

	% Quest1 is the continuation after the PC has defeated the pirates so many times.
	Quest1 <*SUCCESS1.AgainstPirates 1>

	sub
		Persona 1
		% V1 = Number of times PC has been rewarded.
		rumor0 <%name1% is looking to hire some mecha pilots.>
		greeting <if= QuestStatus %qid% %id% else GoCheckQuest Goto GoQuestInProgress>
		*GoQuestInProgress <*ENEMY_CHECK GoCheckSuccess ChatNPCFac GoMakeEnemy>
		GoCheckSuccess <ifG ItemVar %2% 3 V1 else GoWaiting NewChat Say 2 AddChat 1>
		GoWaiting <NewChat Say 1>

		GoCheckQuest <if= QuestStatus %qid% 0 else GoBasicChat Goto GoStartQuest>
		*GoStartQuest <*ENEMY_CHECK GoFirstGreeting ChatNPCFac GoMakeEnemy>
		*GoFirstGreeting <*NiceToMeetYou GoOfferMission>
		GoOfferMission <NewChat Say 1 SetQuestStatus %qid% %id% ItemVar= %2% 1 ComTime ItemVar= %2% 2 d10 QMemo %qid% 4>

		GoBasicChat <NewChat SayAnything>
		GoMakeEnemy <PCEnemy %1% SetQuestStatus %qid% -2>

		%% If the PC has defeated the pirates enough times, branch to the next
		%% part of this story. This happens when the encounter difficulcy rating
		%% exceeds the fragment difficulcy rating.
		Result1 <ifG ItemVar %2% 2  %threat%  else GoR1NoBranch Goto .%id1%_GoInit>
		%% Upon revealing a win against the pirates, give a reward, then set the local victory
		%% counter, reset the pirate reappearance counter, and up its difficulcy rating.
		GoR1NoBranch <NewChat Say 3 AddReact d5 CashPrize Reward ItemVar %2% 2 100  V= 1 ItemVar %2% 3 ItemVar= %2% 1 ComTime ItemVar+ %2% 1 d86400 ItemVar+ %2% 1 d86400 ItemVar+ %2% 2 d10  QMemo %qid% 4>

		Msg1 <Recently our colony has been attacked by pirates. There's been a bounty set for bringing them to justice.>
		Msg1_1 <If you're planning to fly around outside the station, you should know that there have been many pirate attacks recently. We're offering a bounty for anyone who can get rid of them.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <There are too many pirates nearby. A bounty has been set on them.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <It won't be long before our pirate problems are all solved. I've authorized a bounty for anyone who can rid us of them.>
		CMsg1_3 <ifNPCCheerful Accept>
		Msg1_4 <It's terrible... our station is under constant attack by pirates. A bounty has been set for bringing them to justice.>
		CMsg1_4 <ifNPCMelancholy Accept>
		Msg1_5 <Everyone should keep calm about the recent pirate attacks. I've set a bounty against the pirate fleets, so it shouldn't be long before they're brought to justice.>
		CMsg1_5 <ifNPCEasygoing Accept>
		Msg1_6 <The pirate attacks against our station must stop now. I'm offering a reward for anyone who can defeat them.>
		CMsg1_6 <ifNPCPassionate Accept>
		Msg2 <I hear you defeated some pirates.>
		Msg2_1 <I heard that you battled some pirates outside the station. Would you care to confirm that?>
		CMsg2_1 <ifNPCSociable Accept>
		Msg2_2 <Did you find the pirates?>
		CMsg2_2 <ifNPCShy Accept>
		Msg2_3 <I've heard that someone found the pirates. Was it you?>
		CMsg2_3 <ifNPCCheerful Accept>
		Msg2_4 <A patrol found the wreckage from a pirate lance. Did you have anything to do with that?>
		CMsg2_4 <ifNPCMelancholy Accept>
		Msg2_5 <Have you encountered any pirates?>
		CMsg2_5 <ifNPCEasygoing Accept>
		Msg2_6 <There's one less pirate lance out there today. Did you have anything to do with that?>
		CMsg2_6 <ifNPCPassionate Accept>
		Msg3 <Good job. Here's a reward... Let me know if you encounter any more of them.>
		Msg3_1 <You've done a great service for \SCENE %s1% . Come back and see me if you find any more.>
		Msg3_2 <I'm glad to hear it. Here's a reward... Let me know if you find any more pirates.>
		Msg4 <%name1% in \SCENE %s1% told you that there's a reward for fighting pirates outside of the colony.>

		Prompt1 <Yes, and I defeated them...>
		CPrompt1 <ifG ItemVar %2% 3 V1 Accept>
		Prompt1_1 <Yes. They're gone now.>
		Prompt1_2 <Yes. I've dealt with them.>

	end
	inv
		Encounter
		name <Pirates>
		Special <NoMSID>
		% V1 = Visibility Clock. If ComTime > V1 and V1 <> 0, encounter is active
		% V2 = Difficulcy Rating.
		% V3 = Victory Counter. Increases by one each time the PC defeats the pirates.
		update <if# V1 0 else GoHide ifG ComTime V1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		% Sets this encounter's orders to Passive
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		use <ifG StatVal STAT_MetaVisibility -1  Print -1  Goto GoStartCombat>
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifUStealth 15 else GoAutoAttack ifYesNo -3 -4 -5 else GoAvoidAttack Goto GoAutoAttack>
		GoAutoAttack <Alert -2 Goto GoStartCombat>
		GoAvoidAttack <Print -6 AddSociable -1>

		GoStartCombat <V= 1 0  SavePos Dynamic 2 V2 100 .nu1 .nu2 ComposeD Msg1 .Msg1 DynaFaction 8>
		.nu1 <if= T1 0  Return   if= L1 0  L= 1 1  AddRenown -5  ItemVar= %2% 1 ComTime ItemVar+ %2% 1 d86400 ItemVar+ %2% 1 43200>
		.nu2 <if= T2 0           if= L1 0  L= 1 1  XPV 100  AddRenown 1   ItemVar+ %2% 3 1  QMemo %qid% 1>
		EncounterMove 50
		Msg-1 <You attack the pirates!>
		Msg-2 <You are attacked by pirates!>
		.Msg1 <You defeated some pirates, and should return to %name1% in \SCENE %s1% for a reward.>
	end

