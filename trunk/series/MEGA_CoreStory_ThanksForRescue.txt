%%
%% *CS_ThanksForRescue Content
%%	&FromCSEnemy	You rescued the NPC directly from the core story enemy NPC
%%
%% The PC has rescued the NPC. Hooray.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The one who was rescued
%%

%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************

Content
	name <I need someone like you>
	desc <The NPC will offer the PC a job.>
	requires <*CS_ThanksForRescue (+P--|+Pun) +C-- (1:MILIT|1:POLIT|1:POLIC|1:CORPO) -1:ENEMY P:-->

	% E1 is the NPC who was rescued
	% E2 is a local building allied with the NPC
	Element2 <Scene Building Public !Ally 1 !Near -7>

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	SubPlot1 <*CS_DelayedVictory>

	sub
		Persona 1
		% v%id%01 = React bonus counter
		Greeting <if= PlotStatus %plotid% %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*ThanksForRescue .%id%_GoReveal>
		.%id%_GoReveal <if# PCFac ChatNPCFac else .%id%_GoOrder if# 0 ChatNPCFac else .%id%_GoOrder NewChat Say %id%01 AddChat %id%01 AddChat %id%02 if= V%id%01 0 V= %id%01 1 AddReact 10>
		.%id%_GoOrder <NewChat Say %id%06 SMemo %id%03 &SetTargetNPC %1%  AlterContext .%id%_Changes  &SetCompScene %2% SetPlotStatus %plotid% %id1%  FacXP+ ChatNPCFac 5>
		result%id%01 <NewChat Say %id%02 SMemo %id%03 History %id%04 XPV 100 &SetTargetNPC %1% SetPCFac ChatNPCFac &SetAllyFac ChatNPCFac AlterContext .%id%_Changes2 &SetCompScene %2% SetPlotStatus %plotid% %id1%>
		result%id%02 <NewChat Say %id%05 SMemo %id%03 &SetTargetNPC %1%  AlterContext .%id%_Changes  SetPlotStatus %plotid% %id1%  ifG ChatNPCHeroism -1 PCFriend %1%>
		.%id%_Changes <+Tgt +Fmi>
		.%id%_Changes2 <+Tgt +Fmi +Cro>
		Msg%id%01 <I want you to know that \FACTION ChatNPCFac could use you on our side. I'd like for you to come and work for us.>
		Msg%id%02 <Welcome to the team. Come meet me at %name2% in a couple of hours; I'll have a mission for you then.>
		Msg%id%03 <%name1% told you to meet \OPR %1% at %name2% later on.>
		Msg%id%04 <After rescuing %name1%, \SPR %1% offered you a job with \FACTION ChatNPCFac .>
		Msg%id%05 <I'm sorry to hear that. Still, if you're looking for work, come meet me at %name2% in a few hours and I'll have a mission you can have.>
		Msg%id%06 <I have a feeling that we've only just seen the beginning of this. Come meet me at %name2% in a few hours and I'll have a mission for you.>
		Prompt%id%01 <You're offering me a job? Sweet.>
		Prompt%id%02 <Sorry, but I'd prefer to remain independant.>
	end

Content
	name <The Treasure Hunter>
	desc <The NPC you rescued was looking for treasure.>
	requires <*CS_ThanksForRescue (+P--|+Pun) -1:ENEMY -1:FRIEND ~+Gkn>

	% E1 is the NPC being sought
	% E2 is the artifact that E1 was seeking
	element2 <Artifact Item PreZero>

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		Greeting <if= PlotStatus %plotid% %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*ThanksForRescue .%id%_GoReveal>
		.%id%_GoReveal <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoWin <StoryNote %id%03 &SetTargetItem %2% FreezeItem %2% NTBWinComp 0 .%id%_next>
		.%id%_next <+Pla +T-- +F-->
		result%id%01 <NewChat Say %id%02 AddChat %id%03 AddChat %id%04 Goto .%id%_GoWin>
		result%id%02 <NewChat Say %id%04 AddEasygoing -d8   AddChat %id%03 AddChat %id%04 Goto .%id%_GoWin>
		result%id%03 <NewChat Say %id%05 AddChat %id%05>
		result%id%04 <NewChat Say %id%06 AddChat %id%05>
		result%id%05 <NewChat Say %id%07>
		Msg%id%01 <If it wasn't for you, I might not even be here right now. I'm just glad that it's over.>
		Msg%id%02 <They're probably after me because they think I know where the %name2% is. \ITEM_DESC %2% I'm looking for it, you see, but I haven't found anything yet.>
		Msg%id%03 <You learned about the %name2% from %name1%.>
		Msg%id%04 <That's the problem, you see. Right now I'm searching for the %name2%. \ITEM_DESC %2% Once I find it I'll have plenty of cash, but until that time I'm frankly broke.>
		Msg%id%05 <\ITEM_HISTORY %2% I thought there'd be some clue as to its location around here.>
		Msg%id%06 <The %name2% is worth more than just money. \ITEM_USAGE %2%>
		Msg%id%07 <I had planned on continuing my search, but this brush with death has made me reconsider. I just might take up chess.>
		Prompt%id%01 <Do you know why they were after you?>
		Prompt%id%01_1 <Any idea why those guys were after you?>
		Prompt%id%02 <You should give me some kind of reward, then.>
		Prompt%id%02_1 <How about a reward for my effort, then?>
		Prompt%id%03 <What do you know about this %name2%?>
		Prompt%id%03_1 <What can you tell me about the %name2%?>
		Prompt%id%04 <That must be pretty valuable.>
		Prompt%id%04_1 <Sounds like it must be valuable.>
		Prompt%id%05 <What are you going to do now?>
	end

%%  *****************************************
%%  ***   +Pme	PC meets an enemy pilot   ***
%%  *****************************************


%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************


%%  ***********************************************************
%%  ***   +Psh	PC is shanghaied into working for faction   ***
%%  ***********************************************************


%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************


%%  ****************************************
%%  ***   +Pew  Enemy  Weapon  Program   ***
%%  ****************************************

Content
	name <They're Kidnapping Scientists>
	desc <The enemy faction has been kidnapping scientists.>
	requires <*CS_ThanksForRescue +Pew (T:ACADE|T:CORPO) (F:AEGIS|F:CRIME)>

	% E1 is the character who's been rescued
	% E2 is an environs scene
	% E3 is the encounter for the secret research base
	% E4 is the enemy faction
	Element2 <Scene Environs !Near -7>
	Element3 <Prefab>
	Place3 <2>
	Element4 <Grab 2>

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	% SubPlot1 is the base metascene
	% SubPlot2 is the activation subplot
	SubPlot1 <*CS_RaidEnemyBase&Research 3>
	SubPlot2 <*CS_RevealEncounter&EnemyBase&Research 3 2>

	sub
		Persona 1
		Greeting <if= PlotStatus %plotid% %id% else %pop% goto .%id%_GoThanks>
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoTell>
		.%id%_GoTell <NewChat Say %id%01 SMemo %id%02 History %id%03 AddChat %id%01 AddChat %id%02 SetPlotStatus %plotid% %id2%>
		result%id%01 <NewChat Say %id%04>
		result%id%02 <NewChat Say %id%05>
		Msg%id%01 <I had heard rumors that %name4% are kidnapping scientists, but I never imagined that I'd get first hand confirmation of this fact. They must have a research base nearby.>
		Msg%id%02 <You learned from %name1% that %name4% is kidnapping scientists to aid in their weapons program. They apparently have a research base nearby.>
		Msg%id%03 <You learned from %name1% that %name4% was kidnapping scientists.>
		Msg%id%04 <No, sorry, I don't... though I'm sure they can't be running an operation like this in perfect secrecy.>
		Msg%id%05 <Supposedly they're working on a new weapons program. I guess they need more researchers... or test subjects.>
		Prompt%id%01 <Any idea where that base might be?>
		Prompt%id%02 <Why are they doing this?>
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <Secret Research Base>
	end

%%  *********************************
%%  ***   +C--  Nothing Special   ***
%%  *********************************

Content
	name <You can be my bodyguard>
	desc <The NPC will ask the PC to guard him.>
	requires <*CS_ThanksForRescue +C-- -1:ENEMY R:-- ~+Gkn ~1:MEDIA ~1:FAITH ~1:ACADE>

	% E1 is the character who's been rescued
	% P%id%01 = Initialization Counter
	update <if= PlotStatus %plotid% %id% else %pop% if= P%id%01 0 P= %id%01 1 NPCLevel %1% StoryDL>
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		Greeting <if= PlotStatus %plotid% %id% else %pop% goto .%id%_GoThanks>
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoAsk>
		.%id%_GoAsk <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		result%id%01 <NewChat Say %id%02 StoryNote %id%03 &SetTargetNPC 0 &SetPartnerNPC %1% &AddPartnerToLance SetXXRAttitude %1% XXR_A_HasSecret WinComp 0 .%id%_Join>
		.%id%_Join <+T-- +F-->
		result%id%02 <NewChat Say %id%04 AddChat %id%03 AddCHat %id%04>
		result%id%03 <NewChat Say %id%05 StoryNote %id%06 &SetTargetNPC 0 &SetAmoreNPC %1% WinComp 0 .%id%_Love>
		.%id%_Love <+T-- +F-->
		result%id%04 <NewChat Say %id%07 StoryNote %id%08  CashPrize Reward StoryDL 100  WinComp 0 .%id%_Pass>
		.%id%_Pass <+T-- +F-->
		Msg%id%01 <I've learned how dangerous the world is, but there are certain things that I have to do... Say, how would you like to be my bodyguard? I can't afford to pay you much, but you can share in the adventure!>
		Msg%id%02 <That's great! I'll let you know when there's something I need to do. Until then, I'll help you out as well as I can.>
		Msg%id%03 <%name1% asked you to be \PPR %1% bodyguard, and you agreed.>
		Msg%id%04 <That's too bad. I don't know if I'll be able to do this by myself... well, I guess this is the last time we'll see each other.>
		Msg%id%05 <Really? Me too... I'd like that a lot.>
		Msg%id%06 <%name1% asked you to be \PPR %1% bodyguard.>
		Msg%id%07 <Well, at least I can give you this small reward for saving me once. I'll see you around, \PC .>
		Msg%id%08 <%name1% asked you to be \PPR %1% bodyguard, but you declined.>
		Prompt%id%01 <Sure. I'd like that.>
		Prompt%id%02 <Sorry, but I have my own problems.>
		Prompt%id%02_1 <Coming with me would be too dangerous.>
		Prompt%id%03 <Actually, I'd like to see more of you.>
		CPrompt%id%03 <if= 0 &AmoreNPC ifChatNPCSexy Accept>
		Prompt%id%04 <Probably.>
	end

%%  *******************
%%  ***   GENERAL   ***
%%  *******************

Content
	name <I'll tell you a secret>
	desc <In thanks for rescuing him, the NPC will reveal a secret.>
	requires <*CS_ThanksForRescue ~+Gkn ~C:POLIT ~C:MEDIA ~C:ACADE>

	% E1 is the character who's been rescued

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	% SubPlot1 is the secret itself
	SubPlot1 <*CS_Revelation&Friendly 1>

	sub
		Persona 1
		Greeting <if= PlotStatus %plotid% %id% else %pop% goto .%id%_GoThanks>
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoReward>
		.%id%_GoReward <NewChat Say %id%01 AddChat %id%01>
		result%id%01 <Goto .%id1%_GoInit>
		Msg%id%01 <In thanks for your help, I've got some information that you might find useful.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable Accept>
		Msg%id%01_2 <>
		CMsg%id%01_2 <ifNPCShy Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCEasygoing Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCPassionate Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful Accept>
		Msg%id%01_6 <>
		CMsg%id%01_6 <ifNPCMelancholy Accept>
		Prompt%id%01 <Let me hear it.>
		Prompt%id%01_1 <Great. What is it?>
		Prompt%id%01_2 <Information is always handy.>
	end
Content
	name <Let Me Join You>
	desc <The NPC wants revenge against the enemy character, himself.>
	requires <*CS_ThanksForRescue &FromCSEnemy R:-->

	% E1 is the character who's been rescued

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		Greeting <if= PlotStatus %plotid% %id% else %pop% NTBWinComp 0 .%id%_Next goto .%id%_GoThanks>
		.%id%_Next <+T-- +F-->
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoReward>
		.%id%_GoReward <ifG ChatNPCRelationship NAV_Friend else .%id%_GoJoin  Goto .%id%_GoOffer>
		.%id%_GoJoin <PCAlly %1%  Goto .%id%_GoOffer>
		.%id%_GoOffer <NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03>

		result%id%01 <&SetPartnerNPC %1%  &AddPartnerToLance  StoryNote %id%02  Goto .%id%_GoWelcome>
		*.%id%_GoWelcome <*WelcomeToLance>
		result%id%02 <NewChat Say %id%03>
		result%id%03 <NewChat Say %id%03 if= ChatNPCRelationship NAV_ArchAlly  ifChatNPCSexy  ifFlirtation SkillTar StoryDL PCLover %1%>

		Msg%id%01 <If it's alright with you, I'd like to join your lance. It's the least I could do to repay you.>
		Msg%id%01_1 <>
		CMsg%id%01_1 <ifNPCSociable ifNPCOK &EnemyNPC Accept>
		Msg%id%01_2 <I want to come with you. I could repay you for saving me, and get my revenge against \PERSONA &EnemyNPC at the same time.>
		CMsg%id%01_2 <ifNPCShy ifNPCOK &EnemyNPC Accept>
		Msg%id%01_3 <>
		CMsg%id%01_3 <ifNPCEasygoing ifNPCOK &EnemyNPC Accept>
		Msg%id%01_4 <>
		CMsg%id%01_4 <ifNPCPassionate ifNPCOK &EnemyNPC Accept>
		Msg%id%01_5 <>
		CMsg%id%01_5 <ifNPCCheerful ifNPCOK &EnemyNPC Accept>
		Msg%id%01_6 <Let me join your lance... I must have revenge against \PERSONA &EnemyNPC .>
		CMsg%id%01_6 <ifNPCMelancholy ifNPCOK &EnemyNPC Accept>

		Msg%id%02_1 <%name1% joined your lance to help fight \PERSONA &EnemyNPC .>
		CMsg%id%02_1 <ifNPCOK &EnemyNPC Accept>
		Msg%id%02_2 <%name1% joined your lance.>
		CMsg%id%02_2 <ifNPCDead &EnemyNPC Accept>
		Msg%id%03 <Always remember, I'll be right here if you need me.>
		Msg%id%03_1 <>
		Msg%id%03_2 <>
		Prompt%id%01 <I'd love to have you at my side.>
		CPrompt%id%01 <ifChatNPCCanJoinLance Accept>
		Prompt%id%01_1 <Alright, come along.>
		Prompt%id%02 <Maybe some other time.>
		Prompt%id%02_1 <Sorry, but now's not a good time.>
		Prompt%id%03 <I think you'd be safer here.>
		Prompt%id%03_1 <It's too dangerous to come with me.>
	end

Content
	name <Rescued an Ally>
	desc <The PC has just rescued an ally, who will suggest that he go speak with another member of the faction.>
	requires <*CS_ThanksForRescue 1:Ally -1:PDASS>

	% E1 is the character who's been rescued
	% E2 is a public scene in this city, to be used as home scene if a
	%    pre-existing E4 can't be found.
	% E3 is another character allied with E2
	element2 <Scene Public !Okay -3 Building !Near -7>
	element3 <Character !Comrade 1 !Near 2 Neverfail>
	NeverFail3 <Mecha Pilot>

	% P1 = Initialization Counter
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		% V1 = Can only get reward once
		Greeting <if= PlotStatus %plotid% %id% else %pop% goto .%id%_GoThanks>
		*.%id%_GoThanks <*ThanksForRescue .%id%_GoReward>
		.%id%_GoReward <if= V%id%01 0 else .%id%_GoBriefing V= %id%01 1 CashPrize Reward StoryDL 200 NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoBriefing <ifNPCOK %3% else .%id%_GoE3Dead NewChat Say %id%02 StoryNote %id%03 &SetTargetNPC %3%  WinComp NPCScene %3% .%id%_changes>
		.%id%_changes <+Tgt +Fmi>
		.%id%_GoE3Dead <NewChat Say %id%05 SMemo 0 NTBWinComp 0 .%id%_changes2>
		result%id%01 <Goto .%id%_GoBriefing>
		result%id%02 <if= ChatNPCFac PCFac else .%id%_GoR2Reject if# PCFac 0 else .%id%_GoR2Reject NewChat Say %id%04 AddChat %id%03>
		*.%id%_GoR2Reject <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <NTBWinComp 0 .%id%_changes2>
		.%id%_changes2 <+T-- +F-->
		result%id%03 <Goto .%id%_GoBriefing>
		Msg%id%01 <Here's a reward for your quick thinking. This matter needs to be dealt with quickly, before it gets out of hand. We could use someone like you on our side.>
		Msg%id%02 <Go speak with %name3% at \Scene NPCScene %3% . I think \SPR %3% 'll have a mission you can help with.>
		Msg%id%03 <%name1% sent you to speak with %name3% at \SCENE NPCScene %3% .>
		Msg%id%04 <This is now official \FACTION ChatNPCFac business. All your other missions can wait; this is top priority.>
		Msg%id%05 <With %name3% dead, all our operatives will have to be extra careful. Stay on your toes for the next little while.>
		Prompt%id%01 <Tell me what you need.>
		Prompt%id%02 <Forget it, I'm finished here.>
		Prompt%id%03 <Understood. Tell me what I have to do.>
		Prompt%id%03_1 <Yeah, I'll be a good little quest-bitch...>
		CPrompt%id%03_1 <ifPCPassionate ifPCMelancholy Accept>
	end


Content
	name <Thanks for the Help>
	desc <The PC has rescued someone, who now wants to get connected to the core story.>
	requires <*CS_ThanksForRescue -1:Ally -1:Enemy ~+Glo A:-- R:-->

	%% E1 is the NPC that the PC rescued

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .loss>
	.loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop% Goto .%id%_GoStart>
		*.%id%_GoStart <*ThanksForRescue .%id%_GoOffer>
		.%id%_GoOffer <PCFriend %1% ifG React d10 else .%id%_GoNoOffer ifG PCCharm 10 else .%id%_GoNoOffer NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		.%id%_GoNoOffer <ifChatNPCSexy else .%id%_GoCashAward ifFlirtation SkillTar StoryDL else .%id%_GoCashAward ifG React d10 else .%id%_GoCashAward NewChat Say %id%08 AddChat %id%06 AddChat %id%07>
		.%id%_GoCashAward <NewChat Say %id%07  StoryNote %id%05 CashPrize Reward StoryDL 150 NTBWinComp 0 .%id%_next>
		result%id%01 <NewChat Say %id%02 StoryNote %id%03 PCAlly %1% &SetPartnerNPC %1% &AddPartnerToLance WinComp 0 .%id%_next>
		.%id%_next <+T-- +F-->
		result%id%02 <NewChat Say %id%04 StoryNote %id%05 CashPrize Reward StoryDL 150 NTBWinComp 0 .%id%_next>
		result%id%03 <NewChat Say %id%06 AddChat %id%04 AddChat %id%05>
		result%id%04 <Goto result%id%01>
		result%id%05 <Goto result%id%02>
		result%id%06 <NewChat Say %id%09 StoryNote %id%010 &SetAmoreNPC %1% WinComp 0 .%id%_date>
		.%id%_date <+T-- +F-->
		result%id%07 <Goto result%id%02>
		Msg%id%01 <I thought that to repay you for saving my life, I could join your team and maybe help out. What do you think?>
		Msg%id%02 <Thanks! I won't let you down!>
		Msg%id%03 <%name1% joined your lance in thanks for saving \PPR %1% life.>
		Msg%id%04 <I understand. Well, the least I can do is give you some money. Thanks for your help. I won't forget it.>
		Msg%id%05 <%name1% rewarded you for saving \PPR %1% life.>
		Msg%id%06 <I know, but... I have other talents that could be useful. And you could teach me to fight. Can't I come along with you, please?>
		Msg%id%07 <In thanks for saving my life, I'd like you to have this money.>
		Msg%id%08 <In thanks for saving my life, I'd like... honestly, I'd like to go out with you sometime.>
		Msg%id%09 <Great! Well, I'll see you around.>
		Msg%id%010 <After saving \PPR %1% life, %name1% asked for a date sometime.>
		Prompt%id%01 <I think it's a good idea.>
		Prompt%id%01_1 <I'd be honored to have you join me.>
		Prompt%id%02 <No thanks. I don't need any help.>
		Prompt%id%02_1 <Sorry, I'm not looking for another lancemate.>
		Prompt%id%03 <Thanks, but aren't you a bit useless?>
		CPrompt%id%03 <if= NPCSkill E2 10 0 Accept>
		Prompt%id%03_1 <Thanks, but you aren't much of a fighter.>
		Prompt%id%04 <Alright. You can join me.>
		Prompt%id%05 <I don't have time to look after you.>
		Prompt%id%06 <I'd like that too.>
		Prompt%id%07 <This isn't a good time for that.>
	end

