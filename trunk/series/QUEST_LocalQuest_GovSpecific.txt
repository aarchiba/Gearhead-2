%%
%%  *:LocalQuest Content
%%
%% The local quests in this file are tied specifically to a certain
%% government, hence a specific region in the game world.

Content
	name <Revenge Road>
	desc <A Crihna pirate seeks revenge against the Rishiri Dominion.>
	requires <*:LocalQuest RISHI -!Ne>

	% Plot breakdown: There are pirates in this colony. Defeat all but one
	% pirate to reveal the mothership. Enter the mothership to speak with the
	% pirate captain, who wants bloody revenge against Rishiri for the
	% destruction of Crihna Rock. The player can defeat this pirate or
	% agree to help his plans.

	% E1 is the pirate
	% E2 is the entrance to the pirate ship
	% E3 is the initial pirate encounter
	Element1 <Prefab>
	Scene1 <New *BLANK.NPCLair>
	Team1 <Guards>
	TeamData1 <SD>

	Element2 <Prefab>
	Scene2 <Environs>

	Element3 <Prefab>
	Scene3 <Grab 2>

	% SubQuest1 is the victory if the PC defeats the pirate
	% SubQuest2 is the mission the pirate might offer to the PC
	% SubQuest3 is the victory if the PC completes SQ2
	quest1 <*:Vanquish.Criminal 1>
	quest2 <*:GiveMission 1>
	quest3 <*Success1.FinishMission 1>

	sub
		Persona 1
		rumor0 <there's a group of Crihna pirates nearby, and they're planning trouble.>
		% V1 = Have earned reaction bonus
		% V2 = Reputation counter
		special <Unlisted>
		greeting <if= QuestStatus %qid2% -1 else GoCheckLoss EndChat Say 1 AddChat 1>
		GoCheckLoss <if= QuestStatus %qid2% -2 else GoCheckFirst SetQuestStatus %qid% -2 Goto GoEndInLoss>
		*GoEndInLoss <*MissionWasFailure na na na>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat ifChatNPCSurrendered else GoGreet NewChat Say 2 AddChat 2 AddChat 3>
		GoGreet <NewChat Say 3 AddChat 4 AddChat 5 AddChat 6 AddChat 7>
		GoChat <NewChat SayAnything>
		result1 <Goto .%id3%_GoInit>
		result2 <EndChat Say 4 AddLawful 1 ReAttack %1%>
		result3 <EndChat Say 5 AddHeroic 1 FreezeNPC %1% PCAlly %1% SetQuestStatus %qid% -1 SetQuestStatus %qid1% %id1% History 13>
		result4 <EndChat Say 6 TeamAttack 2>
		result5 <EndChat Say 7 AddChat 8>
		result6 <NewChat Say 8 AddChat 9 if= V1 0 V= 1 1 AddReact 15>
		result7 <NewChat Say 9 AddChat 10 AddChat 11 if= V2 0 V= 2 1 AddEasygoing d10>
		result8 <NewChat Say 10 AddChat 10 AddChat 11>
		result9 <NewChat Say 11 AddChat 12>
		result10 <SetQEncActive %2% SetQuestStatus %qid2% %id2% SetQuestStatus %qid% %id% Goto .%id2%_GoInit>
		result11 <EndChat Say 12 TeamAttack 2>
		result12 <Goto result10>
		Msg1 <So, you did it. Good. You'll be a bloody fine pirate yet.>
		Msg2 <You've bested me... I can't ask for mercy, not from Rishiri, and I sure wouldn't give it myself. Let's just get this over quickly.>
		Msg3 <You've made it to my ship, that means you're tough. I suppose you must have some reason for coming to see me.>
		Msg4 <To the death, then!>
		Msg5 <Now there's an offer I can't refuse. I leave you with some advice- be very careful, lest one of these days your merciful nature comes back to bite you in the ass.>
		Msg6 <That's the same reason I'm here- to bring the Dominion to justice for what they did to Crihna Rock! If you stand by those murderers, then you can die alongside them as well!>
		Msg7 <I'm here on a mission of revenge. Thirty years ago Rishiri destroyed Crihna Rock. They said they were gunning for pirates, but they killed everyone nice and equal.>
		Msg8 <Well, how were we supposed to know that? Sure, we could go by the colors of your mecha, but you know what notorious liars us pirates are. Anyhow, it's good to meet you. I'm Captain %name1%.>
		Msg9 <Is that so? Well, if you're up for a bit of piracy, I've got a mission you could help me with.>
		Msg10 <Now, I don't believe in justice, but I do believe in revenge. The dogs of Rishiri must be brought to task for what they've done. If you're with me on that, I have a mission you could do.>
		Msg11 <Splendid. As long as you're here, I've got a bit of a mission you could help out on.>
		Msg12 <Such a shame. Well, I trust you'll understand that I can't just have you barge in here and walk out again alive. It wouldn't be fit by the pirate code.>
		Msg13 <You spared the life of pirate captain %name1%.>
		Prompt1 <[Continue]>
		Prompt2 <Ask and you shall receive.>
		Prompt3 <Leave this city now, and I'll let you live.>
		Prompt4 <I'm here to bring you to justice.>
		CPrompt4 <if# PCFac ChatNPCFac Accept>
		Prompt5 <I'm wondering what brought you here, actually.>
		Prompt6 <Why did you attack me? I'm a Blade too!>
		CPrompt6 <if= PCFac ChatNPCFac Accept>
		Prompt7 <Nope. No reason.>
		Prompt8 <[Continue]>
		Prompt9 <I'm \rank \PC .>
		Prompt10 <I'd like that.>
		CPrompt10 <ifFactionEnemy ChatNPCFac else GoC10Accept>
		GoC10Accept <Accept>
		Prompt11 <No way. Piracy is bad.>
		CPrompt11 <if# PCFac ChatNPCFac Accept>
		Prompt12 <Give me the details.>

		MetaScene 1
		% L1 = Initialization
		% L2 = Reward counter
		start <if= L1 0 else GoBeenBefore Alert 1 Monologue %1% 3 L= 1 1 WMonster 2 %threat% 100>
		GoBeenBefore <Print 2>
		Faint%1% <if= QuestStatus %qid% 0 if= L2 0 L= 2 1 XPV 100 History 4>
		Msg1 <As you enter the cramped hold of the pirate spaceship, the ship's captain greets you with a steely glare.>
		Msg2 <You enter \SCENE SceneID .>
		Msg3 <You've made it this far. I won't refuse you the right of parley.>
		Msg4 <You killed the pirate captain %name1%.>

		sub
			Team 1

			Team 2
			name <Guards>
			type <Criminal Robot>
		end
	end
	inv
		NPC Pirate
		SetFaction 8
		job_desig <THIEF>

		STC QUEST-MAPMARKER-STATIONARY-DEFENDED
		% V11 = Appearance Time Limit
		%   To make this encounter perminantly visible, use SetQEncVisible.
		name <Crihna Ship>
		desig <ENTRANCE %s1%>
		update <ifG V11 ComTime else GoCheckPerm  SetStat STAT_MetaVisibility 0  ShowEncounter  Goto GoSetOrders>
		GoCheckPerm <ifThisQEncActive else GoHide SetStat STAT_MetaVisibility 0  ShowEncounter  Goto GoSetOrders>
		SetFaction 8

		STC DYNAMICENCOUNTER-DAILY-WANDER
		name <Crihna Raiders>
		SetFaction 8
		update <if= QuestStatus %qid% 0 else GoDelete ifG ComTime V1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		GoDelete <DeleteSelf>
		GoStartCombat <SavePos Dynamic 2 %threat% 100 .nu1 .nu2 ComposeD start .start ComposeD Msg1 .Msg1 ComposeD Msg3 .Msg3 ComposeD Msg2 .Msg2 ComposeD GoCheckOne .GoCheckOne DynaVar= 1 %3% DynaVar= 11 %2% DynaFaction SelfFac V= 1 ComTime V+ 1 86400>
		.nu1 <if= T1 0  Return   if= L2 0  L= 2 1  AddRenown -5>
		.nu2 <if= T2 0 else GoCheckOne           if= L2 0  L= 2 1  XPV 100   Alert 1>
		.start <if= T2 1 Alert 3  Retreat 2 ItemVar= L11 11 ComTime ItemVar+ L11 11 21600>
		.GoCheckOne <if= T2 1 Alert 2 Retreat 2 ItemVar= L11 11 ComTime ItemVar+ L11 11 21600>
		.msg1 <You have defeated the mecha.>
		.msg2 <The last surviving raider turns and flees for a spaceship in the distance.>
		.msg3 <The lone raider facing you turns and flees for a spaceship in the distance.>
	end

