Plot
	name <Conference Was Moved>
	requires <#W +Tgs (+Fpa|+Fcc)>
	desc <The event has been moved. No-one told the PC.>

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the town for the next episode
	% E4 is a metascene within that town
	element1 <Grab 6>
	element2 <Character Citizen !Near -7>
	Place2 <1 (Citizens) pass>
	Element3 <Scene !Far -7 !Comrade -7 Town>
	element4 <MetaScene !Near 3 *Building NeverFail>

	start <ifNPCDead %1% &SetCompScene 0 SMemo 0 WinEpisode %3% 0 .next>
	.next <+T-- +F-->

	sub
		Persona 2
		special <UNLISTED>
		rumor <%name2% is the head of the cleaning crew at %name1%.>
		greeting <NewChat Say %id%01 SMemo %id%02 History %id%03 &SetCompScene 0 WinEpisode %3% %4% .next>
		.next <>
		Msg%id%01 <The event they were going to have here has been moved to %name4% in %name3%. Unless you're a member of the cleaning crew, I'm afraid that you'll have to go home.>
		Msg%id%02 <The event at %name1% was moved to %name4% in %name3%.>
		Msg%id%03 <The event at %name1% was moved to %name3%.>

		MetaScene 1
		desig <MS_PRESENTATION>
		%% This is a press conference or similar media event
		ClubMap
		MapWidth 25
		MapHeight 25
		type <BUILDING PUBLIC>
		special <AddExit>
		nu1 <if= T1 0 Return>
		sub
			Team 1

			Team 2
			name <Civilian>
			setally 1
			Passive

			room
			name <Foyer>
			desig <EntranceGrid>
		end
	end

Plot
	name <Should Go Get Job>
	requires <#W +T-- P:++ ~+Gmo>
	desc <You've completed this mission. Now you can socialize.>

	% E1 is an ally of the target faction
	% E2 is the next scene to visit
	Element1 <Character !Ally -3 !Far -7>
	Element2 <Scene !Near 1 !Okay -7 Town>

	start <ifScene .desc if= P1 0 P= 1 1 Alert 1 Alert 2 SMemo 3 History 4 &SetTargetNPC %1%  WinEpisode %2% 0 .next>
	.desc <Outdoors>
	.next <+Tgt +Fmi>
	Msg1 <Things appear to be resolved in \SCENE &EpisodeScene , for the time being.>
	Msg2 <With nothing else to do here, you might as well head over to %name2% to see if %name1% has any work for you.>
	Msg3 <You are going to visit %name1% in %name2% to see if \SPR %1% has a mission for you.>
	Msg4 <You decided to go look for work in %name2%.>

Plot
	name <Should Go See Ally>
	requires <#W +T-->
	desc <You've completed this mission. Now you can socialize.>

	% E1 is your ally
	% E2 is the next scene to visit
	Element1 <Character PCFac !Far -7>
	Element2 <Scene !Near 1 !Okay -7 Town>

	start <if= SceneID &EpisodeScene if= P1 0 P= 1 1 Alert 1 Alert 2 SMemo 3 History 4 &SetTargetNPC %1% ifG d100 RReact %1% else GoSocial WinEpisode %2% 0 .next>
	.next <+Tgt +Fmi>
	GoSocial <WinEpisode %2% 0 .social>
	.next <+Tgt +F-->
	Msg1 <Things appear to be resolved in \SCENE &EpisodeScene , for the time being.>
	Msg2 <Perhaps %name1% of \FACTION PCFac will be able to shed some light on this situation. You decide to visit %name2% and find out.>
	Msg3 <You are going to visit your ally %name1% in %name2%.>
	Msg4 <You decided to visit your ally %name1% in %name2%.>

Plot
	name <Should Go See Friend>
	requires <#W +T-->
	desc <You've completed this mission. Now you can socialize.>

	% E1 is the next scene to visit
	% E2 is a public building in that city
	% E3 is a friend who lives there
	Element1 <Scene !Far -7 !Comrade -7 Town>
	Element2 <Scene Building Public !Near 1>
	Element3 <NewNPC 0 2 PCFriend>
	Place3 <2>

	start <if= SceneID &EpisodeScene if= P1 0 P= 1 1 Alert 1 Alert 2 SMemo 3 History 4 &SetTargetNPC %3% WinEpisode %1% 0 .next>
	.next <+Tgt +F-->
	Msg1 <Things appear to be resolved in \SCENE &EpisodeScene , for the time being.>
	Msg2 <While you have some free time, you feel that you should head to %name1% and visit your friend %name3%.>
	Msg3 <You are going to visit your friend %name3% in %name1%.>
	Msg4 <You decided to visit your friend %name3% in %name1%.>

Plot
	name <Enemy Has Moved>
	requires <#W +Tgs E:++>
	desc <You will learn that your enemy has gone to another city.>

	% E1 is the scene you were to visit
	% E2 is a citizen who can be found there
	% E3 is the next city to visit
	% E4 is the enemy NPC
	Element1 <Grab 6>
	Element2 <Character Citizen !Near 1 NeverFail>
	Place2 <1 (Citizens) Ally Pass>
	NeverFail2 <Citizen>
	Element3 <Scene !Far -7 !Comrade -7 Town>
	Element4 <Grab 1>

	% V1 = Lancemate Hint
	start <ifNPCDead %2% else GoCheckPartner LoseComp 0 na>
	GoCheckPartner <ifNPCOK &PartnerNPC if= SceneID %1% if= V1 0 Monologue &PartnerNPC 1 V= 1 1>
	Msg1 <Look at %name2%... I have a hunch that \SPR %2% might know something.>

	sub
		Persona 2
		rumor <%name2% has seen %name4%.>
		Greeting <NewChat Say 1 StoryNote 2 WinEpisode %3% 0 .next>
		.next <+T-- +F-->
		Msg1 <You missed all the excitement. A little while ago %name4% came through here, and it was terrifying! Fortunately, \SPR %4% 's gone to %name3% now.>
		Msg2 <You learned from %name2% that %name4% went to %name3%.>

		STC MS_CAFE
		SetID 1
	end

Plot
	name <Call From HQ>
	requires <#W +T-- P:ALLY R:PCFAC>
	desc <Your partner gets a call from HQ to proceed there for a mission.>

	% E1 is the partner
	% E2 is a faction member far away
	% E3 is the city where E2 is
	% E4 is the scene for the meeting
	Element1 <Grab 4>
	Element2 <Character !Ally 1 !Far -7>
	Element3 <Scene Town !Near 2 !Okay 1>
	Element4 <Scene Building Public !Near 3 !Okay 1>

	% V1 = Conversation Counter
	start <ifNPCDead %2% LoseComp 0 na>

	5Min <if= v1 0 ifSafeArea if= d4 1 ForceChat %1% V= 1 1>

	sub
		Persona 1
		*Greeting <*LancemateWantsToTalk GoNow GoLater>
		GoNow <NewChat Say %id%01 Goto GoEndEpisode>
		GoLater <NewChat Say %id%04 Goto GoEndEpisode>
		GoEndEpisode <SMemo %id%02 History %id%03 &SetTargetNPC %2% WinEpisode %3% %4% .next>
		.next <+Tgt +Fmi>
		Msg%id%01 <I've been contacted by %name2% at %name3%. We've got a mission to do for \FACTION ChatNPCFac over there.>
		Msg%id%02 <%name1% told you that \SPR %1% had been contacted by %name2%. You've been asked to go to %name3% to do a mission for \FACTION ChatNPCFac .>
		Msg%id%03 <You were called to %name3% to do a mission for \FACTION ChatNPCFac .>
		Msg%id%04 <This won't take long. I've been contacted by %name2% at %name3%. We've got a mission to do for \FACTION ChatNPCFac .>
	end

Plot
	name <Thanks for the Help>
	requires <#W +Pun +T-- (F:--|F:CRIME|F:MILIT) (P:--|P:MILIT|P:POLIC) -L:ENEMY ~L:ALLY>
	desc <A local politician will personally thank the PC.>

	% E1 is a public scene for a meeting
	% E2 is the politician
	% E3 is the new city
	Element1 <Scene Building Public !Near -7 !Ally -7>
	Element2 <Character POLIT !Near -7 !Ally -7 -ENEMY>
	Place2 <1 (Citizens) ally pass>
	Element3 <Scene !Far -7 !Comrade -7 Town>

	% P1 = email indicator
	start <ifNPCDead %2% LoseComp 0 na>
	hour <if= P1 0 email 1 P= 1 1>

	Msg1 <%name2%@%name1%:// I'd like to commend you on your recent actions, \PC . Come see me at %name1%.>

	sub
		Persona 2
		*greeting <*DidYouGetEmail GoExplain>
		GoExplain <PCFriend %2% NewChat Say 1 StoryNote 2 WinEpisode %3% 0 .next>
		.next <+T-- +F-->
		Msg1 <I'd like to thank you for all you've done for \SCENE RootSceneID . You know, they say that there have been unexplained mecha attacks in %name3% recently, too. Maybe you could help them as well.>
		Msg2 <%name2% told you that %name3% has also been under attack.>
	end

Plot
	name <Target Went Elsewhere>
	requires <#W (+Tgt|+Tht|+Tpt) T:++>
	desc <The PC will be told where the target went.>

	% E1 is the target
	% E2 is the informant
	% E3 is the new city
	Element1 <Grab 5>
	Element2 <Character !Near -7 !Okay 1>
	Element3 <Scene !Far -7 !Comrade -7 Town>

	% v%id%01 = Initialization
	update <if= v%id%01 0 v= %id%01 1 FreezeNPC %1%>
	start <ifNPCDead %2% LoseComp 0 na>

	sub
		Persona 2
		rumor <%name2% was speaking with %name1% recently.>
		*greeting <*LookingForNPC %1% GoSuccess GoFail>
		GoSuccess <NewChat Say 1 StoryNote 2   WinEpisode %3% 0 .next>
		GoFail <NewChat Say 3 StoryNote 2   WinEpisode %3% 0 .next>
		.next <>
		Msg1 <I'm afraid that you've missed \OPR %1% ; \SPR %1% 's just left for %name3%.>
		Msg2 <%name2% told you that %name1% went to %name3%.>
		Msg3 <Well, you're out of luck. %name1% isn't in \SCENE RootSceneID any more... as far as I know, \SPR %1% 's gone to %name3%.>
	end


