%% The first component to be introduced, wherein the PC meets a friend and is
%% told vaguely what's going on in the game world.

Plot
	name <Normal Day>
	Size 0
	desc <You are going to meet your friend.>
	requires <#A +T01 ~P:-- ~C:LABOR ~C:POLIT>

	% E1 is the metascene to be used for the meeting.
	% E2 is the scene where the newly created friend normally resides.
	% E3 is the scene where the "Intro NPC" is
	% E4 is the "Intro NPC"
	% E5 is the friend you're going to meet
	element1 <MetaScene !Near -7 *Building NeverFail>
	element2 <Scene !Near -7 building public>
	element3 <Scene !Near -7 cavclub>
	element4 <Prefab>
	place4 <3 pass>
	element5 <Prefab>
	place5 <2 Pass>

	update <Print 1 ForceChat E4>
	Msg1 <\ELEMENT 4 strikes up a conversation with you.>

	inv
		NPC Citizen
		home <The Start Position>
		PCFriend

		NPC Citizen
		PCFriend
	end
	sub
		Persona 4
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <&SetTargetChar E5   SMemo 2  WinComp E1 .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+Tgt +F-->
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1>
		*GoAdvice <*INTRO_Advice>
		result1 <Goto GoAdvice>
		Msg1 <Oh yeah, I was supposed to deliver a message. \PERSONA E5 is waiting for you at \SCENE E1 .>
		Msg1_1 <I just remembered... I have a message for you. \PERSONA E5 wants to meet at \SCENE E1 later today. I don't know why.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <Oh yeah... \PERSONA E5 asked me to tell you to meet \OPR E5 at \SCENE E1 .>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Did you meet with \PERSONA E5 yet? Oh, wait, I think I forgot to tell you... \OPR E5 's waiting for you at \SCENE E1 .>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <Crap, I almost forgot! \PERSONA E5 wants to meet you today. I was supposed to deliver the message... \SPR E5 'll be at \SCENE E1 .>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <\PERSONA E5 asked me to tell you \OPR E5 wants to meet at \SCENE E1 later on today. Have a good time together.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <I guess you won't be able to hang out with me, today... \ELEMENT 5 is waiting for you at \ELEMENT 1 . I don't know why \SPR E5 wants to meet, or why I wasn't invited too...>
		CMsg1_6 <ifNPCMelancholy Accept>

		Msg2 <You are going to meet your old friend \PERSONA E5 at \SCENE E1 .>

		Prompt1 <Thanks. Do you have anything else to tell me?>
		Prompt1_1 <Is there anything else I should know?>

	end

