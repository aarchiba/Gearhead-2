%% The first component to be introduced, wherein the PC meets a friend and is
%% told vaguely what's going on in the game world.

Plot
	name <Invasion Alert>
	Size 0
	desc <You're informed that the town is under attack.>
	requires <#A +T01 P:-- -L:ENEMY>

	% E1 is the cavclub scene
	% E2 is your friend
	% E3 is the "scapegoat" faction- may or may not actually be the one doing the damage
	element1 <Scene !Near -7 cavclub>
	element2 <NewNPC -3 -7 PCFriend>
	place2 <1 pass>
	element3 <Faction !Enemy -7>

	update <ForceChat %2%>

	sub
		Persona 2
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <StoryNote 2  WinComp 0 .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+T-- +Pun>
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1 AddChat 2 AddChat 3>
		*GoAdvice <*INTRO_Advice>
		result1 <NewChat Say 3 AddEasygoing -d4  AddChat 4>
		result2 <NewChat Say 4 AddChat 4>
		result3 <NewChat Say 5 AddCheerful -d10  AddChat 4>
		result4 <Goto GoAdvice>
		Msg1 <Have you heard about the invaders who've been clashing with \NARRATIVE 7 security? There was a bit about it in the news, but there's not a lot of information yet.>
		Msg1_1 <These days everybody's talking about a group of invaders who have supposedly clashed with \NARRATIVE 7 security . Personally, I don't know if I believe it or not. Did you hear about that?>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <Have you heard anything about the invaders who have supposedly been fighting with the \NARRATIVE 7 guard? Apparently the military is trying to keep a lid on the story.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <I hear that there's been some unauthorized mecha spotted near \NARRATIVE 7 . The security forces are on alert, but it'll probably turn out to be nothing. Did you hear anything about it?>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <Right now \SCENE RootSceneID security has its panties in a bunch over some unauthorized mecha violating our space. So far there hasn't been a lot of info in the news about it. Did you hear anything?>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <All the local pilots are talking about some supposed invaders who have been spotted near the city... That means there's likely to be a lot of jobs available very soon. Did you hear anything about that?>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <Have you heard about the unknown mecha who've attacked our city guard? They could be terrorists, or worse...>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <You learned that mysterious invaders are attacking \NARRATIVE 7 .>
		Msg3 <According to the initial reports it doesn't look like them. I'd be less worried if it was, honestly... At least we know what %name3% wants.>
		Msg3_1 <Maybe. This doesn't seem like their usual mode of operations, but they have done covert ops before. We'll have to wait and see I guess.>
		Msg4 <A lot of the other pilots are talking about it. Some of them have been trying to find these invaders themselves. Personally I think that's a bad idea, but no guts no glory I suppose.>
		Msg4_1 <You really ought to work on your contacts, \PC . Most of your job opportunities will come from rumors. If you don't have anyone to go to for info you'll be left in the cold.>
		Msg5 <You might want to stick around for a while yet. I've got a feeling this situation's going to result in trouble, and whenever there's trouble there's plenty of jobs for pilots.>
		Msg5_1 <Don't be racing off so fast... If there really are hostile mecha out there, that's going to mean lots of jobs for mecha pilots.>
		Prompt1 <It must be %name3% again.>
		Prompt2 <No, I didn't hear about that.>
		Prompt3 <Yet one more reason to leave this place.>
		Prompt4 <Anything else before I head out?>
	end


Plot
	name <Criminal At Large>
	Size 0
	desc <A wanted criminal is on the loose, and the PC intends to capture him.>
	requires <#A +T01 C:POLIC>

	% E1 is the scene where the "Intro NPC" is
	% E2 is the "Intro NPC"
	% E3 is the criminal to be sought
	element1 <Scene !Near -7 cavclub>
	element2 <NewNPC -3 -7 PCFriend>
	place2 <1 pass>
	element3 <NewNPC 0 -7 Criminal>

	update <ForceChat %2%>

	sub
		Persona 2
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <&SetTargetChar %3% FreezeNPC %3%   StoryNote 2  WinComp 0 .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+Tht +Far>
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1>

		*result1 <*INTRO_Advice>
		Msg1 <Did you hear that the wanted outlaw %name3% has been spotted in town? Every bounty hunter in \NARRATIVE 7 is on the lookout.>
		Msg1_1 <I guess you've already heard that %name3% has been spotted in \NARRATIVE 7 . I don't know whether the rumors are true or not, but the police seem to be taking them seriously.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <I've got a tip for you. The wanted fugitive %name3% has been spotted in town.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <This is an exciting time to be in law enforcement. I just heard that a fugitive named %name3% is on the loose in \NARRATIVE 7 .>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <You decided to try and capture the wanted outlaw %name3%.>
		Prompt1 <It's my goal to capture \OPR %3% . Have any advice?>
		Prompt1_1 <I plan to capture \OPR %3% . Any advice for me?>
	end

Plot
	name <You've Got Work>
	Size 0
	desc <You're reminded of a business meeting.>
	requires <#A +T01 P:++ -P:CRM>

	% E1 is the metascene to be used for the meeting.
	% E2 is the associate you're going to meet.
	% E3 is the scene where the "Intro NPC" is
	% E4 is the "Intro NPC"
	% E5 is the PC's faction
	element1 <MetaScene !Near -7 *Building NeverFail>
	element2 <Character !Comrade -3 !Near -7>
	element3 <Scene !Near -7 cavclub>
	element4 <NewNPC -3 -7 PCFriend>
	place4 <3 pass>
	element5 <Grab 3>

	update <ForceChat %4%>

	sub
		Persona 4
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <&SetTargetChar %2%   SMemo 2  WinComp %1% .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+Tgt +Fmi>
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1>
		*GoAdvice <*INTRO_Advice>
		result1 <Goto GoAdvice>
		Msg1 <I know you're very excited about being accepted into %name5% . Are you all ready for your meeting with %name2% at %name1% today?>
		Msg1_1 <Everyone's talking about how great it is that you've been accepted into %name5% . Do you have everything ready for the meeting with %name2% at %name1% later on today?>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <I see. All ready for your big meeting with %name2% at %name1% today?>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Being a member of %name5% must really keep you busy. Are you ready for that big meeting with %name2% at %name1% ?>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <It must be a great honor for you to be a part of %name5% . Are you ready for the meeting with %name2% at %name1% later today?>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <Congratulations on being accepted into %name5% . All ready for the big meeting with %name2% at %name1% ?>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <I guess the work never ends now that you're a member of %name5% . Did you get everything ready for the meeting with %name2% at %name1% later on today?>
		CMsg1_6 <ifNPCMelancholy Accept>

		Msg2 <You have to meet %name2% at %name1% to discuss %name5%.>

		Prompt1 <I'm ready. Have any advice?>
		Prompt1_1 <Yeah. Do you have any advice for me?>

	end


Plot
	name <The First Duel>
	desc <You're going to challenge your rival.>
	requires <#A +T01 (C:ADVEN|+Gpo)>

	% E1 is the metascene to be used for the meeting.
	% E2 is the scene where the newly created arena pilot normally resides.
	% E3 is the scene where the "Intro NPC" is
	% E4 is the "Intro NPC"
	% E5 is the friend you're going to challenge
	element1 <MetaScene !Near -7 *Building NeverFail>
	element2 <Scene !Near -7 building public>
	element3 <Scene !Near -7 cavclub>
	element4 <NewNPC -3 -7 PCFriend>
	place4 <3 pass>
	element5 <NewNPC 0 -7 PCFriend>
	place5 <2 Pass>

	update <ForceChat %4%>

	start <ifNPCDead %4% LoseComp 0 .next>
	.next <+T-- +F-->

	sub
		Persona 4
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <if= V1 0 else GoStoryInfo V= 1 1 Goto GoGreeting>
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1  AddChat 1 AddChat 2>
		*GoAdvice <*INTRO_Advice>
		result1 <NewChat Say 4   AddChat 3   Goto GoAcceptDuel>
		result2 <NewChat Say 5   AddChat 4   Goto GoRejectDuel>
		result3 <Goto GoAdvice>
		result4 <Goto GoAdvice>
		GoAcceptDuel <&SetTargetChar %5% SMemo 2  WinComp %1% .duel>
		.duel <+Tgt +Fch>
		GoRejectDuel <&SetTargetChar %5% SMemo 3  WinComp %1% .noduel>
		.noduel <+Tgt +F-->
		Msg1 <Did %name5% get in contact with you? Apparently %5% wants to duel. Something about trying out a new mecha that \SPR %5% just got.>
		Msg1_1 <Did you see %name5%'s new mecha? It's kind of low-end, but still pretty cool. I think \SPR %5% 's hoping to challenge you to a duel.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <You should know that %name5% 's looking for you. I think \SPR %5% wants to duel.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <%name5% has just gotten a new mecha and is looking for anyone to duel with it. Did you hear about that?>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <Did you see %name5% 's new mecha? It's awesome! I think \SPR %5% was hoping you'd agree to a match sometime.>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <Here's some interesting news. %name5% recently purchased a new mecha, and I think \SPR %5% 's hoping you'll join \OPR %5% for a friendly duel.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <Has %name5% been bugging you about \PPR %5% new mecha? Ever since \SPR %5% got it \SPR %5% 's been looking for people to duel...>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <You are going to meet %name5% at %name1%, and challenge \OPR %5% to a duel.>
		Msg3 <You are going to meet %name5% at %name1%.>
		Msg4 <Be careful. I know it's just going to be a friendly match between two rivals, but one of you could get hurt... or even worse, damage your mecha...>
		Msg5 <What kind of great adventurer dismisses challenges like that? Anyhow, since you're not going to fight, you better go to %name1% and tell %name5% yourself.>
		Prompt1 <We're meeting in %name1% later.>
		Prompt1_1 <I'll be meeting \OPR E6 in %name1% later today.>
		Prompt2 <I told \OPR %5% I'm not interested.>
		Prompt2_1 <Pointless dueling doesn't interest me.>
		Prompt3 <Any other motherly advice before I head out?>
		Prompt3_1 <I know. Do you have any useful advice?>
		Prompt4 <Any final words of advice?>
	end

Plot
	name <I'm a Contender>
	Size 0
	desc <You are going to challenge another arena pilot.>
	requires <#A +T01 P:PDASS ~+Gfa>

	% E1 is the metascene to be used for the meeting.
	% E2 is the scene where the newly created arena pilot normally resides.
	% E3 is the scene where the "Intro NPC" is
	% E4 is the "Intro NPC"
	% E5 is the pilot you're going to challenge
	element1 <MetaScene !Near -7 *Building NeverFail>
	element2 <Scene !Near -7 building public>
	element3 <Scene !Near -7 cavclub>
	element4 <NewNPC -3 -7 PCFriend>
	place4 <3 pass>
	element5 <Prefab>
	place5 <2 Pass>

	update <ForceChat %4%>

	sub
		Persona 4
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <&SetTargetChar %5%   SMemo 2  WinComp E1 .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+Tgt +Fch>
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1>
		*GoAdvice <*INTRO_Advice>
		result1 <Goto GoAdvice>
		Msg1 <I hear that the famous arena pilot %name5% will be at %name1% today. Did you know about that?>
		Msg1_1 <%name5% of the Pro Duelist League is on vacation here in \NARRATIVE 7 . From what I've heard, \SPR E5 'll be in %name1% today.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <Here's a secret... Arena champion %name5% is going to be at %name1% today. This could be your big chance.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Have you been reading the duelist interboard? Some bigshot pilot named %name5% is going to be at %name1% today. I thought maybe you'd like to meet \OPR E5 .>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <You know who %name5% is, right? Pro duelist league bigshot... Well, \SPR E5 's going to be at \ELEMENT 1 today. This could be a big opportunity for you.>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <I heard that the famous arena pilot %name5% is going to be at %name1% today. I'm sure if you ask nicely the two of you could play together.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <When you become a famous arena champion, are you going to forget all of your old friends? That reminds me... I heard that pro duelist %name5% is going to be in %name1% tonight.>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <You are going to challenge %name5% to a mecha duel; \SPR %5% can be found at %name1%.>
		Prompt1 <I'm planning to challenge \OPR E5 . Got any advice?>
		Prompt1_1 <Do you have any advice before I challenge \OPR E5 ?>
	end
	inv
		NPC Arena Pilot
	end

Plot
	name <Crime Wave>
	Size 0
	desc <A criminal faction is on a crime spree.>
	requires <#A +T01 (C:CORPO|C:POLIC|P:COR|P:POL)>

	% E1 is the scene where the "Intro NPC" is
	% E2 is the "Intro NPC"
	% E3 is the local police faction
	% E4 is the crime faction currently rampaging
	element1 <Scene !Near -7 cavclub>
	element2 <NewNPC -3 -7 PCFriend>
	place2 <1 pass>
	element3 <Faction Police !Ally -7>
	element4 <Faction Crime !Enemy 3>

	update <ForceChat %2%>

	sub
		Persona 2
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <&SetEnemyFac %4%   StoryNote 2  WinComp 0 .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+T-- +F-->
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1>
		*result1 <*INTRO_Advice>
		Msg1 <Did you hear about all the robberies by %name4% lately? It's a dangerous time to work in a bank, that's for sure...>
		Msg1_1 <I heard there's been a string of robberies committed by %name4% . If you ask around, I'm sure there must be a bounty for helping to catch them.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <Do you know about %name4% ? They've been responsible for a string of robberies.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <I saw on the news that there have been a number of robberies by %name4% . Did you hear about that?>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <Lately there have been many robberies by %name4% .>
		Prompt1 <Any advice in case I happen to meet them?>
		Prompt1_1 <Do you have any advice in case I run into them?>
	end

Plot
	name <Normal Day>
	Size 0
	desc <You are going to meet your friend.>
	requires <#A +T01 ~P:-- ~C:LABOR ~C:POLIT>

	% E1 is the metascene to be used for the meeting.
	% E2 is the scene where the newly created friend normally resides.
	% E3 is the scene where the "Intro NPC" is
	% E4 is the "Intro NPC"
	% E5 is the friend you're going to meet
	element1 <MetaScene !Near -7 *Building NeverFail>
	element2 <Scene !Near -7 building public>
	element3 <Scene !Near -7 cavclub>
	element4 <NewNPC -3 -7 PCFriend>
	place4 <3 pass>
	element5 <NewNPC -3 -7 PCFriend>
	place5 <2 Pass>

	update <ForceChat %4%>

	sub
		Persona 4
		% THE INTRO
		% - All +T01 components should feature a intro character. They don't have to,
		%   of course, but it'll be a major pissoff if they don't.
		% - The intro character should follow this form, to insure that all the major
		%   bits are in place: the introduction, the question, the story info, and the
		%   advice.
		% V1 = Have given story info.
		greeting <&SetTargetChar %5%   SMemo 2  WinComp E1 .next  if= V1 1 else GoGreeting Goto GoAdvice>
		.next <+Tgt +F-->
		*GoGreeting <*INTRO_Introduction GoAskQuestion>
		*GoAskQuestion <*INTRO_Question GoStoryInfo>
		GoStoryInfo <NewChat Say 1 V= 1 1 AddChat 1>
		*GoAdvice <*INTRO_Advice>
		result1 <Goto GoAdvice>
		Msg1 <Oh yeah, I was supposed to deliver a message. \PERSONA E5 is waiting for you at \SCENE E1 .>
		Msg1_1 <I just remembered... I have a message for you. \PERSONA E5 wants to meet at \SCENE E1 later today. I don't know why.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <Oh yeah... \PERSONA E5 asked me to tell you to meet \OPR E5 at \SCENE E1 .>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Did you meet with \PERSONA E5 yet? Oh, wait, I think I forgot to tell you... \OPR E5 's waiting for you at \SCENE E1 .>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <Crap, I almost forgot! \PERSONA E5 wants to meet you today. I was supposed to deliver the message... \SPR E5 'll be at \SCENE E1 .>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <\PERSONA E5 asked me to tell you \OPR E5 wants to meet at \SCENE E1 later on today. Have a good time together.>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <I guess you won't be able to hang out with me, today... %name5% is waiting for you at %name1% . I don't know why \SPR E5 wants to meet, or why I wasn't invited too...>
		CMsg1_6 <ifNPCMelancholy Accept>

		Msg2 <You are going to meet your old friend \PERSONA E5 at \SCENE E1 .>

		Prompt1 <Thanks. Do you have anything else to tell me?>
		Prompt1_1 <Is there anything else I should know?>

	end

