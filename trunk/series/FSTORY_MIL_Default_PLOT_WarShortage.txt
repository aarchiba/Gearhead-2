Plot 0
	name <Wartime Shortages>

	% E1 is the scene under attack
	element1 <Character Shopkeeper !Near -3>

	start <ifNPCOK E2 else CleanUp if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 15000>
	update <ifNPCOK E2 else CleanUp if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 15000>
	GoCheckTime <ifG ComTime V1 EndPlot>
	CleanUp <EndPlot>

sub
	Persona 2
	greeting <NewChat   Say 1   if=V1 0  AddChat 1   AddChat 2>
	result1 <ifG React 40 else GoR1Test2       EndChat Say 2 Shop .nowares Say 3>
	GoR1Test2 <ifG PCCharm d19 else GoR1Fail   EndChat Say 2 Shop .nowares Say 3>
	GoR1Fail <NewChat Say 4   V= 1 1>
	result2 <NewChat Say 3>

	Msg1 <The shop is closed. Thanks to all those mecha from \NARRATIVE 2 outside I haven't been able to get any stock in.>
	Msg1_1 <This battle with \NARRATIVE 2 had better end soon... Because of the fighting, I'm completely out of stock!>
	CMsg1_1 <ifNPCEasygoing Accept>
	Msg1_2 <The shop's closed. Because of the fighting outside I'm nearly all out of stock. I suppose you've heard about all the trouble with \NARRATIVE 2 ? Come here tomorrow and I might have something for you.>
	CMsg1_2 <ifNPCSociable Accept>
	Msg1_3 <We're closed today. The trucks couldn't make it through because of the fighting, so I have nothing left to sell. If the battle doesn't end soon I'll be out of business.>
	CMsg1_3 <ifNPCMelancholy Accept>
	Msg1_4 <Sorry, I don't have anything to sell today. It's all because of \NARRATIVE 2 ...>
	CMsg1_4 <ifNPCPassionate Accept>
	Msg1_5 <My store's closed. There's a battle going on outside.>
	CMsg1_5 <ifNPCShy Accept>
	Msg1_6 <I'm on what you might call an unexpected holiday. Because of the fighting in town, I haven't been able to get supplies, so my store's closed.>
	CMsg1_6 <ifNPCCheerful Accept>

	Msg2 <I suppose so. I have some duct tape left in the back... that ought to be enough.>

	Msg3 <Remember, come back tomorrow and buy lots of stuff!>

	Msg4 <I told you, I don't have anything! Come back later and maybe we can talk.>
	Msg4_1 <>
	CMsg4_1 <ifNPCSociable Accept>
	Msg4_2 <There's nothing I can do.>
	CMsg4_2 <ifNPCShy Accept>

	Prompt1 <Can I at least get my stuff repaired?>
	Prompt1_1 <I need to get my things repaired.>
	Prompt1_2 <Could I get my stuff repaired?>

	Prompt2 <I'll come back later, then.>
	Prompt2_1 <In that case I'll be back later.>
	Prompt2_2 <Okay, I'll be back later.>

end

