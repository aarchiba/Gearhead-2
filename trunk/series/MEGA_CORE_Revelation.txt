%%
%% *CS_Revelation Content
%%		&Friendly	The NPC makes the revelation freely
%%		&Hostile	The NPC is somehow coerced into making the revelation
%%
%% An NPC is about to reveal a secret to the PC. This secret should have something to do
%% with the core story.
%%
%% Each of these components needs a .%id%_GoInit script in the main persona; each component
%% should be enterable from either setting the PlotID and starting a conversation or from
%% branching within a conversation.
%%
%% This subplot ends the plot.
%%
%% Param1 = The NPC to make the revelation
%%

Content
	name <Revelation: Gather Information>
	desc <This revelation leads directly into a Gather Information subplot.>
	requires <*CS_Revelation>

	% E1 is the NPC making the revelation.

	start <ifNPCDead %1% LoseComp 0 .%id%_end>
	.%id%_end <+T-- +F-->

	.%id1%_%plotid1%_GoWin  <WinComp 0 .%id%_end>
	.%id1%_%plotid1%_GoLoss <LoseComp 0 .%id%_end>

	% SubPlot1 is the Gather Information subplot.
	SubPlot1 <*:CS_GatherInformation 1>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*RevealSecret .%id%_GoInit>
		.%id%_GoInit <WinSubPlot %plotid% SetPlotStatus %plotid1% %id1% Goto .%id1%_GoInform>
	end



%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************


%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************

Content
	name <Your enemy is... the Red Mask Raiders.>
	desc <The PC will learn that the unknown enemy is the red mask raiders.>
	requires <*CS_Revelation +Pun F:-- (L:MAQUI|L:FCOMS|L:RISHI|L:BOHEM|L:CRIHN|L:PRIVA) ~P:FCOMS ~P:CRIHN>

	% Element1 is the NPC
	% Element2 is the new enemy faction
	% Element3 is a place to go for more information
	Element2 <Faction REDMA !Xclude -3 !Okay -1 -ArchAlly>
	Element3 <Scene Public Sleazy !Near -7>

	start <ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*RevealSecret .%id%_GoInit>
		.%id%_GoInit <NewChat Say %id%01 AddChat %id%01 AddChat %id%02 &SetEnemyFac %2%>
		result%id%01 <NewChat Say %id%02 SMemo %id%03 History %id%04 WinComp %3% .%id%_fight>
		.%id%_fight <+Tgs +Ffi>
		result%id%02 <NewChat Say %id%05 SMemo %id%03 History %id%04 WinComp %3% .%id%_info>
		.%id%_info <+Tgs +Fin>
		Msg%id%01 <The mysterious enemy who's been attacking everything? It's the Red Mask Raiders.>
		Msg%id%02 <I heard some pirates talking about it at %name3%. That's probably the best place to go if you want more information, but be careful- it could be dangerous.>
		Msg%id%03 <%name1% revealed that the Red Mask Raiders are behind the recent spate of mecha attacks; \SPR %1% suggested you visit %name3% for more information.>
		Msg%id%04 <%name1% revealed that the Red Mask Raiders were behind the mecha attacks.>
		Msg%id%05 <Given what I know about the Raiders, I'd have to guess that this comes down to either money or revenge. Possibly both. I've heard that there are a lot of pirates at %name3%; maybe you can learn more there.>
		Prompt%id%01 <How do you know that?>
		Prompt%id%02 <What do they want?>
	end

%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************

Content
	name <There's Gonna Be a Conference>
	desc <The NPC will reveal to the PC that there's going to be a conference about the item.>
	requires <*CS_Revelation +Pla I:++ ~+Gkn ~C:POLIT ~C:MEDIA>

	% Element1 is the NPC
	% Element2 is a metascene
	Element2 <MetaScene !Near -7 *Building NeverFail>

	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	sub
		Persona 1
		greeting <if= PlotStatus %plotid% %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*RevealSecret .%id%_GoInit>
		.%id%_GoInit <NewChat Say %id%01 SMemo %id%02 History %id%03 WinComp %2% .%id%_next>
		.%id%_next <+Tgs +Fin>
		Msg%id%01 <There's going to be a conference about the \ITEM &TargetItem at %name2% soon. It's by invitation only, but you shouldn't have any problems sneaking in.>
		Msg%id%02 <%name1% told you that there's going to be a conference about the \ITEM &TargetItem at %name2%.>
		Msg%id%03 <%name1% told you about the conference at %name2%.>
	end

%%  *******************
%%  ***   DEFAULT   ***
%%  *******************


