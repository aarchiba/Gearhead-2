%%
%% *:MECHA_ARENA_CHALLENGE
%%	Contains a rival arena pilot
%%
%%  PARAM1: The Arena Manager
%%  PARAM2: The Arena Itself
%%
%% The persona for the arena manager must contain a .%id%_Init script, to be called
%% when the PC reaches this particular rung of the arena ladder.
%% Said script should set the QuestID to %id%.
%%
%% ASSERT: If .%id%_Init is called, the PC is in the same scene as the arena manager and
%%  the next fight is ready to go.


Content
	name <PC vs Carter>
	requires <*:MECHA_ARENA_CHALLENGE !Md>

	% E1 is the arena manager
	% E2 is the arena proper
	% E3 is the challenger
	Element3 <Prefab>
	Scene3 <Building Public>
	Team3 <Citizens>
	TeamData3 <Ally Pass>

	Quest1 <*CHALLENGER_SUBQUEST 3>

sub
	Persona 1
	Greeting <if= QuestStatus %qid% %id% else %pop%  if= ArenaState %2% NAV_AS_Win else .%id%_GoCheckLoss Goto .%id%_GoArenaWin>
	*.%id%_GoCheckLoss <*ChallengerStatusReport %3% %2% .%id%_GoNPCDied>
	.%id%_GoInit <ifNPCOK %3% else .%id%_GoNPCDied NewChat Say %id%01 SetQuestStatus %qid% %id%  NPCSubQMemo %3% %id% %id%02>
	%% Just in case the NPC who we're supposed to fight has died somehow, just set up a normal fight.
	*.%id%_GoNPCDied <*YourChallengerDied %2% %qid%>
	*.%id%_GoArenaWin <*ChallengeArenaWin %3% %2% .%id%_GoConclude>
	.%id%_GoConclude <ResetArena %2% SetQuestStatus %qid% %id1%>
	Msg%id%01 <For the next battle, you'll be facing %name3%. You can find \OPR %3% in \SCENE %s3% .>
	Msg%id%02 <%name1% sent you to battle %name3% in arena combat; \SPR %3% can be found in \SCENE %s3% .>

	Persona 3
	Greeting <if= QuestStatus %qid% %id% else GoNoMatch  if= ArenaState %2% NAV_AS_Battle else GoCheckStatus Goto GoTrashTalk>
	*GoTrashTalk <*ArenaChallenge GoThemeInfo>
	*GoThemeInfo <*THEME_EXPO&Enemy NA>
	GoCheckStatus <if= ArenaState %2% NAV_AS_Ready else GoCheckWin EndChat Say 1>
	GoCheckWin <if= ArenaState %2% NAV_AS_Win else GoCheckChallenge ifNPCOK %1% else GoManagerDied NewChat Say 2>
	GoManagerDied <NewChat Say %3% SetQuestStatus %qid% -1 ResetArena %2%>
	GoCheckChallenge <ResetArena %2% ifG ComTime ArenaRecharge %2% else GoLater NewChat Say 4 AddChat 1 AddChat 2>
	*GoLater <*ChallengeMeLater>
	*GoNoMatch <*NiceToMeetYou GoChat>
	*GoChat <*MISC_CHATTER>
	GoStartCombat <SetChallengerID %2% %3% SetChallengerHome %2% %s3% MoveNPC %3% %2% PrepArena %2% 0 SetNPCTeam %3% 2>
	*result1 <*MeetMeAtArena %2% GoStartCombat>
	*result2 <*RejectChallenge>
	Msg1 <I'm waiting for you at the arena. Hurry up and get over here.>
	Msg2 <You beat me. You better go talk with %name1% about the reward.>
	Msg3 <Too bad about what happened to %name1%... I guess there's not going to be any more fights until we get a new arena manager.>
	Msg4 <Are you here to challenge me?>
	Prompt1 <That's right.>
	Prompt2 <Not right now; we can fight later.>
end
inv
	NPC Trucker
	name <Carter>
	CharDesc Male Shy Easygoing Lawful
	Age 22
	EquipChar 10000
	StatLine 14 16 12 15 14 11 13 9
	SDL_Portrait <por_m_comiccarter(OUY).png>
	SDL_Colors <25 80 100 255 212 195 122 82 65>
	SetTheme 2
	SetSpecSkill 2
	SetFaction 5
end


Content
	name <Generic Challenger>
	requires <*:MECHA_ARENA_CHALLENGE>

	% E1 is the arena manager
	% E2 is the arena proper
	% E3 is the challenger
	Element3 <Prefab>
	Scene3 <Building Public>
	Team3 <Citizens>
	TeamData3 <Ally Pass>

	Quest1 <*CHALLENGER_SUBQUEST 3>

sub
	Persona 1
	Greeting <if= QuestStatus %qid% %id% else %pop%  if= ArenaState %2% NAV_AS_Win else .%id%_GoCheckLoss Goto .%id%_GoArenaWin>
	*.%id%_GoCheckLoss <*ChallengerStatusReport %3% %2% .%id%_GoNPCDied>
	.%id%_GoInit <ifNPCOK %3% else .%id%_GoNPCDied NewChat Say %id%01 SetQuestStatus %qid% %id%  NPCSubQMemo %3% %id% %id%02>
	%% Just in case the NPC who we're supposed to fight has died somehow, just set up a normal fight.
	*.%id%_GoNPCDied <*YourChallengerDied %2% %qid%>
	*.%id%_GoArenaWin <*ChallengeArenaWin %3% %2% .%id%_GoConclude>
	.%id%_GoConclude <ResetArena %2% SetQuestStatus %qid% %id1%>
	Msg%id%01 <For the next battle, you'll be facing %name3%. You can find \OPR %3% in \SCENE %s3% .>
	Msg%id%02 <%name1% sent you to battle %name3% in arena combat; \SPR %3% can be found in \SCENE %s3% .>

	Persona 3
	Greeting <if= QuestStatus %qid% %id% else GoNoMatch  if= ArenaState %2% NAV_AS_Battle else GoCheckStatus Goto GoTrashTalk>
	*GoTrashTalk <*ArenaChallenge GoThemeInfo>
	*GoThemeInfo <*THEME_EXPO&Enemy NA>
	GoCheckStatus <if= ArenaState %2% NAV_AS_Ready else GoCheckWin EndChat Say 1>
	GoCheckWin <if= ArenaState %2% NAV_AS_Win else GoCheckChallenge ifNPCOK %1% else GoManagerDied NewChat Say 2>
	GoManagerDied <NewChat Say %3% SetQuestStatus %qid% -1 ResetArena %2%>
	GoCheckChallenge <ResetArena %2% ifG ComTime ArenaRecharge %2% else GoLater NewChat Say 4 AddChat 1 AddChat 2>
	*GoLater <*ChallengeMeLater>
	*GoNoMatch <*NiceToMeetYou GoChat>
	*GoChat <*MISC_CHATTER>
	GoStartCombat <SetChallengerID %2% %3% SetChallengerHome %2% %s3% MoveNPC %3% %2% PrepArena %2% 0 SetNPCTeam %3% 2>
	*result1 <*MeetMeAtArena %2% GoStartCombat>
	*result2 <*RejectChallenge>
	Msg1 <I'm waiting for you at the arena. Hurry up and get over here.>
	Msg2 <You beat me. You better go talk with %name1% about the reward.>
	Msg3 <Too bad about what happened to %name1%... I guess there's not going to be any more fights until we get a new arena manager.>
	Msg4 <Are you here to challenge me?>
	Prompt1 <That's right.>
	Prompt2 <Not right now; we can fight later.>
end
inv
	NPC Arena Pilot
	EquipChar 10000

end



Content
	name <PC vs. Hammel>
	requires <*:MECHA_ARENA_CHALLENGE (!Ne|!Lo) L5PAT>

	% E1 is the arena manager
	% E2 is the arena proper
	% E3 is the challenger
	% E4 is his sister
	Element3 <Prefab>
	Scene3 <Building Public>
	Team3 <Citizens>
	TeamData3 <Ally Pass>

	Element4 <Prefab>
	Scene4 <Grab 3>
	Team4 <Citizens>
	TeamData4 <Ally Pass>

	Quest1 <*ProveMyself 3>

sub
	Persona 1
	Greeting <if= QuestStatus %qid% %id% else %pop%  if= ArenaState %2% NAV_AS_Win else .%id%_GoCheckLoss Goto .%id%_GoArenaWin>
	*.%id%_GoCheckLoss <*ChallengerStatusReport %3% %2% .%id%_GoNPCDied>
	.%id%_GoInit <ifNPCOK %3% else .%id%_GoNPCDied NewChat Say %id%01 SetQuestStatus %qid% %id%  NPCSubQMemo %3% %id% %id%02 AddChat %id%01>
	%% Just in case the NPC who we're supposed to fight has died somehow, just set up a normal fight.
	*.%id%_GoNPCDied <*YourChallengerDied %2% %qid%>
	*.%id%_GoArenaWin <*ChallengeArenaWin %3% %2% .%id%_GoConclude>
	.%id%_GoConclude <ResetArena %2% SetQuestStatus %qid% %id1%>
	result%id%01 <NewChat Say %id%03>
	Msg%id%01 <For the next battle, you'll be facing a young pilot named %name3%. You can find him in \SCENE %s3% .>
	Msg%id%02 <%name1% sent you to battle Hammel in arena combat; \SPR %3% can be found in \SCENE %s3% .>
	Msg%id%03 <Not much. Hammel is a new pilot, just starting out. He has a lot of promise but not very much experience yet.>
	Prompt%id%01 <What can you tell me about him?>

	Persona 3
	% V1 = Have spoken about challenge once already
	Greeting <if= QuestStatus %qid% %id% else GoNoMatch  if= ArenaState %2% NAV_AS_Battle else GoCheckStatus Goto GoTrashTalk>
	*GoTrashTalk <*ArenaChallenge GoThemeInfo>
	*GoThemeInfo <*THEME_EXPO&Enemy NA>
	GoCheckStatus <if= ArenaState %2% NAV_AS_Ready else GoCheckWin EndChat Say 1>
	GoCheckWin <if= ArenaState %2% NAV_AS_Win else GoCheckChallenge ifNPCOK %1% else GoManagerDied NewChat Say 2>
	GoManagerDied <NewChat Say %3% SetQuestStatus %qid% -1 ResetArena %2%>
	GoCheckChallenge <ResetArena %2% ifG ComTime ArenaRecharge %2% else GoLater NewChat if= V1 1 else GoFirstTime Say 4 AddChat 1 AddChat 2>
	*GoLater <*ChallengeMeLater>
	*GoNoMatch <*NiceToMeetYou GoChat>
	*GoChat <*MISC_CHATTER>
	GoStartCombat <SetChallengerID %2% %3% SetChallengerHome %2% %s3% MoveNPC %3% %2% PrepArena %2% 0 SetNPCTeam %3% 2>
	GoFirstTime <NewChat Say 5 V= 1 1 AddChat 3 AddChat 4 AddChat 5>
	*result1 <*MeetMeAtArena %2% GoStartCombat>
	*result2 <*RejectChallenge>
	result3 <NewChat Say 6 AddChat 1 AddChat 2>
	result4 <AddEasygoing -d10 AddReact -d10 Goto result3>
	result5 <AddCheerful d10  Goto result3>
	Msg1 <I'm waiting for you at the arena. Hurry up and get over here.>
	Msg2 <You beat me. You better go talk with %name1% about the reward.>
	Msg3 <Too bad about what happened to %name1%... I guess there's not going to be any more fights until we get a new arena manager.>
	Msg4 <Are you ready to fight me now?>
	Msg5_1 <Are you the guy they sent to challenge me? Wow, I mean, I've heard of you before. It's an honor to be paired against such an accomplished mecha pilot.>
	CMsg5_1 <ifG PCRenown 20 Accept>
	Msg5_2 <So, you're the one they sent to challenge me, huh? That's cool.>
	CMsg5_2 <ifG 21 PCRenown Accept>
	Msg6 <Great. Um... do you have to get your gear ready, or should we go to the arena right away?>
	Prompt1 <Let's do it.>
	Prompt2 <No, not right now.>
	Prompt3 <Shall we get started?>
	Prompt4 <It will be my pleasure to defeat you.>
	Prompt5 <I've heard good things about you.>

	Persona 4
	rumor%id1% <Lehya is worried about her brother Hammel.>
	% V1 = Have concluded the quest
	% V2 = Have spoken about loss of confidence
	greeting <if= V1 1 else GoCheckSuccess Goto GoMiscChat>
	*GoMiscChat <*MISC_CHATTER>
	GoCheckSuccess <if= QuestStatus %qid% -1 else GoCheckProof ifNPCOK %3% else GoCheckDeath NewChat Say 1 V= 1 1 PCFriend %4% XPV 50>
	GoCheckProof <if= QuestStatus %qid% %id1% else GoCheckDeath if= V2 0 else GoRepeat NewChat Say 2 AddChat 1 AddChat 2 AddChat 3>
	GoRepeat <NewChat Say 3>
	GoCheckDeath <ifNPCDead %3% else GoGreet NewChat Say 9 V= 1 1>
	*GoGreet <*NiceToMeetYou GoChat>
	GoChat <NewChat Say 10>
	result1 <NewChat V= 2 1 Say 4 AddChat 4 AddChat 5>
	result2 <NewChat V= 2 1 Say 5 AddSociable -d4 AddReact -d4>
	result3 <NewChat V= 2 1 Say 6 AddCheerful -d6 AddChat 4 AddChat 5>
	result4 <NewChat Say 7>
	result5 <NewChat Say 8 AddSociable -d10>
	Msg1 <Thank you for helping my brother to find his spirit. I, too, feel like I don't have to worry any more.>
	Msg2 <I'm worried about my brother, Hammel. Ever since you defeated him at the arena, he's lost his confidence. I know that he looks up to you as a fellow pilot... Could you help him regain his fighting spirit?>
	Msg3 <I'm worried about my brother, Hammel. Ever since you defeated him at the arena, he's lost his confidence.>
	Msg4 <I don't, but... It's what he feels he has to do. This feeling is killing him on the inside. I'd rather see him back in the arena than see him like this.>
	Msg5 <I know, it's not your problem. I wasn't trying to blame you for it. Hammel has to learn how to accept the risks of what he does... he has to see that one setback doesn't make him a failure.>
	Msg6 <I think that's true. Still, it would be easier if he had someone to talk with. He has to learn that one setback doesn't make him a failure.>
	Msg7 <Thank you.>
	Msg8 <You may be right...>
	Msg9 <If only Hammel had listened to me, he could still be alive...>
	Msg10 <My brother Hammel wants to be an arena champion. I want him to succeed, but I worry that he's going to get hurt.>
	Prompt1 <I thought you didn't want him fighting...>
	Prompt2 <I don't care about his problems.>
	Prompt3 <Your brother just needs to grow up.>
	Prompt4 <Alright, I'll speak with him.>
	Prompt5 <He can work this out by himself.>

end
inv
	NPC Arena Pilot
	name <Hammel>
	EquipChar 10000
	Chardesc Male Cheerful Passionate Heroic
	Statline 17 12 16 14  11 13 12 12
	Age -3
	SDL_PORTRAIT <por_m_lightjacket(Y--).png>
	SDL_COLORS <136 141 101 245 213 160 77 35 101>
	Skill 42 3

	NPC Citizen
	name <Lehya>
	CharDesc Female Cheerful Easygoing Heroic
	Statline 14 13 14 12  13 10 16 13
	Age -1
	SDL_PORTRAIT <por_f_smart(-P-).png>
	SDL_COLORS <136 141 101 245 213 160 60 25 81>
	Skill 11 7
	Skill 29 1
	Skill 37 1
end

