%% *CS_GiveMission Content
%%
%% The PC will be given something to do by an NPC.
%% The mission is separated from the plot so that the same mission code
%% can be used in multiple plot instances- the two main instances are
%% in "+Tgt +Fmi" when the PC has to see an NPC about a mission, and
%% also "+T--" when a member of the PC's faction could contact him with
%% a mission.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC who will be giving the mission. This NPC must have been
%% moved to the correct place by the parent plot.
%%

%%  *******************
%%  ***   DEFAULT   ***
%%  *******************
%% Use this section for truly default missions, or for missions which have wide application
%% across a number of plot states. See below for the state-specific missions.

Content
	name <Protect Scientist>
	desc <The PC will be sent to protect a scientist.>
	requires <*CS_GiveMission I:++ (+P--|+Pla) P:++>

	% E1 is the NPC that the PC will speak with
	% E2 is the NPC that the PC has to guard
	% E3 is the scene where the NPC currently is
	% E4 is the item
	Element2 <Character ACADE !Near -7 NeverFail>
	NeverFail2 <Professor>
	Place2 </>
	Element3 <MetaScene !Near -7 *Building NeverFail>
	Element4 <Grab 8>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	sub
		Persona 1
		greeting <if= PlotStatus %id% else %pop% ifChatNPCUnknown else .%id%_GoKnownGreeting ifChatNPCAlly else .%id%_GoUnknownGreeting Goto .%id%_GoKnownGreeting>
		*.%id%_GoKnownGreeting <*IHaveAJobForYou .%id%_GoBriefing>
		*.%id%_GoUnknownGreeting <*AreYouHereAboutJob .%id%_GoBriefing>
		.%id%_GoBriefing <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		*result%id%01 <*GoodLuckOnMission&Advance .%id%_GoR1Exit ChatNPCFac &EnemyFac>
		.%id%_GoR1Exit <SMemo %id%02 History %id%03 CashPrize Reward StoryDL 100 &SetTargetNPC %2% WinComp %3% .%id%_next>
		.%id%_next <+Tpt +Fin>
		*result%id%02 <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <SMemo 0 LoseComp 0 .%id%_reject>
		.%id%_reject <+T-- +F-->
		Msg%id%01 <%name2% has been researching the %name4%, and because of this may be in danger. I want you to protect \OPR %2% and hopefully find out some of what \SPR %2% knows. You will be paid in advance for this mission.>
		Msg%id%02 <%name1% sent you to %name3% to protect %name2%, who has been researching the %name4%.>
		Msg%id%03 <%name1% sent you to protect %name2%, who had been researching the %name4%.>
		Prompt%id%01 <For advance pay, I'll do anything.>
		Prompt%id%02 <I'm not here to be anyone's babysitter.>
		CPrompt%id%02 <if# PCFac ChatNPCFac Accept>
	end

Content
	name <Basic Fighting Mission>
	desc <The PC will be asked to go fight some mecha.>
	requires <*CS_GiveMission (+P--|+Pme|+Pun|+Psh|+Pew) (1:MILIT|1:POLIT|1:CORPO|1:ADVEN|1:POLIC) ~F:MILIT ~P:MILIT>

	% E1 is the NPC that the PC will speak with
	% E2 is the outdoors location where the encounter will be placed

	element2 <Scene Environs !Near -7>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	%% SubPlot1 is the combat encounter itself
	SubPlot1 <*CS_MechaEncounter 1 2>

	sub
		Persona 1
		rumor0 <%name1% needs a cavalier for some kind of mission.>
		greeting <if# PlotStatus %id% else .%id%_GoOfferMission if# PlotStatus %id1% else .%id%_GoRemind Goto %pop%>
		.%id%_GoOfferMission <ifChatNPCUnknown else .%id%_GoKnownGreeting ifChatNPCAlly else .%id%_GoUnknownGreeting Goto .%id%_GoKnownGreeting>
		*.%id%_GoRemind <*MechaMissionReminder %2%>
		*.%id%_GoKnownGreeting <*IHaveAJobForYou .%id%_GoBriefing>
		*.%id%_GoUnknownGreeting <*AreYouHereAboutJob&Mecha .%id%_GoBriefing>
		*.%id%_GoBriefing <*CORE_BasicJob .%id%_GoAccept .%id%_GoDeny %2%>
		*.%id%_GoAccept <*GoodLuckOnMission .%id%_GoR1Exit ChatNPCFac &EnemyFac>
		.%id%_GoR1Exit <SetPlotStatus %id1%>
		*.%id%_GoDeny <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <LoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
	end


%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************

Content
	name <They must have a base nearby>
	requires <*CS_GiveMission (+P--|+Pme) (1:LABOR|1:CRAFT|1:TRADE|1:CORPO) (E:THIEF|E:CRIME)>
	desc <This character has been robbed by the enemy NPC; the thieves must have a secret base nearby.>

	% E1 is the NPC that the PC will speak with
	% E2 is an outdoors scene for the base
	% E3 is the metascene for the base itself
	Element2 <Scene Outdoors !Near -7>
	Element3 <Prefab>
	Place3 <2>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	% SubPlot1 is the base metascene
	% SubPlot2 is the activation subplot
	SubPlot1 <*CS_RaidEnemyBase 3>
	SubPlot2 <*CS_RevealEncounter&EnemyBase 3 2>

	sub
		Persona 3
		greeting <if= PlotStatus %id% else %pop% Goto .%id%_GoGreet>
		*.%id%_GoGreet <*DestructionGreeting .%id%_GoGiveMission &EnemyFac>
		.%id%_GoGiveMission <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		.%id%_GoActivateBase <SMemo %id%04 History %id%05 SetPlotStatus %id2%>
		result%id%01 <NewChat Say %id%02 Goto .%id%_GoActivateBase>
		result%id%02 <NewChat Say %id%03  AddCheerful -d10  Goto .%id%_GoActivateBase>
		Msg%id%01 <I've just been robbed! This is terrible... they've taken everything.>
		Msg%id%02 <I don't know who it was; do I look like the kind of person who mingles with scum like that? I'm not the only person to be robbed lately; these bandits must have a hideout nearby!>
		Msg%id%02_10 <It was that bandit \PERSONA &EnemyNPC ! I'm not the only one who's been robbed lately; \SPR &EnemyNPC must have a secret hideout somewhere near here, I'm sure of it.>
		CMsg%id%02_10 <ifNPCOK &EnemyNPC Accept>
		Msg%id%03 <Well, it means that I won't have any cash to hire you for a mission, if that's what you're after. These bandits must be stopped... they must have a secret hideout somewhere in this town.>
		Msg%id%04 <%name3% was robbed; \SPR %3% thinks the robbers have a secret hideout nearby.>
		Msg%id%05 <%name3% was robbed; \SPR %3% claimed there must be a hideout nearby.>
		Prompt%id%01 <Calm down. Can you tell me about the thief?>
		Prompt%id%02 <Too bad. How does this affect me?>
	end
	inv
		STC CORE-ACTIVATABLE-STATIONARY-DEFENDED
		name <the Secret Hideout>
	end


%%  *****************************************
%%  ***   +Pme	PC meets an enemy pilot   ***
%%  *****************************************


%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************


%%  ***********************************************************
%%  ***   +Psh	PC is shanghaied into working for faction   ***
%%  ***********************************************************


%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************


%%  ****************************************
%%  ***   +Pew  Enemy  Weapon  Program   ***
%%  ****************************************

%%  ************************************
%%  ***   +Cme  Mentoring  Partner   ***
%%  ************************************

Content
	name <Training Mission>
	desc <The PC will be sent on a combat mission, for his partner's sake.>
	requires <*CS_GiveMission +Cme R:++ 1:Ally (1:MILIT|1:ADVEN|1:CORPO) ~R:PCFAC>

	% E1 is the NPC that the PC will speak with
	% E2 is the outdoors location where the encounter will be placed
	% E3 is the partner
	element2 <Scene Environs !Near -7>
	element3 <Grab 4>

	start <if= PlotStatus %id% else %pop% ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	%% SubPlot1 is the combat encounter itself
	SubPlot1 <*CS_MechaEncounter 1 2>

	sub
		Persona 1
		rumor0 <%name1% needs a cavalier for some kind of mission.>
		greeting <if# PlotStatus %id% else .%id%_GoOfferMission if# PlotStatus %id1% else .%id%_GoRemind Goto %pop%>
		.%id%_GoOfferMission <NewChat Say %id%01 AddChat %id%01>
		*.%id%_GoRemind <*MechaMissionReminder %2%>
		*.%id%_GoBriefing <*CORE_BasicJob .%id%_GoAccept .%id%_GoDeny %2%>
		*.%id%_GoAccept <*GoodLuckOnMission .%id%_GoR1Exit ChatNPCFac &EnemyFac>
		.%id%_GoR1Exit <SetPlotStatus %id1%>
		*.%id%_GoDeny <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <LoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
		Result%id%01 <Goto .%id%_GoBriefing>
		Msg%id%01 <I think that %name3% could use some practice, so I've lined up a combat mission for the two of you.>
		Prompt%id%01 <Give me the details.>
	end

