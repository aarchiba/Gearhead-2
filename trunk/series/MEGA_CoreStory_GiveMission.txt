%% *CS_GiveMission Content
%%
%% The PC will be given something to do by an NPC.
%% The mission is separated from the plot so that the same mission code
%% can be used in multiple plot instances- the two main instances are
%% in "+Tgt +Fmi" when the PC has to see an NPC about a mission, and
%% also "+T--" when a member of the PC's faction could contact him with
%% a mission.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC who will be giving the mission. This NPC must have been
%% moved to the correct place by the parent plot.
%%

%%  *******************
%%  ***   DEFAULT   ***
%%  *******************
%% Use this section for truly default missions, or for missions which have wide application
%% across a number of plot states. See below for the state-specific missions.


%% Seto's Bride

Content
	name <Basic Fetch Mission>
	desc <The PC will be asked to go fetch an item.>
	requires <*CS_GiveMission ~C:THIEF>

	% E1 is the NPC that the PC will speak with
	% E2 is the item to fetch
	Element2 <Prefab>

	% Fail Conditions:
	% - Mission giver dies before encounter is activated
	% - Fetch subplot fails

	end <if= PlotStatus %plotid1% 0 else .%id%_GoCheckReveal ifNPCDead %1% else %pop% LoseComp 0 .%id%_loss>
	.%id%_GoCheckReveal <ifSubPlotLost %plotid1% SMemo %id%01 SetPlotStatus %plotid3% %id3%>
	.%id%_loss <+T-->

	% Upon getting the item, set the win subplot.
	Get%2% <SetPlotStatus %plotid2% %id2%>

	Msg%id%01 <You failed to recover the datachip. You should go report to %name1% at \SCENE NPCScene %1% .>


	%% SubPlot1 is Fetch Item
	%% SubPlot2 is the winning condition
	%% SubPlot3 is the loss condition
	SubPlot1 <*:CS_FetchItem 2>
	SubPlot2 <*CS_WinFetchItem 1 2>
	SubPlot3 <*CS_CALoseMission 1>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid1% 0 else .%id%_GoRemind if# PlotStatus %plotid% %id% else .%id%_GoOfferMission Goto %pop%>
		.%id%_GoOfferMission <ifChatNPCUnknown else .%id%_GoKnownGreeting ifChatNPCAlly else .%id%_GoUnknownGreeting Goto .%id%_GoKnownGreeting>
		*.%id%_GoRemind <*GenericMissionReminder>
		*.%id%_GoKnownGreeting <*IHaveAJobForYou .%id%_GoBriefing>
		*.%id%_GoUnknownGreeting <*AreYouHereAboutJob .%id%_GoBriefing>
		.%id%_GoBriefing <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		*result%id%01 <*GoodLuckOnMission .%id%_GoR1Exit ChatNPCFac na>
		.%id%_GoR1Exit <StoryNote %id%02 SetPlotStatus %plotid1% %id1%>
		*Result%id%02 <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <LoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
		Msg%id%01 <We've lost some very sensitive information in the form of a datachip. I need for you to locate this chip and return it to me.>
		Msg%id%02 <%name1% hired you to recover a lost datachip.>
		Prompt%id%01 <I'll be back when I have it.>
		Prompt%id%02 <Sorry, I'm only interested in fighting.>
		CPrompt%id%02 <if# PCFac ChatNPCFac Accept>
	end
	inv
		Treasure 5
		name <Datachip>
		desc <A mysterious datachip. Who knows what it contains?>
	end

Content
	name <Basic Investigation Mission>
	desc <The PC will be asked to gain some information.>
	requires <*CS_GiveMission ~C:POLIT ~C:ACADE ~C:MEDIA>

	% E1 is the NPC that the PC will speak with

	% Fail Conditions:
	% - Mission giver dies before mission is activated

	end <if= PlotStatus %plotid1% 0  ifNPCDead %1% LoseComp 0 .%id%_loss>
	.%id%_loss <+T-->

	.%id1%_%plotid1%_GoWin <SetPlotStatus %plotid2% %id2%     SMemo %id%01>
	.%id1%_%plotid1%_GoLoss <SetPlotStatus %plotid3% %id3%    SMemo %id%02>

	Msg%id%01 <You won the mission. You should go see %name1% at \SCENE NPCScene %1% about the reward.>
	Msg%id%02 <You lost the mission. You should go report to %name1% at \SCENE NPCScene %1% .>


	%% SubPlot1 is Gather Information
	%% SubPlot2 is the winning condition
	%% SubPlot3 is the loss condition
	SubPlot1 <*:CS_GatherInformation 1>
	SubPlot2 <*CS_CAWinMission 1>
	SubPlot3 <*CS_CALoseMission 1>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid1% 0 else .%id%_GoRemind if# PlotStatus %plotid% %id% else .%id%_GoOfferMission Goto %pop%>
		.%id%_GoOfferMission <ifChatNPCUnknown else .%id%_GoKnownGreeting ifChatNPCAlly else .%id%_GoUnknownGreeting Goto .%id%_GoKnownGreeting>
		*.%id%_GoRemind <*GenericMissionReminder>
		*.%id%_GoKnownGreeting <*IHaveAJobForYou .%id%_GoBriefing>
		*.%id%_GoUnknownGreeting <*AreYouHereAboutJob&Mecha .%id%_GoBriefing>
		.%id%_GoBriefing <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
		Result%id%01 <History %id%02 SetPlotStatus %plotid1% %id1% Goto .%id1%_GoInform>
		*Result%id%02 <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <LoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
		Msg%id%01 <We have a situation going on, \PC . I'd like for you to investigate it and see what you can find out.>
		Msg%id%02 <%name1% hired you to investigate things.>
		Prompt%id%01 <I'll do it.>
		Prompt%id%02 <Sorry, I'm only interested in fighting.>
		CPrompt%id%02 <if# PCFac ChatNPCFac Accept>
	end

Content
	name <Basic Fighting Mission>
	desc <The PC will be asked to go fight some mecha.>
	requires <*CS_GiveMission ~C:MILIT>

	% E1 is the NPC that the PC will speak with
	% E2 is the outdoors location where the encounter will be placed
	% E3 is the encounter to be found

	element2 <Scene Environs !Near -7>
	element3 <Prefab>
	place3 <2>

	% Fail Conditions:
	% - Mission giver dies before encounter is activated
	% - Revalation subplot fails

	end <if= PlotStatus %plotid1% 0 else .%id%_GoCheckReveal ifNPCDead %1% else %pop% LoseComp 0 .%id%_loss>
	.%id%_GoCheckReveal <ifSubPlotLost %plotid2% SMemo %id%02 SetPlotStatus %plotid5% %id5%>
	.%id%_loss <+T-->

	.%id1%_%plotid1%_GoWin <SetPlotStatus %plotid3% %id3%     SMemo %id%01>
	.%id1%_%plotid1%_GoLoss <SetPlotStatus %plotid4% %id4%    SMemo %id%02>

	Msg%id%01 <You won the mission. You should go see %name1% at \SCENE NPCScene %1% about the reward.>
	Msg%id%02 <You lost the mission. You should go report to %name1% at \SCENE NPCScene %1% .>


	%% SubPlot1 is the combat encounter itself
	%% SubPlot2 is the encounter revealation
	%% SubPlot3 is the winning condition
	%% SubPlot4 is the loss condition
	%% SubPlot5 is the unwinnable condition
	SubPlot1 <*:CS_MechaEncounter&Raiders 2 3>
	SubPlot2 <*:CS_RevealEncounter_Raiders 2 3>
	SubPlot3 <*CS_CAWinMission 1>
	SubPlot4 <*CS_CALoseMission 1>
	SubPlot5 <*CS_MootMission 1>

	sub
		Persona 1
		greeting <if= PlotStatus %plotid1% 0 else .%id%_GoRemind if# PlotStatus %plotid% %id% else .%id%_GoOfferMission Goto %pop%>
		.%id%_GoOfferMission <ifChatNPCUnknown else .%id%_GoKnownGreeting ifChatNPCAlly else .%id%_GoUnknownGreeting Goto .%id%_GoKnownGreeting>
		*.%id%_GoRemind <*GenericMissionReminder>
		*.%id%_GoKnownGreeting <*IHaveAJobForYou .%id%_GoBriefing>
		*.%id%_GoUnknownGreeting <*AreYouHereAboutJob&Mecha .%id%_GoBriefing>
		*.%id%_GoBriefing <*CS_BasicFightingMission .%id%_GoAccept .%id%_GoDeny>
		*.%id%_GoAccept <*GoodLuckOnMission .%id%_GoR1Exit ChatNPCFac &EnemyFac>
		.%id%_GoR1Exit <SetPlotStatus %plotid1% %id1%  SetPlotStatus %plotid2% %id2%>
		*.%id%_GoDeny <*RejectMission .%id%_GoR2Exit>
		.%id%_GoR2Exit <LoseComp 0 .%id%_changes>
		.%id%_changes <+T-- +F-->
	end
	inv
		stc CORE-ACTIVATABLE
		name <%name1%'s Mission>
	end


%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************



%%  *****************************************
%%  ***   +Pme	PC meets an enemy pilot   ***
%%  *****************************************


%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************


%%  ***********************************************************
%%  ***   +Psh	PC is shanghaied into working for faction   ***
%%  ***********************************************************


%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************


%%  ****************************************
%%  ***   +Pew  Enemy  Weapon  Program   ***
%%  ****************************************



%%  ************************************
%%  ***   +Cme  Mentoring  Partner   ***
%%  ************************************


