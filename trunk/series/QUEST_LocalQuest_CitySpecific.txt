%%
%%  *:GuaranteedQuest Content
%%
%% This content is guaranteed to show up at the given location.

Content
	name <Climbing the Ladder, First Rung>
	desc <Theles Spinner is divided into regions based on social class. The PC has a chance to climb the ladder.>
	requires <*:GuaranteedQuest THESP>

	% The first rung is pretty simple- the player can either buy his way into
	% the top level, or complete a grueling dungeon.
	% E1 is the "gateman"
	% E2 is the entrance to the gateman's scene
	% E3 is the dungeon leading to the top
	% E4 is the middle layer of the spinner
	Element1 <Prefab>
	Scene1 <New *THESP_FirstRung>
	Team1 <Citizens>
	TeamData1 <Pass>

	Element2 <Prefab>
	Scene2 <City Urban>

	Scene3 <New *THESP_IndustrialSpire>

	Scene4 <New *THESP_SecondLevel>

	% SubQuest1 is rung two: the journey from the middle layer to joining the ranks of the technocrats.
	Quest1 <*THELES_RungTwo #50 4>

	sub
		Persona 1
		% V1 = Threat Counter
		greeting <if= QuestStatus %qid% 0 else GoChat NewChat Say 1 AddChat 1 AddChat 2>
		GoChat <NewChat SayAnything>
		result1 <ifG PCRenown 10 else GoR1Fail ifG PCLaw -1 else GoR1Fail NewChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		GoR1Fail <NewChat Say 3 AddChat 4>
		result2 <NewChat Say 4>
		result3 <NewChat Say 5 SetQuestStatus %qid% %id1% XPV 100 Cash+ -500000>
		result4 <NewChat Say 6 V= 1 1>
		result5 <NewChat Say 7>
		Msg1 <Access to the second level is restricted to those with SecTag Beta. I'm afraid that I can't let you pass.>
		Msg2 <In order to weed out the scum and the riffraff, applicants for SecTag Beta must perform a demonstration of their industriousness and determination. This demonstration takes the form of a $500,000 fee.>
		Msg3 <I'm sorry, but you are strictly inelgible for this security clearance. The technocrats only allow those who have proven their ability, drive, and strength of character to advance.>
		Msg4 <Of course not! You don't think the technocrats would be stupid enough to allow that, do you? Even just wondering about such things is treasonous...>
		Msg5 <This money proves your worth to Theles Spinner. When you step into the elevator, your brain-pattern will be scanned and added to the database. On level two you should head to the Ascension Complex; they'll probably have work for you there.>
		Msg6 <It's not up to me, I just explain how things work. The real decision is made by the Ascension Complex. If an unauthorized person attempts to board the elevator, instead of going to the second level it goes straight to a plasma furnace.>
		Msg7 <Be seeing you.>
		Prompt1 <How do I get this "SecTag Beta"?>
		Prompt2 <Isn't there any other way up there?>
		Prompt3 <No problem. [Give $500000]>
		CPrompt3 <ifG PC$ 499999 Accept>
		Prompt4 <Let me in now, or else.>
		CPrompt4 <if= V1 0 Accept>
		Prompt5 <I'll be back later.>

		MetaScene 1
		sub
			Room
			minimap <##2##............1..#---#>
			special <SHAREDPALETTE NOGO>
			desig <HOME>
			inv
				Elevator
				MiniMapComponent 2
				desig <ENTRANCE %s4%>
				use <if# QuestStatus %qid% 0 else GoAccessDenied Print -1 Exit Destination>
				GoAccessDenied <Print 1>
				Msg1 <You have not been authorized to use this elevator.>
			end

			Room
			minimap <##2###...###?###3.4###=##>
			special <SHAREDPALETTE NOGO>
			sub
				Door
				use <if= StatVal STAT_Lock 0 else GoDoorLocked if= v1 0 else GoJustUse ifYesNo 101 102 103 else GoLeaveDoor V= 1 1 Goto GoJustUse>
				GoJustUse <ifG -99 MetaPass else GoCloseDoor  if= StatVal STAT_Lock 0 else GoDoorLocked Print 1  SetStat STAT_Pass 0  Transform 1>
				GoLeaveDoor <Print 104>
				Msg101 <Central life support and mechanicals. Access strictly prohibited.>
				Msg102 <Open the door>
				Msg103 <Leave it alone>
				Msg104 <You leave the door alone.>
			end
			inv
				Elevator
				MiniMapComponent 2
				desig <ENTRANCE %s3%>

				STC Crate
				MiniMapComponent 3

				STC Crate
				MiniMapComponent 4
			end
		end

		MetaScene 3
		sub
			Room
			minimap <............2............>
			special <SHAREDPALETTE>
			inv
				StairsUp
				use <Print 1 Exit %s4% SeekGate %s1%>
				MiniMapComponent 2
				update <SetDestination %s4%>
			end
		end

		MetaScene 4
		start <if= QuestStatus %qid% 0 else %pop% SetQuestStatus %qid% %id1% Alert 11 XPV 100 Goto %pop%>
		Msg11 <[Voice]: You have proven yourself worthy of level two clearance. Please report to Ascension Complex immediately or face termination.>
		inv
			STC Building
			name <Theles Control Office>
			Destination -1
		end
	end
	inv
		NPC Soldier
		job <Gateman>

		STC Building
		name <Theles Control Office>
		desig <ENTRANCE %s1%>
	end

Content
	name <Climbing the Ladder, Second Rung>
	desc <Theles Spinner is divided into regions based on social class. The PC has a chance to climb the ladder.>
	requires <*THELES_RungTwo>

	% For the second rung, the PC must prove his or her worth to the technocracy
	% in order to gain access to the third level. This can be done by completing
	% a series of quests for the residents of the second level.
	% E1 is the second level outdoors scene.
	% E2 is the second gateman at the Ascension Complex.
	% E3 is the Ascension Complex entrance
	% E4 is the NPC at the shopping center who will offer a quest
	% E5 is the entrance to the shopping center
	% E6 is the NPC at the factory control who will offer a quest
	% E7 is the entrance to the factory control office
	% E8 is the third level
	% E9 is the NPC at the population control who will offer a quest
	% E10 is the entrance to the population control office
	Element2 <Prefab>
	Scene2 <New *THESP_AscensionComplex>
	Team2 <Citizens>
	TeamData2 <Pass>
	Element3 <Prefab>
	Scene3 <Grab 1>
	Element4 <Prefab>
	Scene4 <New *THESP_Armory>
	Team4 <Citizens>
	TeamData4 <Pass>
	Element5 <Prefab>
	Scene5 <Grab 1>
	Element6 <Prefab>
	Scene6 <New *THESP_Industrial>
	Team6 <Citizens>
	TeamData6 <Pass>
	Element7 <Prefab>
	Scene7 <Grab 1>
	Scene8 <New *THESP_ThirdLevel>
	Element9 <Prefab>
	Scene9 <New *THESP_Population>
	Team9 <Citizens>
	TeamData9 <Pass>
	Element10 <Prefab>
	Scene10 <Grab 1>

	% SubQuest1 is E4's mission
	% SubQuest2 is E6's mission
	% SubQuest3 is E9's mission
	% SubQuest4 is the third rung
	Quest1 <*:GiveMission #50 4>
	Quest2 <*:GiveMission #50 6>
	Quest3 <*:GiveMission #50 9>
	zQuest4 <*THELES_RungThree #60 8>

	sub
		Persona 2
		rumor%id% <people granted access to level three never return to the lower levels.>
		% V1 = Asked for work counter
		greeting <if= QuestStatus %qid% %id% else GoChat NewChat Say 1 AddChat 1 AddChat 2>
		GoChat <NewChat SayAnything>
		result1 <NewChat Say 2 AddChat 3>
		result2 <NewChat Say 3 v= 1 1 AddChat 4>
		result3 <NewChat Say 4 AddChat 4>
		result4 <NewChat Say 5 AddChat 5>
		result5 <NewChat Say 6>
		Msg1 <This is the Ascension Complex. It is our job to enact the orders of the technocrats above on the city floor below.>
		Msg2 <All residents of level two are expected to file periodic reports for the technocrat council. You can enclose your questions or comments with this report.>
		Msg3 <Let me check the databoard... No specific orders for you have come from level three, but if you ask around I'm sure you'll find things which need doing.>
		Msg4 <I'm afraid that's not possible. The technocrats never come down to the lower levels. When they choose to interact directly with the rest of us, which isn't very often, they do so via phone or holopresence.>
		Msg5 <Sometimes residents of the second level are promoted into the ranks of the technocrats. If you want to advance you should work hard and prove your value to Theles Spinner, though I should warn you that it might not be a good idea.>
		Msg6 <I've seen loads of people go into that door, but I've never seen anyone come back out. I don't know what happens to the people who become technocrats but I do know that they never leave.>
		Prompt1 <I want to speak with the technocrats.>
		Prompt2 <I was told you'd have some work for me?>
		CPrompt2 <if= V1 0 Accept>
		Prompt3 <No, I mean I want to speak with them in person.>
		Prompt4 <How do I gain access to level three myself?>
		Prompt5 <Why do you say that?>

		Persona 4
		% V%id%01 = Have given reward
		greeting <if= QuestStatus              %qid1% -1   else GoCheckLoss if= v%id%01 0 else GoWin v= %id%01 1  CashPrize Reward %threat% 200  Goto GoWin>
		GoCheckLoss <if= QuestStatus           %qid1% -2   else GoCheckFirst Goto GoLose>
		GoCheckFirst <if= QuestStatus          %qid1%  0   else GoChat if= QuestStatus %qid% %id% else GoChat Goto GoOfferMission>
		*GoOfferMission <*AreYouHereAboutJob  .%id1%_GoInit>
		GoEnd <SetQuestStatus                  %qid1% -3>
		GoChat <NewChat SayAnything>
		*GoLose <*MissionWasFailure GoEnd na na>
		*GoWin <*MissionWasSuccess&Reward GoEnd na na>

		Persona 6
		% V%id%01 = Have given reward
		greeting <if= QuestStatus              %qid2% -1   else GoCheckLoss if= v%id%01 0 else GoWin v= %id%01 1  CashPrize Reward %threat% 200  Goto GoWin>
		GoCheckLoss <if= QuestStatus           %qid2% -2   else GoCheckFirst Goto GoLose>
		GoCheckFirst <if= QuestStatus          %qid2%  0   else GoChat if= QuestStatus %qid% %id% else GoChat Goto GoOfferMission>
		*GoOfferMission <*AreYouHereAboutJob  .%id2%_GoInit>
		GoEnd <SetQuestStatus                  %qid2% -3>
		GoChat <NewChat SayAnything>
		*GoLose <*MissionWasFailure GoEnd na na>
		*GoWin <*MissionWasSuccess&Reward GoEnd na na>

		Persona 9
		% V%id%01 = Have given reward
		greeting <if= QuestStatus              %qid3% -1   else GoCheckLoss if= v%id%01 0 else GoWin v= %id%01 1  CashPrize Reward %threat% 200  Goto GoWin>
		GoCheckLoss <if= QuestStatus           %qid3% -2   else GoCheckFirst Goto GoLose>
		GoCheckFirst <if= QuestStatus          %qid3%  0   else GoChat if= QuestStatus %qid% %id% else GoChat Goto GoOfferMission>
		*GoOfferMission <*AreYouHereAboutJob  .%id3%_GoInit>
		GoEnd <SetQuestStatus                  %qid3% -3>
		GoChat <NewChat SayAnything>
		*GoLose <*MissionWasFailure GoEnd na na>
		*GoWin <*MissionWasSuccess&Reward GoEnd na na>

		MetaScene 2
		% V11 = surprise message counter
		start <if= L11 1 else %pop% ifNPCOK %2% else %pop% Monologue %2% 11 L= 11 2>
		Msg11 <Well, there's a first time for everything!>
		sub
			Room
			minimap <##2##............1..#---#>
			special <SHAREDPALETTE NOGO>
			desig <HOME>
			inv
				Elevator
				% V1 = Have used the elevator already				MiniMapComponent 2
				desig <ENTRANCE %s8%>
				use <if= V1 1 else GoCheckEligibility  Print -1 Exit Destination>
				%% There are two ways to gain access to L3: First, the PC
				%% can simply obtain a lot of renown and money, and the door
				%% will magically open. Second, the player can complete all
				%% the miniquests on this level, in which case the door will
				%% also open.
				GoCheckEligibility <ifG PCRenown 60 else GoCheckM1 ifG PC$ 1000000 else GoCheckM1 Goto GoAccessGranted>
				GoCheckM1 <if= QuestStatus %qid1% -1 else GoAccessDenied if= QuestStatus %qid2% -1 else GoAccessDenied if= QuestStatus %qid3% -1 else GoAccessDenied Goto GoAccessGranted>
				GoAccessGranted <ifYesNo 2 3 4 V= 1 1 XPV 100 SetQuestSTatus %qid% %id4% L= 11 1 Print -1 Exit Destination>
				GoAccessDenied <Print 1>
				Msg1 <You have not been authorized to use this elevator.>
				Msg2 <The door of the elevator opens as you approach. You think back on the stories you've heard- people who go to level three never return to the lower levels.>
				Msg3 <Go to level three.>
				Msg4 <Stay right here.>
			end
		end

	end
	inv
		NPC Soldier
		job <Gateman>

		STC Building
		name <Ascension Complex>
		desig <ENTRANCE %s2%>

		NPC Shopkeeper
		job <Production Manager>

		STC Building
		name <Second Tier Armory>
		desig <ENTRANCE %s4%>

		NPC Mechanic
		job <Production Manager>

		STC Building
		name <Theles Industrial>
		desig <ENTRANCE %s6%>

		NPC Politician
		job <Security Chief>

		STC Building
		name <Population Control>
		desig <ENTRANCE %s6%>
	end


Content
	name <Ronald McComet Must Die>
	desc <Someone doesn't like CometBurger. Will the PC stand up for fast food or become a culinary terrorist?>
	requires <*:GuaranteedQuest FUCSP>

	% Element1 is the terrorist
	% Element2 is the L5Law officer investigating the claim
	% Element3 is the robot-infested factory level
	% Element4 is the burger vat level
	% Element5 is the Bac-Bomh
	% Element6 is the meme
	Element1 <Prefab>
	Scene1 <Building Public>
	team1 <Citizens>
	teamdata1 <Pass Ally>
	Element2 <Prefab>
	Scene2 <Building CometBurger Public>
	team2 <Citizens>
	teamdata2 <Pass Ally>
	Scene3 <New *RobotFactory>
	Scene4 <New *BLANK.BoxBasement>
	Element5 <Prefab>
	Place5 <1>
	Element6 <Prefab>

	% SubQuest1 is the E1 victory
	% SubQuest2 is the E2 victory
	Quest1 <*Success1.CriminalTransaction 1>
	Quest2 <*SuccessVsChar.Evidence 2 1>

	sub
		Persona 1
		% V1 = Have given quest
		rumor0 <%name1% has a vendetta against Comet Burger.>
		KeyWorrds <TERRORIST>
		greeting <if# QuestStatus %qid% %id% else GoDoQuest if# QuestStatus %qid% 0 else GoDoQuest Goto GoNoQuest>
		GoDoQuest <if= V1 1 else GoOfferQuest NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoNoQuest <NewChat Say 17>
		% If the PC has accepted the job offer from E2, getting the job offer here will
		% be more difficult.
		GoOfferQuest <ifItemOK %5% else GoRuinQuest if= QuestStatus %qid% %id% else GoPlainOffer NewChat Say 13 AddChat 12 AddChat 13>
		GoPlainOffer <NewChat Say 5 AddChat 5 AddChat 6>
		GoRuinQuest <SetQuestStatus %id% -2 NewChat SayAnything>
		result1 <EndChat Say 2 AddChat 4>
		result2 <NewChat Say 3>
		result3 <SetQuestStatus %qid% -2 Goto GoR3Finish>
		*GoR3Finish <*YouLostItem>
		result4 <AddLawful -5 AddEasygoing -10 AddReact 20 History 4 DeleteItem %5% XPV 100 ActivateMeme %6% %s1% Goto .%id1%_GoInit>
		result5 <EndChat Say 6>
		result6 <NewChat Say 7 AddChat 7 AddChat 8 AddChat 9>
		result7 <ifChatNPCInPlay else GoR7NotHere NewChat Say 8 QMemo %qid% 9 V= 1 1 SetQuestStatus %qid% %id% GiveItem %5%>
		GoR7NotHere <NewChat Say 18>
		result8 <NewChat Say 10>
		result9 <NewChat Say 11 AddChat 10 AddChat 11>
		result10 <Goto result7>
		result11 <NewChat Say 12>
		result12 <ifIntimidation SkillTar %threat% else GoR12Fail NewChat Say 14 AddChat 10 AddChat 8 AddChat 9>
		GoR12Fail <NewChat Say 15>
		result13 <ifConversation SkillTar %threat% else GoR13Fail NewChat Say 14 AddChat 10 AddChat 8 AddChat 9>
		GoR13Fail <NewChat Say 16>
		Msg1 <Have you planted the Bac-Bomb in the CometBurger basement yet?>
		Msg2 <Bwa ha ha! When I press this button to detonate the bomb, Comet Burger will be forced to close! At the very least they'll be forced to throw out lots of patties and detoxify the area, which should eat into their profits quite a bit!!!>
		Msg3 <Every minute you waste is another two hundred bland mass-produced hamburgers sold to unwitting consumers!>
		Msg4 <You helped %name1% attack the CometBurger main office.>
		Msg5 <Do you like Comet Burger?>
		Msg6 <They say that repeated consumption dulls your taste buds and rots your brain... it may already be too late to save you.>
		Msg7 <Do you hate Comet Burger with the same passion I feel? Do you hate them enough to cross the law and risk your very life to bring them down?>
		Msg8 <Then take this device- sneak the bacteria bomb into the sub-basement of CometBurger's main office, then come back here and we can detonate it remotely. No people will be hurt but all their patties will be ruined!>
		Msg9 <%name1% in \SCENE %s1% gave you a bacteria bomb to detonate in the sub-basement of the CometBurger main office.>
		Msg10 <That's the trouble these days- people no longer care about the food they eat! It's all CometBurger's fault for lowering the bar!>
		Msg11 <Why? Because they are an affront to all that is good and pure in the world. Their mass-produced flavorless slop is an insult to true cuisine. Now, are you going to help me destroy them, or are you one of the enemy?>
		Msg12 <I tried that. It didn't work.>
		Msg13 <What are you looking at? I'm a very busy person... I don't have time to waste talking to crazies.>
		Msg14 <I'm plotting the destruction of CometBurger! Their loathsome fast food must be stopped, and if the law won't do anything it's up to us to do it ourselves! Are you with me or against me?>
		Msg15 <You don't sound like you really mean that... the burger sauce must have rotted your brain...>
		Msg16 <I'm not telling you my plan... You might be one of them!>
		Msg17 <The price we pay for good food is eternal vigilance and sometimes violence.>
		Msg18 <Come see me in \SCENE %s1% then. I can't do everything over the phone!>
		Prompt1 <It has been done.>
		CPrompt1 <if= ItemScene %5% %s4% Accept>
		Prompt2 <Uh, no, not yet.>
		Prompt3 <I sort of lost the bomb.>
		CPrompt3 <ifItemDestroyed %5% Accept>
		Prompt4 <[Continue]>
		Prompt5 <Yes I do. It's great!>
		Prompt6 <No, not particularly.>
		Prompt7 <Yes, absolutely!>
		Prompt8 <No, I don't care that much.>
		Prompt9 <Why don't you like Comet Burger?>
		Prompt10 <I'll help you!>
		Prompt11 <What you need is psychological help...>
		Prompt12 <I hate Comet Burger!>
		Prompt13 <What are you doing?>

		Persona 2
		% V1 = Have given quest
		rumor0 <%name2% is investigating a culinary terrorist.>
		greeting <if# QuestStatus %qid% %id% else GoDoQuest if# QuestStatus %qid% 0 else GoDoQuest Goto GoNoQuest>
		GoDoQuest <if= V1 1 else GoOfferQuest  NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoNoQuest <*MISC_CHATTER>
		*GoOfferQuest <*ENEMY_CHECK GoRealOffer 9 na>
		% If the PC has accepted the job offer from E2, giving the Bac-Bomb to E2 will be
		% a bit of a letdown.
		GoRealOffer <if= QuestStatus %qid% %id% else GoPlainOffer NewChat Say 4 AddChat 4>
		GoPlainOffer <NewChat Say 6 AddChat 5 AddChat 6>
		result1 <FreezeNPC %1% PCEnemy %1% History 2 DeleteItem %5% AddLawful 1 Goto .%id2%_GoInit>
		result2 <NewChat Say 3>
		result3 <SetQuestStatus %qid% -2 Goto GoR3Finish>
		*GoR3Finish <*YouLostItem>
		result4 <NewChat Say 5 DeleteItem %5% AddLawful 1 SetQuestStatus %qid% -2>
		result5 <NewChat Say 7 QMemo %qid% 8 V= 1 1 SetQuestStatus %qid% %id% AddChat 7>
		result6 <NewChat Say 9>
		result7 <NewChat Say 10>
		Msg1 <Have you collected any evidence against %name1% yet?>
		Msg2 <You helped %name2% stop %name1% from attacking CometBurger HQ.>
		Msg3 <Well then, see what you can manage.>
		Msg4 <We've received a tip that a culinary terrorist is planning to strike against this building. Unfortunately we don't yet have any evidence with which to make a case.>
		Msg5 <Thank you... I'm not sure exactly what it is, but the lab guys will find out. I'm sure this might be an object of particular significance.>
		Msg6 <There have been reports about a culinary terrorist planning to strike this building, but the police have everything under control.>
		Msg7 <It's certainly possible. Our main suspect is %name1%, but so far we haven't been able to find any evidence implicating \OPR %1% . If you could obtain something like that it would really help our investigation.>
		Msg8 <%name2% sent you to find evidence that %name1% in \SCENE %s1% is planning an attack against CoometBurger.>
		Msg9 <Good. It's nice to know that we have the trust of the public.>
		Msg10 <Reports indicate that \OPR %1% can be found at \SCENE %s1% . Good luck.>
		Prompt1 <I have \PPR %1% Bac-Bomb right here.>
		CPrompt1 <ifKeyItem %5% ifChatNPCInPlay Accept>
		Prompt2 <Still working on that.>
		Prompt3 <I think the evidence has been destroyed.>
		CPrompt3 <ifItemDestroyed %5% Accept>
		Prompt4 <I have some evidence. [give Bac-Bomb]>
		CPrompt4 <ifKeyItem %5% ifChatNPCInPlay Accept>
		Prompt5 <Is there anything i could do to help?>
		Prompt6 <I'm not too worried about it.>
		Prompt7 <Where can I find %name1%?>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		get%5% <if= QuestStatus %qid% 0 ifNPCOK %1% ForceChat %1%>

		MetaScene 2
		sub
			Room
			minimap <.......2............##+##>
			special <SHAREDPALETTE NOGO>
			sub
				Door
				use <if= v1 0 else GoJustUse ifYesNo 101 102 103 else GoLeaveDoor V= 1 1 Goto GoJustUse>
				GoJustUse <ifG -99 MetaPass else GoCloseDoor  if= StatVal STAT_Lock 0 else GoDoorLocked Print 1  SetStat STAT_Pass 0  Transform 1>
				GoLeaveDoor <Print 104>
				Msg101 <WARNING: Factory level contains unsupervised workbots. Enter at own risk.>
				Msg102 <Open the door>
				Msg103 <Leave it alone>
				Msg104 <You leave the door alone.>
			end
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s3%>
			end
		end

		MetaScene 3
		% Contains the entrance to the patty-making level.
		sub
			Room
			minimap <#...#.......2.......#...#>
			special <SHAREDPALETTE>
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s4%>
			end
		end

		MetaScene 4
		start <if= v1 0 else GoBeenBefore Alert 1 V= 1 1>
		GoBeenBefore <Print 2>
		Msg1 <This level contains the cometburger generation facilities. Translucent patties of cloned meat can be seen floating in nutrient tanks.>
		Msg2 <You enter \SCENE SceneID .>
		sub
			Team 1

			Team 2

			Room
			minimap<......2.3.......4.5......>
			special <SHAREDPALETTE>
			inv
				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 2
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 3
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 4
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 5
				SetTeam 2
			end

			Room
			minimap<......2.3.......4.5......>
			special <SHAREDPALETTE>
			inv
				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 2
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 3
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 4
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 5
				SetTeam 2
			end

			Room
			minimap<......2.3.......4.5......>
			special <SHAREDPALETTE>
			inv
				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 2
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 3
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 4
				SetTeam 2

				STC BIOTANK
				use <Print 1>
				msg1 <You can see thousands of cometburger patties floating in the nutrient solution.>
				MiniMapComponent 5
				SetTeam 2
			end

		end
	end
	inv
		NPC Citizen
		Combatant
		CharDesc Criminal Passionate
		job <Food Critic>
		job_desig <ADVEN>

		NPC Police Officer
		SetFaction 9

		Treasure 15
		name <Bac-Bomb>
		desc <A home-made bacteriological bomb. There is no obvious way to trigger it.>

		Meme
		MaxMemeViews 3
		Msg <Did you hear that someone set off a bomb in the CometBurger factory? I never thought it could happen here...>
		Msg_1 <Everybody's talking about the bomb that went off at CometBurger. I wonder who could have done something like that...>
		CMsg_1 <ifNPCSociable Accept>
		Msg_2 <I think I know who planted the bomb at CometBurger, but I can't say until I find some evidence.>
		CMsg_2 <ifNPCShy Accept>
		Msg_3 <How could anyone do such a crappy thing to CometBurger? I mean, everybody loves the food there!>
		CMsg_3 <ifNPCEasygoing Accept>
		Msg_4 <It's terrible what happened at CometBurger headquarters... but a part of me thinks they brought it on themselves for serving crappy food.>
		CMsg_4 <ifNPCPassionate if# ChatNPCFac 2 Accept>
		Msg_5 <>
		CMsg_5 <ifNPCCheerful Accept>
		Msg_6 <>
		CMsg_6 <ifNPCMelancholy Accept>
		Msg_7 <Somebody bombed CometBurger! Oh yeah, stickin' it to the man!>
		CMsg_7 <ifNPCCriminal if# ChatNPCFac 2 Accept>
		Msg_8 <This attack must have been carried out by our enemies. The strange thing is, if it were MUGL the bomb would have been painted pink, and if it were Hoeller they would have just sent mecha.>
		CMsg_8 <if= ChatNPCFac 2 Accept>

	end

%%
%%  *:LocalQuest Content
%%
%% This content is tied to a city rather than any specific character within it.
%% Unlike the regular LocalQuests, these quests are tied to specific cities.


Content
	name <The Cayley Instrumentality Project>
	desc <Hoeller industries is building a new factory here; The PC may need to help.>
	requires <*:LocalQuest "CAYLE">

	% E1 is the hoeller executive
	% E2 is the construction site
	% E3 is the entry to the construction site
	% E4 is the corporate office that opens after this quest is completed
	%  Initially E4 has no faction assigned; once its faction gets assigned, it's
	%  open for business.
	% E5 is the meme telling all about it
	element1 <Prefab>
	Scene1 <Building Public>

	Scene2 <New *ConstructionSite>

	element3 <Prefab>
	Scene3 <Environs>

	Scene4 <New *Office>

	Element5 <Prefab>

	Quest1 <*:ConstructionProblem 1 2>
	Quest2 <*Success1.FinishMission 1>

	sub
		Persona 1
		rumor0 <%name1% is in charge of building the new Hoeller factory.>
		greeting <if= QuestStatus %qid% %id% else GoFirstTime NewChat Say 3 AddChat 3 AddChat 4 AddChat 5>
		*GoFirstTime <*ENEMY_CHECK GoGreeting ChatNPCFac GoMakeEnemy>
		GoMakeEnemy <PCEnemy %1% SetQuestStatus %qid% -2>
		*GoGreeting <*NiceToMeetYou GoCheckFaction>
		GoCheckFaction <if= QuestStatus %qid% 0 else GoChat  if= PCFac ChatNPCFac else GoCheckReact  Goto GoOfferMission>
		GoCheckReact <ifG React %threat% else GoChat  Goto GoOfferMission>
		*GoChat <*MISC_CHATTER>
		GoOfferMission <NewChat Say 1 AddChat 1 AddChat 2 AddChat 6>
		result1 <NewChat Say 2 SetQuestStatus %qid% %id%  SetQuestStatus %qid1% %id1%  QMemo %qid% 5>
		result2 <NewCHat Say 4 SetQuestStatus %qid% -2 ifNPCVillainous PCEnemy %1%>
		result3 <FacXP+ 7 5   SetSceneFaction %4% 7   ActivateMeme %5% %s1%   Goto .%id2%_GoInit>
		result4 <NewChat Say 6>
		result5 <NewChat Say 7 SetQuestStatus %qid% -2 AddReact -10>
		result6 <NewChat Say 8 AddChat 7 AddChat 8>
		result7 <Goto result1>
		result8 <Goto result2>
		Msg1 <Hoeller Industries is building a new factory complex here at Cayley Rock. Unfortunately, our progress has been slower than anticipated. What I need is a spy at the job site, who can tell me why things aren't going as planned.>
		Msg2 <Fantastic. You can find the construction site on the surface of the asteroid. Come back and let me know when you have anything to report.>
		Msg3 <What have you discovered about the factory project?>
		Msg4 <Aha. Well, don't worry about it, then.>
		Msg5 <%name1% in \SCENE %s1% asked you to find out why the new Hoeller building hasn't been completed yet.>
		Msg6 <I see. Well, keep up the good work... and come back when you do have something to tell.>
		Msg7 <Hmm. The board of directors is not going to like this...>
		Msg8 <We already have a smelter here. Since the mine closed there are lots of skilled workers available, and labor costs are lower than anywhere but Theles Spinner. Cayley Rock is also close to several major trade points. So, can you help me?>
		Prompt1 <I could do that for you.>
		Prompt2 <That doesn't sound like my kind of job.>
		CPrompt2 <if# PCFac ChatNPCFac Accept>
		Prompt3 <Construction will be completed shortly.>
		CPrompt3 <if= QuestStatus %qid1% -1 Accept>
		Prompt4 <I haven't found anything yet.>
		Prompt5 <The building project cannot be completed.>
		CPrompt5 <if= QuestStatus %qid1% -2 Accept>
		Prompt6 <Why build a factory here?>
		Prompt7 <Let me see what I can find out.>
		Prompt8 <Sorry, I don't think I can.>
		CPrompt8 <if# PCFac ChatNPCFac Accept>

		Metascene 2
		sub
			room
			minimap <......###..&2&...........>
			special <SHAREDPALETTE>
			sub
				Elevator
				name <Business Level>
				desig <Entrance %s4%>
				MiniMapComponent 2
				use <if# SceneFac %4% 0 else GoLocked Print 1 Exit Destination>
				GoLocked <Print 2>
				Msg1 <You board the elevator.>
				Msg2 <They haven't finished work on this level yet.>
			end

		end


	end
	inv
		NPC Corporate Executive
		SetFaction 7

		STC Building
		name <Hoeller Construction Site>
		desig <ENTRANCE %s2%>

		Meme
		%% After the corporate office is opened, this meme will let the player know.
		MaxMemeViews 2
		Msg <The new Hoeller corporate office has opened for business. They have some shops there, too.>
		Msg_1 <Have you seen the new Hoeller office? It's good that there's finally some new stores here.>

	end

