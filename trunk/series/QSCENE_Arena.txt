%%
%%	Mecha Arena Scenes
%%

%% All the arenas follow basic rules. If you want a championship fight to take place
%% under different rules, you must make it a custom arena scene.

%% Arena State = Tells what should happen on START, and what checks should be made
%%	for nu1 and nu2.
%%  AS_Vacant = Nothing is currently happening in the arena
%%  AS_Ready  = When the PC enters the arena, start combat, set to AS_Battle
%%  AS_Battle = Check T1 and T2; if either hit 0, set AS_Win or AS_Loss
%%  AS_Win    = PC has won the battle; manager can reset the arena
%%  AS_Lose   = PC has lost the battle; manager can reset the arena

%% Arena Wins = How many times the PC has won at this arena
%% Arena Rewards = How many times the PC has collected rewards at this arena

%% Arena Threat = Difficulcy rating of the arena, usually set by manager during first chat
%% Arena Forces = A value describing how many mecha to add; set when activating ArenaState

%% Challenger ID = A single NPC who has been moved here to fight the PC
%% Challenger Home = Where to move the challenger after the fight is over


Scene
	name <# Arena>

	type <Arena>
	special <SOLO ARENA>
	terrain <GROUND>

	start <if= SelfArenaState NAG_AS_Ready else GoCheckBattle          Print 1  SetSelfArenaState NAG_AS_Battle if# SelfChallengerID 0 Print 4 ForceChat SelfChallengerID>
	GoCheckBattle <if= SelfArenaState NAG_AS_Battle else GoBoringStart Print 2>
	GoBoringStart <Print 3 Return>

	nu1 <if= T1 0 Return   if= SelfArenaState NAG_AS_Battle SetSelfArenaState NAG_AS_Loss  Print 5  AddRenown -5 Goto GoEndBattle>

	nu2 <if= T2 0 Return   if= SelfArenaState NAG_AS_Battle SetSelfArenaState NAG_AS_Win   Print 6  AddSelfArenaWin  AddSelfArenaThreat d10  XPV 100 FacXP+ 5 1 AddRenown 1 Goto GoEndBattle>

	GoEndBattle <ifNPCOK SelfChallengerID ForceChat SelfChallengerID MoveNPC SelfChallengerID SelfChallengerHome ClearSelfChallengerID>

	Msg1 <You enter the arena. Prepare to battle.>
	Msg2 <The arena battle rages on.>
	Msg3 <You enter the arena. There's no fight going on here now.>
	Msg4 <\PERSONA SelfChallengerID contacts you.>
	Msg5 <You have lost the battle.>
	Msg6 <You have won the battle.>

sub
	Team 1
	name <Player Team>
	SetEnemy 2

	Team 2
	name <Enemies>
	SetFaction 5
	SetEnemy 1
	Deploy <if= SelfArenaState NAG_AS_Ready ifG SelfArenaForces 0 WMecha 2 Threat SelfArenaThreat SelfArenaForces>

end
