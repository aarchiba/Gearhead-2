Plot 0

name <Defense Patrol>

context <=MIL_DEFAULT_DEFENSE>

element1 <Character HasMecha !Comrade -1>
element2 <Scene Outdoors !Near -3>
element3 <Prefab>
place3 <2>

% We need to store the two factions involved for later...
Element4 <Grab 1>
Element5 <Grab 2>

% P1 = Timer
% P2 = Faction of PC's enemy team
%      If PC is helping E1, this will be N2
%      If PC is fighting E1, this will be N1
% P3 = Combat has started; after combat has started, don't delete this plot from cleanup or timeout.
%	This var also functions as the initialization counter for the metascene.
% P4 = Victory condition achieved
% P5 = Difficulcy Rating

start <ifNPCOK E1 else CleanUp ifG V1 0 Goto GoCheckTime>
update <ifNPCOK E1 else CleanUp if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 86400 MoveNPC E1 E3  SetNPCTeam E1 3  SetEncounterName E3 .name  P= 5 PCRenown P+ 5 d6 P+ 5 -d8 ifG 2 P5 P= 5 d4>
.name <\ELEMENT 1 's Patrol>
GoCheckTime <ifG ComTime V1  if= P3 0  EndPlot>
CleanUp <if= P3 0  EndPlot>

sub
	Persona 1
	special <UNLISTED NOESCAPE>
	rumor <%name1% is conducting a patrol of %name2%.>
	% V1 = Reward offered
	greeting <if= P4 1 else GoNoWin CashPrize V1  EndPlot  Goto GoWeWon>
	*GoWeWon <*WeWon&Reward E4 E5>

	GoNoWin <if= P3 1 else GoCheckEnemy   EndChat SayAnything>

	GoCheckEnemy <Print 1   ifFactionEnemy N1 else GoCheckMember   P= 2 N1  SetNPCTeam E1 2   Goto GoFightPC>
	*GoFightPC <*BattleChallenge&Avoid GoThemeExpo GoExit>
	*GoThemeExpo <*Theme_Expo&Enemy GoStartCombat>

	GoCheckMember <P= 2 N2   V= 1 Reward Threat P5 150 120   if= PCFac N1 else GoCheckMission   Goto GoMemberJoin>

	*GoMemberJoin <*JoinOurFacPatrol GoJoinPatrol GoExit N1 N2>

	*GoCheckMission <*JoinMyPatrolTest GoJoinPatrol GoNoMission  P5  V1  GoExit  N1 N2>

	*GoJoinPatrol <*LetsStartPatrol GoStartCombat>
	GoStartCombat <Exit E3>
	GoNoMission <EndChat    Say 2   EndPlot>
	GoExit <EndPlot>

	Msg1 <You are contacted by \ELEMENT 1 .>
	Msg2 <I'm patrolling the city. No sign of enemy activity here.>
	Msg2_1 <You should know that mecha from \NARRATIVE 2 have been spotted inside \NARRATIVE 3 . There is no cause for civilians to be alarmed, as \NARRATIVE 1 will continue to protect this area against all threats.>
	CMsg2_1 <ifNPCSociable Accept>
	Msg2_2 <Nothing to see here. Please move along.>
	CMsg2_2 <ifNPCShy Accept>
	Msg2_3 <Everything's clear... no sign of \NARRATIVE 2 anywhere.>
	CMsg2_3 <ifNPCEasygoing Accept>
	Msg2_4 <I'm aching for combat and \NARRATIVE 2 are in the area...>
	CMsg2_4 <ifNPCPassionate Accept>
	Msg2_5 <Everything's fine, so long as \NARRATIVE 1 are here to keep \NARRATIVE 3 safe.>
	CMsg2_5 <ifNPCCheerful Accept>
	Msg2_6 <Be careful- this place could turn into a combat zone at any time.>
	CMsg2_6 <ifNPCMelancholy Accept>
	Msg2_7 <Rest assured that \NARRATIVE 1 will keep \NARRATIVE 3 safe from \NARRATIVE 2 .>
	CMsg2_7 <ifNPCHeroic Accept>
	Msg2_8 <I'm looking for some \NARRATIVE 2 pilots to kill... You seen any?>
	CMsg2_8 <ifNPCVillainous Accept>



	MetaScene 3 2
	MapWidth 30
	MapHeight 30
	content <Some 1 15 Sub *BATTLE_X ->

	% P3 = Initialization
	% L2 = Victory Counter

	start <if= P3 0 P= 3 1 PCFEnemy P2>

	nu1 <if= T1 0 Return if= L2 0 L= 2 1  AddRenown -8                    Goto GoLoss>
	nu2 <if= T2 0        if= L2 0 L= 2 1  XPV 100  AddRenown 1   P= 4 1   Goto GoVictory>

	% The PC has won this encounter... what happens next depends on who the PC was fighting.
	GoVictory <if= P2 E5 else GoVictoryForInvaders  S+ 101 1  FacXP+ E4 1   SALVAGE  ifNPCOK E1 else GoE1Died ForceChat E1>
	GoE1Died <EndPlot>
	GoVictoryForInvaders <S+ 101 -1   FacXP+ E5 2  EndPlot>

	% The PC has lost this encounter... what happens next depends on who the PC was fighting.
	GoLoss <if= P2 E5 else GoLossForInvaders  S+ 101 -1  EndPlot>
	GoLossForInvaders <S+ 101 1   EndPlot>

	sub
		Team 1
		SetAlly 3
		SetEnemy 2

		Team 2
		SetEnemy 1 3
		Deploy <SetSelfFaction P2   WMecha 2 Threat P5 150>

		Team 3
		SetAlly 1
		SetEnemy 2

	end

end
inv
	STC ENCOUNTER-WANDER
	use <ifG StatVal STAT_MetaVisibility -1      ifG PCScale 0   ForceChat E1>

	ATTACK <ifG StatVal STAT_MetaVisibility -1    ifG PCScale 0  ifUStealth 15 else GoAutoAttack ifYesNo 1 2 3 else GoAvoidAttack  ForceChat E1>
	GoAutoAttack <Print 5 ForceChat E1>
	GoAvoidAttack <Print 4 AddSociable -1>

	Msg1 <You spot a defense patrol led by \ELEMENT 1 of \FACTION NPCFac E1 .>
	Msg2 <Contact \ELEMENT 1 .>
	Msg3 <Avoid \ELEMENT 1 .>
	Msg4 <You slip away unnoticed.>


	ENCOUNTER_Defense

end
