%% *:CS_StopNPCMission Content
%%
%%	&IsEnemyNPC	The character to be stopped is the enemy NPC
%%	&Secondary	Is a secondary combat; disallow this tag if making
%%			major context changes
%%
%% Somebody is going on a mission. It's the PC's job to locate that
%% person and stop them from completing the mission. Most of these
%% components are timed- if the PC doesn't discover and halt the mission
%% in time, it counts as a loss.
%%
%% This subplot may alter the story context.
%%
%% When this subplot concludes, it sets one of the following triggers:
%%  .%id%_%plotid%_GoWin
%%  .%id%_%plotid%_GoLoss
%% It will also hide the encounter with SetEncounterInactive.
%%
%% This is a MAIN COURSE subplot. Favored by: POLIC
%%
%%
%%  Param1: The NPC to be stopped
%%

Content
	name <A Little Help From Your Friends>
	desc <The NPC reacts badly to a loss, but fortunately you have backup.>
	requires <*:CS_StopNPCMission &IsEnemyNPC E:A.equ (E:M.com|E:M.ggd)>
	changes <E:A>
	Size 5

	% E1 is the NPC launching this attack
	% E2 is an outdoors scene for the encounters
	% E3 is an encounter for the supposed mission- the PC completes this one first
	% E4 is the encounter for the ambush- after the PC wins E3
	% E5 is a friend/lover/ally to come to the rescue
	% E6 is a second one
	Place1 </>
	Element2 <Scene Outdoors !Near -7>
	Element3 <Prefab>
	Place3 <2>
	Element4 <Prefab>
	Place4 <2>
	Element5 <Character (Friend|Lover|Family|Lancemate) HasMecha !Global>
	Place5 <4 (Allies) sd ally>
	Element6 <Character (Friend|Lover|Family|Lancemate|PCFAC) HasMecha !Global>
	Place6 <4 (Allies) sd ally>

	% P%id%01 = Initialization Counter

	% At initialization, activate all the subplots and level the NPCs.
	update <if= p%id%01 0 p= %id%01 1 NPCLevel %5% StoryDL NPCLevel %6% StoryDL  SetPlotStatus %plotid1% %id1%  SetPlotStatus %plotid2% %id2%>

	% Upon the Mecha Encounter subplot being won, this subplot's ambush will be activated.
	% If the sub-encounter is lost, this plot is lost without needing an ambush.
	.%id1%_%plotid1%_GoWin <SetEncounterActive %4%>
	.%id1%_%plotid1%_GoLoss <Trigger0 .%id%_%plotid%_GoLoss LoseSubPlot %plotid%>

	% SubPlot1 is the combat encounter that serves as the supposed mission
	% SubPlot2 is the locate encounter thingamabob
	SubPlot1 <*:CS_MechaEncounter&Secondary&Raiders 2 3>
	SubPlot2 <*:CS_DiscoverNPCMission&EnemyNPC&GatheringForces 2 3 1>

	sub
		Persona 1
		special <UNLISTED NOESCAPE>
		greeting <EndChat Say 1 AddChat 1 AddChat 2>
		GoEndConversation <EndChat Say 2>
		result1 <AddSociable -d8   Goto GoEndConversation>
		result2 <AddSociable  d8   Goto GoEndConversation>
		Msg1 <You!!! I had this perfectly planned out, why did your friends have to come along and ruin it!?>
		Msg2 <I have to go, but my lance should still be able to defeat you! Men, don't allow any of them to escape!>
		Prompt1 <Don't look at me. I'm just as surprised as you are.>
		Prompt2 <Hey, I can't help it if I'm popular.>

		Metascene 4 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 50
		MapHeight 50

		start <if= L2 0 L= 2 1  Alert 1 Monologue %1% 2 Monologue %5% 3 Monologue %1% 4 Monologue %6% 5 ForceChat %1% History 6 SetXXRAttitude %1% XXR_A_EnviesPC>
		end <SetEncounterInactive %4%>

		nu1 <if= T1 0   Return   if= L1 0 L= 1 1  History 7                     Trigger0 .%id%_%plotid%_GoLoss   LoseSubPlot %plotid%>
		nu2 <if= T2 0   if= L1 0 L= 1 1   Alert 8 History 9  XPV 100  SALVAGE   Trigger0 .%id%_%plotid%_GoWin    WinSubPlot %plotid%>

		Msg1 <Without warning, you are ambushed by %name1%!>
		Msg2 <You can't win, \PC . As you can see I have predicted your every move.>
		Msg3 <Yeah, but you didn't predict that \PC would have backup!>
		Msg4 <Huh!? What in the name of vector sigma are you doing here?>
		Msg5 <We're here to defeat you once and for all. Don't worry, \PC , we can handle these creeps.>
		Msg6 <You were ambushed by %name1%, but %name5% and %name6% came to your aid.>
		Msg7 <Despite their help you were defeated.>
		Msg8 <%name1%'s lance has been defeated.>
		Msg9 <With their help you defeated %name1%'s lance.>

		sub
			team 1
			SetEnemy 2
			SetAlly 3
			ParaX 5
			ParaY 10

			team 2
			SetEnemy 1 3
			Deploy <if= PlotStatus %plotid% %id%  SetSelfFaction &EnemyFac  WMecha 2 StoryDL 100>
			ParaX 45
			ParaY 45

			team 3
			name <Allies>
			SetAlly 1
			SetEnemy 2
			ParaX 10
			ParaY 5
		end
	end
	inv
		stc CORE-ACTIVATABLE
		name <%name1%'s Forces>

		STC ENCOUNTER-SEEKPC
		EncounterMove 100
		name <Surprise Ambush>
		update <ifThisQEncActive else GoHide  SetStat STAT_MetaVisibility 0 SetSelfX PCX SetSelfY PCY Goto GoSetOrders>
	end


Content
	name <Factory Attack>
	desc <The NPC is going to launch an attack against a factory, but won't participate himself.>
	requires <*:CS_StopNPCMission ~&IsEnemyNPC (!Ne|!Lo) &Secondary>
	Size 8

	% E1 is the NPC launching the attack
	% E2 is an urban scene for placing the factory
	% E3 is the metascene for the mission
	Element2 <Scene Urban !Near -7>
	Element3 <Prefab>
	Place3 <2>

	%% FAIL CONDITIONS
	%% - The DiscoverNPCMission subplot fails
	%% - The timer runs out before combat is entered

	% p%id%01 = Initialization Counter
	% p%id%02 = Timer (24 to 30 hours)
	% p%id%03 = Reinforcements Counter

	% At initialization, set the timer and activate the two subplots.
	update <if= p%id%01 0 p= %id%01 1 p= %id%02 ComTime P+ %id%02 86400 P+ %id%02 d21600 SetPlotStatus %plotid1% %id1% SetPlotStatus %plotid2% %id2%>
	HalfHour <if# p%id%02 0 ifG ComTime p%id%02  CancelSubPlot %plotid1%  CancelSubPlot %plotid2%  LoseSubPlot %plotid% Trigger0 .%id%_%plotid%_GoLoss  History %id%01 if= RootSceneID &EpisodeScene else .%id%_GoDistant Alert %id%02>
	.%id%_GoDistant <Alert %id%03>

	end <ifSubPlotLost %plotid1% CancelSubPlot %plotid2% P= %id%02 -1 Goto HalfHour>

	.%id2%_%plotid2%_GoReinforcements <P= %id%03 1>

	Msg%id%01 <You were too late to prevent %name1% from destroying a factory.>
	Msg%id%02 <\SCENE &EpisodeScene is rocked as %name1%'s lance destroys its target.>
	Msg%id%03 <You feel a great disturbance in \SCENE &EpisodeScene .>

	% SubPlot1 is the DiscoverNPCMission task
	% SubPlot2 is the GainAdvantage_VsNPC task
	SubPlot1 <*:CS_DiscoverNPCMission&EnemyNPC&DestroyFactory 2 3 1>
	SubPlot2 <*:GainAdvantageVsMecha&Reinforcements>

	sub
		MetaScene 3 2
		% L1 = Initialization Counter
		% L2 = Victory Counter
		MapWidth 50
		MapHeight 50

		CityBlockMap
		terrain <GROUND>

		% At startup, clear the timer so the mission doesn't timeout while the PC is defending
		% the factory. That would just be silly.
		Start <if= L1 0  Alert 1  L= 1 1 P= %id%02 0  if# P%id%03 0 Alert 4>
		nu1 <if= T1 0 Return if= L2 0 L= 2 1 Goto GoLoseMission>
		nu2 <if= T2 0 if= T4 0 if= L2 0 L= 2 1 Salvage ifG T3 0 else GoLoseMission Goto GoWinMission>
		GoLoseMission <Alert 2             Trigger0 .%id%_%plotid%_GoLoss  LoseSubPlot %plotid%>
		GoWinMission  <Alert 3   History 5   XPV 100   Trigger0 .%id%_%plotid%_GoWin   WinSubPlot %plotid%>

		end <SetEncounterInactive %3%>

		Msg1 <You arrive just in time to defend the factory against %name1%'s forces. Fortunately, %name1% doesn't appear to be present.>
		Msg2 <You did not manage to save the factory.>
		Msg3 <You defended the factory against %name1%'s forces.>
		Msg4 <Your reinforcements are here, as promised.>
		Msg5 <You defended a factory against %name1%'s forces.>

		sub
			Team 1
			SetEnemy 2
			SetAlly 3 4
			ParaX 7
			ParaY 7

			Team 2
			SetEnemy 1 3 4
			Deploy <if= PlotStatus %plotid% %id%  SetSelfFaction &EnemyFac WMecha 2 ArenaRenown 200>
			ParaX 45
			ParaY 45

			Team 3
			SetEnemy 2
			SetAlly 1 4

			team 4
			setally 1 3
			setenemy 2
			Deploy <if= PlotStatus %plotid% %id%  if# P%id%03 0   WMecha 4 StoryDL 80>
			ParaX 25
			ParaY 7


			rect
			name <The Factory>
			desig <NW>
			sub
				SuperProp
				requires <*Fortress>
				SetTeam 3
			end
		end

	end
	inv
		stc CORE-ACTIVATABLE
		name <%name1%'s Goal>
	end


