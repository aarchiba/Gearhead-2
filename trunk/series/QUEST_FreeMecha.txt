%%
%% *:FreeMecha
%%
%% At the beginning of the game, there should be a chance for an enterprising
%% young character to earn a free mecha. Lazy and old characters should also be
%% able to get one. That's what these quests are for.
%%
%% If there's an NPC associated with this quest, he should be placed in the
%% Cavalier's Club, so the PC can find him easily.
%%
%% The free mecha should be generated using renown 15.

Content
	name <First Job>
	desc <There's someone looking for help, and you'll get paid in mecha.>
	requires <*:FreeMecha>

	% E1 is the root scene
	% E2 is the person who will be giving the mission.
	Element1 <.>
	Element2 <NewNPC 0 1 Easygoing>
	Scene2 <cavclub>
	Team2 <Citizens>
	TeamData2 <Pass>

	% SubQuest1 is the mission.
	Quest1 <*:GiveMission 2>

	sub
		Persona 2
		rumor0 <%name2% is looking for a cavalier.>
		% V1 = Have asked once
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst if= QUestStatus %qid1% -1 else GoCheckLoss EndChat Say 1 AddChat 1>
		GoCheckLoss <if= QuestStatus %qid1% -2 else GoCheckFirst SetQuestStatus %qid% -2 Goto GoEndInLoss>
		*GoEndInLoss <*MissionWasFailure na na na>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat if= V1 0 else GoBeenBefore NewChat Say 2 AddChat 2 AddChat 3>
		GoBeenBefore <NewChat Say 3 AddChat 2 AddChat 3>
		GoChat <NewChat SayAnything>
		result1 <NewChat Say 4 RandomMecha .mek 15 SetQuestStatus %qid% -1>
		.mek <GENERAL \FACTION_DESIG ChatNPCFac>
		result2 <SetQuestStatus %qid% %id% Goto .%id1%_GoInit>
		result3 <NewChat Say 5 V= 1 1 AddReact -1>
		Msg1 <You did it! I don't know how I'll ever be able to thank you.>
		Msg2 <Hey, are you a cavalier? I'm looking for one right now. There's a matter that I need some help with...>
		Msg3 <I still haven't found anyone to help me.>
		Msg4 <I don't have any cash on me to pay you with, but maybe you'd like this mecha. Have fun with it.>
		Msg5 <That's the problem... nobody does.>
		Prompt1 <[Continue]>
		Prompt2 <I'll do it. Tell me what you need.>
		Prompt3 <Tell someone who cares.>

		MetaScene 2
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %2% else %pop% SetQuestStatus %qid% -2 SetQuestStatus %qid1% -2 Goto %pop%>
	end


Content
	name <Hogye Memories>
	desc <There's an asteroid mine outside the station where you can find a mecha.>
	requires <*:FreeMecha (SPINNER|ASTEROID)>

	% E1 is the mine entrance
	% E2 is the dungeon
	% E3 is the elevator in the dungeon that will provide the mecha
	Element1 <Prefab>
	Scene1 <Environs>
	Scene2 <New *Dungeon.AsteroidMine>
	Element3 <Prefab>
	Scene3 <Grab 2>

	sub
		MetaScene 3
		sub
			room
			minimap <......###..##1..###......>
			special <SHAREDPALETTE>
			desig <HOME>
		end
	end
	inv
		STC QUEST-MAPMARKER-ACTIVE-STATIONARY
		name <Abandoned Mine>
		desig <Entrance %s2%>
		rumor0 <when the asteroid mine got abandoned, some heavy equipment was left behind.>

		Elevator
		name <Mecha Bay>
		use <if= QuestStatus %qid% 0 else GoBeenBefore ifYesNo 1 2 3 SetQuestStatus %qid% -1 RandomMecha .mektype 15>
		.mektype <GENERAL MUGLE>
		GoBeenBefore <Print 4>
		Msg1 <You find an old mecha left behind when the mine closed.>
		Msg2 <Open external bay doors.>
		Msg3 <Leave the mecha where it is.>
		Msg4 <There's nothing left here now.>
	end




