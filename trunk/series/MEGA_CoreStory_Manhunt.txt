%%
%% *CS_ManHunt_Confrontation Content
%%
%% The PC has found somebody who didn't want to be found.
%% What happens now? A fight? A conversation? Absolutely nothing?
%% Only the RNG knows for sure.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% Param1: The NPC being sought.
%% Param2: The scene where he'll be found
%%	If a metascene is provided, don't script it at all- we'll probably want to do
%%	scripting here.
%%

Content
	name <Target Has Hostage>
	desc <That target you've been chasing has a hostage.>
	requires <*CS_ManHunt_Confrontation (+Far|+Ffi) 2:META>
	Size 6

	% E3 is the hostage
	Element3 <NewNPC -3 -7>
	Place3 <2 (Citizens) pass>

	update <if= PlotStatus %id% else %pop% if= P%id%01 0 P= %id%01 1 NPCLevel %1% StoryDL>

	sub
		Persona 1
		% The person being sought... may be forced to surrender.
		Special <UNLISTED>
		greeting <if= PlotStatus %id% else %pop% ifChatNPCSurrendered else .%id%_GoFirstTime Goto .%id%_GoWillSurrender>
		.%id%_GoFirstTime <if= L2 0 else .%id%_GoGreet SoloAttack %1% L= 2 1 Goto .%id%_GoGreet>
		.%id%_GoGreet <EndChat Say %id%01 AddChat %id%01 AddChat %id%02>
		*.%id%_GoWillSurrender <*ISurrender&Arrest .%id%_GoSurrender>
		*.%id%_GoWillResist <*IDisagreeYouDie na>
		.%id%_GoSurrender <L= 2 2 Print %id%02 FreezeNPC %1% PCEnemy %1% ifNPCOK %3% else .%id%_GoE3Died StoryNote %id%03 Print %id%04 FreezeNPC %3% &SetTargetNPC %3% WinComp 0 .%id%_win>
		.%id%_GoE3Died <StoryNote %id%05 WinComp 0 .%id%_loss>
		.%id%_win <+Tgt +Frt>
		.%id%_loss <+T-- +F-->
		result%id%01 <ifConversation HardSkillTar StoryDL else .%id%_GoWillResist FreezeNPC %3% EndChat Say %id%06 XPV 100>
		result%id%02 <ifIntimidation HardSkillTar StoryDL else .%id%_GoWillResist EndChat Say %id%07 AddChat %id%03>
		result%id%03 <Goto .%id%_GoWillSurrender>
		Msg%id%01_10 <Why do you have to make this difficult, \PC ? I didn't want to hurt anyone today but it seems that you're tying my hands...>
		CMsg%id%01_10 <ifChatNPCKnown Accept>
		Msg%id%01_20 <You're getting involved in something you don't understand. %name3% and I have unfinished business.>
		CMsg%id%01_20 <ifChatNPCUnknown Accept>
		Msg%id%02 <%name1% is captured.>
		Msg%id%03 <You rescued %name3% from %name1%.>
		Msg%id%04 <%name3% slips away and disappears...>
		Msg%id%05 <You captured %name1%, but not soon enough to save %name3%.>
		Msg%id%06 <I'll let %name3% go, all right, but I've got no interest in talking...>
		Msg%id%07 <Wait, I've got a hostage, you can't attack me now!>
		Prompt%id%01 <Let %name3% go and we can talk.>
		Prompt%id%01_1 <You don't need a hostage; let %name3% go.>
		Prompt%id%01_2 <I want you to release %name3%.>
		Prompt%id%02 <Give up now before I'm forced to kill you.>
		Prompt%id%02_1 <You have one chance to surrender, then you die.>
		Prompt%id%02_2 <It's over, %name1%. Give up.>
		Prompt%id%03 <Do I look like I care?>

		Metascene 2
		% Contains scripts for the scene.
		% L1 = Initialization Counter
		% L2 = Have started fight (1), have ended fight (2)
		start <if= L1 0 L= 1 1 Alert 1 Monologue %3% 2 ForceChat %1%>
		end <if= L2 1 FreezeNPC %1% PCEnemy %1% DeleteNPC %3% StoryNote 3 LoseComp 0 .loss>
		.loss <+T-- +F-->
		Surrender%1% <ForceChat %1%>
		Faint%1% <L= 2 2 ifNPCOK %3% else GoE3Died StoryNote 4 Print 5 FreezeNPC %3% &SetTargetNPC %3% WinComp 0 .win>
		GoE3Died <StoryNote 6 WinComp 0 .loss>
		.win <+Tgt +Frt>
		Msg1 <As you enter %name1%'s hideout, you see that \SPR %1% has a hostage.>
		Msg2 <Please, help me get away from here!>
		Msg3 <You failed to save %name3% from %name1%.>
		Msg4 <You rescued %name3% from %name1%.>
		Msg5 <In the chaos of battle, %name3% slips away and disappears...>
		Msg6 <You defeated %name1%, but not soon enough to save %name3%.>
	end


Content
	name <Target becomes Enemy>
	desc <That target you've been chasing? Surprise! He's your new enemy!>
	requires <*CS_ManHunt_Confrontation (+Far|+Ffi) E:-- ~+P-->

	% E3 is the note left for the PC
	Element3 <Prefab>

	update <if= PlotStatus %id% else %pop% FreezeNPC %1% MoveItem %3% %2%>
	start <if= PlotStatus %id% else %pop% if= SceneID %2% Alert 1 History 2 &SetEnemyNPC %1% &SetTargetNPC 0 WinComp 0 .%id%_Next>
	.%id%_Next <+T-- +F-->

	Msg1 <There's no sign of %name1% here; \SPR %1% must have known you were coming.>
	Msg2 <%name1% evaded your attempt to capture \OPR %1% .>

	inv
		Treasure 1
		name <Note>
		desc <Don't bother looking for me. From now on, I'll be looking for you instead. - %name1%>
	end

%%
%% *CS_Manhunt_Search Content
%%
%% The PC is searching for someone who doesn't want to be found.
%% This component will include a search path.
%%
%% The parent plot sets the plot status to this layer's ID when ready.
%%
%% This component will either call the MANHUNT_CONFRONTATION above, or
%% resolve the manhunt in some other way.
%%
%% Param1: The NPC being sought. Must set a PLACE for it.
%%

Content
	name <Basic Hideout>
	requires <*CS_Manhunt_Search>
	Size 2

	% E1 is the character being sought; set PLACE here.
	% E2 is an urban scene, for placing the encounter
	% E3 is the encounter/metascene for the confrontation
	% E4 is a character who can tell the PC where the hideout is.
	Place1 <3 (Citizens) pass>
	Element2 <Scene Urban !Near -7>
	Element3 <Prefab>
	Place3 <2>
	Element4 <Character !Near -7 !Okay 1>

	% P%id%01 = Have discovered location of target

	% If this subplot has been activated, check to make sure the informant is OK.
	Start <if= PlotStatus %id% else %pop% ifNPCDead %4% FreezeNPC %1% LoseComp 0 .%id%_next>
	.%id%_next <+T-- +F-->

	% SubPlot1 is the confrontation, to take place at E3
	SubPlot1 <*CS_ManHunt_Confrontation 1 3>

sub
	Persona 4
	rumor%id% <%name4% knows where %name1% is.>
	greeting <if= PlotStatus %id% else GoChat Goto GoGetInfo>
	*GoChat <*IAmBusy>
	*GoGetInfo <*LookingForNPC %1% GoSuccess GoFailure>
	GoSuccess <NewChat Say 1 P= %id%01 1 SMemo 2 SetPlotStatus %id1%>
	*GoFailure <*BrushOff&Questioning>
	Msg1 <I'll tell you- %name1% used to have a hideout in %name2%. If you're looking for \OPR %1% , that's the first place I'd check.>
	Msg2 <%name5% told you that %name1%'s hideout can be found in %name2%.>

	MetaScene 3
	special <NOEXIT>
	% L1 = Initialization Counter
	% L2 = Fight has started
	BoxMap
	MapWidth 12
	MapHeight 17
	content <Fill Sub *HIDEOUT_X na>
	sub
		Team 1

		Team 2
		name <Citizens>

		Room
		special <STARTHERE>
		minimap <......###..#1#...........>
		sub
			Elevator
			Destination -1
			MiniMapComponent 1
			use <Print 1 Return>
		end
	end
end
inv
	STC ENCOUNTER-DUEL
	name <%name1%'s Hideout>
	update <if= P%id%01 1 else GoHide  SetStat STAT_MetaVisibility 0  ShowEncounter  Goto GoSetOrders>
	ENCOUNTER_Hostile
end

