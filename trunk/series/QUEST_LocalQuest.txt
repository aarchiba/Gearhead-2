%%
%%  *:LocalQuest Content
%%
%% This content is tied to a city rather than any specific character within it.

Content
	name <Tourists Gone Nuts>
	desc <While on vacation, a tourist ended up as part of a risque video... Can the PC get the videoslate back?>
	requires <*:LocalQuest "RESORT" ~"SAFE">

	% E1 is the tourist
	% E2 is the cameraman
	% E3 is the videoslate of ill repute
	Element1 <NewNPC 0 0 Young Easygoing>
	Scene1 <Building Public Shuttle>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	Element2 <Prefab>
	Scene2 <Building Public Meeting>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <Prefab>
	Place3 <2>

	% Quest1 is the success reward
	Quest1 <*SuccessGetItem.giveitem.NPCSecret 1 3>

	sub
		Persona 1
		rumor0 <%name1% did some embarassing things while on vacation.>
		% V2 = Ransom amount
		greeting <if= QuestStatus %qid% %id% else GoCheckOffer NewChat Say 9 AddChat 6 AddChat 7 AddChat 8>
		GoCheckOffer <if= QuestStatus %qid% 0 else GoChat ifG PCLaw d20 else GoCheckReact Goto GoOfferMission>
		GoCheckReact <ifG React %threat% else GoCheckItem Goto GoOfferMission>
		GoCheckItem <ifKeyItem %3% else GoChat NewChat Say 12 AddChat 9>
		GoChat <NewChat SayAnything>
		*GoOfferMission <*INeedYourHelp&SomethingPersonal GoAccept GoDeny>
		GoAccept <EndChat Say 2 SetQuestStatus %qid% %id% QMemo %qid% 3 AddChat 1>
		GoDeny <NewChat Say 1 AddReact -d10>
		GoOfferItem <V= 2 Reward Threat %threat% 100 1000 NewChat Say 13 AddChat 10 AddChat 11 AddChat 12>
		Result1 <NewChat Say 4 AddChat 2 AddChat 3 AddChat 4>
		Result2 <NewChat Say 5  AddCheerful  d10  AddChat 5>
		Result3 <NewChat Say 6  AddCheerful -d10  AddChat 5>
		Result4 <NewChat Say 7                    AddChat 5>
		Result5 <NewChat Say 8>
		Result6 <NewChat Say 10 SetQUestStatus %qid% -2>
		Result7 <Goto GoOfferItem>
		Result8 <NewChat Say 11>
		Result9 <Goto GoOfferItem>
		Result10 <XPV 100 DeleteItem %3% Goto .%id1%_GoInit>
		Result11 <NewChat Say 16 AddChat 13 AddChat 14>
		Result12 <NewChat Say 15 AddChat 10 AddChat 11>
		Result13 <Say 14 AddLawful -1 AddRenown -1 AddReact -30 CashPrize V2 DeleteItem %3% SetQuestStatus %qid% -1 if= d3 1 PCEnemy %1%>
		Result14 <Goto Result10>
		Msg1 <I see. Well, don't let me keep you. You're obviously doing something very important.>
		Msg2 <While here on vacation I did a few things I'm not proud of... unfortunately, someone caught those things on video.>
		Msg3 <%name1% was filmed for "Tourists Gone Nuts"; \SPR %1% wants you to locate the videoslate before it can be made public.>
		mSG4 <I went to a party being sponsored by "Tourists Gone Nuts" because I heard they were giving out free beer. The next morning I woke up naked in an alley with a copy of the release form I apparently signed.>
		Msg5 <I've learned that nothing is more expensive that something given away for free... Anyhow, if you could find that video and give it to me before copies are posted to Thruweb, I'd be sure to reward you.>
		Msg6 <That's a bit harsh, don't you think? I know I was a bit stupid, but this punishment is way over the top.  Anyhow, if you could find that video and give it to me, I'd have a nice reward for you.>
		Msg7 <Exactly. If you could do that, I'd have a nice reward waiting here for you.>
		Msg8 <I don't know. I don't recall meeting any of the photographers, and my memory of the entire night is a bit hazy... If you ask around you may be able to find something, I hope.>
		Msg9 <Have you found the video yet? I'm hoping to get this over with before my vacation is done.>
		Msg10 <This is terrible... my reputation is going to be ruined!>
		Msg11 <Please hurry back when you have. I'll be waiting.>
		Msg12 <Oh, hi! You must be an adventurer. This has been a wild vacation!>
		Msg13 <That's it! Hurry, please give it to me so I can destroy it!>
		Msg14 <This is extortion! I'll pay, because I really need that video, but I won't forget this...>
		Msg15 <I'd rather not say. The contents of the video are of a personal nature. If you give it to me, I'll provide a generous reward.>
		Msg16 <That's too much! You can't seriously expect me to pay that, can you?>
		Prompt1 <[Continue]>
		Prompt2 <At least you got some free beer.>
		Prompt3 <Sounds like you got what you deserved.>
		Prompt4 <You want me to recover that video?>
		Prompt5 <So where do you think I can find it?>
		Prompt6 <I don't think you're going to find the video.>
		CPrompt6 <ifItemDestroyed %3% Accept>
		Prompt7 <I've got the video right here.>
		CPrompt7 <ifKeyItem %3% Accept>
		Prompt8 <Sorry, not yet.>
		Prompt9 <Say, does this videoslate belong to you?>
		Prompt10 <Here you go. [Give Videoslate]>
		Prompt11 <Is it worth $ \VAL V2 to you?>
		CPrompt11 <ifG PCEgo 12 Accept>
		Prompt12 <Why is it so important?>
		CPrompt12 <if= QuestStatus %qid% 0 Accept>
		Prompt13 <You'll find the money or this video goes public.>
		Prompt14 <Pay whatever you can. [Give Videoslate]>

		Persona 2
		rumor%id% <%name2% is filming a new video series called "Tourists Gone Nuts".>
		% V1 = Have spoken before
		% V2 = Cost to buy video
		% V3 = Have given video
		greeting <if= QuestStatus %qid% %id% else GoGreet if= V3 0 else GoChat if= V1 1 else GoFirstTime NewChat Say 2 AddChat 1 AddChat 3>
		*GoFirstTime <*IWantToTalkAboutNPC&YouBadNPC %1% GoExplain>
		GoExplain <V= 1 1 V= 2 Threat %threat% 10 QMemo %qid% 4 NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoItemGone <NewChat Say 8 V= 3 1>
		GoGiveItem <QMemo %qid% 7 V= 3 1 GiveItem %3%>
		*GoGreet <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
		Result1 <ifItemOK %3% ELSE GoItemGone NewChat Say 3 AddChat 4 AddChat 5 AddChat 6 AddChat 7>
		Result2 <NewChat Say 5>
		Result3 <ifItemOK %3% ELSE GoItemGone NewChat Say 6 Goto GoGiveItem>
		Result4 <NewChat  Say 9  AddEasygoing -d8  AddReact -d10  Goto GoGiveItem>
		Result5 <NewChat  Say 10  Cash+ -V2  Goto GoGiveItem>
		% Offering to appear in a naughty video will tarnish your reputation a bit... but threatening
		% to kill someone won't. What a strange universe this is.
		Result6 <NewChat  Say 11  AddEasygoing -10  AddCheerful d10  AddPragmatic d10  AddRenown -1  SkillXP 27 100  ifG PCHeroism 0 else GoGiveItem  AddHeroic -1  Goto GoGiveItem>
		Result7 <NewChat Say 12>
		Msg1 <%name1%... Yes, that name does ring a bell. I believe \SPR %1% appeared in one of my recent videos.>
		Msg2 <Hello \PC . I guess you're back to talk about %name1%'s video again.>
		Msg3 <Why would I do that? This video is worth a lot of money... unless you have a better offer, of course.>
		Msg4 <You discovered that %name2% has the "Tourists Gone Nuts" video of %name1%.>
		Msg5 <Er... okay?>
		Msg6 <No way! %name1% told me \SPR %1% was 21! Here, you can have the videoslate... If the cops catch me with it they'll close us down for sure.>
		Msg7 <%name2% gave you the "Tourists Gone Nuts" videoslate featuring %name1%.>
		Msg8 <I don't have the video anymore. I don't know what happened to it, but it's not here.>
		Msg9 <Um, that sounds like a fair trade. Here, you take the video... I'll be going away now.>
		Msg10 <That's a fair price. Here, you take the video, and I'll take the money.>
		Msg11 <That deal is more than fair. Here's the video; I'll be sure to let you know when I have a position that needs your "special touch".>
		Msg12 <You better hurry, I'll be putting this video on our netsite soon.>
		Prompt1 <I want you to give me the video.>
		Prompt2 <That's all I wanted to know.>
		Prompt3 <You do know that %name1% is underage, right?>
		% I'm setting the age of majority here to 18, even though you can apparently get a mecha
		% and go adventuring at 16.
		% Only a perceptive PC will think to mention this, however.
		CPrompt3 <ifG PCPerception 14 ifG -2 NPCDAge %1% Accept>
		Prompt4 <Give me the video, and I let you live.>
		CPrompt4 <ifIntimidation HardSkillTar %threat% Accept>
		Prompt5 <I'll buy it for $ \VAL V2 .>
		CPrompt5 <ifG PC$ V2 Accept>
		Prompt6 <I'll help you make a much better video, later.>
		CPrompt6 <ifChatNPCSexy ifFlirtation SkillTar %threat% Accept>
		Prompt7 <I'll get back to you on that.>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 DeleteNPC %2% Goto %pop%>
	end
	inv
		NPC Citizen
		job <Photographer>
		job_desig <MEDIA>

		Treasure 10
		name <Videoslate>
		desc <The name "%name1%" is marked on its label.>
	end

Content
	name <Mecha on Strike!>
	desc <A group of workers have gone on strike, and they're picketing in their mecha.>
	% The outline for this quest came from my wife. It may or may not be based on actual
	% events at the Hyundai car factory in Ulsan.
	requires <*:LocalQuest "INDUSTRIAL">

	% E1 is the factory boss
	% E2 is the union leader
	% E3 is the picket encounter
	Element1 <Prefab>
	Scene1 <Building Public>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	Element2 <Prefab>
	Scene2 <New *Blank.CityBlock>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <Prefab>
	Scene3 <Urban>

	% SubQuest1 is the success for getting the union back to the table.
	Quest1 <*Success1.BusinessDeal 1>

	sub
		Persona 1
		rumor0 <the workers are on strike at %name1%'s factory.>
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst  NewChat Say 6 AddChat 4 AddChat 5 AddChat 6>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat  ifG React d20 else GoChat Goto GoOfferMission>
		GoChat <NewChat SayAnything>
		*GoOfferMission <*DoYouWantAJob GoExplain>
		GoExplain <NewChat Say 1 QMemo %qid% 2 SetQuestStatus %qid% %id% AddChat 1 AddChat 2 AddChat 3>
		GoQuitFail <NewChat Say 9>
		GoQuitSucceed <NewChat Say 10 MoveAndPacifyNPC %2% %s1% SetQuestStatus %qid% -2 ifNPCVillainous XPV 50 AddRenown 1 AddHeroic 1>
		Result1 <NewChat Say 3>
		Result2 <AddEasygoing -d8  NewChat Say 4>
		Result3 <AddCheerful  -d4  NewChat Say 5>
		Result4 <NewChat Say 8>
		Result5 <NewChat Say 7 AddChat 7 AddChat 8>
		Result6 <if= SceneVar %s2% 11 2 else .%id1%_GoInit AddRenown 1 XPV 100 Goto .%id1%_GoInit>
		Result7 <ifConversation HardSkillTar %threat% else GoQuitFail Goto GoQuitSucceed>
		Result8 <AddEasygoing -d12 Goto GoQuitSucceed>
		Msg1 <The workers from my factory have gone on strike. Under normal circumstances that would be bad, but I'm afraid these are not normal circumstances... You see, they've formed a picket line using their mecha. Things could soon get out of hand.>
		Msg2 <%name1% asked you to deal with the mecha picket line set up by \PPR %1% striking workers.>
		Msg3 <Yes, if you could just get them back to the bargaining table and their mecha off the street, it would save our company a whole lot of trouble. Right now this is a public relations disaster just waiting to happen.>
		Msg4 <No, the last thing we need is another riot. All I want is for them to come back to the bargaining table and take their mecha off the street. Right now this is a public relations disaster just waiting to happen.>
		Msg5 <I'd prefer to not think of things that way as they could certainly get much worse. All I want is for the workers to come back to the bargaining table and take their mecha off the street. Right now this is a public relations disaster just waiting to happen.>
		Msg6 <Has there been any progress with the striking workers?>
		Msg7 <Why should I give in to any of their demands?>
		Msg8 <I'll be waiting to hear back from you, then.>
		Msg9 <I'm not going to be swayed by weak arguments like that. That's why we need to get back to the bargaining table, so %name1% and I can butt heads and get this finished quickly.>
		Msg10 <Alright, I'll give the workers what they want, it's apparently the only way I'll ever get my factory running again.>
		Prompt1 <Do you want me to talk with them?>
		Prompt2 <Do you want me to bust some heads?>
		Prompt3 <You mean things aren't already out of hand?>
		Prompt4 <No, not yet.>
		Prompt5 <I think you should give in to their demands.>
		Prompt6 <They'll be coming back to the bargaining table.>
		CPrompt6 <if# SceneVar %s2% 11 0 Accept>
		Prompt7 <Because it's the right thing to do.>
		Prompt8 <Because I'll hurt you if you don't.>
		CPrompt8 <ifIntimidation SkillTar %threat% Accept>

		Persona 2
		rumor%id% <%name2% is the boss of the striking union.>
		% V1 = Have already fought NPC
		% V2 = Fake counter
		greeting <if= QUestStatus %qid% %id% else GoCheckPreQuest if# SceneVar %s2% 11 0 else GoCheckChance NewChat Say 1>
		GoCheckChance <if= V1 1 else GoCheckFight EndChat Say 2>
		GoCheckFight <ifChatNPCInPlay else GoPreFight TeamAttack 2 V= 1 1 EndChat Say 3 AddChat 1 AddChat 2>
		GoPreFight <NewChat Say 11 AddChat 6>
		GoCheckPreQuest <if# QuestStatus %qid% 0 else GoBuzzOff NewChat SayAnything>
		*GoBuzzOff <*IAmBusy&OnPhone>
		Result1 <AddReact -d10 EndChat Say 4 AddChat 3 AddChat 4 AddChat 5>
		Result2 <EndChat Say 5 AddChat 3 AddChat 4 AddChat 5>
		Result3 <ifConversation HardSkillTar %threat% else GoR3Fail EndChat Say 6 QMemo %qid% 7 MoveAndPacifyNPC %2% %s1% SceneVar= %s2% 11 2 Return>
		GoR3Fail <EndChat Say 8>
		% Intimidation is actually impossible, but make a skill roll just to tease the player.
		Result4 <V= 2 Intimidation EndChat Say 9>
		Result5 <EndChat Say 10 Return>
		Result6 <ifConversation SkillTar %threat% else GoR6Fail NewChat Say 6 QMemo %qid% 7 MoveAndPacifyNPC %2% %s1% SceneVar= %s2% 11 2>
		GoR6Fail <EndChat Say 12>
		Msg1 <You should go talk to %name1% about what just happened.>
		Msg2 <I'm done speaking with you. Either you're with us, or you're against us.>
		Msg3 <Hey, what are you doing here? You're not one of the workers.>
		Msg4 <%name1%, huh? You can tell \OPR %1% that we're not going back to work until conditions at the factory improve.>
		Msg5 <This protest is the only way we're going to get our message across. You can tell %name1% that nobody here is going back to work until conditions at the factory improve.>
		Msg6 <So \SPR %1% 's ready to talk again, huh? I knew this strike would be effective. You can let \OPR %1% know that we're ready to negotiate.>
		Msg7 <You convinced the striking workers from %name2%'s factory to return to negotiation.>
		Msg8 <You think %name1% is actually interested in negotiation? If you believe in \OPR %1% so much, you can die for \OPR %1% !>
		Msg9 <We're not going to be intimidated by one of %name1%'s lackeys!>
		Msg10 <We're not going to bend until %name1% acknowledges our grievances.>
		Msg11 <Hello, who is this? I'm a bit busy leading a strike at the moment.>
		Msg12 <Do you know what kind of a boss %name1% is? We're not coming back to negotiation unless we get some concessions!>
		Prompt1 <I was sent by %name1%.>
		Prompt2 <You should get out of the street before someone gets hurt.>
		Prompt3 <It'd be more effective if you told \OPR %1% yourself.>
		Prompt4 <You're going to end this strike now, or else.>
		Prompt5 <I'll go do that.>
		Prompt6 <%name1% wants to negotiate with you.>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 DeleteNPC %2% Goto %pop%>

		MetaScene 2
		% SceneVar %s2% 11 = Victory Counter; if nonzero, PC has resolved conflict.
		%      if 1, the PC resolved the conflict through violence
		%      if 2, the PC resolved the conflict through negotiation
		% Scenevar %s2% 12 = ForceChat Counter
		start <if= L12 0 L= 12 1 ForceChat %2%>
		nu2 <if= T2 0 if= L11 0 if= QuestStatus %qid% %id% L= 11 1 XPV 100 Print 11 QMemo %qid% 12 ifNPCOK %2% MoveAndPacifyNPC %2% %s1% Monologue %2% 13>
		Msg11 <That appears to be the last of the strikers.>
		Msg12 <You defeated the striking workers. %name1% will want to hear about this.>
		Msg13 <By union rules, the results of a mecha duel are to be legally binding upon both sides. You can tell %name1% that we're coming back to the bargaining table.>
		Sub
			Team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			Team 2
			name <Citizens>
			ParaX 25
			ParaY 25
			Deploy <if= L11 0  if= QuestStatus %qid% %id%   WMecha 2 Threat %threat% 100>
		end
	end
	inv
		NPC Corporate Executive

		NPC Construction Worker
		Chardesc Passionate
		Combatant

		STC QUEST-MAPMARKER-AUTO-STATIONARY
		name <Picket Line>
		desig <ENTRANCE %s2%>		
	end

Content
	name <Tower of Decay>
	desc <Not all the wreckage from the disaster has been removed, nor all the survivors located.>
	requires <*:LocalQuest "DISASTER" ~"Dangerous">

	% E1 is the ruined building
	% E2 is the entrance to this building
	% E3 is hermit who lives at the top of the tower
	% E4 is the "orphan" looking for E3
	% E5 is the town scene
	Scene1 <New *Dungeon.UrbanHellhole>
	Element2 <Prefab>
	Scene2 <Urban>
	Element3 <NewNPC 0 0 Old Shy Melancholy>
	Scene3 <New *HideOut&BoxMap>
	Team3 <Citizens>
	TeamData3 <Pass>
	Element4 <NewNPC 0 0 Young Sociable Melancholy>
	Scene4 <Building Public>
	Team4 <Citizens>
	TeamData4 <Pass Ally>
	Scene5 <Town>

	Quest1 <*SuccessRescue 4 3>

	sub
		Persona 3
		rumor%id% <%name4% was orphaned by the disaster.>
		greeting <if= QuestStatus %qid% %id% else GoFirstTime   NewChat Say 4 AddChat 4 AddChat 5>
		GoFirstTime <if= QuestStatus %qid% 0 else GoChat NewChat Say 1 AddChat 1 SetQuestStatus %qid% %id%>
		GoChat <NewChat SayAnything>
		Result1 <NewChat Say 2 AddChat 2 AddChat 3>
		Result2 <NewChat Say 3>
		*Result3 <*GOODBYE>
		Result4 <NewChat Say 5 AddChat 6>
		Result5 <NewChat Say 6 AddChat 3>
		Result6 <NewChat Say 7>
		Msg1 <You! What are you doing in here?>
		Msg2 <What am I doing here? I live here, of course. This is my house... I'm one of the last survivors, now that %name5% has been destroyed.>
		Msg3 <It's been destroyed as far as I'm concerned. All of my children are dead... This is the best place for me now. I'll just stay right here, here where I belong...>
		Msg4 <Oh, it's you again... I'm surprised the rats didn't eat you. Maybe they think you're a visitor.>
		Msg5 <I couldn't do that. Who'd take care of my plants? Besides, there's nothing out there for me anymore.>
		Msg6 <It's not so bad. I caught a nice juicy rat for lunch. There's plenty to share if you want to have some.>
		Msg7 <It's not safe there! Everything is in ruins, everybody is dead. I'm better off staying here. If you were smart you'd find a nice hole to hide in, as well.>
		Prompt1 <What am I doing!? What are you doing here?>
		Prompt2 <%name5% wasn't destroyed.>
		Prompt3 <Well, I'll just leave you, then.>
		Prompt4 <You should come back to %name5% with me.>
		Prompt5 <How's life been treating you?>
		Prompt6 <It's not safe here.>

		Persona 4
		greeting <if# QuestStatus %qid% %id% else GoLament if# QuestStatus %qid% 0 else GoLament NewCHat SayAnything>
		GoLament <ifNPCOK %3% else GoQuit NewChat Say 1 AddChat 1>
		GoQuit <NewChat SayAnything SetQuestStatus %qid% -2>
		result1 <EndChat Say 2 AddChat 2>
		result2 <XPV 100  MoveNPC %3% %s4% Goto .%id1%_GoInit>
		Msg1_10 <My father %name3% was killed in the %name5% Incident. Although \PPR %3% body was never found in the rubble, I've learned to accept that \SPR %3% won't be coming back.>
		CMsg1_10 <ifNPCMale %3% Accept>
		Msg1_20 <My mother %name3% was killed in the %name5% Incident. Although \PPR %3% body was never found in the rubble, I've learned to accept that \SPR %3% won't be coming back.>
		CMsg1_20 <ifNPCFemale %3% Accept>
		Msg2 <Is that true!? Then, after all this time, I will finally see \OPR %3% again!>
		Prompt1 <Actually, I met %name3% in a ruined building.>
		CPrompt1 <if= QuestStatus %qid% %id% Accept>
		Prompt2 <[Continue]>

		MetaScene 1
		sub
			Room
			special <SHAREDPALETTE>
			MiniMap <............2............>
			inv
				StairsUp
				desig <ENTRANCE %s3%>
				MiniMapComponent 2
			end
		end
	end
	inv
		STC Building
		name <Destroyed Apartment>
		desig <ENTRANCE %s1%>
	end

Content
	name <Losing my Inventory>
	desc <A local mechanic is losing his repair parts. The PC may be able to help.>
	requires <*:LocalQuest>

	% E1 is the mechanic. The end.
	Element1 <Prefab>
	Scene1 <Building Garage Public>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	% SubQuest1 is the search for the missing parts.
	% SubQuest2 is the reward for finding the parts.
	Quest1 <*:StolenMaterials 1>
	Quest2 <*Success1.BusinessDeal 1>

	sub
		Persona 1
		rumor0 <%name1% doesn't have enough spare parts to keep the garage in business.>
		Greeting <if= QuestStatus %qid% %id% else GoGreet NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoGreet <*NiceToMeetYou GoCheckFirst>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoCheckClose NewChat Say 2 AddChat 4 AddChat 5>
		GoCheckClose <if= QuestStatus %qid% -2 else GoRegularService NewChat Say 6>
		*GoRegularService <*SHOP_MECHA GoBye>
		*GoBye <*GOODBYE>
		Result1 <NewChat Say 3>
		result2 <EndChat Say 4 AddChat 6>
		result3 <NewChat Say 5 SetQuestStatus %qid% -2>
		result4 <Goto GoBye>
		result5 <NewChat Say 7 QMemo %qid% 8 SetQuestStatus %qid1% %id1% SetQuestStatus %qid% %id% AddChat 7>
		result6 <Goto .%id2%_GoInit>
		result7 <NewChat Say 9>
		Msg1 <Have you found out what happened to all my missing repair parts yet?>
		Msg2 <I usually do service on all kinds of machine. I also sell a variety of new mecha and replacement parts.>
		Msg3 <Three hundred tons of salvage doesn't just disappear... It's got to be around this place somewhere.>
		Msg4 <That's fantastic news! I'll be able to re-open my shop shortly.>
		Msg5 <That's serious bad news. My business can't afford a loss like that... I can't see any way to re-open the shop.>
		Msg6 <The shop's closed.>
		Msg7 <Well, you see, that's a problem. I'm in a bit of a jam right now- all of my stock has gone missing. It seems likely that someone stole it, but then again it seems unlikely that anyone would steal 300 tons of scrap metal.>
		Msg8 <%name1% in \SCENE %s1% had to close \PPR %1% garage because all of \PPR %1% stock went missing.>
		Msg9 <It'd be great if you could. Without those parts, I won't be able to stay in business.>
		Prompt1 <No, I haven't.>
		Prompt2 <Yes, I have.>
		CPrompt2 <if= QuestStatus %qid1% -1 Accept>
		Prompt3 <I don't think you'll be getting your parts.>
		CPrompt3 <if= QuestStatus %qid1% -2 Accept>
		Prompt4 <Thanks, but I don't need anything now.>
		Prompt5 <Let me see what you have.>
		Prompt6 <[Continue]>
		Prompt7 <I could try to find it for you.>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <..............1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
		end

	end
	inv
		NPC Mechanic
		Skill 31 5
		CharDesc Easygoing
	end

Content
	name <Addiction Issues>
	desc <An NPC asks you to help his cousin escape a life of sin and debauchery.>
	requires <*:LocalQuest "City">

	% E1 is the home scene
	% E2 is the mission-giving NPC
	% E3 is the wayward cousin
	Element1 <Scene Town>

	Element2 <NewNPC 1 1 Lawful Easygoing>
	Scene2 <Building Legit Public>
	Team2 <Citizens>
	TeamData2 <Pass Ally>

	Element3 <NewNPC 0 1 Criminal Passionate Young>
	Scene3 <New *BlackMarket>
	Team3 <Citizens>
	TeamData3 <Pass Ally>

	% SubQuest1 is the intervention for the target
	% SubQuest2 is the victory if E2 is reformed
	Quest1 <*:PersonalIntervention 3 2>
	Quest2 <*SuccessVsChar.Redemption 2 3>

	sub
		Persona 2
		rumor0 <%name2% has been very worried about %name3%.>
		greeting <if= QuestStatus %qid% %id% else GoGiveQuest if= QuestStatus %qid1% -1 else GoCheckQuest ifNPCOK %3% else GoCheckQuest EndChat Say 5 AddChat 7>
		GoCheckQuest <NewChat Say 4 AddChat 4 AddChat 5 AddChat 6>
		GoGiveQuest <if= QuestStatus %qid% 0 else GoNoQuest ifNPCOK %3% else GoCancelQuest ifG React d20 else GoNoQuest Goto GoAskHelp>
		GoCancelQuest <SetQuestStatus %qid% -2 Goto GoNoQuest>
		*GoAskHelp <*INeedYourHelp GoAccept GoRefuse>
		GoNoQuest <NewChat SayAnything>
		GoAccept <NewChat Say 1 QMemo %qid% 2 SetQuestStatus %qid% %id% SetQuestStatus %qid1% %id1% AddChat 1 AddChat 2 AddChat 3>
		GoRefuse <NewChat Say 3 AddReact -React>
		Result1 <NewChat Say 6>
		Result2 <NewChat Say 7 AddEasygoing -d10>
		result3 <NewChat Say 8>
		result4 <NewChat Say 8>
		result5 <NewChat Say 9 SetQuestStatus %qid% -2>
		result6 <NewChat Say 9 SetQuestStatus %qid% -2>
		Result7 <Goto .%id2%_GoInit>
		Msg1 <My cousin %name3% has been missing for a couple of days now. In the past \SPR %3% 's had trouble with drugs and gambling, so I dread to think what \SPR %3% might be up to now.>
		Msg2 <%name2% in \SCENE %s2% is worried that \PPR %2% cousin %name3% may have fallen off the wagon.>
		Msg3 <Suit yourself, if you don't want to get involved.>
		Msg4 <Have you had any news about %name3% yet?>
		Msg5 <%name3% has returned! This is wonderful. Thank you so much for your help!>
		Msg6 <I couldn't say... \SPR %3% always had a thing for drugs, gambling, and pornography, so wherever you can find those you should be able to find \OPR %3% .>
		Msg7 <You may be right, but I can't help but worry... we are family, after all. If you see %name3% please tell \OPR %3% to come home.>
		Msg8 <Thank you. Please, let me know when you have any information.>
		Msg9 <That is terrible news... Thank you for letting me know.>
		Prompt1 <Where do you think %name3% is?>
		Prompt2 <What %name3% wants to do is none of your business.>
		Prompt3 <I'll see what I can find out.>
		Prompt4 <I'm still working on it.>
		Prompt5 <%name3% has died.>
		CPrompt5 <ifNPCDead %3% Accept>
		Prompt6 <%name3% won't be coming back.>
		CPrompt6 <if= QuestStatus %qid1% -2 Accept>
		Prompt7 <[Continue]>

		Persona 3
		special <UNLISTED>
		Greeting <NewChat SayAnything>
	end

Content
	name <Restaurant Dreams>
	desc <An aspiring chef wants to start a restaurant. Maybe the PC can help.>
	requires <*:LocalQuest "Cuisine">

	% E1 is the cook
	Element1 <Prefab>
	Scene1 <Building Mall Public>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	% SubQuest1 is the search for money
	% SubQuest2 is the victory of the chef
	Quest1 <*:FindMoney.Investor 1>
	Quest2 <*Success1.BusinessDeal 1>

	sub
		Persona 1
		rumor0 <%name1%'s dream is to some day open a restaurant.>
		greeting <if= QuestStatus %qid% %id% else GoCheckOffer NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoCheckOffer <if= QuestStatus %qid% 0 else GoChat  ifG React 30 else GoNoLike NewChat Say 10 AddChat 10 AddChat 11>
		GoNoLike <NewChat SayAnything AddChat 7>
		*GoChat <*MISC_CHATTER>
		GoStartQuest <SetQuestStatus %qid% %id% SetQuestStatus %qid1% %id1% QMemo %qid% 7>
		result1 <Goto .%id2%_GoInit>
		result2 <NewChat Say 2>
		result3 <NewChat Say 3 AddChat 4 AddChat 5>
		result4 <EndChat Say 4 AddChat 6>
		result5 <NewChat Say 5>
		result6 <Cash+ -2000000 AddReact d10 ifConversation SkillTar %threat% else .%id2%_GoInit PCFriend %1% Goto .%id2%_GoInit>
		result7 <NewChat Say 6 AddChat 8 AddChat 3 AddChat 9 Goto GoStartQuest>
		result8 <NewChat Say 8>
		result9 <NewChat Say 9>
		result10 <NewChat Say 11 AddChat 8 AddChat 3 AddChat 9 Goto GoStartQuest>
		result11 <NewChat Say 12 AddReact -React>
		Msg1 <So, have you had any luck finding investors for my restaurant yet?>
		Msg2 <Well, until such a time as you have, I'll be right here reading these cookbooks. I've got to be ready for my big day.>
		Msg3 <It's going to take at least $2,000,000 to set up a decent restaurant, and even then I might not be able to afford chairs. It shouldn't matter, though- for fine cuisine, people should be willing to stand.>
		Msg4 <Wow, you'd do that? Thanks! I'll be sure to name a salad in your honor... "Salad du \PC "...>
		Msg5 <It's a bit too steep for me as well, and that's the problem. Let me know when you find something.>
		Msg6 <Yes, that's right. Why does that matter to you?>
		msg7 <%name1% in \SCENE %s1% needs an investor so \SPR %1% can open a restaurant.>
		msg8 <It'd be great if you could. Come back here and let me know if you find anything.>
		Msg9 <Well, if you come up with anything, come back here and let me know. I'll take all the help I can get.>
		Msg10 <Hey, how would you like to help me with something?>
		Msg11 <I came to this city to study cooking. It's my dream to open a restaurant, but right now I don't have the money. I heard that cavaliers know lots of people; do you think maybe someone you know could help me?>
		Msg12 <I understand. Maybe someday when I'm rich and successful I'll be able to brush people off like that too.>
		Prompt1 <As a matter of fact, I have.>
		CPrompt1 <if= QuestStatus %qid1% -1 Accept>
		Prompt2 <No, not yet.>
		Prompt3 <I'd like to invest in it myself.>
		CPrompt3 <ifPCRenowned Accept>
		Prompt4 <I can afford that. [Give Money]>
		CPrompt4 <ifG PC$ 1999999 Accept>
		Prompt5 <That's a bit too steep for me.>
		Prompt6 <[Continue]>
		Prompt7 <Aren't you the person who wants to open a restaurant?>
		CPrompt7 <ifG PCPerception 14 Accept>
		Prompt8 <Maybe I could find you an investor.>
		Prompt9 <I don't know.>
		Prompt10 <Tell me what you want.>
		Prompt11 <Sorry, kid, I don't have the time.>
	end
	inv
		NPC Cook
		chardesc Passionate Young
	end
Content
	name <Pirates Love Vegetables>
	desc <There's a hydroponics station that's been taken over by pirates.>
	requires <*:LocalQuest "Spinner" ~"Cuisine" ~"Dangerous">

	% E1 is the cook who owns the station
	% E2 is the station encounter, activated when QuestSTatus = %id%
	% E3 is the pirate captain
	% E4 is a place for the captain to go after the battle
	% E5 is the station itself
	Element1 <Prefab>
	Scene1 <Building Mall Public>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	Element2 <Prefab>
	Scene2 <Environs Space>

	Element3 <Prefab>
	Scene3 <New *BLANK.Space.Mecha>
	Team3 <Enemies>
	TeamData3 <SD Enemy>

	Scene4 <Building Public>

	Element5 <Prefab>
	Scene5 <Grab 3>

	% SubQuest1 is the victory against the pirates.
	Quest1 <*Success1.AgainstPirates 1>

	sub
		Persona 1
		rumor0 <%name1% gets fresh vegetables from a hydroponic microstation.>
		% V1 = Stupid Question Counter
		greeting <if= QuestStatus %qid% %id% else GoGreet NewChat Say 8 AddChat 7 AddChat 8 AddChat 9>
		*GoGreet <*NiceToMeetYou GoOpenStore>
		GoOpenStore <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		result1 <EndChat Say 2 PCEat Cash+ -300 PCMoraleDmg -15 AddChat 4>
		*result2 <*GoodBye>
		result3 <NewChat Say 3 V= 1 1 AddChat 1 AddChat 2>
		result4 <NewChat Say 4 AddChat 5 AddChat 6>
		result5 <NewChat Say 5 QMemo %qid% 6 SetQuestStatus %qid% %id%>
		result6 <NewChat Say 7 SetQuestStatus %qid% -2>
		result7 <ifItemOK %5% else GoR7Fail Goto .%id1%_GoInit>
		GoR7Fail <NewChat Say 10 CashPrize * 15 d30 AddRenown -2 SetQuestStatus %qid% -2>
		result8 <NewChat Say 9>
		result9 <Goto result1>
		Msg1 <This is %name1%'s Garden of Wonder. We serve a variety of dishes prepared with fresh, naturally grown ingredients. All entrees are $300.>
		Msg2 <Here's your order. Please enjoy your meal.>
		Msg3 <As I said, this food is both fresh and naturally grown. That's not easy to do in these parts. Now, do you want anything or not?>
		Msg4 <Say, are you a cavalier? I've got a problem you might be able to help with. The microstation where I grow my vegetables has been taken over by a gang of pirates. I'm offering a reward to get rid of them.>
		Msg5 <You can find the station in \SCENE %s2% . Make sure you don't damage the station while driving them off. Come back here when you're finished and I'll give you the reward.>
		Msg6 <%name1% in \SCENE %s1% is offering a reward for anyone who can remove the pirates from \PPR %1% microstation in \SCENE %s2% .>
		Msg7 <Okay, no problem.>
		Msg8 <Have you cleared the pirates from my station yet?>
		Msg9 <Well, come back when you have.>
		Msg10 <The problem being, so is my station! Still, you did fight the pirates, so I'd hate to see you go away empty handed... Here's a small reward to cover your repairs.>
		Prompt1 <I'll take the daily special.>
		CPrompt1 <ifG PCHunger 10 ifChatNPCInPlay ifG PC$ 299 Accept>
		Prompt2 <I don't want anything right now.>
		Prompt3 <Don't you have anything cheaper?>
		CPrompt3 <if= v1 0 Accept>
		Prompt4 <[Continue]>
		CPrompt4 <if= QuestStatus %qid% 0 Accept>
		Prompt5 <Give me the details.>
		Prompt6 <I'm not interested.>
		Prompt7 <The pirates are gone.>
		CPrompt7 <if= SceneVar %s3% 2 1 Accept>
		Prompt8 <Sorry, not yet.>
		Prompt9 <Actually I'd like some food.>
		CPrompt9 <ifG PCHunger 10 ifChatNPCInPlay ifG PC$ 299 Accept>

		Persona 3
		special <UNLISTED>
		% V1 = Have given victory speech
		greeting <if= QuestStatus %qid% %id% else GoCheckVictory EndChat Say 2 AddChat 1>
		GoCheckVictory <if= SceneVar %s3% 2 1 else GoChat if= V1 0 else GoChat V= 1 1 NewChat Say 1>
		GoChat <NewChat SayAnything>
		result1 <EndChat Say 3 AddChat 2>
		*result2 <*THEME_EXPO&Enemy na>
		Msg1 <You defeated me fair, \PC , but I'd be lying if I said I didn't miss the taste of those fresh taters...>
		Msg2 <I'd be advising you to spark off, this here station belongs to me and my mates.>
		Msg3 <Haven't you ever heard of a little thing called scurvy? It's the bane of us pirates... but with this station, we'll be in salad and vegetable cocktail for as long as we wants!>
		Prompt1 <Why do you need an agricultural station?>
		Prompt2 <[Continue]>

		MetaScene 1
		start <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <............1.......#---#>
			special <SHAREDPALETTE>
			desig <HOME>
		end

		MetaScene 3
		% L1 = Initialization counter
		% L2 = Have completed mission
		start <if= L1 0 ForceChat %3% L= 1 1>
		nu2 <if= L2 0 if= T2 0 Print 1 L= 2 1 Print 1 QMemo %qid% 2 XPV 100 ifNPCOK %3% MoveAndPacifyNPC %3% %s4% ClearOriginalHome %3%>
		Msg1 <You have defeated all the pirates.>
		Msg2 <You drove the pirates away from %name1%'s microstation.>
		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Enemies>
			SetEnemy 1
			SetFaction 8
			Deploy <if= L2 0 WMecha 2 Threat %threat% 100>
		end
	end
	inv
		NPC Cook
		chardesc Renowned Easygoing

		STC QUEST-MAPMARKER-AUTO-STATIONARY
		name <%name1%'s Station>
		desig <ENTRANCE %s3%>

		NPC Pirate
		SetFaction 8

		STC COLONY-PROP
		name <Agricultural Station>
		% v1 = Food recharge timer
		use <ifG t2 0 else GoReallyUse Print 1>
		GoReallyUse <ifG ComTime V1 else GoWaitRecharge ifYesNo 2 3 4 Print 5 GiveSTC .desc V= 1 ComTime V+ 1 90000>
		.desc <SALAD-1>
		GoWaitRecharge <Print 6>
		Msg1 <You can't examine the station while there are still pirates around.>
		Msg2 <This is %name1%'s agricultural microstation.>
		Msg3 <Go in and get some food>
		Msg4 <Leave it alone>
		Msg5 <You enter the microstation and make yourself a salad.>
		Msg6 <You've already had one salad today.>
	end

Content
	name <Aspiring Pilot>
	desc <A young pilot has lots of problems. This is truly a lame-ass quest.>
	requires <*:LocalQuest (!Ne|!Lo)>

	% E1 is the pilot.
	Element1 <Prefab>
	Scene1 <Building Public>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	Quest1 <*:MechaHeld 1>
	Quest2 <*ProveMyself 1>

	sub
		Persona 1
		rumor0 <%name1% dreams of becoming a famous mecha pilot.>
		greeting <if= QuestStatus %qid% %id% else GoCheckFirst if= QuestStatus %qid1% -1 else GoCheckLoss NewChat Say 3 SetQuestStatus %qid% %id2%>
		GoCheckLoss <if= QuestStatus %qid1% -2 else GoQuestInProgress NewChat Say 4 SetQuestStatus %qid% -2>
		GoQuestInProgress <NewChat Say 1>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoNoQuest NewChat Say 1 QMemo %qid% 2 SetQuestStatus %qid% %id% SetQuestStatus %qid1% %id1%>
		*GoNoQuest <*MISC_Chatter>
		Msg1 <I'd like to become a famous pilot some day, but in order to do that I'm going to need my mecha back...>
		Msg2 <%name1% told you that \SPR %1% needs to get \PPR %1% mecha back from somewhere.>
		Msg3 <Thanks for helping to get my mecha back! Now all I need is a chance to prove what a good pilot I could be...>
		Msg4 <Without a mecha, I guess I'm just nothing...>
	end
	inv
		NPC Mecha Pilot
		Chardesc Young
	end

Content
	name <Mine Hideout>
	desc <A gang of thieves has left behind a base in the mine.>
	requires <*:LocalQuest (!Ne|!Lo) ~"Dangerous">

	% E1 is the police officer.
	% E2 is the thief.
	% E3 is the item they both want.
	% E4 is the mine level where the entrance to the thives den is.
	% E5 is the entrance to the thieves den itself.
	element1 <Prefab>
	Scene1 <Building Public>
	team2 <Citizens>
	teamdata2 <Pass>
	element2 <Prefab>
	Scene2 <Building Sleazy>
	team2 <Citizens>
	teamdata2 <Pass>
	element3 <Prefab>
	Scene3 <New *BLANK.Basement>
	Scene4 <Dungeon Mine "!Lo">
	Element5 <Prefab>
	Scene5 <Grab 4>

	% SubQuest 1 is the success call when the PC gives the computer to the police
	% SubQuest 2 is the success call when the PC gives the computer to the thief
	Quest1 <*SuccessGetItem.giveitem 1 3>
	Quest2 <*SuccessGetItem.giveitem 2 3>

	sub
		Persona 1
		% V1 = Have given mission
		rumor0 <%name1% is looking for evidence about a criminal gang that used to operate here.>
		greeting <if= QUestStatus %qid% %id% else GoGreet if= V1 1 else GoCounteroffer NewChat Say 9 AddChat 5 AddChat 6 AddChat 7>
		GoCounterOffer <NewChat Say 5 QMemo %qid% 8 V= 1 1 AddChat 3 AddChat 4>
		*GoGreet <*NiceToMeetYou GoCheckFirst>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat ifG React d10 else GoChat Goto GoOfferJob>
		*GoOfferJob <*AreYouHereAboutJob&Investigation GoStartMission>
		GoStartMission <V= 1 1 SetQuestStatus %qid% %id% NewChat Say 1 QMemo %qid% 2 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 3>
		result2 <NewChat Say 4>
		result3 <NewChat Say 6 AddSociable -d10>
		result4 <NewChat Say 7>
		result5 <DeleteItem %3% AddLawful 1 AddRenown 1 PCEnemy %2% Goto .%id1%_GoInit>
		result6 <NewChat Say 10>
		result7 <SetQuestStatus %qid% -2  AddRenown -3  Goto .GoR7Final>
		*GoR7Final <*YouLostItem>
		Msg1 <Recently we busted a smuggling ring whose hideout was in the abandoned mine. Unfortunately, we never recovered their data core. If we could find this computer it would surely lead us to their business partners and clients.>
		Msg2 <%name1% in \SCENE %s1% wants you to locate a criminal database hidden somewhere in the mines.>
		Msg3 <Yes, exactly. Search the mines for their hideout. If you recover the data core, bring it here and I'll make sure you're adequetly rewarded.>
		Msg4 <Not even if there's a reward involved? If you find the database while exploring bring it here. I'll give you a better price for it than anyone else.>
		Msg5 <I hear that %name2%'s asked you to retrieve something from the mines... You realize that dealing in stolen merchandise is a crime, right? Deal with me and you'll earn way more than %name2% can afford to pay.>
		Msg6 <You better not be lying about that. If you find an old abandoned computer while exploring the mines, bring it here...>
		Msg7 <You do that.>
		Msg8 <%name1% in \SCENE %s1% and %name2% in \SCENE %s2% have both asked you to bring them the smuggling database from the mines.>
		Msg9 <Have you located the smuggler's computer yet?>
		Prompt1 <So you want me to find it for you?>
		Prompt2 <I don't work for the law.>
		Prompt3 <I don't know what you're talking about.>
		Prompt4 <I'll think about it.>
		Prompt5 <I have it right here.>
		CPrompt5 <ifKeyItem %3% ifChatNPCInPlay Accept>
		Prompt6 <I'm still looking for it.>
		Prompt7 <I think I lost the %name3%...>
		CPrompt7 <ifItemDestroyed %3% Accept>

		Persona 2
		% V1 = Have given mission
		rumor0 <%name2% once belonged to a criminal gang.>
		greeting <if= QUestStatus %qid% %id% else GoGreet if= V1 1 else GoCounteroffer NewChat Say 9 AddCHat 5 AddChat 6 AddCHat 7>
		GoCounterOffer <NewChat Say 5 QMemo %qid% 6 V= 1 1 AddChat 3 AddChat 4>
		*GoGreet <*NiceToMeetYou GoCheckFirst>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat ifG React d10 else GoChat Goto GoOfferJob>
		*GoOfferJob <*DoYouWantAJob GoStartMission>
		GoStartMission <V= 1 1 SetQuestStatus %qid% %id% NewChat Say 1 QMemo %qid% 2 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 3>
		result2 <NewChat Say 4>
		result3 <NewChat Say 7>
		result4 <NewChat Say 8>
		result5 <DeleteItem %3% PCFriend %2% PCEnemy %1% AddLawful -2 Goto .%id2%_GoInit>
		result6 <NewChat Say 10>
		result7 <SetQuestStatus %qid% -2  AddRenown -3  Goto .GoR7Final>
		*GoR7Final <*YouLostItem>
		Msg1 <There used to be a smuggler's den hidden in the abandoned mine. They were careless and got caught, but the police never found their data core. If I could get my hands on that computer I could really move up in the world, if you catch my drift.>
		Msg2 <%name2% in \SCENE %s2% wants you to locate a criminal database hidden somewhere in the mines.>
		Msg3 <You do that. Bring it back here and I'll reward you properly. Until then, don't talk about this to anyone... We don't need outsiders moving in on our turf.>
		Msg4 <So that's how it is, is it? Fine. Still, if you happen to find the data core while exploring, remember my offer.>
		Msg5 <I hear that %name1%'s made you an offer for some broken down old computer in the mines... Well, whatever it is, I'll double it. Really.>
		Msg6 <%name2% in \SCENE %s2% and %name1% in \SCENE %s1% have both asked you to bring them the smuggling database from the mines.>
		Msg7 <You say you don't, but we both know where the real money is. Keep my offer in mind, okay?>
		Msg8 <You do that.>
		Msg9 <Any luck finding the you-know-what yet?>
		Msg10 <Let me know when your luck improves, then. Recovering that computer is going to be lucky for the two of us.>
		Prompt1 <I'll see if I can find it.>
		Prompt2 <No way, I'm not interested.>
		Prompt3 <I don't work for thieves.>
		Prompt4 <I'll think about it.>
		Prompt5 <I have it right here.>
		CPrompt5 <ifKeyItem %3% ifChatNPCInPlay Accept>
		Prompt6 <I'm still looking for it.>
		Prompt7 <I think I lost the %name3%...>
		CPrompt7 <ifItemDestroyed %3% Accept>

		MetaScene 3
		%% Content for the blank thieves den.
		sub
			room
			minimap <......###..?1#..###......>
			special <SHAREDPALETTE>
			desig <HOME>

			Team 1
			SetEnemy 2

			Team 2
			name <Monsters>
			type <Robot Vermin Mine>
			SetEnemy 1
			Stat 2 12
		end
	end
	inv
		NPC Bounty Hunter
		CharDesc Lawful

		NPC Thief
		CharDesc Young Criminal
		Combatant

		Computer 4
		name <Crimedex>

		Trapdoor
		rumor%id% <a gang of smugglers once had a hideout in %name4% .>
		desig <ENTRANCE %s3%>
		update <if= QuestStatus %qid% %id% if= V1 0 SetStat 5 3 V= 1 1>
	end

Content
	name <Molybdenum Chef>
	desc <A chef seeks rare mushrooms that grow in the vacuum of space.>
	requires <*:LocalQuest "ASTEROID">

	% E1 is the chef
	% E2 is the mine
	% E3 is an entrance to the mine
	% E4 is the mushroom

	Element1 <Prefab>
	scene1 <Building Public>
	team1 <Citizens>
	teamdata1 <Pass>
	Element2 <Prefab>
	Scene2 <New *Dungeon.Asteroid>
	Element3 <Prefab>
	Scene3 <Environs>


	%% Q1 is the success branch for locating the item
	Quest1 <*SuccessGetItem.giveitem 1 2>

	sub
		Persona 1
		rumor0 <%name1% is always trying to get rare ingredients to cook with.>
		greeting <if= QuestStatus %qid% %id% else GoGreet ifKeyItem %2% else GoOpenShop ifChatNPCInPlay else GoOpenShop NewChat Say 6 AddChat 6 AddChat 7>
		*GoGreet <*NiceToMeetYou GoGiveQuest>
		GoGiveQuest <if= QuestStatus %qid% 0 else GoOpenShop ifG React 0 else GoOpenShop NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		*GoOpenShop <*SERVICE_RESTAURANT GoBye>
		*GoBye <*GOODBYE>
		result1 <Goto GoOpenShop>
		result2 <NewChat Say 2 QMemo %qid% 3 SetQuestSTatus %qid% %id% AddChat 4 AddChat 5>
		result3 <NewChat Say 5 AddChat 6 AddChat 7>
		result4 <NewChat Say 4 AddChat 5>
		result5 <NewChat Say 7>
		result6 <DeleteItem %2% PCFriend %1% Goto .%id1%_GoInit>
		result7 <NewChat Say 8 AddEasygoing -d10  SetQuestStatus %qid% -2>
		Msg1 <Living on this barren rock, it's tough getting fresh produce to cook with.>
		Msg2 <There might be. I've heard tell that edible fungus grows in Geozian Mine. I'd go try to gather some myself, but the non-edible fungus that lives there makes things dangerous.>
		Msg3 <%name1% in \SCENE %s1% wants you to bring \OPR %1% some wild space mushrooms from Geozian Mine.>
		Msg4 <Why not? It's perfectly natural... or as natural as anything living in space can be. They're safe enough to eat so long as you wash them thoroughly and deionize them first.>
		Msg5 <That's a geozian mushroom! Why, where did you find one? May I have it?>
		Msg6 <Ah, I see that you've returned with the mushroom! May I have it, please?>
		Msg7 <Thank you very much.>
		Msg8 <I understand. It just looks so delicious, doesn't it? If you had given it to me I would have been tempted to eat it myself right away...>
		Prompt1 <Do you have any food for sale, then?>
		Prompt2 <Is there anything I can do to help?>
		Prompt3 <Maybe these mushrooms would help.>
		CPrompt3 <ifKeyItem %2% Accept>
		Prompt4 <Ewww, that can't possibly be healthy.>
		Prompt5 <I'll see if I can get some for you.>
		Prompt5_1 <If I find any, I'll let you know.>
		Prompt6 <Here you go. [Give Mushroom]>
		Prompt7 <No, I want to keep it.>

		MetaScene 1
		start <ifItemDestroyed %2% else %pop% ifG QuestStatus %qid% -1 else %pop% SetQuestStatus %qid% -2 Goto %pop%>
		sub
			room
			minimap <..............1.....#---#>
			special <SHAREDPALETTE>
			desig <HOME>
		end
	end
	inv
		NPC Cook
		Chardesc Passionate

		food 30
		name <Geozian Mushroom>
		desc <This rare mushroom grows only in the vacuum of space.>

		Building
		name <Geozian Mine>
		desig <ENTRANCE %s2%>
	end

Content
	name <The Collector of Stuff>
	desc <A collector of rare things will hire the PC to recover an item.>
	requires <*:LocalQuest>

	%% E1 is the collector.
	%% E2 is the artifact to be sought
	%% E3 is the city this quest is located in
	%%  S2 is the scene where it is hidden
	Element1 <Prefab>
	scene1 <Building Public>
	team1 <Citizens>
	teamdata1 <Pass>
	Element2 <ARTIFACT ITEM PREZERO>
	Scene2 <New *ItemCrypt>
	Element3 <Scene town>

	%% Q1 is the success branch for locating the item
	Quest1 <*SuccessGetItem.giveitem 1 2>

	sub
		Persona 1
		rumor0 <%name1% is a collector of rare items.>
		greeting <if= QuestSTatus %qid% %id% else GoCheckFirst NewChat Say 10 AddChat 8 AddChat 9 AddChat 10>
		GoCheckFirst <if= QuestStatus %qid% 0 else GoChat ifG React 0 else GoBlowOff NewChat Say 2 AddChat 1 AddChat 2>
		GoBlowOff <NewChat Say 1>
		*GoChat <*MISC_CHATTER>
		result1 <NewChat Say 3 QMemo %qid% 11    SetQuestStatus %qid% %id% AddChat 3 AddChat 4>
		result2 <NewChat Say 4 AddSociable -d8   SetQuestStatus %qid% -2>
		result3 <NewChat Say 5 AddCheerful -d6   AddChat 5 AddChat 6 AddChat 7>
		result4 <NewChat Say 6                   AddChat 5 AddChat 6 AddChat 7>
		result5 <NewChat Say 7 AddChat 6 AddCHat 7 AddChat 11>
		result6 <NewChat Say 8 AddChat 5 AddCHat 7 AddChat 11>
		result7 <NewChat Say 9 AddChat 5 AddCHat 6 AddChat 11>
		result8 <DeleteItem %2% Goto .%id1%_GoInit>
		result9 <NewChat Say 12 AddChat 5 AddChat 6 AddChat 7>
		result10 <SetQuestStatus %qid% -2  Goto .GoR10Final>
		*GoR10Final <*YouLostItem>
		result11 <NewChat Say 13>
		Msg1 <I'm here on business, but why must everyone in this town be so unpleasant?>
		Msg2 <You there, you're a cavalier, aren't you? I am in %name3% to locate a very valuable item. To do this, I will require the services of an adventurer.>
		Msg3 <Fantastic. I need you to find the %name2%... Now realize, I have no idea where it is, just that it's somewhere in %name3%. At least it was at one point, maybe recently.>
		Msg4 <Aha, quite alright, that. Go on with you then. I should get back to my novel.>
		Msg5 <No, I suppose it isn't, but I can tell you all kinds of things about the %name2% other than its location. What do you want to know?>
		Msg6 <That depends. What exactly do you want to know about it?>
		Msg7 <Let me see if I can give you a short answer... \ITEM_DESC %2%>
		Msg8 <The %name2% has a long and interesting history. \ITEM_HISTORY %2%>
		Msg9 <I just want it for my collection, but that's not the only use of the %name2%. \ITEM_USAGE %2%>
		Msg10 <Oh, it's you again. Any word on that object I wanted you to find?>
		Msg11 <%name1% in %name3% asked you to find the %name2% for \OPR %1% .>
		Msg12 <Certainly. What is it that you need to know about the %name2%?>
		Msg13 <Come back here once you've found it. I'm eager to see if it's still in mint condition.>
		Prompt1 <Okay, I can help you.>
		Prompt2 <Sorry, I'm not interested.>
		Prompt3 <That's not very helpful.>
		Prompt4 <What can you tell me about it?>
		Prompt5 <What is it, exactly?>
		Prompt6 <Where did it come from?>
		Prompt7 <Why do you want it?>
		Prompt8 <I've returned with the %name2%.>
		CPrompt8 <ifKeyItem %2% ifChatNPCInPlay Accept>
		Prompt9 <I need some more info about it.>
		Prompt10 <I think I lost the %name2%...>
		CPrompt10 <ifItemDestroyed %2% Accept>
		Prompt11 <I'll get to work now.>

	end
	inv
		NPC Professor
		job <Collector>
	end

Content
	name <Asteroid Mine>
	desc <There's an asteroid mine in space nearby.>
	requires <*:LocalQuest "Spinner">

	% E1 is the owner of the mine.
	% E2 is the entrance of the mine.
	% E3 is the missing worker in the mine dungeon.
	element1 <Prefab>
	scene1 <New *MicroStation>
	team1 <Citizens>
	teamdata1 <Pass Ally>

	element2 <Prefab>
	scene2 <SPACE ENVIRONS>

	element3 <Prefab>
	scene3 <New *Dungeon.Asteroid>
	team3 <Citizens>
	teamdata3 <Pass>

	Quest1 <*Success1.Rescue 1>

	sub
		Persona 1
		rumor0 <%name1% has an asteroid mine near the station.>
		greeting <if= QuestStatus %qid% %id% else GoIntroduction if= SceneVar %s1% 1 1 else GoStillLooking NewChat Say 1 AddChat 1>
		GoStillLooking <NewChat Say 2 AddCHat 2 AddChat 3>
		*GoIntroduction <*NiceToMeetYou GoInfo>
		GoInfo <if= QuestStatus %qid% 0 else GoNoQuest NewChat Say 3 SetQuestStatus %qid% %id%  QMemo %qid% 4>
		*GoNoQuest <*Misc_Chatter>
		result1 <Goto .%id1%_GoInit>
		result2 <NewChat Say 4>
		result3 <NewChat Say 5 SetQuestStatus %qid% -2>
		Msg1 <Looks like you rescued %name3%.>
		Msg2 <Any luck searching for %name3%?>
		Msg3 <%name3% hasn't come back up from the mine... I hope everything is okay.>
		Msg4 <%name1% said that %name3% hadn't come back from the asteroid mine in \SCENE %2% .>
		Prompt1 <I did it!>
		Prompt2 <I'm still looking.>
		Prompt3 <%name3% has died.>
		CPrompt3 <ifNPCDead %3% Accept>

		MetaScene 1
		start <if= L1 0 else %pop% ifNPCInPlay %3% else %pop% L= 1 1 RemoveTempLancemate %3% XPV 200 ForceChat %3% Goto %pop%>
		sub
			room
			special <SharedPalette>
			sub
				Trapdoor
				name <Mine Entrance>
				desig <Entrance %s3%>
			end
		end

		Persona 3
		% V1 = Have returned to station
		special <UNLISTED>
		greeting <if= QuestStatus %qid% %id% else GoNoQuest if= V1 1 else GoCheckStation NewChat Say 5>
		GoCheckStation <if= SceneID %s1% else GoInMine NewChat Say 4  AddReact d10  V= 1 1>
		GoInMine <ifNPCLancemate else GoAskHelp NewChat Say 3>
		GoAskHelp <NewChat Say 2 AddTempLancemate %3%>
		GoNoQuest <NewChat Say 1>
		Msg1 <Always a lot of work to do...>
		Msg2 <Let's get out of here!>
		Msg3 <Come on, let's get back to the station!>
		Msg4 <We're back! Thank you so much.>
		Msg5 <Thanks again!>
	end
	inv
		NPC Miner
		job <Mine Boss>
		chardesc Old

		STC QUEST-MAPMARKER-ACTIVE-STATIONARY
		name <%name1%'s Station>
		desig <Entrance %s1%>

		NPC Miner
		LMFollow
	end

Content
	name <Fight Over Something>
	desc <The PC will encounter a NPC in space, who will try to drive him away.>
	%% Upon defeating E1, he will be sent to a nearby building. If the PC speaks with
	%% him again, he'll find out what E1 was guarding.
	requires <*:LocalQuest "Spinner" "L5PAT" ~"Dangerous">

	% E1 is the character the PC will be fighting
	% E2 is the entrance to the space scene
	% E3 is the scene where E1 will go after the fight
	% E4 is a copy of E1's scene; needed for the SubQuest parameter
	element1 <Prefab>
	scene1 <New *BLANK.Space.Mecha>
	team1 <ENEMIES>
	teamdata1 <SD Enemy>

	element2 <Prefab>
	scene2 <SPACE ENVIRONS>

	Scene3 <Building Public>

	Scene4 <Grab 1>

	Quest1 <*SpaceSalvage 1 4>

	sub
		Persona 1
		special <UNLISTED>
		rumor0 <there's a lot of old junk floating outside the station.>
		rumor%id% <%name1% has been recovering from your battle at %name3%.>
		greeting <if= QuestStatus %qid% %id% else GoCheckVictory  NewChat Say 3 AddChat 1>
		GoCheckVictory <if= QuestStatus %qid% 0 else GoChat   if= SceneVar %s1% 2 1 else GoCheckLoss SetQuestStatus %qid% %id% QMemo %qid% 4 Goto GoPCWon>
		*GoPCWon <*YouWonFair&MeetAgain>
		GoCheckLoss <if= T1 0 else GoGreet Goto GoPCLost>
		*GoPCLost <*YouLose&DontComeBack>
		GoGreet <ifChatNPCKnown else GoFirstTime EndChat Say 1>
		GoFirstTime <EndChat Say 2 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <Goto .%id1%_GoInit>
		*result2 <*THEME_EXPO&ENEMY na>
		Msg1 <Back again? Want some more punishment? Good enough, bring it on!>
		Msg2 <What are you doing out here? Moving in on my claim, that's what!!! No way am I giving it up that easy...>
		Msg3 <\PC ! You beat me fair and square. If you want, you can claim my salvage for yourself.>
		Msg4 <You defeated %name1% in a space battle, but you don't really know why \SPR %1% attacked you.>
		Prompt1 <What is it that you found?>
		Prompt2 <I don't know what you're talking about!>

		MetaScene 1
		%% Because scene 1 is a blank, this metascene holds all the important stuff.
		% L1 = Conversation Initializer; reset to 0 when PC leaves the scene.
		% L2 = Victory Counter
		start <if= L1 0 ForceChat %1% L= 1 1>
		nu1 <if= T1 0 Return if= L1 1 if= L2 0 ifNPCOK %1% Print 1 ForceChat %1% L= 1 0>
		nu2 <if= T2 0 if= L1 1 if= L2 0   XPV 100   L= 2 1   ifNPCOK %1% Print 1 ForceChat %1% MoveAndPacifyNPC %1% %s3%>

		sub
			Team 1
			name <Player Team>
			ParaX 5
			ParaY 5
			SetEnemy 2

			Team 2
			name <Enemies>
			Deploy <if= L2 0  SetSelfFaction NPCFac %1%   WMecha 2 Threat %threat% 80>
			ParaX 25
			ParaY 25
			SetEnemy 1
		end
	end
	inv
		NPC Explorer
		SetFaction 10
		CharDesc passionate lawful

		STC QUEST-ENCOUNTER-ACTIVE-STATIONARY
		name <Strange Object>
		desig <ENTRANCE %s1%>
	end

Content
	name <Trucker's Luck>
	desc <The PC will encounter a space trucker being attacked, and may help.>
	%% Upon saving E1, he will be sent to a nearby building. If the PC speaks with
	%% him again, he'll find out what E1 found.
	requires <*:LocalQuest "Spinner" ~"Dangerous">

	% E1 is the character the PC will be helping
	% E2 is the entrance to the space scene
	% E3 is the scene where E1 will go after the fight
	% E4 is a copy of E1's scene; needed for the SubQuest parameter
	element1 <Prefab>
	scene1 <New *BLANK.Space.Mecha>
	team1 <Truckers>
	teamdata1 <SD Ally>

	element2 <Prefab>
	scene2 <SPACE ENVIRONS>

	Scene3 <Building Garage>

	Scene4 <Grab 1>

	Quest1 <*SpaceSalvage 1 4>

	sub
		Persona 1
		special <UNLISTED>
		rumor0 <raiders frequently hide in the dust clouds outside the station.>
		rumor%id% <%name1% has been getting repairs at %name3%.>
		greeting <if= QuestStatus %qid% %id% else GoCheckVictory  NewChat Say 3 AddChat 1>
		GoCheckVictory <if= QuestStatus %qid% 0 else GoChat if= SceneVar %s1% 2 1 else GoKeepFighting SetQuestStatus %qid% %id% QMemo %qid% 4 Goto GoPCWon>
		*GoPCWon <*WeWon 0 8>
		GoKeepFighting <ifChatNPCKnown else GoFirstTime EndChat Say 1>
		*GoFirstTime <*HelpMeVsMecha GoWillHelp GoWontHelp>
		*GoWillHelp <*LetsDefeatThem 8>
		GoWontHelp <EndChat Say 2 SetNPCTeam %1% 2 L= 3 1>
		*GoChat <*MISC_CHATTER>
		result1 <Goto .%id1%_GoInit>
		Msg1 <The pirates aren't defeated yet! Let's keep fighting!>
		Msg2 <If you're not going to help, just stay out of the way!!!>
		Msg3 <Good to see you again. I never got a chance to thank you... Those pirates, I don't think they were after me just for the cargo. I think it's because of what I found in the dust cloud.>
		Msg4 <You rescued %name1% from space pirates.>
		Prompt1 <What did you find?>

		MetaScene 1
		%% Because scene 1 is a blank, this metascene holds all the important stuff.
		% L1 = Conversation Initializer
		% L2 = Victory Counter
		% L3 = If nonzero, PC has attacked E1
		% L4 = Value of E1's cargo
		start <if= L1 0  ForceChat %1% L= 1 1>
		% If the PC leaves the scene before defeating all enemies, there's no second chance.
		nu1 <if= T1 0 Return  if= L2 0   L= 2 -1  DeleteNPC %1%  SetQuestStatus %qid% -2>
		nu2 <if= T2 0         if= L2 0   XPV 100   L= 2 1   if= L3 0 else GoFightE1  ifNPCOK %1%  Print 2 ForceChat %1% MoveAndPacifyNPC %1% %s3%>
		GoFightE1 <L= 2 -1 L= 4 Reward Threat %threat% 100 500  Alert 1 AddLawful -5 SetQuestStatus %qid% -2  CashPrize L4 ifNPCOK %1% FreezeNPC %1% PCEnemy %1%>
		Faint%1% <SetQuestStatus %qid% -2>

		Msg1 <You recover $ \VAL L4 from %name1%'s cargo.>

		sub
			Team 1
			name <Player Team>
			ParaX 5
			ParaY 5
			SetEnemy 2

			Team 2
			name <Enemies>
			SetFaction 8
			Deploy <if= L2 0   WMecha 2 Threat %threat% 150>
			ParaX 25
			ParaY 25
			SetEnemy 1 3

			Team 3
			name <Truckers>
			SetEnemy 2
			ParaX 35
			ParaY 10
		end
	end
	inv
		NPC Trucker
		SetTeam 3

		STC QUEST-ENCOUNTER-ACTIVE-STATIONARY
		name <Dust Cloud>
		desig <ENTRANCE %s1%>
	end


Content
	name <Rishtan: Mechanical Genius>
	desc <The local mechanic wants to start a customization shop, but lacks money.>
	requires <*:LocalQuest L5PAT ~"Safe">

	quest1 <*:FindMoney.Investor 2>
	quest2 <*Success1.BusinessDeal 1>

	Element1 <Prefab>	% The manager of the shop
	Scene1 <Building Public Garage>
	Team1 <Citizens>
	TeamData1 <Pass>

	Element2 <Prefab>
	Scene2 <New *Workshop>
	Team2 <Citizens>
	TeamData2 <Pass>

	sub
		Persona 1
		Greeting <if= QuestStatus %qid% %id% else GoGreet if= NPCVar %2% 1 1 else GoCheckQuest NewChat Say 1 AddChat 1>
		GoCheckQuest <if= QuestStatus %qid1% -1 else GoGreet ifNPCOK %2% else GoKillQuest NewChat Say 2>
		GoKillQuest <SetQuestStatus %qid% -2 Goto GoGreet>
		*GoGreet <*NiceToMeetYou GoOpenShop>
		*GoOpenShop <*SHOP_GENERAL GoGoodbye>
		*GoGoodbye <*GOODBYE>
		Result1 <Goto .%id2%_GoInit>
		Msg1 <You did a really good thing for %name2%, for both of us really. I just wanted the chance to thank you.>
		Msg2 <You found an investor? Then you better get downstairs and let %name2% know about it, before he blows a diode or something.>
		Prompt1 <It was the least I could do.>

		Persona 2
		% V1 = Conversation skill roll counter
		Greeting <if= NPCVar %2% 1 1 else GoCheckQuest NewChat Say 18 AddChat 16 AddChat 17>
		GoCheckQuest <if= QuestStatus %qid% %id% else GoIntro NewChat Say 16 AddCHat 14 AddChat 15>
		GoIntro <if= QuestStatus %qid% 0 else GoChat  ifG PCRenown %threat% else GoLowIntro ifG React d6 else GoLowIntro NewChat Say 9  AddChat 7 AddChat 8 AddChat 9>
		GoLowIntro <ifChatNPCUnknown else GoSecondTime NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoSecondTime <NewChat Say 2  AddChat 1 AddChat 2 AddChat 3>
		GoBeginQuest <SetQuestStatus %qid% %id%  QMemo %qid% 20  SetQuestStatus %qid1% %id1%>
		*GoChat <*MISC_CHATTER>
		*GoGoodbye <*GOODBYE>
		result1 <NewChat Say 3 AddChat 4 AddCHat 5 AddChat 3>
		result2 <EndChat Say 4>
		result3 <NewChat Say 5 AddChat 4 AddChat 1 AddChat 6>
		result4 <Goto result2>
		result5 <if= V1 0 else GoR5Fail V= 1 1  ifConversation %sktar% else GoR5Fail NewChat Say 6  AddChat 10 AddChat 11  Goto GoBeginQuest>
		GoR5Fail <EndChat Say 7>
		result6 <EndChat Say 8>
		result7 <NewChat Say 10 AddChat 10 AddChat 11  Goto GoBeginQuest>
		result8 <NewChat Say 11 AddReact -d30  AddNPCReact %1% -15   AddSociable -d8   SetQuestStatus %qid% -2>
		result9 <NewChat Say 12 AddChat 12 AddChat 13>
		result10 <NewChat Say 13 AddChat 11 AddChat 14>
		result11 <NewChat Say 14>
		result12 <AddCheerful d10 Goto result7>
		result13 <AddCheerful d10 Goto result8>
		result14 <NewChat Say 15 AddReact 15  NPCVar= %2% 1 1 Cash+ -5000000 XPV 100 AddRenown 1>
		result15 <NewChat Say 17 AddReact d8  NPCVar= %2% 1 1                XPV 100>
		result16 <EndChat Say 19 shop .wares Goto GoGoodbye>
		.wares <MECHA MEXTRA>
		result17 <Goto GoChat>
		Msg1 <What are you doing down here? This is a place of business, not a social club.>
		Msg2 <So you're back again. I hope you've got a good reason for bothering me this time.>
		Msg3 <I fix things for %name1%... Mecha, mostly, but occasionally robots and office equipment. Of course what I'd really like to do is open up a conversion shop and become a famous mecha designer, but that's never going to happen.>
		Msg4 <Look, I've got to get these repairs done, I don't have time to chat. Maybe if you come back later I'll have more time but right now I'm swamped.>
		Msg5 <Then you need to speak with %name1%, upstairs. I handle the labor, \SPR %1% handles the business. The exit is just over there.>
		Msg6 <First of all because we don't have the tools I need here for making custom parts. Unfortunately we don't have enough money to upgrade the workshop, so until this shop starts doing better I'm out of luck.>
		Msg7 <Because... look, you wouldn't be interested in my life story, and I don't have time to tell it. I have to get back to work.>
		Msg8 <I don't need any help, I just need to get back to work. You can talk with %name1% if you'd like a job here but I honestly don't think we can afford another mechanic.>
		Msg9 <Look who it is... the famous \PC . If you're not too busy on your fantastic adventures I have a business offer for you.>
		Msg10 <Well, I'd like to expand the garage, start doing mecha customizations... Unfortunately, I don't have the equipment we'd need. If you would invest some money now then maybe in a few years there'll be a huge amount of profit.>
		Msg11 <I see... and this without even hearing my idea. That's just great. Fine, I don't need you... I can do this all on my own...>
		Msg12 <Yes, I remember that very well... Though my counselor says I'll be learning to live with the trauma soon. Now do you want to hear about my business idea or not?>
		Msg13 <I figure we'll need around five million for the basic equipment. After that I just need to build a reputation as a designer, then soon we might even make a profit.>
		Msg14 <If you can, that would be great. I just know that I can make it as a mecha designer, all I need is one chance to prove it!>
		Msg15 <Wow, this is great! Thank you so much! I promise that when this shop starts making profit, I'll pay you back with interest...>
		Msg16 <In order to start my customization business, I'll need at least five million credits for new equipment.>
		Msg17 <That's fantastic news! I'll order the new equipment right away... Come back here later and I'll show you what I can do!>
		Msg18 <Welcome to %name2%'s custom mecha shop. Thank you again for helping this dream to come true!>
		Msg19 <I have a number of mecha for sale, some of which I've customized myself. I also have some replacement parts in case you'd like to try your hand at mecha engineering.>
		Msg20 <%name2% in \SCENE %1% wants to open a mecha customization shop, but needs money to buy the equipment.>
		Prompt1 <What is it that you do down here?>
		Prompt2 <I just wanted to chat.>
		Prompt3 <I need to get my mecha repaired.>
		Prompt4 <Uh-huh. So do you know any rumors?>
		Prompt5 <Why do you think it can't happen?>
		Prompt6 <Maybe I could help you out.>
		Prompt7 <Alright, what kind of offer?>
		Prompt8 <I'm not interested.>
		Prompt9 <I was in here bothering you before! Remember?>
		CPrompt9 <ifChatNPCKnown Accept>
		Prompt10 <How much money do you need?>
		Prompt11 <Maybe I can find someone else to invest.>
		Prompt12 <Okay. Tell me about it.>
		Prompt13 <Sorry, it doesn't sound interesting.>
		Prompt14 <I can do that. [Give $5M]>
		CPrompt14 <ifG PC$ 4999999 Accept>
		Prompt15 <I've found an investor for you.>
		CPrompt15 <if= QuestStatus %qid1% -1 Accept>
		Prompt16 <I'd like to see what you have.>
		Prompt17 <Just stopped by to chat, thanks.>

		MetaScene 1
		sub
			room
			minimap <.......2......1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s2%>
			end
		end

		MetaScene 2
		sub
			room
			minimap <......##...#2.....1......>
			special <SHAREDPALETTE>
			desig <HOME>
			inv
				STC COMPUTER-1
				name <Design Console>
				MiniMapComponent 2
			end
		end

	end
	inv
		NPC Mechanic

		NPC Mechanic
		name <Rishtan>
		CharDesc Male Pragmatic Shy
		% Actual game age = 24
		Age 4
		% V1 = Have arranged funding, thereby unlocking store
		Skill 21 8
		Skill 31 10
	end

Content
	name <Rat Killing>
	desc <The PC has to kill a rat for the NPC.>
	requires <*:LocalQuest>

	% Quest1 is the continuation after the PC has cleared the basement.
	Quest1 <*SUCCESS1.Extermination 1>

	element1 <Prefab>
	Scene1 <Mall Public>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	Scene2 <NEW *ExterminationBasement>
	Team2 <Monsters>
	TeamData2 <SD Enemy>

	sub
		Persona 1
		greeting <if= QuestStatus %qid% %id% else GoFirstTime NewChat Say 1 AddChat 1 AddChat 2>
		*GoFirstTime <*NiceToMeetYou GoCheckRenown>
		GoCheckRenown <if= QuestStatus %qid% 0 else GoOpenShop  ifG PCRenown d%threat% else GoCheckWangtta   Goto GoOfferMission>
		GoCheckWangtta <ifG React d10 else GoOpenShop ifG -d10 PCRenown else GoOpenShop   Goto GoOfferMission>
		GoOfferMission <NewChat Say 2 AddChat 3 AddChat 4>
		*GoOpenShop <*SHOP_GENERAL GoGoodbye>
		*GoGoodbye <*GOODBYE>
		Result1 <Goto GoOpenShop>
		Result2 <Goto .%id1%_GoInit>
		Result3 <NewChat Say 3  SetQuestStatus %qid% %id% QMemo %qid% 5 UpdateProps>
		result4 <NewChat Say 4  SetQuestStatus %qid% -2>
		Msg1 <So, how goes the rat killing?>
		Msg2 <Hey, I was wondering if you could help me with a little problem.>
		Msg3 <There are some rats or something infesting the basement. One of my workers got bit just recently... I need you to exterminate them. This door will take you down there.>
		Msg4 <I understand. I won't bother you about it again.>
		Msg5 <%name1% in \SCENE %s1% asked you to clear some vermin out of \PPR %1% basement.>
		Prompt1 <I'm still working on it.>
		Prompt2 <Your basement has been cleared.>
		CPrompt2 <if= SceneVar %2% 1 1 Accept>
		Prompt3 <What kind of problem?>
		Prompt4 <Sorry, I'm not interested.>

		MetaScene 1
		sub
			room
			minimap <2#...+#.......1.....&---&>
			special <SHAREDPALETTE>
			desig <HOME>
			sub
				Door
				update <if= V1 0 else GoCheckQuest V= 1 1 SetStat STAT_Lock %sktar_hard%>
				GoCheckQuest <ifG QuestStatus %qid% 0 SetStat STAT_Lock 0>
			end
			inv
				TrapDoor
				MiniMapComponent 2
				desig <ENTRANCE %s2%>
			end
		end
	end
	inv
		NPC Shopkeeper
	end

