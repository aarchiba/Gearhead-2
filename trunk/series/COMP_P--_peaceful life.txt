%% COMP_P--.txt
%%
%%  Peaceful Life components

Plot
	name <The Police Are Hiring>
	desc <The PC will be offered a job by a police officer.>
	requires <#A (+P--|+Pme|+Pun) +T-- F:CRIME ~P:-->

	% E1 is the scene where this component will take place
	% E2 is the NPC that the PC will speak with
	% E3 is the enemy faction
	element1 <Scene Building Public !Okay -3 !Near -7>
	element2 <Character Police !Near -7 !Okay -7 !Enemy -2 -ArchEnemy>
	Place2 <1 (Guards) SD>
	element3 <Grab 2>

	% All this plot does is activate the mission subplot
	update <if= PlotStatus 0  SetPlotStatus %id1%>

	%% SubPlot1 is the mission to be offered
	SubPlot1 <*CS_GiveMission 2>

	sub
		Persona 2
		rumor%id1% <%name2% in %name1% has been fighting %name3%.>
	end

Plot
	name <Mysterious Seeker>
	desc <Someone else is also looking for your target.>
	requires <#A +Tgt (T:ACADE|T:MEDIA) S:-- (F:CORPO|F:CRIME|F:POLIT) (+P--|+Pme)>

	% E1 is a public scene for this component.
	% E2 is a member of the enemy faction
	% E3 is the person being sought
	Element1 <Scene Public !Near -7>
	Element2 <Character Wangtta !Comrade -2 NeverFail>
	Place2 <1 (Citizens) Pass>
	NeverFail2 <Spy>
	Element3 <Grab 5>
	Place3 </>

	start <ifNPCDead %2% LoseComp 0 .next>
	.next <>

	sub
		Persona 2
		rumor <%name2% has been asking suspicious questions about %name3%.>
		greeting <NewChat Say 1 AddChat 1 AddChat 2>
		GoEnd <StoryNote 4 WinComp 0 .next>
		.next <+Tpt>
		result1 <NewChat Say 2 Goto GoEnd>
		result2 <NewChat Say 3 Goto GoEnd>
		Msg1 <Hey, you wouldn't happen to know where %name3% is, would you?>
		Msg2 <You are? Er... well, forget that I asked anything, then. I'm not looking for %name3% myself... and \FACTION &EnemyFac aren't either.>
		Msg3 <Um... well, it's certainly not because I was sent here by \FACTION &EnemyFac . Definitely not.>
		Msg4 <You met a shady character who was looking for %name3%.>
		Prompt1 <I'm looking for \OPR %3% myself.>
		Prompt2 <Why do you want to find \OPR %3% ?>
	end

Plot
	name <Destroyed Spaceship>
	desc <The PC can rescue someone from pirates, or just loot the wreckage.>
	requires <#A +Ten +P-- ~F:CRIHN ~E:CRIHN -P:CRIHN>
	Size 5

	% E1 is the encounter for this component
	% E2 is the pilot who may be rescued, or the pirate who the PC ripped off.
	%   Isn't it wonderful to define new elements and then decide what they're
	%   going to be based on the player's actions? Of course it's wonderful.
	% E3 is the Captain's log which may be recovered
	% E4 is the Blades of Crihna faction, needed for the encounter
	% E5 is the local hospital, needed if the PC decides to be heroic
	% E6 is the town itself, needed for the diary
	Element1 <Grab 6>
	Element2 <NewNPC 0 -7>
	Place2 </>
	Element3 <Prefab>
	Element4 <Faction CRIHN>
	Element5 <Scene Hospital !Near -7>
	Element6 <Scene Town !Near -7>

	% SubPlot1 is the rescue scenario.
	SubPlot1 <*CS_ThanksForRescue 2>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30
		Start <if= L2 0 L= 2 1 Alert 1 History 7>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   LoseComp 0 .loss>
		.loss <+Pun +T-- +F-->
		nu2 <if= T2 0   if= V1 0 V= 1 1   AddRenown 1   SALVAGE   ifYesNo 2 3 4 else GoRansack Goto GoRescue>
		GoRescue  <Print 5 StoryNote 6 XPV 100 AddHeroic 1 MoveNPC %2% %5% Exit %5% SetPlotStatus %id1%>
		GoRansack <Print 8 StoryNote 9 GiveItem %3% CashPrize Reward StoryDL 500 XPV 100 AddHeroic -1 PCEnemy %2% if= &EnemyNPC 0 else GoEndRansack &SetEnemyNPC %2% Goto GoEndRansack>
		GoEndRansack <WinComp 0 .ransack>
		.ransack <+Pla +T-- +F-->
		Msg1 <You find a heavily damaged spaceship besieged by pirates. It looks like it's too late to save anybody on board.>
		Msg2 <The pirates have been defeated. You are now free to loot the wreckage yourself, or maybe look for survivors.>
		Msg3 <Look for survivors.>
		Msg4 <Loot the wreckage.>
		Msg5 <You find %name2% in the wreckage, and bring \OPR %2% to %name5%.>
		Msg6 <You rescued %name2% from the wreckage.>
		Msg7 <You found a spaceship being attacked by pirates.>
		Msg8 <While searching the wreckage, you find an archaic paper diary. It appears to be the captain's log.>
		Msg9 <You recovered the captain's log from the wreckage.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction %4%   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25

			void
			name <Enemy Zone>
			width 12
			height 12
			MFX 1
			MFY 19
			sub
				SuperProp
				requires <*Battleship>
				SetTeam 0
			end
		end
	end
	inv
		Treasure
		name <Captain's Log>
		desc <An archaic-style paper diary. It apparently belonged to the ship's captain.>
		use <Alert 1 Alert 2 Alert 3 Alert 4>
		Msg1 <[The log describes the crew's search for a hidden treasure. The exact nature of this treasure isn't specified.]>
		Msg2 <"Our wandering has led us here, to %name6%. The pirates have been on our tail every step of the way, which means we must be close. I can almost smell it.">
		Msg3 <"Still, we aren't out of trouble yet. There are others who are on the same trail. If we can't find it before %name2% gets here, then I fear we may all be as good as dead.">
		Msg4 <[The log ends here.]>
	end


Plot
	name <I will not be stopped!>
	desc <The PC will make a new enemy. Hooray!>
	requires <+Ten (+P--|+Pun|+Pla) E:-->
	Size 5

	% E1 is the encounter for this component
	% E2 is a new enemy NPC
	Element1 <Grab 6>
	Element2 <NewNPC -2 -7 Villainous>
	% Note that even though E2 talks in this encounter, he doesn't actually show up
	% in person.
	Place2 </>

	sub
		Persona 2
		*GREETING <*BattleIntroduction GoInfo>
		GoInfo <EndChat Say 1>
		Msg1 <I don't care who sent you; you have no chance of stopping my plan! My men should be able to easily defeat you on their own.>

		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1 ForceChat %2% &SetEnemyNPC %2% StoryNote 1>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   LoseComp 0 .next>
		nu2 <if= T2 0   if= V1 0 V= 1 1   Print 2 XPV 100  AddRenown 1   SALVAGE   WinComp 0 .next>
		.next <+T-- +F-->
		Msg1 <You met %name2% in battle, and fought \PPR %2% henchmen.>
		Msg2 <You have defeated the henchmen.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction N2   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25
		end
	end

Plot
	name <Proving Ground>
	desc <The PC can help a pilot, and get a job.>
	requires <+Ten (!Ne|!Lo) (+F--|+Ffi) (+P--|+Pun) P:-- -L:ENEMY>
	Size 4

	% E1 is the encounter for this component
	% E2 is a local scene for the next meeting
	% E3 is a mecha pilot for the PC to help
	Element1 <Grab 6>
	Element2 <Scene Building Public !Near -7 !Ally -7>
	Element3 <Character HasMecha !Comrade 2 NeverFail>
	Place3 <1 (Allies) sd ally>

	% P1 = Initialization Counter
	start <ifNPCDead %3% LoseComp 0 .next>
	.next <+T-- +F-->
	update <if= P1 0 P= 1 1 NPCLevel %3% StoryDL>

	sub
		Persona 3
		special <Unlisted NoEscape>
		greeting <if# V1 0 else GoFirst EndChat Say 1>
		*GoFirst <*HelpMeVsMecha GoAccept GoDeny>
		GoAccept <V= 1 1 EndChat Say 2 AddReact 10>
		GoDeny <V= 1 1 EndChat Say 3 AddReact -10>
		Msg1 <Shouldn't we concentrate on fighting right now?>
		Msg2 <Great! If you can prove yourself against these foes, I'll have something for you later.>
		Msg3 <You coward! You would abandon \SCENE RootSceneID to its enemies?>

		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		% L1 = Match Over counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1 ForceChat %3%>
		% If the NPC dies, this scenario ends in a loss.
		Faint%3% <L= 1 1  Goto GoLoseMission>
		% If the PC defeats all of T2 before the NPC dies, it's a win.
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   Goto GoLoseMission>
		nu2 <if= T2 0            if= V1 0 V= 1 1   Goto GoWinMission>

		GoWinMission <XPV 100  AddRenown 1   Monologue %3% 1 Print 2 MoveAndPacifyNPC %3% %2% SMemo 3 History 4 &SetTargetNPC %3% WinComp %2% .win>
		.win <+Tgt +Fmi>
		GoLoseMission <AddRenown -5 SMemo 0 History 5 LoseComp 0 .loss ifNPCOK %3% MoveAndPacifyNPC %3% %2%>
		.loss <+T-- +F-->

		Msg1 <Thanks for helping me out! Meet me at %name2% later and I can repay the favor by giving you a mission.>
		Msg2 <%name3% leaves the area.>
		Msg3 <In thanks for helping %name3% to defeat some mecha, \SPR %3% offered to give you a mission later at %name2%.>
		Msg4 <You helped %name3% defeat some mecha.>
		Msg5 <You failed to help %name3% fight off some mecha.>

		sub
			team 1
			SetAlly 3
			SetEnemy 2
			ParaX 5
			ParaY 10

			team 2
			SetEnemy 1 3
			Deploy <SetSelfFaction &EnemyFac   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25

			team 3
			name <Allies>
			SetAlly 1
			SetEnemy 2
			ParaX 10
			ParaY 5
		end
	end

Plot
	name <Lover's Quarrel>
	desc <An ex-boyfriend and ex-girlfriend are argueing at the party.>
	requires <+Tsh (+P--|+Pun|+Pla) E:-- ~+Fpa ~+Glo>
	Size 3

	% E1 is the scene where this component will take place
	% E2 is the male character
	% E3 is the female character
	% E4 is a scene for the rescued character to go to afterwards
	Element1 <Grab 6>
	Element2 <NewNPC 0 -7 Male Sociable Passionate>
	Place2 <1 (Citizens) Pass Ally>
	Element3 <NewNPC 0 -7 Female Sociable Passionate>
	Place3 <1 (Citizens) Pass Ally>
	Element4 <Scene Building Public !Near -7 !Okay 2 !Okay 3>

	start <ifNPCOK %2% else GoDelete ifNPCOK %3% else GoDelete >
	GoDelete <LoseComp 0 .next>
	.next <+T-- +F-->

	sub
		STC MS_CLUB
		SetID 1
		% L11 = Identity of aggressor
		% At START, E2 and E3 will fight.
		Start <if= L11 0 ifNPCInPlay %2% ifNPCInPlay %3% ifNPCSexy %3% else GoMaleVictim Monologue %2% 11 Monologue %3% 12 L= 11 2 StoryNote 13>
		GoMaleVictim <Monologue %3% 11 Monologue %2% 12 L= 11 3 StoryNote 13>
		Msg11_1 <You're coming with me.>
		Msg11_2 <You're making me angry. I said, let's go.>
		Msg11_3 <This place sucks. Come on, let's split.>
		Msg12_1 <I'm not going anywhere with you. Not anymore.>
		Msg12_2 <In case you've forgotten, we're not together anymore.>
		Msg12_3 <I told you already to get lost. We're over.>
		Msg13 <You found %name2% and %name3% quarreling at %name1%.>

		Persona 2
		special <UNLISTED>
		% V1 = Drive Away Attempt Counter
		&ExID <%3%>
		greeting <ifNPCOK &ExID else GoChat if= L11 2 else GoIsVictim NewChat Say 1 AddChat 1 AddChat 2>
		GoIsVictim <NewChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		GoChat <NewChat SayAnything>
		GoDriveAway <Print 11 XPV 100 History 12 SMemo 13 &SetTargetNPC &ExID &SetEnemyNPC ChatNPCID FreezeNPC ChatNPCID FreezeNPC &ExID WinComp 0 .next>
		GoBigFail <EndChat Say 14 Print 11 History 12 SMemo 13 &SetTargetNPC &ExID &SetEnemyNPC ChatNPCID FreezeNPC ChatNPCID FreezeNPC &ExID LoseComp 0 .next>
		.next <+Tpt +F-->
		result1 <NewChat Say 3 AddChat 6 AddChat 7 AddChat 8 AddChat 9>
		result2 <NewChat Say 4>
		result3 <NewChat Say 5>
		result4 <NewChat Say 6 AddChat 3 AddChat 5>
		result5 <EndChat Monologue &ExID 7 Say 8 StoryNote 9 &SetEnemyNPC &ExID FreezeNPC &ExID MoveNPC ChatNPCID %4% NegateComp 0 .negate>
		.negate <+T-- +F-->
		result6 <ifUFlirtation SkillTar StoryDL else GoR6Fail EndChat Say 10 Goto GoDriveAway>
		GoR6Fail <V+ 1 1 ifG 4 V1 else GoBigFail NewChat Say 15 AddChat 10 AddChat 11>
		*result7 <*InsultContest GoR7Win GoBigFail StoryDL>
		GoR7Win <EndChat Say 16 Goto GoDriveAway>
		result8 <ifUConversation SkillTar StoryDL else GoR8Fail EndChat Say 17 Goto GoDriveAway>
		GoR8Fail <V+ 1 1 ifG 4 V1 else GoBigFail NewChat Say 18 AddChat 10 AddChat 11>
		result9 <ifUIntimidation SkillTar StoryDL else GoR9Fail EndChat Say 19 Goto GoDriveAway>
		GoR9Fail <V+ 1 1 ifG 4 V1 else GoBigFail NewChat Say 20 AddChat 10 AddChat 11>
		result10 <NewChat Say 3 AddChat 6 AddChat 7 AddChat 8 AddChat 9>
		result11 <NewChat Say 21>
		Msg1 <Yeah, what do you want?>
		Msg2 <I wish \PERSONA &ExID would just leave me alone...>
		Msg3 <Why? Don't tell me you're \PPR &ExID new fling... talk about trading down. You don't know anything about what's going on between me and \PERSONA &ExID so you better just butt out.>
		Msg4 <\PERSONA &ExID 's been acting strange lately. Why won't \SPR &ExID just do everything I say?>
		Msg5 <No, please don't... \SPR &ExID has an awful temper. I don't want to see anyone else get hurt.>
		Msg6 <I'm not having a problem, it's \PERSONA &ExID who just can't accept that we're through. I've tried telling \OPR &ExID over and over but it's no good.>
		Msg7 <Hey, what are you doing talking to \PERSONA ChatNPCID ?! Don't you know \SPR &ExID belongs to me?>
		Msg8 <Oh no... \PERSONA &ExID is really jealous. I better try to get \OPR &ExID out of here before \SPR &ExID starts a fight.>
		Msg9 <\PERSONA &ExID became jealous when you spoke with \PERSONA ChatNPCID .>
		Msg10 <Is that so? Well, I've got news for you... \PERSONA &ExID belongs to me, understand?>
		Msg11 <\PERSONA ChatNPCID storms out of the building. As you look around, you notice that \PERSONA &ExID has disappeared. Hopefully \SPR &ExID will be alright.>
		Msg12 <You drove away \PERSONA ChatNPCID , who seemed intent on revenge.>
		Msg13 <You drove \PERSONA ChatNPCID away from %name1%, but worry that \SPR ChatNPCID might come for revenge against either you or \PPR ChatNPCID ex-lover \PERSONA &ExID .>
		Msg14 <I've heard enough. I don't know what's going on between you and \PERSONA &ExID , but I'm going to put a stop to it.>
		Msg15 <You and \PERSONA &ExID ? Don't make me laugh.>
		Msg16 <I guess both you and \PERSONA &ExID are going to have to learn the hard way that insulting me isn't a smart idea.>
		Msg17 <Why are you so interested in \PERSONA &ExID ? I'll go... but don't think you're going to get away with this.>
		Msg18 <This isn't your business, so it's no difference to you how we try to solve it!>
		Msg19 <I don't want to start a fight here, not now... but I'll find you later and we can settle it then.>
		Msg20 <As if I'm supposed to care about that.>
		Msg21 <Yeah, I was getting the same impression.>
		Prompt1 <I think you should leave \PERSONA &ExID alone.>
		Prompt2 <Nothing...>
		Prompt3 <If you want, I can get rid of \OPR &ExID .>
		Prompt4 <Are you two having problems?>
		Prompt5 <You're ruining the party. The two of you should go.>
		Prompt6 <Maybe I am \PERSONA &ExID 's new fling.>
		Prompt7 <My, you really are a stupid one, aren't you?>
		Prompt8 <You and \PERSONA &ExID can settle your differences in private.>
		Prompt9 <I know that you're starting to piss me off.>
		Prompt10 <Let me try something else.>
		Prompt11 <I can see it's useless talking to you...>

		Persona 3
		special <UNLISTED>
		% V1 = Drive Away Attempt Counter
		&ExID <%2%>
		greeting <ifNPCOK &ExID else GoChat if= L11 3 else GoIsVictim NewChat Say 1 AddChat 1 AddChat 2>
		GoIsVictim <NewChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		GoChat <NewChat SayAnything>
		GoDriveAway <Print 11 XPV 100 History 12 SMemo 13 &SetTargetNPC &ExID &SetEnemyNPC ChatNPCID FreezeNPC ChatNPCID FreezeNPC &ExID WinComp 0 .next>
		GoBigFail <EndChat Say 14 Print 11 History 12 SMemo 13 &SetTargetNPC &ExID &SetEnemyNPC ChatNPCID FreezeNPC ChatNPCID FreezeNPC &ExID LoseComp 0 .next>
		.next <+Tpt +F-->
		result1 <NewChat Say 3 AddChat 6 AddChat 7 AddChat 8 AddChat 9>
		result2 <NewChat Say 4>
		result3 <NewChat Say 5>
		result4 <NewChat Say 6 AddChat 3 AddChat 5>
		result5 <EndChat Monologue &ExID 7 Say 8 StoryNote 9 &SetEnemyNPC &ExID FreezeNPC &ExID MoveNPC ChatNPCID %4% NegateComp 0 .negate>
		.negate <+T-- +F-->
		result6 <ifUFlirtation SkillTar StoryDL else GoR6Fail EndChat Say 10 Goto GoDriveAway>
		GoR6Fail <V+ 1 1 ifG 4 V1 else GoBigFail NewChat Say 15 AddChat 10 AddChat 11>
		*result7 <*InsultContest GoR7Win GoBigFail StoryDL>
		GoR7Win <EndChat Say 16 Goto GoDriveAway>
		result8 <ifUConversation SkillTar StoryDL else GoR8Fail EndChat Say 17 Goto GoDriveAway>
		GoR8Fail <V+ 1 1 ifG 4 V1 else GoBigFail NewChat Say 18 AddChat 10 AddChat 11>
		result9 <ifUIntimidation SkillTar StoryDL else GoR9Fail EndChat Say 19 Goto GoDriveAway>
		GoR9Fail <V+ 1 1 ifG 4 V1 else GoBigFail NewChat Say 20 AddChat 10 AddChat 11>
		result10 <NewChat Say 3 AddChat 6 AddChat 7 AddChat 8 AddChat 9>
		result11 <NewChat Say 21>
		Msg1 <Yeah, what do you want?>
		Msg2 <I wish \PERSONA &ExID would just leave me alone...>
		Msg3 <Why? Don't tell me you're \PPR &ExID new fling... talk about trading down. You don't know anything about what's going on between me and \PERSONA &ExID so you better just butt out.>
		Msg4 <\PERSONA &ExID 's been acting strange lately. Why won't \SPR &ExID just do everything I say?>
		Msg5 <No, please don't... \SPR &ExID has an awful temper. I don't want to see anyone else get hurt.>
		Msg6 <I'm not having a problem, it's \PERSONA &ExID who just can't accept that we're through. I've tried telling \OPR &ExID over and over but it's no good.>
		Msg7 <Hey, what are you doing talking to \PERSONA ChatNPCID ?! Don't you know \SPR &ExID belongs to me?>
		Msg8 <Oh no... \PERSONA &ExID is really jealous. I better try to get \OPR &ExID out of here before \SPR &ExID starts a fight.>
		Msg9 <\PERSONA &ExID became jealous when you spoke with \PERSONA ChatNPCID .>
		Msg10 <Is that so? Well, I've got news for you... \PERSONA &ExID belongs to me, understand?>
		Msg11 <\PERSONA ChatNPCID storms out of the building. As you look around, you notice that \PERSONA &ExID has disappeared. Hopefully \SPR &ExID will be alright.>
		Msg12 <You drove away \PERSONA ChatNPCID , who seemed intent on revenge.>
		Msg13 <You drove \PERSONA ChatNPCID away from %name1%, but worry that \SPR ChatNPCID might come for revenge against either you or \PPR ChatNPCID ex-lover \PERSONA &ExID .>
		Msg14 <I've heard enough. I don't know what's going on between you and \PERSONA &ExID , but I'm going to put a stop to it.>
		Msg15 <You and \PERSONA &ExID ? Don't make me laugh.>
		Msg16 <I guess both you and \PERSONA &ExID are going to have to learn the hard way that insulting me isn't a smart idea.>
		Msg17 <Why are you so interested in \PERSONA &ExID ? I'll go... but don't think you're going to get away with this.>
		Msg18 <This isn't your business, so it's no difference to you how we try to solve it!>
		Msg19 <I don't want to start a fight here, not now... but I'll find you later and we can settle it then.>
		Msg20 <As if I'm supposed to care about that.>
		Msg21 <Yeah, I was getting the same impression.>
		Prompt1 <I think you should leave \PERSONA &ExID alone.>
		Prompt2 <Nothing...>
		Prompt3 <If you want, I can get rid of \OPR &ExID .>
		Prompt4 <Are you two having problems?>
		Prompt5 <You're ruining the party. The two of you should go.>
		Prompt6 <Maybe I am \PERSONA &ExID 's new fling.>
		Prompt7 <My, you really are a stupid one, aren't you?>
		Prompt8 <You and \PERSONA &ExID can settle your differences in private.>
		Prompt9 <I know that you're starting to piss me off.>
		Prompt10 <Let me try something else.>
		Prompt11 <I can see it's useless talking to you...>
	end

Plot
	name <Recruiting Drive>
	desc <Recruiters from two corporate factions are at the conference; maybe the PC will be recruited.>
	requires <+Tsh (+P--|+Pme|+Pla) P:-- F:-- (C:CORPO|C:LABOR|C:CRAFT|C:TRADE) ~+Fcc>
	Size 1

	% E1 is the scene where this component will take place
	% E2 is a corporate building in this city
	% E3 is Corporate Faction 1, ally of E2
	% E4 is Corporate Faction 2, enemy of E3
	% E5 is a member of E3
	% E6 is a public building
	% E7 is a member of E4
	Element1 <Grab 6>
	Element2 <Scene Building Corporate !Near -7>
	Element3 <Faction Corporate !Comrade 2>
	Element4 <Faction Corporate !Enemy 3>
	ELement5 <Character !Comrade 3 !Near 2 NeverFail>
	NeverFail5 <Corporate Executive>
	Place5 <1 (Citizens) Pass Ally>
	Element6 <Scene Building Public !Okay 4 !Near -7>
	ELement7 <Character !Comrade 4 !Near 6 NeverFail>
	NeverFail7 <Corporate Executive>
	Place7 <1 (Citizens) Pass Ally>

	% P5 = Have been offered job by E5
	% P7 = Have been offered job by E7

	start <ifNPCDead %5% ifNPCDead %7% LoseComp 0 .next>
	.next <+T-- +F-->

	% SubPlot1 is the delayed victory, if the PC accepts the job.
	SubPlot1 <*CS_DelayedVictory>

	sub
		Persona 5
		rumor0 <%name5% is recruiting for %name3% at %name1% today.>
		% V1 = Mecha Size
		% V2 = Signing Bonus
		&RecFac <%3%>
		&AltFac <%4%>
		greeting <if= PlotStatus 0 else GoChat Goto GoCheckFirst>
		GoChat <NewChat SayAnything>
		GoCheckFirst <IFFACTIONENEMY &RecFac else GoOfferJoin NewChat Say 1 StoryNote 2 &SetEnemyFac &RecFac WinComp 0 .make_enemy>
		GoOfferJoin <NewChat Say 3 AddChat 1 AddChat 2 AddChat 3 AddChat 4 P= 5 1>
		.make_enemy <+T-- +F-->
		result1 <NewChat Say 4 AddChat 2 AddChat 3 AddChat 5 AddChat 6 V= 1 StoryDL V= 2 150000>
		result2 <NewChat Say 5 StoryNote 6 &SetEnemyFac &RecFac WinComp 0 .make_enemy>
		result3 <NewChat Say 7>
		result4 <NewChat Say 8 SMemo 0 &SetAllyFac PCFac WinComp 0 .make_enemy>
		result5 <NewChat Say 9 StoryNote 10 &SetAllyFac &RecFac  RandomMecha .meks V1  CashPrize V2  XPV 200  AlterContext .make_enemy SetPlotStatus %id1%>
		.meks <GENERAL \FACTION_DESIG &RecFac>
		result6 <NewChat Say 11 AddChat 2 AddChat 3 AddChat 5 V+ 1 10 V= 2 * 2 V2  &SetEnemyFac &AltFac>
		Msg1 <The evil plans of %name3% will soon come to fruition! Oh, wait, you're not the human resources director... forget I said that.>
		Msg2 <%name5% revealed that %name3% is planning something.>
		Msg3 <You should know that %name3% is looking for some fresh talent. If you'd like a position with our company, I can sign you up right away.>
		Msg3_1 <>
		Msg3_2 <>
		Msg4 <For joining %name3% you'll receive a company mecha, skill training, access to company perks and the possibility of rapid promotion. What do you say?>
		Msg4_1 <>
		Msg4_2 <>
		Msg5 <You choose to reject us? Fine... but be warned that the %name3% juggernaut will not be stopped!>
		Msg5_1 <>
		Msg6 <%name5% seemed upset that you refused to join %name3%.>
		Msg7 <Please do. Let me know when you've made your decision.>
		Msg7_1 <>
		Msg8 <Well, isn't that nice. If you'll excuse me I should go scout out some people who are actually looking for jobs.>
		Msg9 <Great! To start with, here's your company mecha and an equipment bonus. We'll let you know when there's some work for you to do.>
		Msg9_1 <>
		Msg10 <You accepted a job offer from %name3%.>
		Msg11 <Is that so? Well, whatever %name4% is offering, %name3% can promise more! How about it, do we have a deal?>
		Msg11_1 <>
		Prompt1 <What do I get for signing on?>
		Prompt2 <No thanks. I'm not interested.>
		CPrompt2 <if# P5 0 Accept>
		Prompt3 <Hmmm... I'll think about that.>
		Prompt4 <I'm quite happy working for \FACTION PCFac .>
		CPrompt4 <if# PCFac 0 Accept>
		Prompt5 <Alright, I'll join!>
		Prompt6 <%name7% offered me a better deal.>
		CPrompt6 <if# P7 0 Accept>

		Persona 7
		rumor0 <%name7% is recruiting for %name4% at %name1% today.>
		% V1 = Mecha Size
		% V2 = Signing Bonus
		&RecFac <%4%>
		&AltFac <%3%>
		greeting <if= PlotStatus 0 else GoChat Goto GoCheckFirst>
		GoChat <NewChat SayAnything>
		GoCheckFirst <IFFACTIONENEMY &RecFac else GoOfferJoin NewChat Say 1 StoryNote 2 &SetEnemyFac &RecFac WinComp 0 .make_enemy>
		GoOfferJoin <NewChat Say 3 AddChat 1 AddChat 2 AddChat 3 AddChat 4 P= 7 1>
		.make_enemy <+T-- +F-->
		result1 <NewChat Say 4 AddChat 2 AddChat 3 AddChat 5 AddChat 6 V= 1 StoryDL V= 2 150000>
		result2 <NewChat Say 5 StoryNote 6 &SetEnemyFac &RecFac WinComp 0 .make_enemy>
		result3 <NewChat Say 7>
		result4 <NewChat Say 8 SMemo 0 &SetAllyFac PCFac WinComp 0 .make_enemy>
		result5 <NewChat Say 9 StoryNote 10 &SetAllyFac &RecFac  RandomMecha .meks V1  CashPrize V2  XPV 200  AlterContext .make_enemy SetPlotStatus %id1%>
		.meks <GENERAL \FACTION_DESIG &RecFac>
		result6 <NewChat Say 11 AddChat 2 AddChat 3 AddChat 5 V+ 1 10 V= 2 * 2 V2  &SetEnemyFac &AltFac>
		Msg1 <The evil plans of %name4% will soon come to fruition! Oh, wait, you're not the human resources director... forget I said that.>
		Msg2 <%name7% revealed that %name4% is planning something.>
		Msg3 <You should know that %name4% is looking for some fresh talent. If you'd like a position with our company, I can sign you up right away.>
		Msg3_1 <>
		Msg3_2 <>
		Msg4 <For joining %name4% you'll receive a company mecha, skill training, access to company perks and the possibility of rapid promotion. What do you say?>
		Msg4_1 <>
		Msg4_2 <>
		Msg5 <You choose to reject us? Fine... but be warned that the %name4% juggernaut will not be stopped!>
		Msg5_1 <>
		Msg6 <%name7% seemed upset that you refused to join %name4%.>
		Msg7 <Please do. Let me know when you've made your decision.>
		Msg7_1 <>
		Msg8 <Well, isn't that nice. If you'll excuse me I should go scout out some people who are actually looking for jobs.>
		Msg9 <Great! To start with, here's your company mecha and an equipment bonus. We'll let you know when there's some work for you to do.>
		Msg9_1 <>
		Msg10 <You accepted a job offer from %name4%.>
		Msg11 <Is that so? Well, whatever %name3% is offering, %name4% can promise more! How about it, do we have a deal?>
		Msg11_1 <>
		Prompt1 <What do I get for signing on?>
		Prompt2 <No thanks. I'm not interested.>
		CPrompt2 <if# P7 0 Accept>
		Prompt3 <Hmmm... I'll think about that.>
		Prompt4 <I'm quite happy working for \FACTION PCFac .>
		CPrompt4 <if# PCFac 0 Accept>
		Prompt5 <Alright, I'll join!>
		Prompt6 <%name5% offered me a better deal.>
		CPrompt6 <if# P5 0 Accept>

		STC MS_SOCIAL
		SetID 1
	end

Plot
	name <Meet Enemy Fighter>
	desc <PC meets a rival corporate pilot at the convention, and will be challenged to duel.>
	requires <+Tsh +P-- E:-- (F:--|F:CORPO) ~+Fcc>
	Size 1

	% E1 is the scene where this component will take place
	% E2 is the faction to which the enemy belongs
	% E3 is the natural home of the enemy
	% E4 is the enemy character itself
	% E5 is a scene outside
	element1 <Grab 6>
	element2 <Faction Corporate ArchEnemy !Ally -2>
	element3 <Scene Building Public !Okay 2>
	element4 <Prefab>
	Place4 <3 Pass>
	element5 <Scene Urban !Near -7>

	start <if= PlotStatus 0 ifNPCDead %4% LoseComp 0 .next>
	.next <+T-- +F-->

	% V1 = Initialization Counter
	update <if= V1 0 V= 1 1  MoveNPC %4% %1%  SetNPCFaction %4% %2%  NPCLevel %4% StoryDL>

	% SubPlot1 is the duel
	SubPlot1 <*CS_Duel 4 3 5>

	% Plot Resolvers
	.%id1%_%plotid%_GoWin <&SetEnemyNPC %4% StoryNote 1 WinComp 0 .win>
	.%id1%_%plotid%_GoLose <&SetEnemyNPC %4% StoryNote 2 LoseComp 0 .win>
	.win <+Pme +T-- +F-->

	Msg1 <You bested %name2% in a duel.>
	Msg2 <You were defeated by %name2% in a duel.>


	sub
		Persona 4
		special <UNLISTED>
		rumor <%name4% has been looking for someone to challenge.>
		Greeting <if# plotStatus 0 else GoGreet NewChat SayAnything>
		*GoGreet <*NiceToMeetYou GoChallenge>
		GoChallenge <NewChat say 1 AddChat 1 AddChat 2>
		GoAcceptChallenge <StoryNote 3 SetPlotStatus %id1%>
		result1 <NewChat Say 2 Goto GoAcceptChallenge>
		result2 <NewChat Say 4 AddChat 3 AddChat 4>
		result3 <Goto result1>
		result4 <&SetEnemyNPC %4% StoryNote 7  ifG PCRenown StoryDL else GoR4Fail NewChat Say 6 AddReact d6  WinComp 0 .loss>
		GoR4Fail <NewChat Say 5 AddRenown -1 LoseComp 0 .loss>
		.loss <+T-- +F-- +Pme>
		Msg1 <You work for the competition, don't you? Let's take our mecha to the streets of %name5% and see which one of us is better.>
		Msg2 <Meet me in \ELEMENT 5 then.>
		Msg3 <You were challenged to a duel by %name2% pilot %name4% in %name5%.>
		Msg4 <Are you afraid that you'll lose? Come now, you can't tell me that you're as timid as that.>
		Msg5 <So that's how it is...>
		Msg6 <I see. Well, I was hoping to test my skill against the famous \PC , but it seems I will have to be disappointed.>
		Msg7 <You were challenged to a duel by %name2% pilot %name4%, but refused.>
		Prompt1 <Sounds good. Let's go.>
		Prompt2 <I've got better things to do than fight you.>
		Prompt3 <Alright, if you really want, let's do this.>
		Prompt4 <No, really, I don't want to fight.>

		STC MS_SOCIAL
		SetID 1
	end
	inv
		NPC Mecha Pilot
		job <Corporate Pilot>
	end

Plot
	name <Not Even The Police Are Safe>
	desc <The officer you have to see for the mission? He got robbed.>
	requires <#A +Tgt (+Fmi|+Fin) +P-- T:POLIC (F:--|F:CRIME)>

	% E1 is the building
	% E2 is the police officer
	% E3 is the thief
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	element3 <Prefab>

	sub
		STC MS_OFFICE
		SetID 1

		Persona 2
		special <UNLISTED>
		greeting <NewChat Say 1 AddChat 1>
		result1 <NewChat Say 2 StoryNote 3 FreezeNPC %3% &SetTargetNPC %3% WinComp 0 .next>
		.next <+Pun +Tht +Far>

		Msg1 <It's a sad day in \SCENE &EpisodeScene when not even the police are safe...>
		Msg1_1 <>
		Msg2 <On my way here today I was robbed by %name3%! This could well be the start of a crime wave. I'm sorry but my mission slate was among the items stolen, so I don't even have any work for you.>
		Msg2_1 <>
		Msg3 <You went to see %name2% , but found \SPR %2% had been robbed by %name3%.>
		Prompt1 <Tell me what happened.>
		Prompt1_1 <What happened here?>
	end
	inv
		NPC Thief
		Combatant
	end

Plot
	name <Heist At Corp Office>
	desc <This corporate office has just been raided. Yay!>
	requires <#A (+P--|+Pme) +T-- F:CRIME ~P:CORPO ~C:CORPO>

	% E1 is the building.
	% E2 is the security head who gives the mission.
	% E3 is the urban scene where the encounter will be placed.
	Element1 <Scene Building Corporate Public !Near -7 !Okay -3>
	Element2 <Prefab>
	Place2 <1 SD Ally>
	Element3 <Scene Urban !Near -7>

	% V1 = Initialization Counter
	start <if= PlotStatus 0 ifNPCOK %2% else GoDelete if= V1 1 if= SceneID %1% Alert 1 SMemo 3 V= 1 2>
	GoDelete <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if= V1 0 V= 1 1  SetNPCFaction %2% SceneFac %1%   News 2>

	Msg1 <This office looks like it's just been attacked. %name2% is surveying the damage.>
	Msg2 <%name1% in \NARRATIVE 7 has been the target of a raid by \NARRATIVE 2 .>
	Msg3 <%name1% was apparently attacked by \NARRATIVE 2 . You could speak with %name2% there about it.>

	%% SubPlot1 is the combat encounter itself
	SubPlot1 <*CS_MechaEncounter 2 3>

	sub
		Persona 2
		rumor0 <%name2% is the new security head at %name1%.>
		special <UNLISTED>
		Greeting <if= PlotStatus %id1% else GoCheckFirst NewChat Say 10>
		GoCheckFirst <if= PlotStatus 0 else GoChat Goto GoGreet>
		*GoGreet <*DestructionGreeting GoGiveMission &EnemyFac>
		*GoChat <*MISC_CHATTER>
		&TheCompany <SceneFac %1%>
		GoGiveMission <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		result1 <NewChat Say 2 AddChat 4 AddChat 5>
		result2 <NewChat Say 3 AddChat 4 AddChat 5>
		result3 <NewChat Say 4 SMemo 5 AddSociable -d6 LoseComp 0 .next>
		result4 <NewChat ifConversation SkillTar StoryDL else GoR4Fail Say 7 RandomMecha .mek StoryDL Goto GoStartMission>
		.mek <GENERAL>
		GoR4Fail <NewChat Say 8 SMemo 5 LoseComp 0 .next>
		*result5 <*GoodLuckOnMission GoStartMission &TheCompany &EnemyFac>
		GoStartMission <SMemo 9 History 6 SetPlotStatus %id1%>
		.next <+T-- +Pun>
		Msg1 <We've been robbed! It was \FACTION &EnemyFac , I'm sure of it... Their mecha blew through the back wall and carried off our data core!>
		Msg2 <The bandits can't have gotten very far... If you search the nearby area I'm sure you'll be able to find them.>
		Msg3 <If you think you can find these bandits and put them out of business, I think I'll be able to find a reward for you. Is it a deal?>
		Msg4 <Well, be careful out there... Those bandits are probably still in the area.>
		Msg5 <%name1% was attacked by bandits, who are probably still in the area.>
		Msg6 <%name2% hired you to locate the bandits who robbed %name1%.>
		Msg7 <That's not a problem. We manufacture all kinds of mecha here... I'll set you up with a brand new unit and you can see about finding those bandits.>
		Msg8 <I see. Well, I'll just have to hope that the police manage to find the bandits. Be careful out there... They could still be somewhere in the city.>
		Msg9 <%name2% hired you to locate the bandits who robbed %name1%. They can probably be found in %name3%.>
		Msg10 <Come back here when you've found the bandits who attacked the place.>
		Prompt1 <Sounds like you could use some help.>
		Prompt2 <Are you offering a reward to find them?>
		Prompt3 <Not my problem. I'll be leaving now.>
		CPrompt3 <if# PCFac &TheCompany Accept>
		Prompt4 <I'd like to help, but I have no mecha.>
		CPrompt4 <if= PCMeks 0 Accept>
		Prompt5 <I'll be back when I've found them.>
	end
	inv
		NPC Soldier
		job <Security Chief>
		chardesc old
	end

Plot
	name <Surprise Attack>
	size 1
	desc <The meeting place is attacked by mecha!>
	requires <#A +Tgt (+P--|+Pme) T:++ S:meta>

	% P1 = Initialization Counter

	% E1 is the scene where this will take place
	% E2 is the NPC the PC wants to talk to
	% E3 is the scene outside the meeting area
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass ally>
	element3 <Scene Urban !Near -7>

	% SubPlot1 contains the ambush battle
	% SubPlot2 contains the victory, if the PC defeats the enemies
	SubPlot1 <*CS_AmbushEncounter 3>
	SubPlot2 <*CS_ThanksForRescue 2>

	% Needed Scripts
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoJustEnd Alert 4 StoryNote 2 SetPlotStatus %id2%>
	.%id1%_%plotid%_GoLose <ifNPCOK %2% else GoJustEnd Alert 3 StoryNote 1 DeleteNPC %2% Goto GoJustEnd>
	GoJustEnd <LoseComp 0 .loss>
	.loss <+T-- +Pun>
	Msg1 <%name2% died during a mecha attack.>
	Msg2 <You rescued %name2% from a mecha attack.>
	Msg3 <%name1% is blasted by the mecha, killing those trapped inside.>
	Msg4 <You have defended %name1% from the mecha attack.>

	sub
		MetaScene 1
		% L1 = Bombing started
		% L2 = Bomb counter

		ClubMap
		MapWidth 20
		MapHeight 25

		type <CLUB LEGIT PUBLIC>
		special <AddExit>
		Content <Some 5 25 Sub *CLUB_X>

		5Min <if= PlotStatus 0 else GoBomb if= L1 0 V+ 2 1 ifG V2 1 if= d2 1 Goto GoStartBombing>
		GoStartBombing <SetPlotStatus %id1% AirRaidSiren Alert 1 StoryNote 2 Bomb  AlterContext .next  L= 1 1>
		Faint%2% <if= PlotStatus 0 Goto GoStartBombing>
		GoBomb <if= PlotStatus %id% Print 3 Bomb>
		nu1 <if= T1 0 Return>
		.next <+Pun>
		Msg1 <Unexpectantly, the building is attacked by mecha!>
		Msg2 <While meeting %name2%, %name1% was attacked by mysterious mecha.>
		Msg3 <The battle outside rages!>
		sub
			Team 1

			Team 2
			name <Citizens>
			setally 1
			Passive

			room
			name <Dance Floor>
			desig <EntranceGrid>
			Content <Some 8 45 Here *URBAN_X>
		end

		Persona 2
		special <UNLISTED>
		greeting <if= PlotStatus %id1% else GoCheckFirstMeeting EndChat Say 4>
		GoCheckFirstMeeting <if= PlotStatus 0 else GoChat Goto GoFirstMeeting>
		*GoFirstMeeting <*HowAreYou GoMessage>
		GoMessage <NewChat Say 1 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <AddEasygoing d6 Goto result2>
		result2 <Alert 5  EndChat Say 2  AirRaidSiren StoryNote 3 SetPlotStatus %id1% AlterContext .next  L= 1 1>
		.next <+Pun>
		Msg1 <Say, do you hear anything?>
		Msg2 <We have to get out of here!>
		Msg3 <While meeting %name2%, %name1% was attacked by mysterious mecha.>
		Msg4 <We're under attack by mecha!>
		Msg5 <Unexpectantly, the building is attacked by mecha!>
		Prompt1 <No, I don't.>
		Prompt2 <Look at the window, there are mecha!>
	end


Plot
	name <A Gentle Reminder>
	desc <Your friend reminds you that you were supposed to attend a meeting.>
	requires <#A +P-- +Tgt ~+F-- T:++ T:FRIEND P:++ -P:PDASS>

	%% E1 is the scene.
	%% E2 is the friend.
	%% E3 is the superior to report to.
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	element3 <Character !Comrade -3 !Near -7 Renowned>

	start <ifNPCDead E2  LoseComp 0 .loss>
	.loss <+T-->

	sub
		STC MS_CLUB
		SetID 1

		Persona 2
		special <UNLISTED>
		*greeting <*HowAreYou GoChat>
		*GoChat <*Shimli_Chatter GoRemind>
		GoRemind <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3 AddChat 8 SMemo 2 History 10 Goto GoWin>
		GoWin <&SetTargetNPC %3%  WinComp NPCScene %3% .next>
		.next <+Tgt +Fmi>
		result1 <NewChat AddEasygoing -d3   Say 3 AddChat 4>
		result2 <NewChat AddEasygoing  d6   Say 4>
		result3 <NewChat AddCheerful   d4   Say 5  AddChat 5 AddChat 6>
		result4 <NewChat Say 6>
		result5 <NewChat Say 7>
		result6 <NewChat AddCheerful d4     Say 8 AddChat 5 AddChat 7>
		result7 <NewChat AddCheerful d4     Say 9>
		result8 <NewChat Say 11 AlterContext .loss>
		.loss <+T-- +F-->
		Msg1 <It's been nice chatting, but you better get a move on. Weren't you supposed to meet your boss %name3% today?>
		Msg1_1 <Say, weren't you supposed to meet your boss %name3% today? I think you mentioned something about it last night.>
		Msg1_2 <I guess you have to leave soon. You've got that big meeting with %name3% today...>
		Msg2 <You have to meet %name3% at \SCENE NPCScene %3% for important business regarding \NARRATIVE 3 .>
		Msg3 <Well, it's a good thing you came here then. Otherwise you would've forgotten altogether.>
		Msg4 <As a member of \FACTION PCFac you're always busy. I'll talk with you again after you finish at \SCENE NPCScene %3% .>
		Msg5 <Last night, when we were talking, I'm quite sure you mentioned a meeting at \SCENE NPCScene %3% ...>
		Msg6 <I think you said the meeting was at \SCENE NPCScene %3% . I'll talk with you after work.>
		Msg7 <Are you feeling OK, \PC ? I guess you've been stressed lately... Anyhow, I'll talk with you after work.>
		Msg8 <It's something about your job with \FACTION PCFac ... Are you really sure you don't remember?>
		Msg9 <Strange, you didn't seem drunk last night... Maybe you should go to \SCENE NPCScene %3% and talk with %name3% anyways, just to be sure.>
		Msg10 <%name2% reminded you to meet %name3%.>
		Msg11 <Oh. Well, I guess you won't be having that meeting, then...>
		Prompt1 <That's right! I totally forgot!>
		CPrompt1 <ifNPCOK %3% Accept>
		Prompt1_1 <Argh! I'm late!!!>
		Prompt2 <Yeah, I know.>
		CPrompt2 <ifNPCOK %3% Accept>
		Prompt2_1 <I'm going there now.>
		Prompt3 <No, I don't think so...>
		CPrompt3 <ifNPCOK %3% Accept>
		Prompt3_1 <I don't remember that...>
		Prompt4 <Thanks for the reminder...>
		Prompt4_1 <Thanks for reminding me.>
		Prompt5 <Ah, now I remember...>
		Prompt5_1 <Yes, that's right!>
		Prompt5_2 <Oh, you mean THAT meeting...>
		Prompt6 <Nope. Not ringing any bells.>
		Prompt7 <I'm getting nothing.>
		Prompt8 <Actually, %name3% is dead.>
		CPrompt8 <ifNPCDead %3% Accept>

		Persona 3
		greeting <NewChat Say 1>
		Msg1 <You're too early for our meeting. Come back later.>
	end

Plot
	name <Boring day, let's go duel!>
	Size 1
	desc <Your friend is bored, and suggests that the two of you go duel.>
	requires <#A +Tgt +P-- (+F--|+Fch) T:FRIEND (T:ADVEN|T:MILIT|T:PDASS) ~P:PDASS ~+Gfa>

	% E1 is the scene where the meeting will take place.
	% E2 is the friend being met.
	% E3 is a location for the duel.
	% E4 is a scene to use if the PC refuses the duel
	% E5 is a character to use if the PC refuses the duel
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>
	Element4 <Scene Building Public !Near -7 !Okay -7>
	Element5 <Character "MILIT" !Near -7 !Ally -7 !Okay -3>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+T-- +F-->

	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>

	sub
		Persona 2
		greeting <if= PlotStatus 0 else GoChat Goto GoGreet>
		*GoChat <*MISC_CHATTER>
		*GoGreet <*HowAreYou GoChallenge>
		GoChallenge <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <NewChat Say 2  StoryNote 4 SetPlotStatus %id1%>
		result2 <NewChat Say 3 AddChat 3 AddChat 4 AddChat 5
		result3 <NewChat Say 5 SMemo 7 &SetTargetNPC %5% NegateComp %4% .gojob>
		.gojob <+Tgt +Fmi>
		result4 <NewChat Say 6 AddEasygoing d8  LoseComp 0 .donothing>
		.donothing <+T-- +F-->
		result5 <Goto result1>
		Msg1 <It's been kind of a boring day. How would you like to try out our new mecha?>
		Msg1_1 <Say, I've been meaning to ask you something. I know that you're a bit of a mecha pilot too... How would you like try out against me in a friendly practice match?>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <I'm bored. How about we go try out our new mecha in a friendly match?>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Absolutely nothing's going on around here. I have an idea, though... What say we go try our new mecha in a friendly match?>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <I've been aching to try out my new mecha. Hey, you're a pilot... How about we go spar?>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <I've got a fun idea to spice up the day. How'd you like to fight a practice match against me?>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <Today has been so dull... I have an idea that might spice things up. How about we have an impromptu mecha fight?>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <Great. Come meet me in %name3% when you're ready.>
		Msg2_1 <I'll give you some time to get ready. Come meet me in %name3% when you are.>
		Msg3 <Is that so? What could be more important than a senseless mecha fight?>
		Msg3_1 <Really? Well, I didn't know that you had plans. What do you have to do, anyways?>
		CMsg3_1 <ifNPCSociable Accept>
		Msg3_2 <Is that so?>
		CMsg3_2 <ifNPCShy Accept>
		Msg3_3 <Cool. So, what's up?>
		CMsg3_3 <ifNPCEasygoing Accept>
		Msg3_4 <What could possibly be more important than defending your warrior honor?>
		CMsg3_4 <ifNPCPassionate Accept>
		Msg3_5 <Don't tell me that you have a date. What's going on?>
		CMsg3_5 <ifNPCCheerful Accept>
		Msg3_6 <Are you sure you're not just wussing out? What do you have to do that's so important?>
		CMsg3_6 <ifNPCMelancholy Accept>
		Msg4 <Your friend %name2% challenged you to a duel in %name3%.>
		Msg5 <Wow, that is a big deal. Good luck with that. I guess you better head on to %name4% then.>
		Msg5_1 <You don't say. I guess that is more important than a duel that doesn't even have a decent wager on it... You better get over to %name4%, then.>
		Msg6 <That's better than dueling? \PC , I'm not certain that you're cut out to be a cavalier.>
		Msg6_1 <Whatever makes you happy, \PC . I'll find something else to do.>
		Msg7 <You are meeting %name5% at %name4% for some kind of mission.>
		Prompt1 <Alright, let's do it.>
		Prompt1_1 <Sure, why not?>
		Prompt1_2 <That sounds like a fun idea.>
		Prompt2 <I've got better things to do.>
		Prompt2_1 <Sorry, I'm really busy right now.>
		Prompt2_2 <Maybe some other time, but not today.>
		Prompt3 <I'll be meeting %name5% about a job.>
		Prompt4 <I'll be doing nothing.>
		Prompt5 <I guess I can have that duel after all.>

		STC MS_CAFE
		SetID 1
	end




