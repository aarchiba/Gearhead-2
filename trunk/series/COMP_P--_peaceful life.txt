%% COMP_P--.txt
%%
%%  Peaceful Life components


Plot
	name <Meet Enemy Fighter>
	desc <PC meets a rival corporate pilot at the convention, and will be challenged to duel.>
	requires <+Tsh +P-- E:-- (F:--|F:CORPO) ~+Fcc>
	Size 1

	% E1 is the scene where this component will take place
	% E2 is the faction to which the enemy belongs
	% E3 is the natural home of the enemy
	% E4 is the enemy character itself
	% E5 is a scene outside
	element1 <Grab 6>
	element2 <Faction Corporate ArchEnemy !Ally -2>
	element3 <Scene Building Public !Okay 2>
	element4 <Prefab>
	Place4 <3 Pass>
	element5 <Scene Urban !Near -7>

	start <if= PlotStatus 0 ifNPCDead %4% LoseComp 0 .next>
	.next <+T-- +F-->

	% V1 = Initialization Counter
	update <if= V1 0 V= 1 1  MoveNPC %4% %1%  SetNPCFaction %4% %2%  NPCLevel %4% StoryDL>

	% SubPlot1 is the duel
	SubPlot1 <*CS_Duel 4 3 5>

	% Plot Resolvers
	.%id1%_%plotid%_GoWin <&SetEnemyNPC %4% StoryNote 1 WinComp 0 .win>
	.%id1%_%plotid%_GoLose <&SetEnemyNPC %4% StoryNote 2 LoseComp 0 .win>
	.win <+Pme +T-- +F-->

	Msg1 <You bested %name2% in a duel.>
	Msg2 <You were defeated by %name2% in a duel.>


	sub
		Persona 4
		special <UNLISTED>
		rumor <%name4% has been looking for someone to challenge.>
		Greeting <if# plotStatus 0 else GoGreet NewChat SayAnything>
		*GoGreet <*NiceToMeetYou GoChallenge>
		GoChallenge <NewChat say 1 AddChat 1 AddChat 2>
		GoAcceptChallenge <StoryNote 3 SetPlotStatus %id1%>
		result1 <NewChat Say 2 Goto GoAcceptChallenge>
		result2 <NewChat Say 4 AddChat 3 AddChat 4>
		result3 <Goto result1>
		result4 <&SetEnemyNPC %4% StoryNote 7  ifG PCRenown StoryDL else GoR4Fail NewChat Say 6 AddReact d6  WinComp 0 .loss>
		GoR4Fail <NewChat Say 5 AddRenown -1 LoseComp 0 .loss>
		.loss <+T-- +F-- +Pme>
		Msg1 <You work for the competition, don't you? Let's take our mecha to the streets of %name5% and see which one of us is better.>
		Msg2 <Meet me in \ELEMENT 5 then.>
		Msg3 <You were challenged to a duel by %name2% pilot %name4% in %name5%.>
		Msg4 <Are you afraid that you'll lose? Come now, you can't tell me that you're as timid as that.>
		Msg5 <So that's how it is...>
		Msg6 <I see. Well, I was hoping to test my skill against the famous \PC , but it seems I will have to be disappointed.>
		Msg7 <You were challenged to a duel by %name2% pilot %name4%, but refused.>
		Prompt1 <Sounds good. Let's go.>
		Prompt2 <I've got better things to do than fight you.>
		Prompt3 <Alright, if you really want, let's do this.>
		Prompt4 <No, really, I don't want to fight.>

		STC MS_SOCIAL
		SetID 1
	end
	inv
		NPC Mecha Pilot
		job <Corporate Pilot>
	end

Plot
	name <Not Even The Police Are Safe>
	desc <The officer you have to see for the mission? He got robbed.>
	requires <#A +Tgt (+Fmi|+Fin) +P-- T:POLIC (F:--|F:CRIME)>

	% E1 is the building
	% E2 is the police officer
	% E3 is the thief
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	element3 <Prefab>

	sub
		STC MS_OFFICE
		SetID 1

		Persona 2
		special <UNLISTED>
		greeting <NewChat Say 1 AddChat 1>
		result1 <NewChat Say 2 StoryNote 3 FreezeNPC %3% &SetTargetNPC %3% WinComp 0 .next>
		.next <+Pun +Tht +Far>

		Msg1 <It's a sad day in \SCENE &EpisodeScene when not even the police are safe...>
		Msg1_1 <>
		Msg2 <On my way here today I was robbed by %name3%! This could well be the start of a crime wave. I'm sorry but my mission slate was among the items stolen, so I don't even have any work for you.>
		Msg2_1 <>
		Msg3 <You went to see %name2% , but found \SPR %2% had been robbed by %name3%.>
		Prompt1 <Tell me what happened.>
		Prompt1_1 <What happened here?>
	end
	inv
		NPC Thief
		Combatant
	end

Plot
	name <Heist At Corp Office>
	desc <This corporate office has just been raided. Yay!>
	requires <#A (+P--|+Pme) +T-- F:CRIME ~P:CORPO ~C:CORPO>

	% E1 is the building.
	% E2 is the security head who gives the mission.
	% E3 is the urban scene where the encounter will be placed.
	Element1 <Scene Building Corporate Public !Near -7 !Okay -3>
	Element2 <Prefab>
	Place2 <1 SD Ally>
	Element3 <Scene Urban !Near -7>

	% V1 = Initialization Counter
	start <if= PlotStatus 0 ifNPCOK %2% else GoDelete if= V1 1 if= SceneID %1% Alert 1 SMemo 3 V= 1 2>
	GoDelete <LoseComp 0 .loss>
	.loss <+T-- +F-->

	update <if= V1 0 V= 1 1  SetNPCFaction %2% SceneFac %1%   News 2>

	Msg1 <This office looks like it's just been attacked. %name2% is surveying the damage.>
	Msg2 <%name1% in \NARRATIVE 7 has been the target of a raid by \NARRATIVE 2 .>
	Msg3 <%name1% was apparently attacked by \NARRATIVE 2 . You could speak with %name2% there about it.>

	%% SubPlot1 is the combat encounter itself
	SubPlot1 <*CS_MechaEncounter 2 3>

	sub
		Persona 2
		rumor0 <%name2% is the new security head at %name1%.>
		special <UNLISTED>
		Greeting <if= PlotStatus %id1% else GoCheckFirst NewChat Say 10>
		GoCheckFirst <if= PlotStatus 0 else GoChat Goto GoGreet>
		*GoGreet <*DestructionGreeting GoGiveMission &EnemyFac>
		*GoChat <*MISC_CHATTER>
		&TheCompany <SceneFac %1%>
		GoGiveMission <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		result1 <NewChat Say 2 AddChat 4 AddChat 5>
		result2 <NewChat Say 3 AddChat 4 AddChat 5>
		result3 <NewChat Say 4 SMemo 5 AddSociable -d6 LoseComp 0 .next>
		result4 <NewChat ifConversation SkillTar StoryDL else GoR4Fail Say 7 RandomMecha .mek StoryDL Goto GoStartMission>
		.mek <GENERAL>
		GoR4Fail <NewChat Say 8 SMemo 5 LoseComp 0 .next>
		*result5 <*GoodLuckOnMission GoStartMission &TheCompany &EnemyFac>
		GoStartMission <SMemo 9 History 6 SetPlotStatus %id1%>
		.next <+T-- +Pun>
		Msg1 <We've been robbed! It was \FACTION &EnemyFac , I'm sure of it... Their mecha blew through the back wall and carried off our data core!>
		Msg2 <The bandits can't have gotten very far... If you search the nearby area I'm sure you'll be able to find them.>
		Msg3 <If you think you can find these bandits and put them out of business, I think I'll be able to find a reward for you. Is it a deal?>
		Msg4 <Well, be careful out there... Those bandits are probably still in the area.>
		Msg5 <%name1% was attacked by bandits, who are probably still in the area.>
		Msg6 <%name2% hired you to locate the bandits who robbed %name1%.>
		Msg7 <That's not a problem. We manufacture all kinds of mecha here... I'll set you up with a brand new unit and you can see about finding those bandits.>
		Msg8 <I see. Well, I'll just have to hope that the police manage to find the bandits. Be careful out there... They could still be somewhere in the city.>
		Msg9 <%name2% hired you to locate the bandits who robbed %name1%. They can probably be found in %name3%.>
		Msg10 <Come back here when you've found the bandits who attacked the place.>
		Prompt1 <Sounds like you could use some help.>
		Prompt2 <Are you offering a reward to find them?>
		Prompt3 <Not my problem. I'll be leaving now.>
		CPrompt3 <if# PCFac &TheCompany Accept>
		Prompt4 <I'd like to help, but I have no mecha.>
		CPrompt4 <if= PCMeks 0 Accept>
		Prompt5 <I'll be back when I've found them.>
	end
	inv
		NPC Soldier
		job <Security Chief>
		chardesc old
	end

Plot
	name <Surprise Attack>
	size 1
	desc <The meeting place is attacked by mecha!>
	requires <#A +Tgt (+P--|+Pme) T:++ S:meta>

	% P1 = Initialization Counter

	% E1 is the scene where this will take place
	% E2 is the NPC the PC wants to talk to
	% E3 is the scene outside the meeting area
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass ally>
	element3 <Scene Urban !Near -7>

	% SubPlot1 contains the ambush battle
	% SubPlot2 contains the victory, if the PC defeats the enemies
	SubPlot1 <*CS_AmbushEncounter 3>
	SubPlot2 <*CS_ThanksForRescue 2>

	% Needed Scripts
	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoJustEnd StoryNote 2 SetPlotStatus %id2%>
	.%id1%_%plotid%_GoLose <ifNPCOK %2% else GoJustEnd StoryNote 1 DeleteNPC %2% Goto GoJustEnd>
	GoJustEnd <LoseComp .loss>
	.loss <+T-- +P-->
	Msg1 <%name2% died during a mecha attack.>
	Msg2 <You rescued %name2% from a mecha attack.>

	sub
		MetaScene 1
		% L1 = Bombing started
		% L2 = Bomb counter

		ClubMap
		MapWidth 20
		MapHeight 25

		type <CLUB LEGIT PUBLIC>
		special <AddExit>
		Content <Some 5 25 Sub *CLUB_X>

		5Min <if= PlotStatus 0 else GoBomb if= L1 0 V+ 2 1 ifG V2 1 if= d2 1 Goto GoStartBombing>
		GoStartBombing <SetPlotStatus %id1% AirRaidSiren Alert 1 StoryNote 2 Bomb  AlterContext .next  L= 1 1>
		Faint%2% <if= PlotStatus 0 Goto GoStartBombing>
		GoBomb <if= PlotStatus %id% Print 3 Bomb>
		nu1 <if= T1 0 Return>
		.next <+Pun>
		Msg1 <Unexpectantly, the building is attacked by mecha!>
		Msg2 <While meeting %name2%, %name1% was attacked by mysterious mecha.>
		Msg3 <The battle outside rages!>
		sub
			Team 1

			Team 2
			name <Citizens>
			setally 1
			Passive

			room
			name <Dance Floor>
			desig <EntranceGrid>
			Content <Some 8 45 Here *URBAN_X>
		end

		Persona 2
		special <UNLISTED>
		greeting <if= PlotStatus %id1% else GoCheckFirstMeeting EndChat Say 4>
		GoCheckFirstMeeting <if= PlotStatus 0 else GoChat Goto GoFirstMeeting>
		*GoFirstMeeting <*HowAreYou GoMessage>
		GoMessage <NewChat Say 1 AddChat 1 AddChat 2>
		*GoChat <*MISC_CHATTER>
		result1 <AddEasygoing d6 Goto result2>
		result2 <Alert 5  EndChat Say 2  AirRaidSiren StoryNote 3 SetPlotStatus %id1% AlterContext .next  L= 1 1>
		.next <+Pun>
		Msg1 <Say, do you hear anything?>
		Msg2 <We have to get out of here!>
		Msg3 <While meeting %name2%, %name1% was attacked by mysterious mecha.>
		Msg4 <We're under attack by mecha!>
		Msg5 <Unexpectantly, the building is attacked by mecha!>
		Prompt1 <No, I don't.>
		Prompt2 <Look at the window, there are mecha!>
	end


Plot
	name <A Gentle Reminder>
	desc <Your friend reminds you that you were supposed to attend a meeting.>
	requires <#A +P-- +Tgt ~+F-- T:++ T:FRIEND P:++ -P:PDASS>

	%% E1 is the scene.
	%% E2 is the friend.
	%% E3 is the superior to report to.
	element1 <Grab 6>
	element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	element3 <Character !Comrade -3 !Near -7 Renowned>

	start <ifNPCDead E2  LoseComp 0 .loss>
	.loss <+T-->

	sub
		STC MS_CLUB
		SetID 1

		Persona 2
		special <UNLISTED>
		*greeting <*HowAreYou GoChat>
		*GoChat <*Shimli_Chatter GoRemind>
		GoRemind <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3 AddChat 8 SMemo 2 History 10 Goto GoWin>
		GoWin <&SetTargetNPC %3%  WinComp NPCScene %3% .next>
		.next <+Tgt +Fmi>
		result1 <NewChat AddEasygoing -d3   Say 3 AddChat 4>
		result2 <NewChat AddEasygoing  d6   Say 4>
		result3 <NewChat AddCheerful   d4   Say 5  AddChat 5 AddChat 6>
		result4 <NewChat Say 6>
		result5 <NewChat Say 7>
		result6 <NewChat AddCheerful d4     Say 8 AddChat 5 AddChat 7>
		result7 <NewChat AddCheerful d4     Say 9>
		result8 <NewChat Say 11 AlterContext .loss>
		.loss <+T-- +F-->
		Msg1 <It's been nice chatting, but you better get a move on. Weren't you supposed to meet your boss %name3% today?>
		Msg1_1 <Say, weren't you supposed to meet your boss %name3% today? I think you mentioned something about it last night.>
		Msg1_2 <I guess you have to leave soon. You've got that big meeting with %name3% today...>
		Msg2 <You have to meet %name3% at \SCENE NPCScene %3% for important business regarding \NARRATIVE 3 .>
		Msg3 <Well, it's a good thing you came here then. Otherwise you would've forgotten altogether.>
		Msg4 <As a member of \FACTION PCFac you're always busy. I'll talk with you again after you finish at \SCENE NPCScene %3% .>
		Msg5 <Last night, when we were talking, I'm quite sure you mentioned a meeting at \SCENE NPCScene %3% ...>
		Msg6 <I think you said the meeting was at \SCENE NPCScene %3% . I'll talk with you after work.>
		Msg7 <Are you feeling OK, \PC ? I guess you've been stressed lately... Anyhow, I'll talk with you after work.>
		Msg8 <It's something about your job with \FACTION PCFac ... Are you really sure you don't remember?>
		Msg9 <Strange, you didn't seem drunk last night... Maybe you should go to \SCENE NPCScene %3% and talk with %name3% anyways, just to be sure.>
		Msg10 <%name2% reminded you to meet %name3%.>
		Msg11 <Oh. Well, I guess you won't be having that meeting, then...>
		Prompt1 <That's right! I totally forgot!>
		CPrompt1 <ifNPCOK %3% Accept>
		Prompt1_1 <Argh! I'm late!!!>
		Prompt2 <Yeah, I know.>
		CPrompt2 <ifNPCOK %3% Accept>
		Prompt2_1 <I'm going there now.>
		Prompt3 <No, I don't think so...>
		CPrompt3 <ifNPCOK %3% Accept>
		Prompt3_1 <I don't remember that...>
		Prompt4 <Thanks for the reminder...>
		Prompt4_1 <Thanks for reminding me.>
		Prompt5 <Ah, now I remember...>
		Prompt5_1 <Yes, that's right!>
		Prompt5_2 <Oh, you mean THAT meeting...>
		Prompt6 <Nope. Not ringing any bells.>
		Prompt7 <I'm getting nothing.>
		Prompt8 <Actually, %name3% is dead.>
		CPrompt8 <ifNPCDead %3% Accept>

		Persona 3
		greeting <NewChat Say 1>
		Msg1 <You're too early for our meeting. Come back later.>
	end

Plot
	name <Boring day, let's go duel!>
	Size 1
	desc <Your friend is bored, and suggests that the two of you go duel.>
	requires <#A +Tgt +P-- (+F--|+Fch) T:FRIEND (T:ADVEN|T:MILIT|T:PDASS) ~P:PDASS ~+Gfa>

	% E1 is the scene where the meeting will take place.
	% E2 is the friend being met.
	% E3 is a location for the duel.
	% E4 is a scene to use if the PC refuses the duel
	% E5 is a character to use if the PC refuses the duel
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 (Citizens) pass>
	Element3 <Scene Environs !Near -7>
	Element4 <Scene Building Public !Okay -7>
	Element5 <Character "MILIT" !Near -7 !Ally -7 !Okay -3>

	% SubPlot1 is the duel itself
	% SubPlot2 is the path if the PC wins the duel
	% SubPlot3 is the path if the PC loses the duel
	SubPlot1 <*CS_Duel 2 1 3>
	SubPlot2 <*CS_WinDuelVs 2>
	SubPlot3 <*CS_LoseDuelVs 2>

	% V1 = Initialization Counter
	update <if= v1 0 V= 1 1 NPCLevel %2% StoryDL>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+T-- +F-->

	.%id1%_%plotid%_GoWin <ifNPCOK %2% else GoE2Died SMemo 1 SetPlotStatus %id2%>
	.%id1%_%plotid%_GoLose <SMemo 2 SetPlotStatus %id3%>
	GoE2Died <SMemo 0 WinComp 0 .changes>

	Msg1 <You bested %name2% in a duel; \SPR %2% is waiting for you at %name1%.>
	Msg2 <You were defeated by %name2% in a duel; \SPR %2% is waiting for you at %name1%.>

	sub
		Persona 2
		greeting <if= PlotStatus 0 else GoChat Goto GoGreet>
		*GoChat <*MISC_CHATTER>
		*GoGreet <*HowAreYou GoChallenge>
		GoChallenge <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <NewChat Say 2  StoryNote 4 SetPlotStatus %id1%>
		result2 <NewChat Say 3 AddChat 3 AddChat 4 AddChat 5
		result3 <NewChat Say 5 SMemo 7 &SetTargetNPC %5% NegateComp %4% .gojob>
		.gojob <+Tgt +Fmi>
		result4 <NewChat Say 6 AddEasygoing d8  LoseComp 0 .donothing>
		.donothing <+T-- +F-->
		result5 <Goto result1>
		Msg1 <It's been kind of a boring day. How would you like to try out our new mecha?>
		Msg1_1 <Say, I've been meaning to ask you something. I know that you're a bit of a mecha pilot too... How would you like try out against me in a friendly practice match?>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <I'm bored. How about we go try out our new mecha in a friendly match?>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <Absolutely nothing's going on around here. I have an idea, though... What say we go try our new mecha in a friendly match?>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <I've been aching to try out my new mecha. Hey, you're a pilot... How about we go spar?>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <I've got a fun idea to spice up the day. How'd you like to fight a practice match against me?>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <Today has been so dull... I have an idea that might spice things up. How about we have an impromptu mecha fight?>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <Great. Come meet me in %name3% when you're ready.>
		Msg2_1 <I'll give you some time to get ready. Come meet me in %name3% when you are.>
		Msg3 <Is that so? What could be more important than a senseless mecha fight?>
		Msg3_1 <Really? Well, I didn't know that you had plans. What do you have to do, anyways?>
		CMsg3_1 <ifNPCSociable Accept>
		Msg3_2 <Is that so?>
		CMsg3_2 <ifNPCShy Accept>
		Msg3_3 <Cool. So, what's up?>
		CMsg3_3 <ifNPCEasygoing Accept>
		Msg3_4 <What could possibly be more important than defending your warrior honor?>
		CMsg3_4 <ifNPCPassionate Accept>
		Msg3_5 <Don't tell me that you have a date. What's going on?>
		CMsg3_5 <ifNPCCheerful Accept>
		Msg3_6 <Are you sure you're not just wussing out? What do you have to do that's so important?>
		CMsg3_6 <ifNPCMelancholy Accept>
		Msg4 <Your friend %name2% challenged you to a duel in %name3%.>
		Msg5 <Wow, that is a big deal. Good luck with that.>
		Msg5_1 <You don't say. I guess that is more important than a duel that doesn't even have a decent wager on it...>
		Msg6 <That's better than dueling? \PC , I'm not certain that you're cut out to be a cavalier.>
		Msg6_1 <Whatever makes you happy, \PC . I'll find something else to do.>
		Msg7 <You are meeting %name5% at %name4% for some kind of mission.>
		Prompt1 <Alright, let's do it.>
		Prompt1_1 <Sure, why not?>
		Prompt1_2 <That sounds like a fun idea.>
		Prompt2 <I've got better things to do.>
		Prompt2_1 <Sorry, I'm really busy right now.>
		Prompt2_2 <Maybe some other time, but not today.>
		Prompt3 <I'll be meeting %name5% about a job.>
		Prompt4 <I'll be doing nothing.>
		Prompt5 <I guess I can have that duel after all.>

		STC MS_CAFE
		SetID 1
	end




