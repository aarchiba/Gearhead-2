%% COMP_Pla.txt
%%
%%	+Pla	PC learns of the existence of an artifact

Plot
	name <Fight Arch-Enemy>
	size 8
	desc <You fight your arch-enemy, who demands to know about TI.>
	requires <#A +Tin ~+Ffi +Pla I:++ -E:ALLY>
	changes <+T-- E:++ +Pen>

	% V1 = Initialization

	update <if= V1 0 V= 1 1 ifNPCOK N1 else GoSetReplacement DeleteNPC E2 E= 2 N1 Goto GoPlaceEnemy>
	GoSetReplacement <&SetEnemyChar E2 SetNPCFaction E2 N2 Goto GoPlaceEnemy>
	GoPlaceEnemy <MoveNPC E2 E1 SetNPCTeam E2 2 NPCLevel E2 StoryDL>

	element1 <Grab 6>
	element2 <Prefab>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		% L3 = Script Immunity Counter
		% L4 = Identity of enemy NPC
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1   ForceChat E2   L= 4 E2   Compose FAINT E2 .E2Faint>
		.E2Faint <if= L1 0  Goto GoDie
		GoDie <L= 1 2  L= 3 1   FreezeNPC E2   StoryNote 4   WinComp 0 .next>
		nu1 <if= T1 0  Return   if= L1 0 L= 1 1   StoryNote 2   ForceChat L4>
		nu2 <if= T2 0           if= L1 0 L= 1 2   StoryNote 3   AddRenown 1   ifNPCOK L4 else GoDie XPV 100   ForceChat L4>
		End <if# L3 0 Print 1>

		.lose <+T-- +Pen>

		Msg1 <\PERSONA L4 's cockpit is empty. Could \SPR E2 have escaped?>
		Msg2 <\ELEMENT 2 confronted you over the \NARRATIVE 8 ; you lost.>
		Msg3 <\ELEMENT 2 confronted you over the \NARRATIVE 8 ; you won.>
		Msg4 <You appeared to kill \ELEMENT 2 in battle, but \PPR E2 body wasn't found...>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction N2   WMecha 2 Threat StoryDL 75>
			ParaX 25
			ParaY 25
		end

		Persona 2
		special <UNLISTED>
		greeting <PCFEnemy N2   if= L1 2 else GoCheckLoss WinComp  0 .next  Goto GoWinSpeech>
		.next <+T-- +Pen>
		*GoWinSpeech <*GetYouNextTime>
		GoCheckLoss <if= L1 1 else GoThreat LoseComp 0 .next EndChat Say 2>
		GoThreat <EndChat Say 1 AddChat 1 AddChat 2>
		*result1 <*THEME_EXPO&ENEMY na>
		result2 <AddEasygoing -d8  Goto GoFight>
		Msg1 <What do you know about the \NARRATIVE 8 ? Tell me now before you die.>
		Msg2 <So, you really know nothing? How unfortunate that you should suffer so pointlessly.>
		Prompt1 <I don't know anything! Really!>
		Prompt1_1 <\NARRATIVE 8 ? What's that?>
		Prompt2 <I'm not telling you anything.>
		Prompt2_1 <You'll have to kill me then.>

	end
	inv
		NPC Mecha Pilot
	end


Plot
	name <Item Stolen>
	desc <PC goes to place, find it stolen by E.>
	requires <#A +Ttt ~+Fin +Pla I:++ S:++ T:ACADE>

	% E1 is the scene where the NPC is
	% E2 is the NPC to be spoken with
	% E3 is the urban scene in this city
	% E4 is the "replacement enemy", in case the PC doesn't already have a nemesis
	% E5 is the encounter for the next component
	element1 <Grab 6>
	element2 <Grab 5>
	element3 <Scene !Near -7 Urban>
	element4 <Prefab>
	element5 <Prefab>
	place5 <3 !Near 1>

	update <if= V1 0 V= 1 1   MoveNPC E2 E1 ifNPCOK N1 else GoReplaceEnemy DeleteNPC E4 E= 4 N1>
	GoReplaceEnemy <&SetEnemyChar E4 SetNPCFaction E4 N2 FreezeNPC E4>

	start <ifNPCOK E2 else GoDelete>
	GoDelete <LoseComp 0 .next>
	.next <+T-->

	sub
		Persona 2
		special <UNLISTED>
		rumor <!2 has been doing research in !1.>
		greeting <NewChat Say 2 AddChat 1 AddChat 2>

		result1 <NewChat   Say 3   AddSociable  d10   Goto GoWin>
		result2 <NewChat   Say 4   AddSociable -d10   Goto GoWin>

		GoWin <StoryNote 1  GrabEntrance E5 GSetSAtt rumor Msg5   WinComp E5 .next>
		.next <+Tin +Ffi +Pst>

		Msg1 <The \NARRATIVE 8 was stolen from \ELEMENT 2 by \ELEMENT 4 .>
		Msg2 <Yes, what do you want? This is not a very good time to be asking me about anything I'm afraid...>
		Msg3 <It's the \NARRATIVE 8 ! I've been doing research on it, but just before you came here some hooligan named \ELEMENT 4 walked in and stole it! If it can't be recovered, all my hard work will be for nought...>
		Msg4 <You and every other gun-toting hooligan, it seems! You're too late... the \NARRATIVE 8 was just stolen by someone called \ELEMENT 4 . If I can't get it back, all my hard work will be for nought...>
		Msg5 <\ELEMENT 4 has been seen in \ELEMENT 3 .>

		Prompt1 <Sorry, what's the matter?>
		Prompt1_1 <What happened to you?>
		Prompt2 <I'm here looking for the \NARRATIVE 8 .>
		Prompt2_1 <I need to know about the \NARRATIVE 8 .>

		STC MS_SCHOOL
		SetID 1
	end
	inv
		NPC Mecha Pilot

		STC Encounter-WANDER
		name <Enemy Mecha>
	end


Plot
	name <The Expert>
	size 2
	desc <Expert tells you something about TI; sends PC to loc.>
	requires <#A +T-- +Pla I:++>

	element1 <MetaScene !Near -7 *Building NeverFail>
	element2 <Prefab>
	element3 <Scene !Near -7 Building public>
	element4 <Character Professor !Near 3 NeverFail>
	neverfail4 <Professor>
	element5 <Grab 8>

	update <if= V1 0 V= 1 1  FreezeNPC E2>

	inv
		NPC Professor
	end
	sub
		Persona 4
		rumor <!4 knows something about !5.>
		*Greeting <*NiceToMeetYou GoAsk>
		GoAsk <NewChat Say 1 AddChat 1 AddChat 2>

		result1 <NewChat Say 2  SkillXP 19 70  Goto GoWin>
		result2 <NewChat Say 3  SkillXP 37 70  Goto GoWin>

		GoWin <StoryNote 4  &SetTargetChar E2 WinComp E1 .next>
		.next <+Tgt +Fin>

		Msg1 <So, what is it that brings you to see me?>
		Msg2 <That's a very interesting subject. It is known that the \NARRATIVE 8 may date to the Age of Superpowers, or even earlier.  \ELEMENT 2 knows more about it than I do; you can find \OPR E2 at \ELEMENT 1 .>
		Msg3 <I've been researching an object known as the \NARRATIVE 8 . \ELEMENT 2 knows more about it than I do; you can find \OPR E2 at \ELEMENT 1 .>
		Msg4 <Professor \ELEMENT 4 said \ELEMENT 2 in \ELEMENT 1 knows about the \NARRATIVE 8 .>

		Prompt1 <What do you know about the \NARRATIVE 8 ?>
		Prompt1_1 <Can you tell me about the \NARRATIVE 8 ?>
		Prompt2 <What have you been researching lately?>
		Prompt2_1 <So, what is it that you study?>
	end



