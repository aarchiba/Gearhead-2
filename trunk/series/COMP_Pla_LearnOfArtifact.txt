%% COMP_Pla.txt
%%
%%	+Pla	PC learns of the existence of an artifact
%%
%%	This state can end at !Lo or above.
%%

Plot
	name <I know where it is: Spaceship>
	desc <The PC has found the item he's been looking for.>
	requires <#A +Pla +Tgt +Fin S:++ I:++ -!Ne -I:TL_Md -I:TL_Hi -I:TL_Ex>
	Size 4

	% E1 is the scene for the meeting
	% E2 is the NPC being sought
	% E3 is an environs scene where the dungeon can be placed
	% E4 is the dungeon scene itself
	% E5 is the item
	Element1 <Grab 6>
	Element2 <Grab 5>
	Place2 <1 pass>
	Element3 <Scene Environs Space !Near -7>
	Element4 <Prefab>
	Place4 <3>
	Element5 <Grab 8>
	Place5 <4>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus 0 ifNPCDead %2%  LoseComp 0 .changes>
	.changes <+T-- +F-->

	% SubPlot1 is the entrance unlocker
	% SubPlot2 is the consequence of recovering the item
	SubPlot1 <*CS_RevealEncounter&Derelict 5 4>
	SubPlot2 <*CS_Pla_GetItem&Derelict 5>

	sub
		Persona 2
		greeting <if# PlotStatus 0 else GoAsk NewChat SayAnything>
		*GoAsk <*LookingForItem %3% GoExplain>
		GoExplain <NewChat Say %id%01 SMemo %id%02 History %id%03 SetPlotStatus %id1% AddChat %id%01 AddChat %id%02>

		Msg%id%01 <It's believed that the %name5% is near here. It was being transported on a spaceship that was, unfortunately, destroyed by pirates. It can probably be found in the wreckage.>
		Msg%id%02 <%name2% revealed that the %name5% may be on the wreck of a spaceship somewhere nearby.>
		Msg%id%03 <%name2% revealed that the %name5% was nearby.>

		Prompt%id%01 <Thanks for the info.>
		Prompt%id%02 <Do you know where this wreck might be?>


		MetaScene 4
		% L1 = Grab Item counter

		special <NOEXIT>
		type <DUNGEON ruin derelict>
		content1 <Some 2 50 Sub *DUNGEON_THREAT>
		content2 <Some 1 30 Sub *DUNGEON_DECOR>
		content3 <Some 1  5 Sub *DUNGEON_REWARD>
		content4 <Some 1 35 Sub *TREASUREROOM_WRECK>
		mapwidth 49
		mapheight 49
		MonkeyMap
		LockedDoorChance 35
		SecretDoorChance 20
		IndustrialTiles

		NeededCells 5

		start <Print 1>
		msg1 <You enter %name4%.>

		% Upon getting the item, start the consequences going
		get%5% <if= L1 0 L= 1 1 SetPlotStatus %id2%>

		% If the item is destroyed, that's gonna have consequences too.
		end <ifItemDestroyed %5% if= L1 0 L= 1 1 SetPlotStatus %id2%>

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Monster>
			type <Fungus Exorg Ruin PreZero>
			SetEnemy 1
			Deploy <if= V1 0 V= 1 1 MonsterUp 2 StoryDL>

			room
			special <STARTHERE>
			minimap <......###..#1#...........>
			sub
				Elevator
				Destination -1
				MiniMapComponent 1
				use <Print 1 Return>
			end

			room
			minimap <......###..#1#..#=#......>
			desig <HOME 5>
		end
	end
	inv
		STC CORE-ACTIVATABLE
		name <Derelict Cruiser>
	end

Plot
	name <You have to get through me first>
	desc <A friend of the person you're looking for will duel you.>
	requires <#A +Pla +Tgt S:-- I:++ (T:ACADE|T:FAITH)>

	% E1 is a public scene for the NPC to wait at
	% E2 is the NPC being sought
	% E3 is the friend
	% E4 is an outdoor scene for the duel to take place in
	% E5 is a public scene for the eventual meeting
	Element1 <Scene Public !Near -7>
	Element2 <Grab 5>
	Place2 </>
	Element3 <NewNPC 2 -7 Heroic>
	Place3 <1 (Citizens) Pass>
	Element4 <Scene Environs !Near -7>
	Element5 <MetaScene !Near -7 *Building NeverFail>

	% SubPlot1 is the duel itself
	SubPlot1 <*CS_Duel&NoDerailment 3 1 4>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus 0 ifNPCDead %2% LoseComp 0 .changes>
	.changes <+Tpt +F-->

	% Duel Results
	.%id1%_%plotid%_GoWin <Monologue %3% 1 SMemo 2 WinComp %5% .win>
	.win <>
	.%id1%_%plotid%_GoLose <Monologue %3% 3 StoryNote 4 LoseComp 0 .loss>
	.loss <+Tpt>

	Msg1 <I concede... %name2% can be found at %name5%.>
	Msg2 <%name3% revealed that %name2% is at %name5%.>
	Msg3 <You lost... I can only hope that the bounty hunters looking for %name2% are as weak as you.>
	Msg4 <%name3% revealed that %name2% is in trouble.>

	sub
		Persona 3
		rumor0 <%name3% is a good friend of %name2%.>
		% V1 = Have attempted bluff
		greeting <if= PlotStatus 0 else GoChat Goto GoGreet>
		GoChat <NewChat SayAnything>
		*GoGreet <*LookingForNPC&NoFailure %2% GoExplain GoExplain>
		GoExplain <NewChat Say 1 AddChat 1 AddChat 2>
		GoStartDuel <EndChat Say 12 History 13 SMemo 14 SetPlotStatus %id1%>
		result1 <NewChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		result2 <if= V1 0 else GoR2Fail AddLawful -1 V= 1 1 ifConversation HardSkillTar StoryDL else GoR2Fail Goto GoR2Pass>
		GoR2Pass <NewChat Say 3 History 4 SMemo 5 XPV 100 WinComp %5% .win_visit>
		GoR2Fail <NewChat Say 6 AddChat 3 AddChat 4 AddChat 5>
		.win_visit <+Tgt>
		result3 <NewChat Say 7 AddChat 4 AddChat 5>
		result4 <NewChat Say 8 AddChat 6 AddChat 7 AddChat 8>
		result5 <NewChat Say 9 History 10 SMemo 11 WinComp %5% .win_protect>
		.win_protect <+Tpt>
		result6 <AddLawful 1 Goto GoStartDuel>
		result7 <AddCheerful d10 Goto GoStartDuel>
		result8 <AddEassygoing -d10 Goto GoStartDuel>
		Msg1 <This wouldn't have anything to do with the \ITEM &TargetItem by any chance, would it?>
		Msg2 <I thought so. Look, I don't want to see %name2% get in any more trouble. There have been enough people bothering \OPR %2% about that... just leave \OPR %2% alone, okay?>
		Msg3 <Oh, alright then. As far as I know %name2% is currently at %name5% doing some kind of research... avoiding actual work seems just as likely.>
		Msg4 <You fooled %name3% into revealing %name2%'s location.>
		Msg5 <You learned from %name3% that %name2% is at %name5%.>
		Msg6 <I don't think so. Look, I don't want to see %name2% get in any more trouble. There have been enough people bothering \OPR %2% about \PPR %2% research... just leave \OPR %2% alone.>
		Msg7 <...and it's very important that %name2% not get killed by all the crazies out looking for the \ITEM &TargetItem .>
		Msg8 <Wait, duel? Usually it's at this point that I've had to defend myself against the people looking for %name2%, but I've never been challenged to duel before.>
		Msg9 <I'm inclined to believe you. Right now %name2%'s at %name5%. I don't know if \SPR %2% needs any help or not, but whatever you offer won't be refused.>
		Msg10 <You promised %name3% that you would protect %name2%.>
		Msg11 <You promised %name3% that you would protect %name2%, who can currently be found at %name5%.>
		Msg12 <Meet me in %name4%. If you win, I'll tell you where to find %name2%.>
		Msg13 <You dueled %name3% to learn the location of %name2%.>
		Msg14 <You will duel %name3% in %name4% to learn where %name2% is.>
		Prompt1 <Actually, it would.>
		Prompt2 <Oh, no, %name2% is an old friend of mine.>
		Prompt3 <It's very important that I find the \ITEM &TargetItem .>
		Prompt4 <How about we duel for the information?>
		Prompt5 <Maybe I can help you protect %name2%.>
		CPrompt5 <ifG PCHeroism StoryDL Accept>
		Prompt6 <Dueling is the honorable way to settle disputes.>
		Prompt7 <If you'd prefer, we can wrestle over it instead.>
		Prompt8 <Enough talking. Let's get this started.>
	end

Plot
	name <Research Press Conference>
	desc <A professor will hold a press conference about the artifact>
	requires <#A +Pla +T-- I:++>

	% E1 is the scene where this component will take place
	% E2 is a public scene in the current city
	% E3 is the NPC that is holding the conference
	element1 <MetaScene !Near -7>
	element2 <Scene Building Public !Near -7>
	element3 <Character ACADE !Near 2 NeverFail>
	NeverFail3 <Professor>
	Place3 <1 (Citizens) pass>

	% All this plot does is move the NPC and activate the mission subplot
	update <if= PlotStatus 0 News 1 SetPlotStatus %id1%>

	%% SubPlot1 is the press conference
	SubPlot1 <*CS_PressConference 1 3>

	Msg1 <%name3% will be holding a lecture about the \ITEM &TargetItem at %name1% in \SCENE &EpisodeScene today.>

	sub
		STC MS_PRESENTATION
		SetID 1
	end



