%%
%% *:GiveMission Content
%%
%% An NPC will give the PC a mission to complete. When the mission is
%% completed, the QuestStatus of this subquest will be set to -1. If
%% the mission is rendered uncompletable, the QuestStatus will be -2.
%%
%% Param1 is the NPC providing the mission
%%   note: Persona 1 must have a .%id%_GoInit script
%%	The parent quest must handle E1's reaction to
%%	successfully completing/losing this subquest,
%%	though this SQ may have a reminder message
%%	in Persona 1.
%%

Content
	name <Go Beat Bandits>
	desc <Some bandits have been attacking truckers. See what you can do.>
	requires <*:GiveMission (1:CORPO|1:TRADE|1:LABOR)>
	special <REUSABLE>

	% E1 is the NPC offering the mission
	% E2 is the encounter that is the mission
	Element2 <Prefab>
	Scene2 <Environs>

	sub
		Persona 1
		Greeting <if= QuestStatus %qid% %id% else %pop% NewChat Say %id%01>
		.%id%_GoInit <SetQuestStatus %qid% %id% NewChat Say %id%02 QMemo %qid% %id%03>
		Msg%id%01 <The bandits who have been attacking our truckers are still on the loose.>
		Msg%id%02 <Several of our truckers have been attacked by bandits in \SCENE %s2% . If you could locate these bandits and eliminate them, I'd have a reward for you.>
		Msg%id%03 <%name1% asked you to locate some bandits in \SCENE %s2% that have been attacking truckers.>

		MetaScene 1
		end <if= QuestStatus %qid% %id% else %pop% ifNPCDead %1% else %pop% SetQuestStatus %qid% -2 Goto %pop%>
	end
	inv
		Encounter
		Special <NoMSID>
		% V1 = Visibility Clock.
		%      If ComTime > V1 and QuestStatus %qid% = %id%, encounter is active
		update <if= QuestStatus %qid% %id% else GoHide ifG ComTime V1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		use <ifG StatVal STAT_MetaVisibility -1  Print -1  Goto GoStartCombat>
		ATTACK <ifG StatVal STAT_MetaVisibility -1    ifUStealth 15 else GoAutoAttack ifYesNo -3 -4 -5 else GoAvoidAttack Goto GoAutoAttack>
		GoAutoAttack <Alert -2 Goto GoStartCombat>
		GoAvoidAttack <Print -6 AddSociable -1>
		GoStartCombat <V= 1 ComTime V+ 1 86400  SavePos Dynamic 2 %threat% 100 .nu1 .nu2 ComposeD Msg1 .Msg1 DynaFaction 0>
		.nu1 <if= T1 0  Return   if= L2 0  L= 2 1  AddRenown -5  ItemVar= %2% 1 ComTime ItemVar+ %2% 1 d86400 ItemVar+ %2% 1 43200>
		.nu2 <if= T2 0           if= L2 0  L= 2 1  XPV 100  AddRenown 1   Alert 1  SetQuestStatus %qid% -1 DeleteItem %2%>
		.msg1 <You have defeated the bandits.>
		EncounterMove 50

	end






