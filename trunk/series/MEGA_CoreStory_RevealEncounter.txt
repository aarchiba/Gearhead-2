%%
%% *:CS_LocateDuel Content
%%
%% Two NPCs are about to fight. The PC needs to find them; this could be to
%% aid one NPC against the other, to stop the fight, or just to watch. Who knows?
%%
%% PARAM1: The outdoors scene where the encounter is
%% PARAM2: The MetaScene being sought
%% PARAM3: The presumed ally in the fight
%% PARAM4: The presumed antagonist in the fight
%%

Content
	name <E4 and the Henchmen>
	desc <E4 can't find E3 either, but you can trail both.>
	requires <*:CS_LocateDuel>
	Size 5

	% E1 is the outdoors scene where the duel is to take place
	% E2 is the metascene being sought
	% E3 is the presumed ally
	% E4 is the presumed antagonist
	% E5 is an encounter for E4's lance
	Element5 <Prefab>
	Place5 <1>

	sub
		MetaScene 5 2
		rumor%id% <%name4% has been searching for %name3% in %name1%.>
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter; resets each time
		% L3 = Storynote 
		% L4 = Time Limit
		% L5 = Number of tries
		MapWidth 30
		MapHeight 30

		% The time limit is five minutes, plus an additional minute for each
		% previous attempt.
		Start <if= L2 0 L= 2 1   L= 1 0   Monologue %4% 1  Alert 2 L= 4 ComTime L+ 4 300 L+ 4 * 60 L5  L+ 5 1  if= L3 0 L= 3 1 SMemo 3>
		nu1 <if= T1 0   Return>
		nu2 <if= T2 0   Return  if= V1 0   V= 1 1   ifG L4 ComTime else GoTooLate Alert 4 StoryNote 5   SetEncounterActive %2% SeekGate %2% WinSubPlot %plotid%>
		GoTooLate <Alert 6  AddRenown -1>
		end <L= 2 0>

		Msg1_1 <So, you're looking for %name3% too? Well, you're not going to find \OPR %3% ... my lancemates will see to that.>
		Msg1_2 <>
		Msg1_3 <>
		Msg1_4 <>
		Msg1_5 <>
		Msg2 <If you can defeat %name4%'s lancemates quickly, you should be able to follow \OPR %4% to %name3%.>
		Msg3 <You encountered %name4% and \PPR %4% lancemates in %name1%.>
		Msg4 <You catch up with %name4%, who has apparently located %name3%.>
		Msg5 <You trailed %name4% to \PPR %4% duel with %name3%.>
		Msg6 <After defeating the henchmen, you attempt to follow %name4% but are too late to find \OPR %4% .>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <if= PlotStatus %plotid% %id% SetSelfFaction &EnemyFac   WMecha 2 StoryDL 80>
			ParaX 25
			ParaY 25
		end
	end
	inv
		Encounter
		name <%name4%'s Lance>
		% V1 = Recharge Counter
		% This encounter will appear once every 5 hours, as long as this is the active subplot.
		update <if= PlotStatus %plotid% %id% else GoHide ifG ComTime V1 else GoHide SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		use <ifG StatVal STAT_MetaVisibility -1  V= 1 ComTime V+ 1 18000  Print -1 Exit Destination>
		GoAutoAttack <V= 1 ComTime V+ 1 18000  Alert -2 Exit Destination>
		GoAvoidAttack <V= 1 ComTime V+ 1 7200  Print -6 AddSociable -1 UpdateProps>
	end

%%
%% *:CS_DiscoverNPCMission Content
%%	&EnemyNPC		The NPC in question is the core story enemy NPC
%%	&DestroyFactory		The mission is to destroy a factory
%%
%% An NPC is going on some kind of mission which the PC has to stop. In order
%% to do this he's going to have to find out what the mission is, then where
%% it is.
%%
%% When the PC has located the encounter, this subplot will make it visible
%% with SetEncounterActive and conclude with WinSubPlot. Revealing the entrance
%% in this subplot will typically give the PC 100XP.
%%
%% PARAM1: The outdoors scene where the encounter is
%% PARAM2: The MetaScene being sought
%% PARAM3: The NPC doing the mission
%%

Content
	name <Henchmen know things about stuff>
	desc <Fight henchmen to gain a clue.>
	requires <*:CS_DiscoverNPCMission &EnemyNPC>
	Size 5

	% E1 is the outdoors scene where the encounter is
	% E2 is the metascene being sought
	% E3 is the NPC doing the mission
	% E4 is an environs scene
	% E5 is a combat encounter against the henchmen
	% E6 is a clue that can be obtained from the wreckage
	Element4 <Scene Environs !Near -7>
	Element5 <Prefab>
	Place5 <4>
	Element6 <Prefab>
	Place6 </>

	% SubPlot1 is the clue identification utility, in case the PC can't do it himself
	SubPlot1 <*CS_AnalyzeClue_LeadToNPC 1 2 3 6>

	sub
		MetaScene 5 2
		rumor%id% <%name3%'s henchmen have been spotted in %name4%.>
		MapWidth 30
		MapHeight 30

		% L1 = Encounter Over Counter
		% L2 = Initialization Counter

		Start <if= L2 0 L= 2 1   L= 1 0   Alert 1>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   AddRenown -1>
		nu2 <if= T2 0   if= V1 0 V= 1 1   Alert 2  History 3   GiveItem %6%   SetPlotStatus %plotid1% %id1%>
		end <L= 2 0>

		Msg1 <You face some of %name3%'s lancemates. Maybe you'll be able to find out about \PPR %3% plans from them.>
		Msg2 <After the battle, you find a diary in the cockpit of one of the defeated mecha.>
		Msg3 <You obtained a diary from your enemy's henchman.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <if= PlotStatus %plotid% %id% SetSelfFaction &EnemyFac   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25
		end

	end
	inv
		Encounter
		name <%name3%'s Henchmen>
		% V1 = Recharge Counter
		% This encounter will appear once every 3 hours, as long as this is the active subplot.
		update <if= PlotStatus %plotid% %id% else GoHide ifG ComTime V1 else GoHide SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		use <ifG StatVal STAT_MetaVisibility -1  V= 1 ComTime V+ 1 10800  Print -1 Exit Destination>
		GoAutoAttack <V= 1 ComTime V+ 1 10800  Alert -2 Exit Destination>
		GoAvoidAttack <V= 1 ComTime V+ 1 3600  Print -6 AddSociable -1 UpdateProps>

		Treasure 1
		name <Henchman's Diary>
		desc <An old fashioned paper diary obtained from the mecha of your enemy's henchman.>
		use <Alert 1>
		GoNoUse <Print 2>
		clue14 <Alert 7>
		clue29 <Alert 7>
		clue32 <if= PlotStatus %plotid% %id1% else GoNoUse ifCodeBreaking HardSkillTar &StoryDL else GoFail  Alert 3 Goto GoReveal>
		clue34 <Alert 7>
		clue37 <if= PlotStatus %plotid% %id1% else GoNoUse ifInvestigation SkillTar &StoryDL else GoFail     Alert 4 Goto GoReveal>
		GoReveal <StoryNote 5 SetEncounterActive %2% WinSubPlot %plotid% XPV 100>
		GoFail <Alert 6>
		Msg1 <This diary tells all the mundane details of being a henchman. Still, you're certain that there must be a clue in it somewhere.>
		Msg2 <This diary isn't much use to you anymore.>
		Msg3 <You discover a secret code used by the henchman to record his missions. Apparently, his next mission is going to take place in %name1%.>
		Msg4 <You piece together clues from the diary, and conclude that the henchman's next mission is going to take place in %name1%.>
		Msg5 <You learned from the diary that %name3% is headed for %name1%.>
		Msg6 <You don't learn anything from the diary.>
		Msg7 <You don't think you can learn anything from the diary with this skill.>
	end

%%
%% *CS_AnalyzeClue_LeadToNPC Content
%%
%% The PC has a clue. Maybe he can't figure it out himself. So, he can bring
%% it to someone who can. Unfortunately, doing so is going to cost both money
%% and time.
%%
%% In this case, analyzing the clue will lead to the whereabouts of a NPC.
%% When the PC has located the encounter, this subplot will make it visible
%% with SetEncounterActive and conclude with WinSubPlot.
%%
%% PARAM1: The outdoors scene where the encounter is
%% PARAM2: The MetaScene being sought
%% PARAM3: The NPC being sought
%% PARAM4: The clue-bearing item
%%

Content
	name <CSI Wannabe>
	desc <You can give the item to a forensic investigator to identify it.>
	requires <*CS_AnalyzeClue_LeadToNPC>
	size 1

	% E1 is the outdoor scene where the encounter is
	% E2 is the metascene being sought
	% E3 is the NPC being sought
	% E4 is the item to be identified
	% E5 is a public scene for the meeting
	% E6 is the investigator
	element5 <Scene Public !Near -7 -Sleazy>
	element6 <Character Forensic Investigator !Near 5 NeverFail>
	NeverFail6 <Forensic Investigator>
	Place6 <5 pass ally>

	%% FAIL CONDITIONS:
	%% - If E4 is destroyed

	end <ifItemDestroyed %4% LoseSubPlot %plotid%>

	sub
		Persona 6
		rumor%id% <%name6% does a bit of freelance forensic work.>
		% v%id%01 = Clue processing time
		% v%id%02 = First time counter/Identification Cost
		greeting <if= PlotStatus %plotid% %id% else GoChat ifChatNPCInPlay else GoNotHere ifItemOK %4% else GoChat ifG v%id%01 0 else GoGreet ifG ComTime v%id%01 else GoNotYet NewChat Say %id%01 AddCHat %id%01>
		GoNotHere <NewChat Say %id%11>
		GoNotYet <NewChat Say %id%02>
		GoChat <NewChat SayAnything>
		GoGreet <if= v%id%02 0 else GoMakeOffer V= %id%02 Threat StoryDL 3 NewChat Say %id%03 AddChat %id%02>
		GoMakeOffer <NewChat Say %id%05 AddCHat %id%03 AddCHat %id%04>
		result%id%01 <NewChat Say %id%06 StoryNote %id%07 SetEncounterActive %2% WinSubPlot %plotid% DeleteItem %4%>
		result%id%02 <NewChat Say %id%04 AddCHat %id%03 AddCHat %id%04>
		result%id%03 <NewChat Say %id%08 FreezeItem %4% Cash+ -v%id%02 StoryNote %id%09 V= %id%01 ComTime V+ %id%01 3600 V+ %id%01 d7200 V+ %id%01 d7200>
		result%id%04 <NewChat Say %id%10>
		Msg%id%01 <Aha, you're back. I've got the results from your %name4% right here.>
		Msg%id%02 <I'm still working on that %name4%. Come back later.>
		Msg%id%03 <Yes, is there something I could help you with?>
		Msg%id%04 <Hmmm. I'd have to examine it thoroughly... Forensic work isn't cheap, you realize. If you want me to investigate this item it's going to cost $ \VAL V%id%02 .>
		Msg%id%05 <Hello again. If you want me to investigate your %name4% it's going to cost $ \VAL V%id%02 .>
		Msg%id%06 <I've learned that someone named %name3% can be found at %name1%. I certainly hope that this information is of use to you.>
		Msg%id%07 <You learned that %name3% can be found in %name1%.>
		Msg%id%08 <Great. Come back here in a few hours and I'll tell you what I've learned.>
		Msg%id%09 <You gave the %name4% to %name6% for investigation.>
		Msg%id%10 <I'll be waiting.>
		Msg%id%11 <Sorry, I can't talk right now. If you've got an item for me to examine why don't you bring it to %name5%?>
		Prompt%id%01 <Well, what can you tell me about it?>
		Prompt%id%02 <What can you tell me about this %name4%?>
		Prompt%id%03 <Here you go. [Give item and money]>
		CPrompt%id%03 <ifG PC$ v%id%02 Accept>
		Prompt%id%04 <I'll be back later.>
	end

%%
%% *:CS_RevealEncounter_Raiders Content
%%
%% There are some hostile mecha out there which are aligned with the
%% enemy faction/enemy NPC.
%%
%% When the PC has located the encounter, this subplot will make it visible
%% with SetEncounterActive and conclude with WinSubPlot. Revealing the entrance
%% in this subplot will typically give the PC 100XP.
%%
%% PARAM1: The outdoors scene where the encounter is
%% PARAM2: The MetaScene being sought
%%

Content
	name <Rowdy Raider>
	desc <The PC will meet one of the raiders, and may gain some information.>
	requires <*:CS_RevealEncounter_Raiders>
	Size 2

	% E1 is the outdoor scene where the encounter is
	% E2 is the MetaScene being sought
	% E3 is a local scene for the rowdy raider
	% E4 is the rowdy raider
	% E5 is the dataslate that may provide a clue
	Element3 <Scene Public !Near -7>
	Element4 <Prefab>
	Place4 </>
	Element5 <Prefab>
	Place5 <4>

	%% FAIL CONDITIONS:
	%% - If Item E5 is destroyed and E4 is dead, this subplot will fail.

	% p%id%01 = Initialization Counter
	% p%id%02 = Message Counter
	start <ifNPCDead %4% else .%id%_GoCheckEntrance ifItemDestroyed %5% LoseSubPlot %plotid%>
	update <if= p%id%01 0   p= %id%01 1   NPCLevel %4% StoryDL   MoveAndPacifyNPC %4% %3%>
	.%id%_GoCheckEntrance <if= P%id%02 0 if= SceneID %3%   ifUAwareness 4   Alert %id%01   P= %id%02 1>

	Surrender%4% <if= PlotStatus %plotid% %id% Monologue %4% %id%02 StoryNote %id%03 Print %id%04 FreezeNPC %4% PCEnemy %4% XPV 100  &%id%_RevealEncounter>

	&%id%_RevealEncounter <SetEncounterActive %2% WinSubPlot %plotid%>

	Msg%id%01 <As you enter %name3%, you notice %name4% looking at an electronic device which \SPR %4% quickly puts away.>
	Msg%id%02 <Alright, I give up... You'll find the mecha you're looking for in %name1%.>
	Msg%id%03 <You defeated %name4%, who revealed that the raiders are in %name1%.>
	Msg%id%04 <%name4% leaves the area.>

	% SubPlot1 is what happens if E4 leaves the area.
	SubPlot1 <*TrailRaiderToBase 1 2 4>

	sub
		Persona 4
		rumor%id% <there's a rowdy mecha pilot hanging out at %name3%.>
		special <UNLISTED>
		greeting <if= PlotStatus %plotid% %id% else GoChat NewChat Say %id%01 AddChat %id%01 AddChat %id%02 AddChat %id%03>
		GoChat <NewChat SayAnything>
		GoThreat <NewChat Say %id%02 AddChat %id%04 AddChat %id%05>
		GoJobOffer <NewChat Say %id%04 AddCHat %id%09 AddChat %id%10>
		GoRaiderFlees <Print %id%06 History %id%07 SMemo %id%16 SetPlotStatus %plotid1% %id1%>
		GoStartFight <EndChat Say %id%09 SoloAttack %4%>
		result%id%01 <ifConversation hardSkillTar StoryDL else GoThreat  NewChat Say %id%03 AddChat %id%06 AddChat %id%07 AddChat %id%08>
		result%id%02 <ifConversation SkillTar StoryDL else GoThreat      Goto GoJobOffer>
		result%id%03 <EndCHat Say %id%05 Goto GoRaiderFlees>
		result%id%04 <EndCHat Say %id%08 Goto GoRaiderFlees>
		result%id%05 <AddEasygoing -d10  ifIntimidation HardSkillTar StoryDL else GoStartFight NewChat Say %id%10 StoryNote %id%11 XPV 100 &%id%_RevealEncounter>
		result%id%06 <Goto GoThreat>
		result%id%07 <Goto GoJobOffer>
		result%id%08 <EndCHat Say %id%12 Goto GoRaiderFlees>
		result%id%09 <ifG React d8 else GoThreat      NewChat Say %id%13 StoryNote %id%14 XPV 100 &%id%_RevealEncounter>
		result%id%10 <EndCHat Say %id%15 Goto GoRaiderFlees>
		Msg%id%01 <Yeah, what do you want?>
		Msg%id%02 <Bad things happen to people who ask too many questions around here. I'd suggest you get out of my face before bad things happen to you.>
		Msg%id%03 <Could be. Why do you care?>
		Msg%id%04 <I don't know... I'm not the one who makes that kind of decision. You'd have to talk to the boss.>
		Msg%id%05 <If you don't have anything to say then keep away from me.>
		Msg%id%06 <%name4% leaves the area.>
		Msg%id%07 <%name4% left before you could learn where the raiders are.>
		Msg%id%08 <Not if I keep an eye on you first... or something. I'm outta here.>
		Msg%id%09 <You're gonna die!>
		Msg%id%10 <Hey, no need for that... if you're seriously looking for them, the team I'm signed on with is assembling at %name1%. As for me, I'm gonna go somewhere very far away...>
		Msg%id%11 <You intimidated %name4% into revealing that the raiders are at %name1%.>
		Msg%id%12 <You've got a lot of nerve... some day that's gonna end up getting you killed.>
		Msg%id%13 <They're gathering in %name1% right now. I should probably get going myself but I'm still a little drunk... Yeah, just tell them %name4% sent you.>
		Msg%id%14 <You fooled %name4% into revealing that the raiders are at %name1%.>
		Msg%id%15 <Yeah, well, I gotta go. We've got a big mission coming up... There's gonna be a real big boom.>
		Msg%id%16 <%name4% left before you could learn where the raiders are, but you may still be able to find \OPR %4% nearby.>
		Prompt%id%01 <I know that you're with the raiders.>
		Prompt%id%02 <I'm looking for work; you know of anything?>
		CPrompt%id%02 <ifG PCKnowledge 14 Accept>
		Prompt%id%03 <Nothing...>
		Prompt%id%04 <I'll be keeping an eye on you, %name4%.>
		Prompt%id%05 <You think I'm scared? Come on, let's settle this now.>
		Prompt%id%06 <Tell me where I can find them.>
		Prompt%id%07 <I was hoping to get a job with them.>
		Prompt%id%08 <I intend to destroy them.>
		Prompt%id%09 <Alright. Where can I find this boss?>
		Prompt%id%10 <Never mind, then.>
	end
	inv
		NPC Mecha Pilot
		Knowledge 7
		Body 15

		Computer 1
		name <Raider's Dataslate>
		desc <A dataslate obtained from a rowdy raider.>
		Category <ELECTRONICS COMPUTER COMMUNICATIONS>
		SDL_PORTRAIT <item_databook.png>
		Memo
		Email
		Mass -1
		use <if= PlotStatus %plotid% %id% else GoNoUse Alert 1 StoryNote 2 &%id%_RevealEncounter  ifNPCOK %4% XPV 100>
		clue32 <goto use>
		clue37 <goto use>
		GoNoUse <Print 3>
		Msg1 <According to a map saved on this dataslate, the raiders are hiding in %name1%.>
		Msg2 <You learned from a dataslate that the raiders are in %name1%.>
		Msg3 <You no longer have any use for this item.>
	end

%%
%% *TrailRaiderToBase Content
%%
%%	The PC must follow an enemy pilot to their base.
%%
%% When the PC has located the encounter, this subplot will make it visible
%% with SetEncounterActive and conclude with WinSubPlot. Revealing the entrance
%% in this subplot will typically not give the PC any XP. Bummer.
%%
%% PARAM1: The outdoors scene where the encounter is
%% PARAM2: The MetaScene being sought
%% PARAM3: The raider being trailed
%%

Content
	name <Speed Match>
	desc <The PC will find the raider's buddies, who he must defeat quickly to follow the raider.>
	requires <*TrailRaiderToBase>
	Size 2

	% E1 is the outdoor scene where the encounter is
	% E2 is the MetaScene being sought
	% E3 is the raider being trailed.
	% E4 is a new encounter for the speed match
	Element4 <Prefab>
	Place4 <1>

	% p%id%01 = Initialization Counter
	update <if= p%id%01 0 p= %id%01 1   FreezeNPC %3%>

	sub
		MetaScene 4 2
		rumor%id% <%name3% has been spotted in %name1%.>
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter; resets each time
		% L3 = Storynote 
		% L4 = Time Limit
		% L5 = Number of tries
		MapWidth 30
		MapHeight 30

		% The time limit is five minutes, plus an additional minute for each
		% previous attempt.
		Start <if= L2 0 L= 2 1   L= 1 0   Monologue %3% 1  Alert 2 L= 4 ComTime L+ 4 300 L+ 4 * 60 L5  L+ 5 1  if= L3 0 L= 3 1 SMemo 3>
		nu1 <if= T1 0   Return>
		nu2 <if= T2 0   Return  if= V1 0   V= 1 1   ifG L4 ComTime else GoTooLate Alert 4 StoryNote 5   SetEncounterActive %2% SeekGate %2% WinSubPlot %plotid%>
		GoTooLate <Alert 6  AddRenown -1>
		end <L= 2 0>

		Msg1_1 <Look- it's that cavalier who's been following me. You guys deal with it; I've got to get back to base.>
		Msg1_2 <>
		Msg1_3 <>
		Msg1_4 <>
		Msg1_5 <>
		Msg2 <If you can defeat %name3%'s lancemates quickly, you should be able to trail \OPR %3% to the base.>
		Msg3 <You encountered %name3% and \PPR %3% lancemates in %name1%.>
		Msg4 <You catch up with %name3%, and follow \OPR %3% to the raider's base.>
		Msg5 <You trailed %name3% to the raider's base in %name1%.>
		Msg6 <After defeating the henchmen, you attempt to follow %name3% but are too late to find \OPR %3% .>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <if= PlotStatus %plotid% %id% SetSelfFaction &EnemyFac   WMecha 2 StoryDL 80>
			ParaX 25
			ParaY 25
		end
	end
	inv
		Encounter
		name <%name3%'s Lance>
		% V1 = Recharge Counter
		% This encounter will appear once every 5 hours, as long as this is the active subplot.
		update <if= PlotStatus %plotid% %id% else GoHide ifG ComTime V1 else GoHide SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
		GoSetOrders <GrabSource GSetNAtt NAG_EpisodeData NAS_Orders 3>
		use <ifG StatVal STAT_MetaVisibility -1  V= 1 ComTime V+ 1 18000  Print -1 Exit Destination>
		GoAutoAttack <V= 1 ComTime V+ 1 18000  Alert -2 Exit Destination>
		GoAvoidAttack <V= 1 ComTime V+ 1 7200  Print -6 AddSociable -1 UpdateProps>
	end

%%
%% *CS_RevealEncounter Content
%%	&Derelict	The encounter in question is a wrecked ship
%%	&EnemyBase	It's an enemy base
%%	&Research	It's a laboratory or research station of some kind
%%
%% There's something out there. The PC knows about it, but doesn't know where
%% it is.
%%
%% PARAM1: The MetaScene being sought
%% PARAM2: The outdoors scene where the encounter is
%%

%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************

%%  *****************************************
%%  ***   +Pme	PC meets an enemy pilot   ***
%%  *****************************************


%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************


%%  ***********************************************************
%%  ***   +Psh	PC is shanghaied into working for faction   ***
%%  ***********************************************************


%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************


%%  ****************************************
%%  ***   +Pew  Enemy  Weapon  Program   ***
%%  ****************************************

Content
	name <Attacked by WHAT!?>
	desc <A scene will be attacked by an experimental weapon.>
	requires <*CS_RevealEncounter (&EnemyBase|&Research) (F:MILIT|F:CRIME) +Pew>
	Size 1

	% Element1 is the metascene being sought
	% Element2 is the outdoors scene where it is
	% Element3 is a metascene to be attacked
	% Element4 is the person to reveal the location
	Element3 <MetaScene !Near -7 *BUILDING NeverFail>
	Element4 <Character HasMecha !Ally -7 !Enemy -2 !Near -7>
	Place4 <3 Pass>

	% P%id%01 = Initialization Counter

	Update <if= PlotStatus %plotid% %id% else %pop% if= p%id%01 0 else %pop% News %id%01 P= %id%01 1>

	% As long as in this phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %4% LoseComp 0 .%id%_changes>
	.%id%_changes <+T-- +F-->

	Msg%id%01_1 <%name3% in \SCENE &EpisodeScene has been attacked by unknown forces. Eyewitnesses claim that a giant monster was involved.>
	Msg%id%01_2 <There has been a disaster at %name3% in \SCENE &EpisodeScene . Details are sketchy, but mecha may have been involved.>

	% SubPlot1 is a blank plot state.
	SubPlot1 <*UTIL_BlankState>

	sub
		Persona 4
		special <Unlisted>
		greeting <if# PlotStatus %plotid% %id% else .%id%_GoGreet NewChat SayAnything>
		*.%id%_GoGreet <*DestructionGreeting .%id%_GoTellStory &EnemyFac>
		.%id%_GoTellStory <NewChat Say %id%01  AddCHat %id%01>
		result%id%01 <NewChat Say %id%02 SMemo %id%03 ShowEntrance %1% SetPlotStatus %plotid% %id1%>
		Msg%id%01 <We were attacked by... something. I've never seen anything like it before. It certainly wasn't a standard mecha. Our fighters didn't stand a chance.>
		Msg%id%02 <Our recon team traced it to some place in %name2%, but after that it disappeared. They probably have a hidden base nearby. I only hope they aren't planning any more attacks.>
		Msg%id%03 <You learned from %name4% that the experimental weapon fled to %name2%.>
		Prompt%id%01 <Do you know where it went?>

		STC MS_EmptyBuilding
		SetID 3
	end

%%  *********************************
%%  ***   +C--  Nothing Special   ***
%%  *********************************


%%  *******************
%%  ***   GENERAL   ***
%%  *******************

Plot
	name <Ask and you shall receive>
	requires <*CS_RevealEncounter !Ne>
	desc <Simply ask for the information and you will be given it.>
	Size 1

	% E1 is the metascene being sought
	% E2 is the outdoors scene where it is
	% E3 is a public scene for the NPC
	% E4 is the NPC
	Element3 <Scene Public !Near -7>
	Element4 <Character HasMecha Sociable -ArchAlly !Near -7>
	Place4 <3 sd ally>

	% SubPlot1 is a blank plot state
	SubPlot1 <*UTIL_BlankState>

	% As long as we're still at this phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %4% LoseComp 0 .%id%_changes>
	.%id%_changes <+T-- +F-->

	sub
		Persona 4
		rumor%id% <%name4% knows about all kinds of strange places.>
		greeting <if# PlotStatus %plotid% %id% else GoExplain NewChat SayAnything>
		*GoExplain <*LookingForScene %1% GoGive>
		GoGive <NewChat Say 1 SMemo 2 History 3 ShowEntrance %1% SetPlotStatus %plotid% %id1%>
		Msg1 <It's said that %name1% can be found in %name2%. I've never been there myself, but it should be easy enough to find.>
		Msg1_1 <Oh, that? As far as I know you can find it in %name2%. Here, I'll sketch you a quick map. Good luck.>
		CMsg1_1 <ifG React d20 Accept>
		Msg1_2 <You mean, you don't even know that? Typical. You can find %name1% in %name2%, of course.>
		CMsg1_2 <ifG 1 React Accept>
		Msg2 <%name4% told you that %name1% can be found in %name2%.>
		Msg3 <You learned from %name4% where to find %name1%.>
	end

Plot
	name <Fifth Column>
	requires <*CS_RevealEncounter &EnemyBase F:++>
	desc <Your friend on the other side will help you out.>
	Size 1

	% E1 is the metascene being sought
	% E2 is the outdoors scene where it is
	% E3 is a public scene for the NPC
	% E4 is the NPC
	Element3 <Scene Public !Near -7 !Okay -2>
	Element4 <Character Friend !Global !Ally -2>
	Place4 <3 (Citizens) pass ally>

	% SubPlot1 is a blank plot state.
	SubPlot1 <*UTIL_BlankState>

	% P%id%01 = EMail Counter
	% As long as we're still at this phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %4% LoseComp 0 .%id%_changes>
	.%id%_changes <+T-- +F-->

	5Min <if= PlotStatus %plotid% %id% else %pop% if= P%id%01 0 if= d4 3 P= %id%01 1 Email %id%01>

	Msg%id%01 <%name4%@ \SCENE &EpisodeScene :// Come see me in %name3%; I've got some important information for you.>

	sub
		Persona 4
		rumor%id% <there's a mysterious person hanging out at %name3%.>
		greeting <if# PlotStatus %plotid% %id% else .%id%_GoGreet NewChat SayAnything>
		*.%id%_GoGreet <*IHaveInformation&AboutScene .%id%_GoReveal %1%>
		.%id%_GoReveal <NewChat Say %id%01 SMemo %id%02 History %id%03 ShowEntrance %1% SetPlotStatus %plotid% %id1% AddChat %id%01 AddChat %id%02>
		result%id%01 <NewChat Say %id%04>
		result%id%02 <NewChat Say %id%05 AddCheerful -d4>
		Msg%id%01 <I'm risking a lot telling you this, but... the secret base belonging to \FACTION &EnemyFac ? You'll find it in %name2%. Here, these navpoints will lead you straight to it.>
		Msg%id%01_1 <I shouldn't tell you this, but... the secret base you're looking for? It's here in %name2%. I'll give you the coordinates.>
		Msg%id%01_2 <This could get me in trouble with \FACTION &EnemyFac , but I thought you should know... Their secret base is in %name2%. I can upload the coordinates to your navcomp.>
		Msg%id%02 <Your friend %name4% told you the location of %name1% in %name2%.>
		Msg%id%03 <Your friend %name4% revealed the location of the base.>
		Msg%id%04 <You're welcome. Good luck out there, OK?>
		Msg%id%04_1 <Hey, that's what friends are for.>
		CMsg%id%04_1 <ifNPCSociable Accept>
		Msg%id%04_2 <Don't mention it. No, seriously, don't mention it...>
		CMsg%id%04_2 <ifNPCShy Accept>
		Msg%id%04_3 <Hey, no problem.>
		CMsg%id%04_3 <ifNPCEasygoing Accept>
		Msg%id%04_4 <Just promise me that you'll make it count, okay?>
		CMsg%id%04_4 <ifNPCPassionate Accept>
		Msg%id%04_5 <I'm sure you'll have a chance to repay the favor, someday. Good luck.>
		CMsg%id%04_5 <ifNPCCheerful Accept>
		Msg%id%04_6 <If word gets out that I passed on this information, I'm toast. Just make sure you make this count.>
		CMsg%id%04_6 <ifNPCMelancholy Accept>
		Msg%id%04_7 <I only wish I could be more help.>
		CMsg%id%04_7 <ifNPCHeroic Accept>
		Msg%id%04_8 <Don't worry, nothing is ever freely given. I expect this kindness of mine to be fully repaid at some point in the future.>
		CMsg%id%04_8 <ifNPCVillainous Accept>
		Msg%id%04_9 <Honestly, I just want to see you humiliate \PERSONA &EnemyNPC . Get over there and give \OPR &EnemyNPC hell!>
		CMsg%id%04_9 <ifNPCOK &EnemyNPC Accept>
		Msg%id%05 <I have my reasons. Look, \PC , you should get going...>
		Msg%id%05_1 <I think we both know why. Good luck out there, \PC .>
		CMsg%id%05_1 <ifNPCSociable Accept>
		Msg%id%05_2 <I'd rather not say. Do your best with the information, \PC .>
		CMsg%id%05_2 <ifNPCShy Accept>
		Msg%id%05_3 <Does it really matter? Good luck storming the base, \PC .>
		CMsg%id%05_3 <ifNPCEasygoing Accept>
		Msg%id%05_4 <I can't just sit idly by and do nothing, can I? I've done what I had to do, now you do what you have to...>
		CMsg%id%05_4 <ifNPCPassionate Accept>
		Msg%id%05_5 <Don't sweat it. I know that you'd do the same for me.>
		CMsg%id%05_5 <ifNPCCheerful Accept>
		Msg%id%05_6 <We mustn't talk about that... You should go. If they see us together, it would be very dangerous.>
		CMsg%id%05_6 <ifNPCMelancholy Accept>
		Msg%id%05_7 <We both know that what \FACTION &EnemyFac is doing is wrong... I only hope that together we can steer them back to rightness.>
		CMsg%id%05_7 <ifNPCHeroic Accept>
		Msg%id%05_8 <Let's just say that when you sweep in to destroy everything, I'll be waiting to pick up the pieces. We both have much to gain.>
		CMsg%id%05_8 <ifNPCVillainous Accept>
		Msg%id%05_9 <Mostly, it's just that I want to see \PERSONA &EnemyNPC defeated.>
		CMsg%id%05_9 <ifNPCOK &EnemyNPC Accept>
		Prompt%id%01 <Thanks. This will be a great help.>
		Prompt%id%02 <Why are you doing this?>
	end

Plot
	name <All-Knowing Smuggler>
	requires <*CS_RevealEncounter>
	desc <There's a smuggler who can tell you how to get where you're going.>
	% This component has no size, even though the PC must perform a task to move on;
	% the size will be provided by the duel subplot.

	% E1 is the metascene being sought
	% E2 is the outdoors scene where it is
	% E3 is a public scene for the NPC
	% E4 is the NPC
	% E5 is an outdoors scene for the duel
	Element3 <Scene Public !Near -7>
	Element4 <Prefab>
	Place4 <3 sd ally>
	Element5 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is a blank plot state
	SubPlot1 <*CS_Duel&NoDerailment 4 3 5>
	SubPlot2 <*UTIL_BlankState>

	% As long as we're still at this phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %4% LoseComp 0 .%id%_changes>
	.%id%_changes <+T-- +F-->

	% Duel Results
	.%id1%_%plotid%_GoWin <Monologue %4% %id%01 SMemo %id%02 ShowEntrance %1% SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <Monologue %4% %id%03 SMemo %id%04 MoveNPC %4% %3% SetPlotStatus %plotid% %id%>
	.%id%_loss <+T-- +F-->

	Msg%id%01 <You won... I'll upload the details for %name1% to your navcomp.>
	Msg%id%02 <%name3% revealed %name1%'s location in %name2%.>
	Msg%id%03 <You lost... I'll be waiting for you at %name3%.>
	Msg%id%04 <You lost the duel against %name4%, so now you have no choice but to pay for the information.>

	sub
		Persona 4
		rumor%id% <%name4% knows where to find just about everything in this town.>
		% V1 = Have previously dueled
		% V2 = Price for revealing location
		greeting <if# PlotStatus %plotid% %id% else GoExplain NewChat SayAnything>
		*GoExplain <*LookingForScene %1% GoSetPrice>
		GoSetPrice <if= V2 0 else GoOffer V= 2 Threat StoryDL 5 Goto GoOffer>
		GoOffer <NewChat Say 1 SMemo 2 AddChat 1 AddChat 2 AddChat 3>
		result1 <NewChat Say 3 History 4 Cash+ -V2 ShowEntrance %1% SetPlotStatus %plotid% %id2%>
		result2 <NewChat Say 5 AddChat 4 AddChat 5 AddChat 6>
		result3 <NewChat Say 6>
		result4 <NewChat Say 7 SMemo 8 History 9 V= 1 1 V= 2 * 2 V2 SetPlotStatus %plotid% %id1%>
		result5 <NewChat Say 10 SMemo 8 History 9 V= 1 1 V= 2 * 3 V2 SetPlotStatus %plotid% %id1%>
		result6 <NewChat Say 11 AddChat 1>
		Msg1 <I know where %name1% is. I could tell you, but information doesn't come cheap... It's going to cost you $ \VAL V2 to find out.>
		Msg2 <%name4% offered to tell you where %name1% is, in exchange for $ \VAL V2 .>
		Msg3 <Ah, it's been a pleasure taking your money. This chip holds the coordinates of %name1% in %name2%. You should be able to find it quite easily.>
		Msg4 <%name3% revealed %name1%'s location in %name2%.>
		Msg5 <Duel for it? What is this, a Love Magnet holiday special? I've got no reason to duel- everything to lose and nothing to gain. I've told you my price. If you don't like it then you can do without.>
		Msg6 <Try to make it quick; I've got bills to pay.>
		Msg7 <Chicken? You think I'm afraid of you!? That's it, we're on. Meet me in %name5%. If you win I'll tell you where to find %name1%, but if I win you'll be paying double for the info.>
		Msg8 <You challenged %name4% to a duel in order to learn the location of %name1%; \SPR %4% 's waiting for you in %name5%.>
		Msg9 <You challenged %name4% to learn the location of %name1%.>
		Msg10 <I can't refuse that. Meet me in %name5% as soon as you're ready.>
		Msg11 <Yes, of course I am. Just come on back whenever you've got the cash and the info will be yours.>
		Prompt1 <This is highway robbery. [Give Money]>
		CPrompt1 <ifG PC$ V2 Accept>
		Prompt2 <How about we duel for the info instead?>
		CPrompt2 <if= V1 0 Accept>
		Prompt3 <I'll be back when I have the money.>
		Prompt4 <It's okay, I can see you're too chicken...>
		CPrompt4 <ifTaunt SkillTar StoryDL Accept>
		Prompt5 <If you win, I'll pay three times what you're asking now.>
		CPrompt5 <ifG PCEgo 10 Accept>
		Prompt6 <I guess you're right.>
	end
	inv
		NPC Pirate
		job <Smuggler>
	end

Plot
	name <Pilot Saw It>
	requires <*CS_RevealEncounter -!Ne -&EnemyBase>
	desc <A pilot saw the location the PC is looking for. Duel him for the answer!>

	% E1 is the metascene being sought
	% E2 is the outdoors scene where it is
	% E3 is a public scene for the NPC
	% E4 is the NPC
	% E5 is an outdoors scene for the duel
	Element3 <Scene Public !Near -7>
	Element4 <Character HasMecha Passionate !Near -7>
	Place4 <3 sd ally>
	Element5 <Scene Environs !Near -7>

	% SubPlot1 is the duel itself
	% SubPlot2 is a blank plot state
	SubPlot1 <*CS_Duel&NoDerailment 4 3 5>
	SubPlot2 <*UTIL_BlankState>

	% As long as we're still at the first phase, the NPC's death will end the plot.
	start <if= PlotStatus %plotid% %id% else %pop% ifNPCDead %4% LoseComp 0 .%id%_changes>
	.%id%_changes <+T-- +F-->

	% Duel Results
	.%id1%_%plotid%_GoWin <Monologue %4% %id%01 SMemo %id%02 ShowEntrance %1% SetPlotStatus %plotid% %id2%>
	.%id1%_%plotid%_GoLose <Monologue %4% %id%03 SMemo 0 LoseComp 0 .%id%_loss>
	.%id%_loss <+T-- +F-->

	Msg%id%01 <You won... I'll show you where %name1% is in %name2%.>
	Msg%id%02 <%name3% revealed that %name1% is at %name2%.>
	Msg%id%03 <You lost... %name1% is all mine!>

	sub
		Persona 4
		rumor%id% <%name4% has been to %name1%.>
		greeting <if# PlotStatus %plotid% %id% else GoExplain NewChat SayAnything>
		*GoExplain <*LookingForScene %1% GoChallenge>
		GoChallenge <NewChat Say 1 AddChat 1 AddChat 2>
		result1 <EndChat Say 2 SMemo 3 History 4 SetPlotStatus %plotid% %id1%>
		result2 <NewChat Say 5 SMemo 6 History 7 XPV 100 AddSociable d10 ShowEntrance %1% SetPlotStatus %plotid% %id2% PCFriend %4%>
		Msg1 <I've been there. I never found anything... but if you're interested in it, I must have missed something. How about this? Let's duel for the information. If you win, I tell you where to find %name1%, and if I win you tell me what's there.>
		Msg2 <Great! Meet me in %name5% and we can start.>
		Msg3 <You agreed to duel %name4% to learn the location of %name1%; \SPR %4% 's waiting for you in %name5%.>
		Msg4 <You agreed to duel %name4% to learn the location of %name1%.>
		Msg5 <Woah- that's a deal too good to pass up. You can find %name1% in %name2%; let me upload the info to your navcomp.>
		Prompt1 <It's a deal.>
		Prompt2 <Here's a better idea: tell me, and I'll give you my number.>
		CPrompt2 <ifChatNPCSexy ifFlirtation SkillTar StoryDL Accept>
	end

