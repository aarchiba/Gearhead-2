%%
%% *CS_RaidEnemyBase Content
%%	&Research	It's a research base
%%
%% The PC is storming an enemy installation. Hooray!
%%
%% The plotID should be set once the player sets foot in the metascene.
%%
%% Param1: The metascene of the base
%%

%%  ********************************
%%  ***   +P--  Peaceful  Life   ***
%%  ********************************

Content
	name <Shanghai Surprise>
	desc <You find the enemy base, and get an offer you can't refuse.>
	requires <*CS_RaidEnemyBase (+P--|+Pme) E:++ F:CRIME (P:--|P:CORPO|P:MILIT)>
	size 6

	% E1 is the base itself
	% E2 is your enemy
	% E3 is a member of the enemy faction
	% E4 is a public scene
	Element2 <Grab 1>
	Place2 <1 (Enemies) SD Enemy>
	Element3 <Character !Comrade -2 !Global Neverfail>
	NeverFail3 <Pirate>
	Place3 </>
	Element4 <Scene Building Public !Okay 3 !Near -7>

	% P%id%01 = Initialization Counter
	update <if= p%id%01 0 else %pop% P= %id%01 1 NPCLevel %2% StoryDL Goto %pop%>

	sub
		MetaScene 1
		mapwidth 17
		mapheight 17
		BoxMap

		% L1 = Initialization Counter
		% L2 = Victory Counter

		content <Fill Sub *HIDEOUT_X na>

		start <if= L1 0 else GoBeenBefore L= 1 1 ForceChat %2% StoryNote 6>
		GoBeenBefore <Print 1>

		surrender%2% <L= 2 1 Monologue %2% 2 StoryNote 3 XPV 100 FreezeNPC %2% Retreat 2 WinComp 0 .surrender>
		nu1 <if= T1 0 Return if= L2 0 L= 2 1 StoryNote 4 LoseComp 0 .loss>
		nu2 <if= T2 0        if= L2 0 L= 2 1 StoryNote 5 XPV 100 WinComp 0 .next>
		.surrender <+Pun +T-- +F-->
		.loss <+Pun +T-- +F-->
		.next <+T-- +F-->

		msg1 <You enter the enemy base.>
		msg2 <Alright, you win this round... but I'll tell you now, once our plan goes into effect you'll wish you had joined \FACTION &EnemyFac when you had the chance.>
		msg3 <%name2% warned that you'd regret not joining \FACTION &EnemyFac .>
		msg4 <You refused %name2%'s offer, and was defeated in combat.>
		msg5 <You refused %name2%'s offer and defeated \OPR %2% in combat.>
		msg6 <At the enemy base, %name2% asked you to join \FACTION &EnemyFac .>

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Enemies>
			type <SYNTH ROBOT CRIMINAL>
			Deploy <if= V1 0 V= 1 1 WMonster 2 StoryDL 150>
			SetEnemy 1

			room
			special <STARTHERE>
			minimap <............2............>
			sub
				TrapDoor
				Destination -1
				MiniMapComponent 2
				use <Print 1 Return if= L2 0 L= 2 1 Monologue %2% 101 StoryNote 102 FreezeNPC %2% LoseComp 0 .next>
				.next <+Pun +T-- +F-->
				msg101 <You're a fool, \PC . When the next part of our plan goes into effect you'll regret not accepting my offer.>
				msg102 <%name2% warned that you'd regret not joining \FACTION &EnemyFac .>
			end
		end

		Persona 2
		special <UNLISTED NOESCAPE>
		*greeting <*WelcomeToMyLair GoQuestion1>
		GoQuestion1 <EndChat Say 1 AddChat 1 AddChat 2>
		GoQuestion2 <EndChat Say 2 AddChat 3 AddChat 4 AddChat 5>
		GoQuestion3 <EndChat Say 6 AddChat 6 AddChat 7>
		GoJoinFac <EndChat Say 5 FreezeNPC %2% Retreat 2 L= 2 1 CustomMecha .mek StoryDL SMemo 3  History 4  XPV 100  AddLawful -2   &SetAllyFac &EnemyFac  &SetEnemyFac 0  StoreOldFaction SetPCFac &AllyFac &SetEnemyNPC 0  &SetTargetNPC %3% WinComp %4% .join>
		.join <+Tgt +Fmi +Psh>
		.mek <GENERAL \FACTION_DESIG &EnemyFac>
		result1 <AddCheerful d4   Goto GoQuestion2>
		result2 <AddSociable d4   Goto GoQuestion2>
		result3 <Goto GoJoinFac>
		result4 <FacXP+ PCFac 1   Goto GoQuestion3>
		result5 <AddLawful 1      Goto GoQuestion3>
		result6 <Goto GoJoinFac>
		result7 <EndChat Say 7>
		Msg1 <I'm quite impressed with your skills. Tell me, \PC , are you happy with your current job? Are they paying you the sort of money we both know you're worth?>
		Msg2 <I'm willing to offer you a post with \FACTION &EnemyFac . Once you sign on you're going to be an outlaw, but the hours are flexible, the supervision minimal, and the pay however much you can steal. I think it'd be good for you.>
		Msg3 <You accepted %name2%'s offer to join \FACTION &EnemyFac ; \SPR %2% told you to visit %name3% in %name4% for your first mission.>
		Msg4 <You accepted %name2%'s offer.>
		Msg5 <Ah, it's good to see that you're willing to listen to reason. Welcome to \FACTION &EnemyFac ; this new mecha marks you as one of us, now. You should head over to %name4% and talk with %name3%; I hear that \SPR %3% 's got some work for you.>
		Msg6 <You should think long and hard about this decision before you say something you'll regret. Take a look around- you're outnumbered and outgunned. If you're not going to join our gang we'll have to deal with you like any other trespasser.>
		Msg7 <That's a shame. Here I was looking forward to working with you, but I'm going to have to bury you instead.>
		Prompt1 <I have no complaints.>
		Prompt2 <I could do better, but... What are you getting at?>
		Prompt3 <I can be a pirate? Cool! I'm in!>
		Prompt4 <No way. My loyalty is to \FACTION PCFac .>
		CPrompt4 <ifFactionExists PCFac Accept>
		Prompt5 <Forget it. I'm not a criminal.>
		Prompt6 <Did I say no? I meant yes.>
		Prompt7 <You won't change my mind. I'm not joining.>
	end


%%  *****************************************
%%  ***   +Pme	PC meets an enemy pilot   ***
%%  *****************************************


%%  *****************************************
%%  ***   +Pun  Unknown  Enemy  Attacks   ***
%%  *****************************************


%%  ***********************************************************
%%  ***   +Psh	PC is shanghaied into working for faction   ***
%%  ***********************************************************


%%  *************************************
%%  ***   +Pla  Learn  of  Artifact   ***
%%  *************************************


%%  ****************************************
%%  ***   +Pew  Enemy  Weapon  Program   ***
%%  ****************************************

Content
	name <Self Destruct Sequence>
	desc <The base self-destruct sequence has been activated, but your enemy plans to keep you trapped!>
	requires <*CS_RaidEnemyBase E:++ E:ENEMY -E:FRIEND +Pew>
	Size 8

	% E1 is the base itself
	% E2 is your enemy
	Element2 <Grab 1>
	Place2 <1 (ArchEnemy) SD Enemy>

	% P%id%01 = Initialization Counter
	update <if= p%id%01 0 else %pop% P= %id%01 1 NPCLevel %2% StoryDL Goto %pop%>

	sub
		MetaScene 1
		mapwidth 35
		mapheight 35
		MonkeyMap
		LockedDoorChance 30
		SecretDoorChance 5
		IndustrialTiles

		NeededCells 3

		% L1 = Plot has ended
		% L2 = Initialization Counter
		% L3 = Death Timer
		% L4 = StoryDL
		% L5 = EnemyFac

		start <if= L2 0 else GoBeenBefore L= 2 1 Alert 2 Alert 3 History 4 ForceChat %2% L= 3 ComTime L+ 3 3600 L= 4 StoryDL L= 5 &EnemyFac>
		GoBeenBefore <Print 1>

		% If E2 dies, the PC wins.
		Faint%2% <if= L1 0 L= 1 1 StoryNote 5 XPV 100 WinComp 0 .next>
		.next <+T-- +F-->

		nu1 <if= T1 0 Return if= L1 0 L= 1 1 LoseComp 0 .next>

		5Min <ifG ComTime L3 Alert 6 History 7 Bomb Bomb Bomb KillPC if= L1 0 L= 1 1 LoseComp 0 .next>

		msg1 <You enter the enemy base.>
		Msg2 <As you enter the base, an alarm sounds: "BASE SECURITY COMPROMISED, SELF-DESTRUCT SEQUENCE INITIATED.">
		Msg3 <"ALL PERSONNEL INSTRUCTED TO EVACUATE IMMEDIATELY. THIS IS NOT A DRILL.">
		Msg4 <When you entered the base, the self-destruct sequence was activated.>
		Msg5 <You killed %name2% before the base exploded.>
		Msg6 <Suddenly, the base explodes around you!!!>
		Msg7 <You did not escape the base before the explosion.>

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Guards>
			type <SYNTH ROBOT>
			Deploy <if= V1 0 V= 1 1 MonsterUp 2 StoryDL>
			SetEnemy 1
			SetAlly 3

			Team 3
			name <ArchEnemy>
			SetEnemy 1
			SetAlly 2

			room
			special <STARTHERE>
			desig <HOME %e2%>
			minimap <1...........2............>
			sub
				TrapDoor
				Destination -1
				MiniMapComponent 2
				use <Print 1 Return if= L1 0 LoseComp 0 .next>
				.next <+T-- +F-->
			end

			room
			% If the PC can locate and steal the experimental mecha, that's a win too.
			minimap <...........#3............>
			sub
				Elevator
				name <Mecha Bay>
				MiniMapComponent 3
				% V1 = Usage Counter
				use <if= V1 0 else GoBeenBefore ifYesNo 1 2 3 V= 1 1 MechaPrize .mektype L4 RandomTheme * 4 d4 Return StoryNote 5 if= L1 0 WinComp 0 .next L= 1 1>
				.next <+T-- +F-->
				.mektype <GENERAL \FACTION_DESIG L5>
				GoBeenBefore <Print 4>
				Msg1 <You find a prototype mecha. It appears to be operational.>
				Msg2 <Escape with it.>
				Msg3 <Leave it alone.>
				Msg4 <There's nothing left here now.>
				Msg5 <You stole an experimental mecha from the base before it exploded.>
			end
		end

		Persona 2
		special <UNLISTED NOESCAPE>
		% V1 = Have spoken already
		greeting <ifChatNPCSurrendered else GoFirstTime EndChat Say 1 AddChat 1 AddChat 2>
		GoFirstTime <if= V1 0 else GoBeenBefore EndChat Say 2 AddChat 3>
		GoBeenBefore <NewChat SayAnything>
		result1 <EndChat Say 3 ReAttack %2%>
		result2 <EndChat Say 4 StoryNote 5 UnSurrender %2% FreezeNPC %2% PCFriend %2% L= 1 1 WinComp 0 .next>
		.next <+T-- +F-->
		result3 <EndChat Say 6>
		Msg1 <You've beaten me... I can't defeat you.>
		Msg2 <I expected that you'd find our base, eventually. Fortunately we have systems in place to prevent our new technology from falling into enemy hands.>
		Msg3 <At least I'll be taking you with me, so we can continue this battle in hell!>
		Msg4 <You are going to spare my life? I... I would not have done the same for you. Goodbye \PC .>
		Msg5 <You spared %name2%'s life in battle.>
		Msg6 <Not while you still breathe. This time, I intend to make sure that you die!>
		Msg6_1 <I'm staying here to make sure my side succeeds in killing you. If I should die as a consequence, that's something I'm willing to accept.>
		CMsg6_1 <ifNPCSociable Accept>
		Msg6_2 <You should really be more concerned with your own survival.>
		CMsg6_2 <ifNPCShy Accept>
		Msg6_3 <I'm in no hurry to leave, at least not so long as you're around.>
		CMsg6_3 <ifNPCEasygoing Accept>
		Msg6_4 <Saving my own life is unimportant... ending yours is what I plan to do!>
		CMsg6_4 <ifNPCPassionate Accept>
		Msg6_5 <What, and miss all the fun? As long as we've prepared such a warm welcome, you might as well stick around and enjoy it.>
		CMsg6_5 <ifNPCCheerful Accept>
		Msg6_6 <I won't be leaving here until you're dead, I'm afraid. Either I'll kill you or the explosion will... it makes little difference.>
		CMsg6_6 <ifNPCMelancholy Accept>
		Prompt1 <You'll die right here, as you deserve.>
		Prompt2 <Get out of here, while there's still time.>
		Prompt3 <Shouldn't you be evacuating with the rest of the crew?>
		Prompt3_1 <You should evacuate with the rest of the crew.>
		Prompt3_2 <Why don't you evacuate with the rest of the crew?>
	end


%%  *********************************
%%  ***   +C--  Nothing Special   ***
%%  *********************************


%%  *******************
%%  ***   GENERAL   ***
%%  *******************

Content
	name <Mobile Base>
	desc <You're searching for the base, but it's gone... in its place a new enemy.>
	requires <*CS_RaidEnemyBase E:-- F:MILIT +Pew>
	Size 8

	% E1 is the base itself
	% E2 is the new enemy
	Element2 <NewNPC -2 -7>
	Place2 </>

	% V%id%01 = Initialization Counter
	update <if= PlotStatus %id% else %pop% if= V%id%01 0 V= %id%01 1   MoveNPC %2% %1% SetNPCTeam %2% 2 NPCLevel %2% StoryDL>

	sub
		MetaScene 1 2
		% L1 = Encounter Over Counter
		% L2 = Initialization Counter
		MapWidth 30
		MapHeight 30

		Start <if= L2 0 L= 2 1   StoryNote 1   ForceChat %2%    &SetEnemyNPC %2%>
		nu1 <if= T1 0   Return   if= V1 0 V= 1 1   LoseComp 0 .next>
		nu2 <if= T2 0   if= V1 0 V= 1 1   XPV 100  SALVAGE   WinComp 0 .next>
		.next <+T-- +F-->

		Msg1 <You were too late to find the base, but fought %name2% instead.>

		sub
			team 1
			SetEnemy 2
			ParaX 5
			ParaY 5

			team 2
			SetEnemy 1
			Deploy <SetSelfFaction &EnemyFac   WMecha 2 StoryDL 100>
			ParaX 25
			ParaY 25
		end

		Persona 2
		special <UNLISTED>
		GREETING <EndChat Say 1 AddChat 1 AddChat 2>
		result1 <EndChat Say 2   AddChat 3   AddEasygoing -d6>
		result2 <EndChat Say 3   AddChat 3   AddEasygoing  d6>
		*result3 <*THEME_EXPO&ENEMY na>
		Msg1 <So, you must be \PC . I'm sorry to disappoint you but our mobile base is no longer here. It wouldn't make very much sense to keep it in one place, now would it?>
		Msg2 <I'm sorry, but do I really look that stupid? There's nothing for you to do here now, but since I know you're going to keep looking I can't very well let you get away.>
		Msg3 <I'm glad to hear it. I'd hate to have left you feeling unfulfilled on our first meeting.>
		Prompt1 <Tell me where you've moved it!>
		Prompt2 <That's fine. I'd just as soon fight you.>
		Prompt3 <[Continue]>
	end


Content
	name <You Just Missed Him!>
	desc <You've arrived at the base too late to stop your arch-enemy from launching for a mission.>
	requires <*CS_RaidEnemyBase E:++ (F:MILIT|F:CORPO|F:--) -L:ENEMY>
	Size 5

	% E1 is the base itself
	% E2 is the comm officer who will tell the PC about N2
	% E3 is the metascene for the upcoming fight
	Element2 <Prefab>
	Place2 <1 pass (Citizens)>
	Element3 <MetaScene !Near -7 *Building NeverFail>

	% P%id%01 = Initialization Counter
	update <if= PlotStatus %id% else %pop% if= p%id%01 0 P= %id%01 1 NPCLevel %2% StoryDL SetNPCFaction %2% &EnemyFac>

	sub
		MetaScene 1
		mapwidth 35
		mapheight 35
		MonkeyMap
		LockedDoorChance 30
		SecretDoorChance 5
		IndustrialTiles

		NeededCells 3

		% L1 = Plot has ended

		start <Print 1>
		msg1 <You enter the enemy base.>

		% If E2 dies, then the PC can't get the proper information.
		Faint%2% <if= L1 0 L= 1 1 LoseComp 0 .next>
		.next <+T-- +F-->

		sub
			Team 1
			SetEnemy 2

			Team 2
			name <Guards>
			type <GUARD ROBOT>
			Deploy <if= V1 0 V= 1 1 MonsterUp 2 StoryDL>
			SetEnemy 1

			Team 3
			name <Citizens>
			Passive

			room
			special <STARTHERE>
			minimap <......###..#1#...........>
			sub
				Elevator
				Destination -1
				MiniMapComponent 1
				use <Print 1 Return>
			end

			room
			minimap <......###..#1#...........>
			desig <HOME %e2%>
		end

		Persona 2
		special <UNLISTED>
		% V1 = StoryDL
		greeting <V= 1 StoryDL if= L1 0 else GoBeenBefore NewChat Say 1 AddChat 1 AddChat 2 XPV 100  StoryNote 2 L= 1 1 WinComp %3% .next>
		.next <+Tgj +Ffi>
		GoBeenBefore <NewChat SayAnything>
		result1 <EndChat Say 3 FreezeNPC %2% ifConversation SkillTar V1 PCFriend %2%>
		result2 <EndChat Say 4 AddHeroic -1 AddEasygoing -d10 AddCheerful -d10 SoloAttack %2%>
		Msg1_10 <Waah... You're that crazy pilot \PERSONA &EnemyNPC told us about! Well, you're too late, \SPR &EnemyNPC 's already launched for a mission against %name3%! Please don't kill me...>
		CMsg1_10 <ifNPCOK &EnemyNPC Accept>
		Msg1_20 <Waah... You're that crazy pilot that's been fighting \FACTION &EnemyFac ... Well, you're too late, our fighters have already launched for a mission against %name3%! Please don't kill me...>
		CMsg1_20 <ifNPCDead &EnemyNPC Accept>
		Msg2 <You arrived at the base only to learn that your enemy had already left for %name3%.>
		Msg3 <Thank you! I won't forget your mercy...>
		Msg4 <I'll fight if I have to!>
		Prompt1 <Get out of here, before I change my mind.>
		Prompt2 <Sorry, but I don't plan to leave any survivors.>
	end
	inv
		NPC Soldier
		job <Comm Officer>
	end

