%%
%% Arena Persona Fragments
%%
%%	This file contains persona fragments needed by the mecha arenas.
%%	Mostly this is for the managers.

%% TYPE: *YourChallengerDied
%%	The challenger the PC was supposed to fight has died, making that fight impossible.
%%	This pfrag will instead set up a generic encounter at the arena, and cancel the
%%	challenger quest. The PC will be jumped directly to the arena.
%%  PARAM1: ID of the arena
%%  PARAM2: QID of subquest

Persona
requires <*YourChallengerDied>
START <NewChat Say %id%01 SavePos PrepArena %1% 150 Jump %1% SetQuestStatus %2% -2>
Msg%id%01 <You were supposed to face a special opponent, but that's not going to happen. Instead you'll just have a regular fight... Come talk to me when you're done.>

%% TYPE: *MeetMeAtArena
%%	The NPC will meet the PC at the arena for a big fight.
%%  PARAM1: ID of the arena
%%  PARAM2: Exit label

Persona
requires <*MeetMeAtArena>
START <EndChat Say %id%01 Goto %2%>
Msg%id%01 <Meet me at the arena. We'll find out who the superior pilot is.>


%% TYPE: *ChallengeMeLater
%%	The PC can't challenge this NPC again so soon; maybe in 24 hours.

Persona
requires <*ChallengeMeLater>
START <NewChat Say %id%01>
Msg%id%01 <Come back tomorrow and you can challenge me again.>


%% TYPE: *ChallengeArenaWin
%%	The PC has won against a NPC challenger. Give some money
%%  PARAM1: CID of the challenger. Be careful, he may be dead.
%%  PARAM2: ID of the arena, needed for calculating the cash prize
%%  PARAM3: Arena reset exit label; called when the prize is given

Persona
requires <*ChallengeArenaWin>
START <NewChat Say %id%01 CashPrize Reward Threat ArenaThreat %2% 100 150 Goto %3%>
Msg%id%01_10 <Good job. Come back tomorrow for another fight.>
CMsg%id%01_10 <ifNPCDead %1% Accept>
Msg%id%01_20 <Good job against \PERSONA %1% . Come back tomorrow for another fight.>
CMsg%id%01_20 <ifNPCOK %1% Accept>


%% TYPE: *BasicArenaWin
%%	The PC has won a non-championship match. Give some money
%%  PARAM1: ID of the arena, needed for calculating the cash prize
%%  PARAM2: Arena reset exit label; called when the prize is given

Persona
requires <*BasicArenaWin>
START <NewChat Say %id%01 CashPrize Reward Threat ArenaThreat %1% 100 100 Goto %2%>
Msg%id%01 <Good job. Come back tomorrow for another fight.>



%% TYPE: *BasicArenaLoss
%%	The PC has lost a match. Basically do nothing.
%%  PARAM1: ID of the arena; might be needed, better include it anyway
%%  PARAM2: Arena reset exit label

Persona
requires <*BasicArenaLoss>
START <NewChat Say %id%01  Goto %2%>
Msg%id%01 <You lost the match. Come back here tomorrow and try your luck again.>



%% TYPE: *ArenaIsBusy
%%	There's no fight right now, but later there will be.
%%  PARAM1: ID of the arena; might be needed, better include it anyway

Persona
requires <*ArenaIsBusy>
START <NewChat Say %id%01>
Msg%id%01 <There's no battle taking place right now. Come back tomorrow and there should be another match.>



%% TYPE: *ArenaIsFull
%%	The PC should be in the arena fighting right now.
%%  PARAM1: ID of the arena; might be needed, better include it anyway

Persona
requires <*ArenaIsFull>
START <NewChat Say %id%01>
Msg%id%01 <What are you doing talking to me? Shouldn't you be fighting now?>



%% TYPE: *RefuseArenaFight
%%	The PC doesn't want to fight right now.
%%  PARAM1: ID of the arena; might be needed, better include it anyway

Persona
requires <*RefuseArenaFight>
START <NewChat Say %id%01>
Msg%id%01 <That's too bad. Come back later if you change your mind.>



%% TYPE: *StartArenaBattle
%%	The PC has agreed to fight. Do something.
%%  PARAM1: ID of the arena; might be needed, better include it anyway
%%  PARAM2: Arena fight prep exit label

Persona
requires <*StartArenaBattle>
START <EndChat Say %id%01  Goto %2%>
Msg%id%01 <Good luck out there. Come back here if you win and I'll give you a prize.>





%% TYPE: *OfferArenaBattle
%%	The NPC will offer the PC a chance to fight at the arena.
%%  PARAM1: Accept battle exit label (no message printed)
%%  PARAM2: Refuse battle exit label (no message printed)

Persona
requires <*OfferArenaBattle>
START <NewChat Say %id%01 AddChat %id%01 AddChat %id%02>
Result%id%01 <Goto %1%>
Result%id%02 <Goto %2%>
Msg%id%01 <>
Prompt%id%01 <>
Prompt%id%02 <>





