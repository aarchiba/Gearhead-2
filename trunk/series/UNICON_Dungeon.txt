%% *DUNGEON_X CONTENT
%%   Dungeon Extras. Little bits of personality for dungeon levels.

Content
name <Dead Miner>
requires <*DUNGEON_X Mine Space Ruin>
element1 <prefab>
element2 <prefab>
place2 <1>

% A dead miner and his harmonica, v2.1
%  a GH2 RANCON_Dungeon content entry written for the purpose of learning ASL.
%	-by Yendaa
%	-edited by Joe
inv
	% v%id%1 - how the body was treated
	%   0 nothing yet
	%   1 rummaged through
	%   2 left alone
	% v%id%02 - Mysticism counter

	MetaTerrain
	name <Dead Miner>
	desc <A dead miner> 
	SDL_Sprite <prop_body.png>
	roguechar <!>
	mass 170
	clue16 <Print %id%06>
	clue20 <Print %id%07>
	clue23 <Print %id%07>
	clue29 <Print %id%06>
	clue34 <Print %id%08 if= v%id%02 0 XPV 50 V= %id%02 1>
	clue37 <Print %id%06>

	use <if= v%id%01 0 else GoLeft ifYesNo %id%01 %id%02 %id%03 else GoLeave goto GoRummage>
	GoLeave <v= %id%01 2 AddHeroic 1>
	GoRummage <V= %id%01 1 Print %id%05 CASHPRIZE d50 GiveKeyItem %2% AddPragmatic 3>

	GoLeft <if= v%id%01 2 else GoRummaged Print %id%01>
	GoRummaged <Print %id%04>

	Msg%id%01 <A dead miner lies on the ground before you, his spacesuit torn and helmet cracked.>
	Msg%id%02 <Rummage through his belongings.>
	Msg%id%03 <Leave him.>
	Msg%id%04 <A dead miner lies here, his corpse desecrated.>
	Msg%id%05 <Cutting open what remains of his spacesuit, you find a couple of credits and an old silver harmonica.>
	Msg%id%06 <This man died of asphyxiation when his spacesuit lost pressure because of damage.>
	Msg%id%07 <It's too late for that. Perhaps if you had been here at the time when he died...>
	Msg%id%08 <You say a prayer for the soul of the deceased.>

	Instrument 2
	name <Old Harmonica>
	desc <Scratches and dents cover the silver surface of this instrument.>
end

%% *DUNGEON_GOAL CONTENT

