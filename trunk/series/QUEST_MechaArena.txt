%%
%% Mecha Arena Quest Components
%%

%%
%% *:MECHA_ARENA_BASE
%%	Contains the arena manager
%%
%%	Set QuestStatus when manager admits PC to arena; end the quest after the PC has
%%	completed the championship.
%%

Content
	name <Mullins Arena>
	requires <*:MECHA_ARENA_BASE>

	% E1 is Mullins
	% E2 is the arena
	% E3 is the entrance to the arena
	Element1 <Prefab>
	Scene1 <Building Public Sleazy>
	Team1 <Citizens>
	TeamData1 <Pass Ally>

	Scene2 <New *Arena_PrivateersArena>

	Element3 <Prefab>
	Scene3 <Environs>

	Quest1 <*:MECHA_ARENA_CHALLENGE #22 1 2>

	sub
		Persona 1
		rumor0 <Mullins used to be one of the greatest privateers, but had to retire when she was crippled in battle.>
		greeting <if# QuestStatus %qid% 0 else GoPreQuest if= ArenaState %2% NAV_AS_Win else GoCheckLoss Goto GoArenaWin>
		GoCheckLoss <if= ArenaState %2% NAV_AS_Loss else GoCheckReady Goto GoArenaLoss>
		GoCheckReady <if= ArenaState %2% NAV_AS_Vacant else GoArenaFull ifG ComTime ArenaRecharge %2% else GoArenaBusy Goto GoOfferFight>

		GoArenaWin <if= ArenaWins %2% 10 else GoMinorWin Goto GoBigWin>
		*GoMinorWin <*BasicArenaWin %2% GoResetArena>
		*GoArenaLoss <*BasicArenaLoss %2% GoResetArena>
		GoResetArena <ResetArena %2%>

		*GoArenaFull <*ArenaIsFull %2%>
		GoArenaBusy <if= QuestStatus %qid% -1 else GoReallyBusy Goto GoSkillTraining>
		*GoReallyBusy <*ArenaIsBusy %2%>

		GoChooseBattle <if= ArenaWins %2% 1 else GoNormalBattle Goto .%id1%_GoInit>
		*GoNormalBattle <*StartArenaBattle %2% GoStartFight>
		GoStartFight <SavePos PrepArena %2% 100 Jump %2%>
		*GoRefuseBattle <*RefuseArenaFight %2%>

		GoStartQuest <SetQuestStatus %qid% %id% SetArenaThreat %2% 10>

		GoOfferFight <NewChat Say 1 AddChat 1 AddChat 2 AddChat 3>
		GoBigWin <NewChat Say 4 History 5 XPV 500 SetQuestStatus %qid% -1 AddChat 4  CashPrize Reward Threat ArenaThreat %1% 100 100 Goto GoResetArena>
		GoSkillTraining <NewChat Say 6 AddChat 5>
		*GoPreQuest <*NiceToMeetYou GoArenaIntro>
		GoArenaIntro <NewChat Say 7 AddChat 6 AddChat 7 AddCHat 8>

		result1 <Goto GoChooseBattle>
		result2 <Goto GoRefuseBattle>
		result3 <EndChat Say 2 School .skills Say 3>
		.skills <1 2 3 4 5 6 7 8 9 10 12 17 18 42>
		result4 <Goto result3>
		result5 <Goto result3>
		result6 <NewChat Say 8 AddChat 1 AddChat 2 Goto GoStartQuest>
		result7 <NewChat Say 9 AddCheerful -1 Goto GoStartQuest>
		result8 <NewChat Say 10 AddChat 9 AddChat 10 AddLawful 1 AddEasygoing d4 AddRenown -1 Goto GoStartQuest>
		result9 <Goto result6>
		result10 <Goto GoRefuseBattle>

		Msg1 <Good to see you again. Are you ready to take part in today's fight?>
		Msg2 <There's more than one path to success in the arena. Choose a strategy and train accordingly.>
		Msg3 <The real test will come on the battlefield.>
		Msg4 <You did it! Congratulations, you are now the champion of the Privateer Arena! In recognition of this feat, I'll give you something more valuable than a prize: the chance to learn from my years of combat experience.>
		Msg5 <You were crowned champion at the Privateer Arena.>
		Msg6 <There's no battle going on right now, but I can teach you about combat if you'd like.>
		Msg7 <Hey, would you like to test your mettle in real combat? Some of the pirates around here run dueling matches just outside the spinner, in the shadow of the solar arrays. If you'd like to take part there's a cash prize for whoever wins.>
		Msg8 <There's a match ready to go right now. Would you like to take part, or will you be back later?>
		Msg9 <Come on back when you're feeling braver.>
		Msg10 <It's technically illegal, but the police have better things to worry about than a few pilots having a bit of fun. Sure people get hurt sometimes but everyone knew what they were getting into. What do you say?>

		Prompt1 <I'm ready to battle.>
		CPrompt1 <ifChatNPCInPlay Accept>
		Prompt2 <Maybe some other time.>
		Prompt3 <I'd like to train my skills.>
		CPrompt3 <if= QuestStatus %qid% -1 Accept>
		Prompt4 <Let's start training right now.>
		Prompt5 <Let's do that.>
		Prompt6 <I'd like that very much.>
		Prompt7 <No thanks, I'd rather live.>
		Prompt8 <Isn't that illegal?>
		Prompt9 <Alright, I could give it a try.>
		Prompt10 <Sorry, that's not my thing.>

	end
	inv
		NPC Mercenary
		name <Mullins>
		% Mullins is Miaga's mother. She was a hero in the privateer fleet
		% until being horribly injured in the line of duty. The left half
		% of Mullins's body is mostly cybernetic.
		mecha <>
		statline 13 10 14 16 8 17 10 13
		CharDesc Female Heroic Criminal Sociable Melancholy Renowned
		% Age 45; since basic char age is 20, add +25.
		Age 25
		%% Mullins is a member of the Pro Duelist Association
		SetFaction 5
		EquipChar 25000
		SDL_PORTRAIT <por_f_mullins(O-Y).png>
		SDL_SPRITE <cha_f_bandit.png>
		SDL_COLORS <200 200 0 142 62 39 1 75 67>

		STC QUEST-MAPMARKER-AUTO-STATIONARY
		name <Privateer's Arena>
		desig <ENTRANCE %s2%>
	end

%%
%% *:MECHA_ARENA_CHALLENGE
%%	Contains a rival arena pilot
%%
%%  PARAM1: The Arena Manager
%%  PARAM2: The Arena Itself
%%
%% The persona for the arena manager must contain a .%id%_Init script, to be called
%% when the PC reaches this particular rung of the arena ladder.
%% Said script should set the QuestID to %id%.
%%
%% ASSERT: If .%id%_Init is called, the PC is in the same scene as the arena manager and
%%  the next fight is ready to go.

Content
	name <Generic Challenger>
	requires <*:MECHA_ARENA_CHALLENGE>

	% E1 is the arena manager
	% E2 is the arena proper
	% E3 is the challenger
	Element3 <Prefab>
	Scene3 <Building Public>
	Team3 <Citizens>
	TeamData3 <Ally Pass>

sub
	Persona 1
	Greeting <if= QuestStatus %qid% %id% else %pop%  if= ArenaState %2% NAV_AS_Win else .%id%_GoCheckLoss Goto .%id%_GoArenaWin>
	.%id%_GoCheckLoss <if= ArenaState %2% NAV_AS_Loss else .%id%_GoCheckAnything NewChat Say %id%06 ResetArena %2%>
	.%id%_GoCheckAnything <if# ArenaState %2% NAV_AS_Vacant else .%id%_GoCheckEligibility NewChat Say %id%04>
	.%id%_GoCheckEligibility <ifNPCOK %3% else .%id%_GoNPCDied NewChat Say %id%05>
	.%id%_GoInit <ifNPCOK %3% else .%id%_GoNPCDied NewChat Say %id%01 SetQuestStatus %qid% %id%  NPCSubQMemo %3% %id% %id%02>
	%% Just in case the NPC who we're supposed to fight has died somehow, just set up a normal fight.
	.%id%_GoNPCDied <NewChat Say %id%03 SavePos PrepArena %2% 150 Jump %2% SetQuestStatus %qid% -2>
	*.%id%_GoArenaWin <*ChallengeArenaWin %3% %2% .%id%_GoConclude>
	.%id%_GoConclude <ResetArena %2% SetQuestStatus %qid% -1>
	Msg%id%01 <For the next battle, you'll be facing %name3%. You can find \OPR %3% in \SCENE %s3% .>
	Msg%id%02 <%name1% sent you to battle %name3% in arena combat; \SPR %3% can be found in \SCENE %s3% .>
	Msg%id%03 <You were supposed to face a special opponent, but that's not going to happen. Instead you'll just have a regular fight... Come talk to me when you're done.>
	Msg%id%04 <You're supposed to fight %name3% in the arena. Get over there and do something!>
	Msg%id%05 <You can't advance in the competition until after you defeat %name3%.>
	Msg%id%06 <%name3% defeated you in the arena. Try challenging \OPR %3% again later; you can't advance in the competition until you defeat \OPR %3% .>

	Persona 3
	Greeting <if= QuestStatus %qid% %id% else GoNoMatch  if= ArenaState %2% NAV_AS_Battle else GoCheckStatus Goto GoTrashTalk>

	*GoTrashTalk <*ArenaChallenge GoThemeInfo>
	*GoThemeInfo <*THEME_EXPO&Enemy NA>

	GoCheckStatus <if= ArenaState %2% NAV_AS_Ready else GoCheckWin EndChat Say 1>
	GoCheckWin <if= ArenaState %2% NAV_AS_Win else GoCheckChallenge ifNPCOK %1% else GoManagerDied NewChat Say 2>
	GoManagerDied <NewChat Say %3% SetQuestStatus %qid% -1 ResetArena %2%>
	GoCheckChallenge <ResetArena %2% ifG ComTime ArenaRecharge %2% else GoLater NewChat Say 4 AddChat 1>
	*GoLater <*ChallengeMeLater>

	*GoNoMatch <*NiceToMeetYou GoChat>
	*GoChat <*MISC_CHATTER>

	GoStartCombat <SetChallengerID %2% %3% SetChallengerHome %2% %s3% MoveNPC %3% %2% PrepArena %2% 0 SetNPCTeam %3% 2>

	*result1 <*MeetMeAtArena %2% GoStartCombat>

	Msg1 <I'm waiting for you at the arena. Hurry up and get over here.>
	Msg2 <You beat me. You better go talk with %name1% about the reward.>
	Msg3 <Too bad about what happened to %name1%... I guess there's not going to be any more fights until we get a new arena manager.>
	Msg4 <Are you here to challenge me?>

	Prompt1 <That's right.>
end
inv
	NPC Mecha Pilot
	SetFaction 5
	EquipChar 10000

end


