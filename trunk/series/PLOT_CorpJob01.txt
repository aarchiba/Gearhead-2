Plot 0
	name <Corporate Job #1>

	%% This is a generic mecha pilot job. The PC will be hired to go eliminate
	%% some bandits who have been attacking the corp's convoys.

	%% E1 is the character to offer the mission
	%% E2 is the faction he belongs to
	%% E3 is where to place the encounter
	%% E4 is the encounter itself
	element1 <Character Corporate>
	element2 <Faction !Comrade 1>
	element3 <Scene Environs !Near 1>
	element4 <Prefab>
	place4 <3>


	%% P1 = Timer/Initialization Counter
	%% P2 = Mission Indicator
	%%      1 = Mission Accepted
	%%	2 = Combat Entered
	%%	3 = Mission Success
	%%	4 = Mission Failure
	%% P3 = Difficulcy Rating
	%% P4 = Email Indicator


	start <ifNPCOK E1 else GoDelete ifG V1 0 Goto GoCheckTime>
	update <ifNPCOK E1 else GoDelete if= V1 0 else GoCheckTime  V= 1 ComTime  V+ 1 86400   P= 3 PCRenown  P+ 3 d4   P+ 3 -d4   ifG 2 P3 P= 3 d4>
	GoDelete <EndPlot>
	GoCheckTime <ifG ComTime V1  if= P2 0 else GoSetEnd  EndPlot>
	CleanUp <if= P2 0 else GoSetEnd  EndPlot>
	%% If the time expires, but the PC is working on the plot, send him a message
	%% to say that everything's over.
	GoSetEnd <if= P2 1  P= 2 4 email 2>


	% Insert email here
	5min <if= P4 0  if= PCFac E2  if= P2 0  if= d20 1  ifG PCCharm d40  Email 1 P= 4 1>

	Msg1 <\ELEMENT 1 @ \SCENE EScene 1 :// I have a mission for you from \ELEMENT 2 .>
	Msg1_1 <\ELEMENT 1 @ \SCENE EScene 1 :// Report to \SCENE EScene 1 for a mission at once.>
	Msg1_2 <\ELEMENT 1 @ \SCENE EScene 1 :// Your services are required by \ELEMENT 2 . Come see me for mission briefing.>
	Msg1_3 <\ELEMENT 1 @ \SCENE EScene 1 :// I've got a mission for you. Come here for the briefing.>
	Msg1_4 <\ELEMENT 1 @ \SCENE EScene 1 :// There's a matter that requires your attention. This is official \ELEMENT 2 business, so come as soon as possible.>

	Msg2 <\ELEMENT 1 @ \SCENE EScene 1 :// The bandits have left the area. Report back here for debriefing.>
	Msg2_1 <\ELEMENT 1 @ \SCENE EScene 1 :// The bandits were eliminated by one of our other operatives. Your mission is over.>

sub
	Persona 1
	rumor <%name1% needs a mecha pilot to do a mission for %name2%.>
	% V1 = Cash reward offered
	greeting <if= P2 4 else GoCheckWin AddReact -1 Goto GoLostMission>
	*GoLostMission <*MissionWasFailure&Mecha GoEnd E2 na>
	GoCheckWin <if= P2 3 else GoCheckMission  AddReact d2  CashPrize V1 Goto GoWonMission>
	*GoWonMission <*MissionWasSuccess&Reward&Mecha GoEnd E2 na>
	GoCheckMission <if# P2 0 else GoCheckRecruit NewChat Say 3 AddChat 2 AddChat 4>
	*GoCheckRecruit <*ENEMY_CHECK GoCheckEmail E2 GoEnd>
	GoCheckEmail <if= P4 1 else GoCheckMember Goto GoGotEmail>
	*GoGotEmail <*DidYouGetEmail GoMissionBriefing>
	GoCheckMember <if= PCFac E2 else GoCheckAuto  Goto GoMissionBriefing>
	*GoCheckAuto <*AutoMissionTest&Mecha GoMissionBriefing GoRejectMission GoCheckSkill E2 na>
	*GoCheckSkill <*GenericMissionTest&Mecha  GoMissionBriefing GoEnd GoRejectMission E2 na>
	*GoRejectMission <*RejectMission GoEnd>
	GoEnd <EndPlot>
	GoMissionBriefing <V= 1 Reward Threat P3 100 150   NewChat Say 1  AddChat 1 AddChat 2 AddChat 3>
	result1 <NewChat Say 2 Memo 4 P= 2 1>
	result2 <NewChat Say 6 AddChat 1 if= P2 0 AddChat 3>
	result3 <Goto GoRejectMission>
	result4 <NewChat Say 5 AddChat 2>
	Msg1 <Recently, a number of our shipments have been attacked by bandits. Your job will be to locate those responsible and make sure they are incapable of attacking us again. For this you will earn $ \VAL V1 .>
	Msg1_1 <You've probably heard that several of our convoys have been attacked recently. We've identified the bandit gang responsible; your job will be to eliminate them once and for all. Do this and you'll earn $ \VAL V1 .>
	CMsg1_1 <ifNPCSociable Accept>
	Msg1_2 <Some bandits have been attacking our shipments. Defeat them and you'll earn $ \VAL V1 .>
	CMsg1_2 <ifNPCShy Accept>
	Msg1_3 <This should be a straightforward assignment. Some bandits have been attacking our shipments; you'll be paid $ \VAL V1 for locating and eliminating them.>
	CMsg1_3 <ifNPCEasygoing Accept>
	Msg1_4 <The enemies of \ELEMENT 2 never sleep. Some bandits have been attacking our property... Your job will be to engage them in combat and eliminate them. Do this and you'll earn $ \VAL V1 .>
	CMsg1_4 <ifNPCPassionate Accept>
	Msg1_5 <I want you to locate a particular bandit gang and disable their mecha. If you can perform this task, \ELEMENT 2 will pay you $ \VAL V1 .>
	CMsg1_5 <ifNPCCheerful Accept>
	Msg1_6 <A gang of bandits has been attacking our shipments; several employees of \ELEMENT 2 have been killed already. I want you to locate the bandits and eliminate them. Upon completion of this mission you'll earn $ \VAL V1 .>
	CMsg1_6 <ifNPCMelancholy Accept>
	Msg2 <The bandits should be found in \ELEMENT 3 . I'll contact you if there's any change in the operation.>
	Msg2_1 <Search \ELEMENT 3 until you've found the bandits, then report back here after you've defeated them. If there's any change in the situation I'll be sure to let you know.>
	CMsg2_1 <ifNPCSociable Accept>
	Msg2_2 <Begin your search in the \ELEMENT 3 region. Report back once you have results.>
	CMsg2_2 <ifNPCShy Accept>
	Msg2_3 <As far as we know, the bandits are hiding somewhere in \ELEMENT 3 . Your best bet is to patrol that area and see what you can find. If you're really lucky, they'll find you...>
	CMsg2_3 <ifNPCEasygoing Accept>
	Msg2_4 <These bandits are cowardly, so you can't expect them to face you openly. Search \ELEMENT 3 and you should find their hidden base.>
	CMsg2_4 <ifNPCPassionate Accept>
	Msg2_5 <Most of the attacks have happened in \ELEMENT 3 , so that's where you can begin your search. Good luck.>
	CMsg2_5 <ifNPCCheerful Accept>
	Msg2_6 <These bandits must be stopped before they can cause even more destruction. Search \ELEMENT 3 until you find them.>
	CMsg2_6 <ifNPCMelancholy Accept>
	Msg3 <Have you started work on that mission I gave you yet?>
	Msg3_1 <Any luck in finding those bandits yet?>
	Msg4 <\ELEMENT 1 in \SCENE EScene 1 hired you to fight some bandits in \ELEMENT 3 for \ELEMENT 2 .>
	Msg5 <You have to search \ELEMENT 3 for some bandits who have been attacking our convoys. Once you've defeated them come back here and I'll give you $ \VAL V1 .>
	Msg5_1 <\PC , you really ought to buy a databook. I asked you to search \ELEMENT 3 for some bandits who have been attacking our convoys. Once you've defeated them come back here and I'll give you $ \VAL V1 .>
	Msg6 <Our research suggests that the bandits have \VAL Threat P3 100 threat points worth of equipment. You better make sure that your own gear is in top condition before you try to face them.>
	Msg6_1 <It's believed that they have a threat value of \VAL Threat P3 100 . Do you think you can handle that?>
	Prompt1 <Sounds good. I'll do it.>
	Prompt1_1 <Alright, I'll do it.>
	Prompt1_2 <I'll get to work immediately.>
	Prompt2 <What am I going to face out there?>
	Prompt2_1 <Do you know what kind of mecha they have?>
	Prompt2_2 <How much firepower am I up against?>
	Prompt3 <Sorry, I don't want this job.>
	CPrompt3 <if# PCFac E2 Accept>
	Prompt3_1 <On second thought, I don't want this job.>
	Prompt3_2 <Really, I don't want to do this.>
	Prompt4 <What am I supposed to be doing again?>
	Prompt4_1 <Could you go over the mission details one more time?>


	MetaScene 4 2
	%% This is the scene where the battle will take place.
	MapWidth 30
	MapHeight 30
	content <Some 1 5 Sub *BATTLE_X ->
	% L1 = Initialization
	% L2 = Victory Counter
	start <if= L1 0 L= 1 1   P= 2 2>
	nu1 <if= T1 0 Return if= L2 0 L= 2 1  AddRenown -8                    Goto GoLoss>
	nu2 <if= T2 0        if= L2 0 L= 2 1  XPV 100  AddRenown 1            Goto GoVictory>

	% The PC has won this encounter...
	GoVictory <FacXP+ E2 1   P= 2 3>

	% The PC has lost this encounter...
	GoLoss <P= 2 4>

	sub
		Team 1
		SetAlly 3
		SetEnemy 2
		ParaX 5
		ParaY 5

		Team 2
		SetEnemy 1 3
		Deploy <WMecha 2 Threat P3 100>
		ParaX 25
		ParaY 25
	end

end
inv
	STC ENCOUNTER-WANDER
	name <Bandit Patrol>
	update <if= P2 1 else GoHide  SetStat STAT_MetaVisibility 0  Goto GoSetOrders>
	ENCOUNTER_Hostile
end




