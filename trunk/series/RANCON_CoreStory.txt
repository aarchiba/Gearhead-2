%%
%% CORE STORY RANDOM CONTENT
%%
%% These fragments are used by the core story. Therefore, they can use all the story
%% context descriptors in their REQUIRES blocks, and can access the XRan palette freely.
%%
%% In addition, some of these fragments are tied to specific components, and may not
%% be readily reusable in other components.
%%

%% *CORE_D_CE_Intro Content
%%	This content contains the NPC who will explain the situation in the Corporate
%%	Espionage component.

Content
	%% Security Guard
	requires <*CORE_D_CE_Intro Common>
	element1 <Character HasMecha !Ally -3 !Near -7>

	sub
		Persona 1
		special <UNLISTED NOPLOTS>
		% V1 = Have been briefed already
		greeting <if= V1 1 else GoGreet Goto GoDebrief>
		*GoDebrief <*CORE_D_CE_DeBriefing>
		*GoGreet <*DefenseCheckIn GoBriefing>
		GoBriefing <V= 1 1 Goto GoBrief>
		*GoBrief <*CORE_D_CE_Briefing>
	end

Content
	%% Security Guard
	requires <*CORE_D_CE_Intro Common>
	element1 <Character Guard !Okay -3 !Near -7>

	sub
		Persona 1
		special <UNLISTED NOPLOTS>
		% V1 = Have been briefed already
		greeting <if= V1 1 else GoGreet Goto GoDebrief>
		*GoDebrief <*CORE_D_CE_DeBriefing>
		*GoGreet <*DefenseCheckIn GoBriefing>
		GoBriefing <V= 1 1 Goto GoBrief>
		*GoBrief <*CORE_D_CE_Briefing>
	end


Content
	%% Security Guard
	requires <*CORE_D_CE_Intro>
	element1 <Prefab>

	sub
		Persona 1
		special <UNLISTED NOPLOTS>
		% V1 = Have been briefed already
		greeting <if= V1 1 else GoGreet Goto GoDebrief>
		*GoDebrief <*CORE_D_CE_DeBriefing>
		*GoGreet <*DefenseCheckIn GoBriefing>
		GoBriefing <V= 1 1 Goto GoBrief>
		*GoBrief <*CORE_D_CE_Briefing>
	end
	inv
		NPC Soldier
	end

%% *CORE_D_CE_Witness
%%	This contains a character who will incriminate the person indicated by the
%%	recieved parameter.
%%   PARAM: The ID of the calling content.
%%	This will set several variables at START: L%PARAM%01 is the ID of the character
%%						  L%PARAM%02 is set when witness gives info

Content
	%% Witness
	requires <*CORE_D_CE_Witness Common>
	minimap <............1.......##+##>

	% E1 is a scene in this city
	% E2 is the witness
	element1 <Scene Building Public !Near -7 !Okay -3>
	element2 <Character !Near 1 !Comrade -3 NeverFail>
	NeverFail2 <Corporate Executive>

	content1 <Optional *CORP_EMPLOYEE na Local>

	sub
		Persona 2
		special <UNLISTED NOPLOTS>
		*greeting <*CORE_D_CE_Questioning GoReveal>
		GoReveal <NewChat Say 1 memo 2 L= %param%02 1>
		Msg1 <\PERSONA L%param%01 has been acting suspiciously lately. You might want to question \OPR L%param%01 .>
		Msg1_1 <I hate to gossip, but everybody says that \PERSONA L%param%01 has been acting strangely these days. If I were you I'd ask \OPR L%param%01 some questions about it.>
		CMsg1_1 <ifNPCSociable Accept>
		Msg1_2 <The person you want is \PERSONA L%param%01 . I have nothing else to say.>
		CMsg1_2 <ifNPCShy Accept>
		Msg1_3 <This is mostly a guess, but you may want to question \PERSONA L%param%01 .>
		CMsg1_3 <ifNPCEasygoing Accept>
		Msg1_4 <You know who the spy probably is? That idiot \PERSONA L%param%01 . I'm certain of it.>
		CMsg1_4 <ifNPCPassionate Accept>
		Msg1_5 <I hope you don't suspect me of having anything to do with it... But if you're looking for someone to blame, what about \PERSONA L%param%01 ?>
		CMsg1_5 <ifNPCCheerful Accept>
		Msg1_6 <If I were doing espionage on the side I'd probably have enough money to fix my car. Hmmm... It seems like \PERSONA L%param%01 has had a lot of extra money recently. You might want to check into that.>
		CMsg1_6 <ifNPCMelancholy Accept>
		Msg2 <\ChatNPC suggested that \PERSONA L%param%01 might be the spy.>
	end


%% *CORE_D_CE_Clue Content
%%	This content is the clue which reveals the identity of whoever it is that is the
%%	spy in the Corporate Espionage component.
%%	Once the clue is revealed, local variable #1 will be set to nonzero.

Content
	%% Suspect
	requires <*CORE_D_CE_Clue>
	minimap <............1.......##+##>

	% E1 is a nearby building
	% E2 is the suspect
	element1 <Scene Building Public !Near -7 !Okay -3>
	element2 <Character !Near 1 !Comrade -3 Criminal NeverFail>
	NeverFail2 <Corporate Executive>

	content1 <Required *CORE_D_CE_Witness %id% Distant>
	content2 <Optional *CORP_EMPLOYEE na Local>

	start <if= L%id%01 0 else %pop% L= %id%01 %2% Goto %pop%>

	sub
		Persona 2
		special <UNLISTED NOPLOTS>
		% V1 = Revelation counter
		*greeting <*CORE_D_CE_Questioning GoEvidence>
		GoEvidence <if= V1 0 else GoAlreadyRevealed NewChat Say 1 AddChat 1>
		GoAlreadyRevealed <NewChat Say 2>
		result1 <NewChat Say 5 AddChat 2 AddChat 3>
		result2 <NewChat Say 3 AddLawful  1   StoryNote 4  xpv 50 SetNPCFaction ChatNPCID 0  FreezeNPC ChatNPCID  PCEnemy ChatNPCID  V= 1 1  L= 1 1  WinComp 0 .Next>
		.Next <+T-- +F-->
		result3 <NewChat Say 6 AddLawful -2   StoryNote 7  CashPrize Reward StoryDL 500  V= 1 1  L= 1 -1  LoseComp 0 .Next>
		Msg1 <If you have no further reason to question me, I have to get back to business.>
		Msg2 <I have nothing else to say to you.>
		Msg3 <Don't think for one minute that I'm going to forget about this... You've made an enemy today, \PC .>
		Msg4 <You discovered \ChatNPC was spying on \ELEMENT 1 .>
		Msg5 <Look, if you could just forget about what you heard, I could make it worth your while. How does $ \VAL CashPrize Reward StoryDL 500 sound?>
		Msg6 <Here you go. It's been a pleasure doing business with you.>
		Msg7 <You accepted a bribe from \ChatNPC to keep \PPR ChatNPCID spying a secret.>
		Prompt1 <There's evidence suggesting you are the spy.>
		CPrompt1 <if# L%id%02 0 Accept>
		Prompt1_1 <From what I heard, you might be the spy.>
		Prompt2 <No deal. I'm reporting you to the authorities.>
		Prompt2_1 <Trying to bribe me? Now you're in deeper trouble.>
		Prompt3 <That sounds pretty good, actually.>
		Prompt3_1 <It's a deal.>
	end

Content
	%% Computer, incriminating email
	requires <*CORE_D_CE_Clue E:-- ~!Ne>
	minimap <######...##.1.##...###.##>

	% E1 is the computer
	% E2 is a nearby scene
	% E3 is the new enemy
	element1 <Prefab>
	element2 <Scene Building Public !Xclude -3 !Near -7>
	element3 <Prefab>
	Place3 <2 Pass>

	start <if= v%id%01 0 else %pop% V= %id%01 1 FreezeNPC %3% goto %pop%>

	inv
		STC COMPUTER-1
		use <if= L1 0 else GoDone ifYesNo 1 2 3 Print 4 Alert 5 Print 8 StoryNote 6   &SetEnemyNPC %3% L= 1 1  WinComp 0 .next>
		.next <+T-- +F-->
		clue32 <Print 7>
		Msg1 <Someone left this computer logged in to their account. Do you want to see what they were reading?>
		Msg2 <Yes, I do.>
		Msg3 <No, I don't.>
		Msg4 <You access the computer.>
		Msg5 <\PERSONA %3% @ \SCENE %2% :// Good work providing me with the maps of \SCENE SceneID . You will not be disappointed with your reward.>
		Msg6 <\ELEMENT 1 was being spied upon by someone named \PERSONA %3% .>
		Msg7 <This computer is already logged in. You don't have to hack anything.>
		Msg8 <You learn the identity of the spy, and also that he was working for someone named \PERSONA %3% .>
		Msg8_1 <You learn the identity of the spy, and also that she was working for someone named \PERSONA %3% .>

		NPC Thief
	end

Content
	%% Hacked Cleanbot
	requires <*CORE_D_CE_Clue ~C:ACADE ~C:THIEF (!Md|!Hi|!Ex)>
	minimap <######...##.1.##...###+##>

	element1 <Prefab>
	team1 <citizens>
	teamdata1 <pass>

	inv
		Monster Cleanbot
		% V1 = General Repair usage counter
		% V2 = Science usage counter
		% V3 = Investigation usage counter
		% V4 = Clue obtained counter
		% V5 = ID of enemy faction
		% General Repair, Science or Investigation can reveal the clue...
		clue23 <if= V4 0 else GoAgain ifGeneralRepair SkillTar StoryDL else GoClueFails  Goto GoReveal>
		clue29 <if= V4 0 else GoAgain ifScience SkillTar StoryDL else GoClueFails        Goto GoReveal>
		clue37 <if= V4 0 else GoAgain ifInvestigation SkillTar StoryDL else GoClueFails  Goto GoReveal>
		GoClueFails <Print 3>
		GoAgain <Print 1>
		GoReveal <Print 1 StoryNote 20  V= 4 1  V= 5 N2  L= 1 1  WinComp 0 .next>
		.next <+T-- +F-->
		% Code Breaking provides extra information once the clue is revealed.
		clue32 <if# V4 0 else GoClueFails Print 5>
		Msg1 <This robot has been modified. An antenna has been attached to the back... This is how they've been spying on the office!>
		Msg20_1 <You found someone had bugged the cleaning robot at \NARRATIVE 6 .>
		CMsg20_1 <if= V5 0 Accept>
		Msg20_2 <You found that \FACTION V5 bugged a cleaning robot at \NARRATIVE 6 .>
		CMsg20_2 <if# V5 0 Accept>
		Msg3 <You don't manage to learn anything from this robot.>
		Msg3_1 <You can't find anything at your current skill level.>
		Msg5_1 <You can't identify who might be recieving the signal from this bug.>
		CMsg5_1 <if= V5 0 Accept>
		Msg5_2 <You can't be sure, but the bug installed in this robot appears to use technology from \FACTION V5 .>
		CMsg5_2 <if# V5 0 Accept>
	end



%% *CORE_D_CE_RedHerring Content
%%	This content is looks like a clue, but isn't really.

Content
	%% Computer, not in the least incriminating
	requires <*CORE_D_CE_RedHerring ~!Ne ~!Lo>
	minimap <######...##.1.##...###.##>

	% E1 is the computer
	element1 <Prefab>

	inv
		STC COMPUTER-1
		use <if= V1 0 else GoBeenBefore ifYesNo 1 2 3 Print 4 Alert 5 V= 1 1>
		GoBeenBefore <Print 7>
		clue32 <Print 6>
		Msg1 <Someone left this computer logged in to their account. Do you want to see what they were reading?>
		Msg2 <Yes, I do.>
		Msg3 <No, I don't.>
		Msg4 <You access the computer.>
		Msg5 <Somebody was playing computer games on company time. Nothing to do with spying, however.>
		Msg5_1 <It's a bunch of programs related to work. Nothing suspicious here.>
		Msg5_2 <Somebody was downloading media files on company time. Nothing to do with spying, however.>
		Msg5_3 <Somebody was browsing porn on company time. That's against the rules, but it has nothing to do with spying.>
		Msg5_4 <It's a huge collection of inter-office chain letters. This user might not be the spy but he definitely deserves to be punished.>
		Msg5_5 <Someone is writing a project report. Nothing suspicious here.>
		Msg5_6 <This user has been browsing online auctions.>
		Msg5_7 <This user has been watching thrunet news feeds.>
		Msg6 <This computer is already logged in. You don't have to hack anything.>
		Msg7 <You've already checked this computer.>
	end

Content
	%% Room full of people
	requires <*CORE_D_CE_RedHerring>
	minimap <............1.......##+##>
	element1 <Prefab>
	team1 <Citizens>
	teamdata1 <ally pass>
	content1 <Optional *CORP_EMPLOYEE - Local>
	sub
		Persona 1
		*greeting <*NiceToMeetYou GoChat>
		*GoChat <*MISC_CHATTER>
	end
	inv
		NPC Corporate Executive
		update <SetSelfFaction LocalFaction>
	end


Content
	%% Person who knows nothing
	requires <*CORE_D_CE_RedHerring>
	minimap <............1.......##+##>

	% E1 is a scene in this city
	% E2 is the witness
	element1 <Scene Building Public !Near -7 !Okay -3>
	element2 <Character !Near 1 !Comrade -3 NeverFail>
	NeverFail2 <Citizen>

	content1 <Optional *CORP_EMPLOYEE na Local>

	sub
		Persona 2
		special <UNLISTED NOPLOTS>
		*greeting <*CORE_D_CE_Questioning GoReveal>
		GoReveal <NewChat Say 1>
		Msg1 <Really, I don't know anything. Why don't you ask someone else?>
	end

Content
	%% Normal Cleanbot
	requires <*CORE_D_CE_RedHerring>
	minimap <######...##.1.##...###+##>

	element1 <Prefab>
	team1 <citizens>
	teamdata1 <pass>

	inv
		Monster Cleanbot
		% General Repair, Science or Investigation can be used... of course, there is no clue.
		clue23 <Goto GoClueFails>
		clue29 <Goto GoClueFails>
		clue32 <Print 1>
		clue37 <Goto GoClueFails>
		GoUsed <Print 2>
		GoClueFails <Print 1>
		Msg1 <You don't manage to learn anything from this robot.>
		Msg1_1 <You can't find anything at your current skill level.>
	end
\

